
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НастройкаФормБольничнаяАптека.ФормаБизнесПроцесса_ПриСозданииНаСервере(ЭтотОбъект);
	
	ЗакрытьФормуВладельцаПоИмени = Параметры.ЗакрытьФормуВладельцаПоИмени;
	
	ИдентификаторБизнесПроцесса = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Объект.Ссылка.Метаданные().ПолноеИмя());
	Копирование = ЗначениеЗаполнено(Параметры.ЗначениеКопирования);
	Если Объект.Ссылка.Пустая() Тогда
		Если Не Копирование Тогда 
			ШаблоныПоПредмету.ЗагрузитьЗначения(
				ШаблоныБизнесПроцессов.ПолучитьШаблоныПоПредмету(
					Объект.Предмет,
					ТипЗнч(Объект.Шаблон),
					,
					ИдентификаторБизнесПроцесса));
			Если ШаблоныПоПредмету.Количество() = 1 Тогда 
				ЗаполнитьПоШаблонуНаСервере(ШаблоныПоПредмету[0].Значение);
			КонецЕсли;
		КонецЕсли;
		
		ИнициализацияФормы();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ИнициализацияФормы();
	
	НастройкаФормБольничнаяАптека.ФормаБизнесПроцесса_ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.Ссылка.Пустая() И Не Копирование Тогда 
		Если ШаблоныПоПредмету.Количество() > 1 Тогда 
			ПодключитьОбработчикОжидания("ВыбратьШаблонБизнесПроцессаПриОткрытии", 0.2, Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ВладелецФормы) = Тип("ФормаКлиентскогоПриложения") И ВладелецФормы.ИмяФормы = ЗакрытьФормуВладельцаПоИмени И ВладелецФормы.Открыта() Тогда
		ВладелецФормы.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаНастройкаОтложенногоСтарта" И Параметр.БизнесПроцесс = Объект.Ссылка Тогда
		ОбработатьИзменениеОтложенногоСтарта();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("Старт") И ПараметрыЗаписи.Старт Тогда
		
	ИначеЕсли Не Объект.Стартован Тогда
		БизнесПроцессыИЗадачиБольничнаяАптекаКлиент.ПроверитьКорректностиЗаполненияОтложенногоПроцесса(
			ЭтотОбъект, Объект, ПараметрыЗаписи, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьВидимостьПолейСостояния();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	НастройкаФормБольничнаяАптека.ФормаБизнесПроцесса_ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("БизнесПроцессИзменен", Объект.Ссылка);
	
	Если ПараметрыЗаписи.Свойство("Старт") И ПараметрыЗаписи.Старт Тогда
		ИнформацияОЗапуске = Новый Структура();
		ИнформацияОЗапуске.Вставить("СсылкаНаБизнесПроцесс", Объект.Ссылка);
		ИнформацияОЗапуске.Вставить("СсылкаНаПредметБизнесПроцесса", Объект.Предмет);
		
		Оповестить("БизнесПроцессСтартован", ИнформацияОЗапуске);
		Если ЗначениеЗаполнено(Объект.Предмет) Тогда
			ОповеститьОбИзменении(Объект.Предмет);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ЗакрытьФормуПослеЗаписи", Истина);
	
	Если Записать(ПараметрыЗаписи) Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтартоватьИЗакрыть(Команда)
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ЗакрытьФормуПослеЗаписи", Истина);
	ПараметрыЗаписи.Вставить("Старт", Истина);
	
	Если Записать(ПараметрыЗаписи) Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблону(Команда)
	
	ШаблоныПоПредмету.ЗагрузитьЗначения(ПолучитьШаблоныПоПредмету(Объект.Предмет, ТипЗнч(Объект.Шаблон), Ложь, ИдентификаторБизнесПроцесса));
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьПоШаблонуЗавершение", ЭтотОбъект);
	ШаблоныБизнесПроцессовКлиент.ВыбратьШаблонБизнесПроцесса(ТипЗнч(Объект.Шаблон), ШаблоныПоПредмету, ИдентификаторБизнесПроцесса, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблонуЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатВыбора) Тогда 
		ЗаполнитьПоШаблонуНаСервере(РезультатВыбора);
		УстановитьДоступностьПоШаблону();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Остановить(Команда)
	
	БизнесПроцессыИЗадачиКлиент.ОстановитьБизнесПроцессИзФормыОбъекта(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГлавнуюЗадачу(Команда)
	
	БизнесПроцессыИЗадачиБольничнаяАптекаКлиент.УстановитьГлавнуюЗадачуБизнесПроцессаИзФормыОбъекта(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьБизнесПроцесс(Команда)
	
	БизнесПроцессыИЗадачиКлиент.ПродолжитьБизнесПроцессИзФормыОбъекта(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтложенныйСтарт(Команда)
	
	ОткрытьНастройкуОтложенногоСтарта();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ГлавнаяЗадачаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(,Объект.ГлавнаяЗадача);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(Неопределено, Объект.Предмет);
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонСогласованияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ШаблоныПоПредмету.ЗагрузитьЗначения(ПолучитьШаблоныПоПредмету(Объект.Предмет, ТипЗнч(Объект.ШаблонСогласования)));
	Оповещение = Новый ОписаниеОповещения("ОбработатьВыборШаблонаДействия", ЭтотОбъект, "ШаблонСогласования");
	ШаблоныБизнесПроцессовКлиент.ВыбратьШаблонБизнесПроцесса(ТипЗнч(Объект.ШаблонСогласования), ШаблоныПоПредмету,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонУтвержденияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ШаблоныПоПредмету.ЗагрузитьЗначения(ПолучитьШаблоныПоПредмету(Объект.Предмет, ТипЗнч(Объект.ШаблонУтверждения)));
	Оповещение = Новый ОписаниеОповещения("ОбработатьВыборШаблонаДействия", ЭтотОбъект, "ШаблонУтверждения");
	ШаблоныБизнесПроцессовКлиент.ВыбратьШаблонБизнесПроцесса(ТипЗнч(Объект.ШаблонУтверждения), ШаблоныПоПредмету,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонИсполненияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ШаблоныПоПредмету.ЗагрузитьЗначения(ПолучитьШаблоныПоПредмету(Объект.Предмет, ТипЗнч(Объект.ШаблонИсполнения)));
	Оповещение = Новый ОписаниеОповещения("ОбработатьВыборШаблонаДействия", ЭтотОбъект, "ШаблонИсполнения");
	ШаблоныБизнесПроцессовКлиент.ВыбратьШаблонБизнесПроцесса(ТипЗнч(Объект.ШаблонИсполнения), ШаблоныПоПредмету,, Оповещение);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализацияФормы()
	
	УстановитьДоступностьПоШаблону();
	УстановитьДоступность();
	
	Элементы.Предмет.Гиперссылка = Объект.Предмет <> Неопределено И Не Объект.Предмет.Пустая();
	ПредметСтрокой = ОбщегоНазначения.ПредметСтрокой(Объект.Предмет);
	
	Если Объект.Стартован Тогда
		Если Объект.Завершен Тогда
			Длительность = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"(выполнялся %1)",
				НРег(БизнесПроцессыИЗадачиБольничнаяАптека.ДлительностьПроцесса(Объект.ДатаНачала, Объект.ДатаЗавершения)));
		Иначе
			Длительность = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"(выполняется %1)",
				НРег(БизнесПроцессыИЗадачиБольничнаяАптека.ДлительностьПроцесса(Объект.ДатаНачала, ТекущаяДатаСеанса())));
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимостьПолейСостояния();
	
	Если Объект.ГлавнаяЗадача = Неопределено Или Объект.ГлавнаяЗадача.Пустая() Тогда
		Элементы.ГлавнаяЗадача.Гиперссылка = Ложь;
		ГлавнаяЗадачаСтрокой = НСтр("ru = 'не задана'");
	Иначе
		ГлавнаяЗадачаСтрокой = Строка(Объект.ГлавнаяЗадача);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПодчиненныеБизнесПроцессы") Тогда
		Элементы.ГлавнаяЗадача.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступность()
	
	Если Объект.Завершен Тогда 
		ЭтотОбъект.ТолькоПросмотр = Истина;
	КонецЕсли;	
	
	Если Объект.Стартован Тогда 
		Элементы.Предмет.ТолькоПросмотр = Истина;
		Элементы.ШаблонСогласования.ТолькоПросмотр = Истина;
		Элементы.ШаблонУтверждения.ТолькоПросмотр = Истина;
		Элементы.ШаблонИсполнения.ТолькоПросмотр = Истина;
		Элементы.ЗаполнитьПоШаблону.Доступность = Ложь;
	КонецЕсли;	
	
	Элементы.ГруппаИнфо.Видимость = Не Объект.Ссылка.Пустая();
	Элементы.Длительность.Видимость = Не Объект.Ссылка.Пустая();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПоШаблону()
	
	ДоступностьПоШаблону = ШаблоныБизнесПроцессов.ДоступностьПоШаблону(Объект);
	
	Элементы.ШаблонСогласования.ТолькоПросмотр = Не ДоступностьПоШаблону;
	Элементы.ШаблонУтверждения.ТолькоПросмотр = Не ДоступностьПоШаблону;
	Элементы.ШаблонИсполнения.ТолькоПросмотр = Не ДоступностьПоШаблону;
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПоШаблонуНаСервере(Шаблон) 
	
	БизнесПроцессОбъект = РеквизитФормыВЗначение("Объект");
	БизнесПроцессОбъект.ЗаполнитьПоШаблону(Шаблон);
	ЗначениеВРеквизитФормы(БизнесПроцессОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПолейСостояния()
	
	БизнесПроцессыИЗадачиБольничнаяАптека.ОбновитьДоступностьПолейСостояния(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьШаблоныПоПредмету(Знач Предмет, Знач Тип, Знач ТолькоНастроенные = Истина, Знач ИдентификаторБизнесПроцесса = Неопределено)
	
	Возврат ШаблоныБизнесПроцессов.ПолучитьШаблоныПоПредмету(Предмет, Тип, ТолькоНастроенные, ИдентификаторБизнесПроцесса)
	
КонецФункции

&НаКлиенте
Процедура ОбработатьВыборШаблонаДействия(РезультатВыбора, ИмяШаблонаДействия) Экспорт
	
	Если ЗначениеЗаполнено(РезультатВыбора) Тогда
		Объект[ИмяШаблонаДействия] = РезультатВыбора;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьШаблонБизнесПроцессаПриОткрытии()
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьШаблонБизнесПроцессаПриОткрытииЗавершение", ЭтотОбъект);
	ШаблоныБизнесПроцессовКлиент.ВыбратьШаблонБизнесПроцесса(ТипЗнч(Объект.Шаблон), ШаблоныПоПредмету,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьШаблонБизнесПроцессаПриОткрытииЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(РезультатВыбора) Тогда 
		Закрыть();
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоШаблонуНаСервере(РезультатВыбора);
	УстановитьДоступностьПоШаблону();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкуОтложенногоСтарта()
	
	Если ПроверитьЗаполнение() Тогда
		БизнесПроцессыИЗадачиКлиент.НастроитьОтложенныйСтарт(Объект.Ссылка, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеОтложенногоСтарта()
	
	БизнесПроцессыИЗадачиБольничнаяАптека.ОбновитьДоступностьПолейСостояния(ЭтотОбъект);
	НастройкаФормБольничнаяАптека.ОбновитьСостояниеБизнесПроцесса(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
