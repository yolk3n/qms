
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	БизнесПроцессыИЗадачиСервер.УстановитьФорматДаты(Элементы.ДатаИсполнения);
	
	ЗадачаСсылка = Параметры.ЗадачаСсылка;
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Задача.БизнесПроцесс    КАК БизнесПроцесс,
	|	Задача.Предмет          КАК Предмет,
	|	Проверка.НомерИтерации  КАК НомерИтерации
	|ИЗ
	|	Задача.ЗадачаИсполнителя КАК Задача
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		БизнесПроцесс.Исполнение.РезультатыПроверки КАК Проверка
	|	ПО
	|		Задача.БизнесПроцесс = Проверка.Ссылка
	|		И Задача.Ссылка = Проверка.ЗадачаПроверяющего
	|ГДЕ
	|	Задача.Ссылка = &ЗадачаИсполнителя
	|");
	Запрос.УстановитьПараметр("ЗадачаИсполнителя", ЗадачаСсылка);
	
	ДанныеЗадачи = Запрос.Выполнить().Выбрать();
	ДанныеЗадачи.Следующий();
	
	Предмет = ДанныеЗадачи.Предмет;
	Элементы.Предмет.Гиперссылка = ЗначениеЗаполнено(Предмет);
	ПредметСтрокой = ОбщегоНазначения.ПредметСтрокой(Предмет);
	
	ТочкиМаршрута = Новый Массив;
	ТочкиМаршрута.Добавить(БизнесПроцессы.Исполнение.ТочкиМаршрута.Исполнить);
	ТочкиМаршрута.Добавить(БизнесПроцессы.Исполнение.ТочкиМаршрута.ОтветственноеИсполнение);
	
	ИсторияИсполнения.Параметры.УстановитьЗначениеПараметра("БизнесПроцесс", ДанныеЗадачи.БизнесПроцесс);
	ИсторияИсполнения.Параметры.УстановитьЗначениеПараметра("НомерИтерации", ДанныеЗадачи.НомерИтерации);
	ИсторияИсполнения.Параметры.УстановитьЗначениеПараметра("ТочкиМаршрута", ТочкиМаршрута);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПредметНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(Неопределено, Предмет);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы
