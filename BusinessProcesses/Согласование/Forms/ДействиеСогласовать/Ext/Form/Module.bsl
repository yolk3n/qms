
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	БизнесПроцессыИЗадачиСервер.ФормаЗадачиПриСозданииНаСервере(
		ЭтотОбъект,
		Объект, 
		Элементы.ГруппаСостояние,
		Элементы.ДатаИсполнения);
	
	РезультатыСогласованияСогласовано = Перечисления.РезультатыСогласования.Согласовано;
	РезультатыСогласованияНеСогласовано = Перечисления.РезультатыСогласования.НеСогласовано;
	РезультатыСогласованияСогласованоСЗамечаниями = Перечисления.РезультатыСогласования.СогласованоСЗамечаниями;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ИнициализацияФормы();
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ИнициализацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	БизнесПроцессыИЗадачиКлиент.ФормаЗадачиОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("ВыполнитьЗадачу") И ПараметрыЗаписи.ВыполнитьЗадачу Тогда 
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(ТекущийОбъект.БизнесПроцесс);
		Исключение
			
			ТекстОшибки = НСтр("ru='При выполнении задачи не удалось заблокировать %Ссылка%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Ссылка%",         ТекущийОбъект.БизнесПроцесс);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ВызватьИсключение ТекстОшибки;
			
		КонецПопытки;
		
		
		УстановитьПривилегированныйРежим(Истина);
		
		СогласованиеОбъект = ТекущийОбъект.БизнесПроцесс.ПолучитьОбъект();
		НайденнаяСтрока = СогласованиеОбъект.РезультатыСогласования.Найти(ТекущийОбъект.Ссылка, "ЗадачаИсполнителя");
		НайденнаяСтрока.РезультатСогласования = ПараметрыЗаписи.РезультатСогласования;
		СогласованиеОбъект.Записать();
		
		РазблокироватьДанныеДляРедактирования(ТекущийОбъект.БизнесПроцесс);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("ВыполнитьЗадачу")
	   И ПараметрыЗаписи.ВыполнитьЗадачу
	   И ПодписыватьЭП Тогда
		
		Если ИнформацияОПодписях.Количество() > 0 Тогда
			ЭлектроннаяПодписьБольничнаяАптека.ЗанестиИнформациюОПодписях(ИнформацияОПодписях.ВыгрузитьЗначения(), УникальныйИдентификатор);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить(Команда)
	
	ОчиститьСообщения();
	Если Записать() Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		ПоказатьОповещениеПользователя(
			"Изменение:", 
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВыполнить(Команда)
	
	Если Записать() Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		ПоказатьОповещениеПользователя(
			"Изменение:", 
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Согласовано(Команда)
	
	СогласоватьВыполнениеЗадачи(РезультатыСогласованияСогласовано);
	
КонецПроцедуры

&НаКлиенте
Процедура СогласованоСЗамечаниями(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.РезультатВыполнения) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Поле ""Комментарий"" не заполнено'"),,, "Объект.РезультатВыполнения");
		Возврат;
	КонецЕсли;
	
	СогласоватьВыполнениеЗадачи(РезультатыСогласованияСогласованоСЗамечаниями);
	
КонецПроцедуры

&НаКлиенте
Процедура НеСогласовано(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.РезультатВыполнения) Тогда 
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		    НСтр("ru = 'Поле ""Комментарий"" не заполнено'"),, 
			"Объект.РезультатВыполнения");
		Возврат;
	КонецЕсли;
	
	ИнформацияОПодписях = Новый СписокЗначений;
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ВыполнитьЗадачу", Истина);
	ПараметрыЗаписи.Вставить("РезультатСогласования", РезультатыСогласованияНеСогласовано);
	
	Если Не Записать(ПараметрыЗаписи) Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Выполнение:'"),
		ПолучитьНавигационнуюСсылку(Объект.Ссылка),
		Строка(Объект.Ссылка),
		БиблиотекаКартинок.Информация32);
	
	Оповестить("ЗадачаВыполнена", Объект.Ссылка);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура История(Команда)
	
	ПараметрыФормы = Новый Структура("ЗадачаСсылка", Объект.Ссылка);
	ОткрытьФорму("БизнесПроцесс.Согласование.Форма.ФормаИсторияСогласования", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Дополнительно(Команда)
	
	БизнесПроцессыИЗадачиКлиент.ОткрытьДопИнформациюОЗадаче(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьБизнесПроцесс(Команда)
	
	ПоказатьЗначение(, Объект.БизнесПроцесс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьКИсполнению(Команда)
	
	БизнесПроцессыИЗадачиКлиент.ПринятьЗадачуКИсполнению(ЭтотОбъект, ТекущийПользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьПринятиеКИсполнению(Команда)
	
	БизнесПроцессыИЗадачиКлиент.ОтменитьПринятиеЗадачиКИсполнению(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПредметНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Объект.Предмет);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Объект.Исполнитель) Тогда
		ПоказатьЗначение(, Объект.Исполнитель);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализацияФормы()
	
	НачальныйПризнакВыполнения = Объект.Выполнена;
	
	БизнесПроцессыИЗадачиБольничнаяАптека.ФормаЗадачиИнициализировать(ЭтотОбъект, Объект, Элементы.СрокИсполнения, Элементы.Предмет);
	
	// номер цикла
	НайденнаяСтрока = Объект.БизнесПроцесс.РезультатыСогласования.Найти(Объект.Ссылка, "ЗадачаИсполнителя");
	Если НайденнаяСтрока <> Неопределено Тогда 
		НомерИтерации = НайденнаяСтрока.НомерИтерации;
		
		Элементы.ТекстРезультатаВыполнения.Заголовок = Строка(НайденнаяСтрока.РезультатСогласования) + ".";
		Если (НайденнаяСтрока.РезультатСогласования = Перечисления.РезультатыСогласования.Согласовано) 
		 Или (НайденнаяСтрока.РезультатСогласования = Перечисления.РезультатыСогласования.СогласованоСЗамечаниями) Тогда 
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		
		ИначеЕсли НайденнаяСтрока.РезультатСогласования = Перечисления.РезультатыСогласования.НеСогласовано Тогда 
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			
		КонецЕсли;
	КонецЕсли;
	
	Предмет = Объект.БизнесПроцесс.Предмет;
	ПодписыватьЭП = Объект.БизнесПроцесс.ПодписыватьЭП;
	
	Если ПодписыватьЭП Тогда
		Команды.Согласовано.Заголовок = НСтр("ru = 'Согласовано (ЭП)'");
		Команды.Согласовано.Подсказка =
			НСтр("ru = 'При нажатии кнопки ""Согласовано"" потребуется выбрать сертификат для подписи и ввести пароль'");
		Команды.СогласованоСЗамечаниями.Заголовок = НСтр("ru = 'Согласовано с замечаниями (ЭП)'");
		Команды.СогласованоСЗамечаниями.Подсказка =
			НСтр("ru = 'При нажатии кнопки ""Согласовано с замечаниями"" потребуется выбрать сертификат для подписи и ввести пароль'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СогласоватьВыполнениеЗадачи(РезультатыСогласования)
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ВыполнитьЗадачу", Истина);
	ПараметрыЗаписи.Вставить("РезультатСогласования", РезультатыСогласования);
	
	Оповещение = Новый ОписаниеОповещения("ПродолжитьСогласованиеПослеПодписанияПредмета", ЭтотОбъект, ПараметрыЗаписи);
	ПодписатьПредмет(РезультатыСогласования, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьСогласованиеПослеПодписанияПредмета(Результат, ПараметрыЗаписи) Экспорт
	
	Если Результат <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать(ПараметрыЗаписи) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Выполнение:'"),
		ПолучитьНавигационнуюСсылку(Объект.Ссылка),
		Строка(Объект.Ссылка),
		БиблиотекаКартинок.Информация32);
	
	Оповестить("ЗадачаВыполнена", Объект.Ссылка);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьПредмет(РезультатСогласования, ОповещениеПослеПодписания)
	
	ИнформацияОПодписях = Новый СписокЗначений;
	
	Если ПодписыватьЭП И ЗначениеЗаполнено(Предмет) Тогда
		Оповещение = Новый ОписаниеОповещения("ПодписатьПредметЗавершение", ЭтотОбъект, Новый Структура("ОповещениеПослеПодписания", ОповещениеПослеПодписания));
		ЭлектроннаяПодписьБольничнаяАптекаКлиент.ПодписатьПредмет(Предмет, УникальныйИдентификатор, Оповещение, Ложь);
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеПослеПодписания, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьПредметЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		
		ПодписанныеДанные = Новый Массив;
		Для Каждого Данные Из Результат.НаборДанных Цикл
			Если Не Данные.Свойство("СвойстваПодписи") Тогда
				Возврат;
			КонецЕсли;
			Элемент = Новый Структура;
			Элемент.Вставить("ПодписанныйОбъект", Данные.Представление);
			Элемент.Вставить("СвойстваПодписи", Данные.СвойстваПодписи);
			ПодписанныеДанные.Добавить(Элемент);
		КонецЦикла;
		
		ИнформацияОПодписях.ЗагрузитьЗначения(ПодписанныеДанные);
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, Результат.Успех);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции


