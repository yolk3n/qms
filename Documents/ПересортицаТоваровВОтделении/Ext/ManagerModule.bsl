#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Имена реквизитов, от значений которых зависят параметры учета номенклатуры
//
// Возвращаемое значение:
//   Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУчетаНоменклатуры() Экспорт
	
	Возврат "Склад";
	
КонецФункции

// Возвращает параметры учета для номенклатуры, указанной в документе
//
// Параметры
//   Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий
// Возвращаемое значение
//   Структура - Состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУчетаНоменклатуры
//
Функция ПараметрыУчетаНоменклатуры(Объект) Экспорт
	
	ПараметрыУчета = ЗапасыСервер.ПараметрыУчетаНоменклатуры();
	ПараметрыУчета.ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	
	ПараметрыУчетаНаСкладе = СкладыСервер.ПараметрыУчетаНоменклатуры(Объект.Склад);
	ПараметрыУчета.ИспользоватьСерии = ПараметрыУчетаНаСкладе.ИспользоватьСерииНоменклатуры;
	ПараметрыУчета.ИспользоватьПартии = ПараметрыУчетаНаСкладе.ИспользоватьПартии;
	ПараметрыУчета.Склад = Объект.Склад;
	
	ПараметрыУчета.СуффиксыСтатусов.Добавить("");
	ПараметрыУчета.СуффиксыСтатусов.Добавить("Оприходование");
	
	Возврат ПараметрыУчета;
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания параметров учета номенклатуры
//
// Параметры
//   ПараметрыУчетаНоменклатуры - Структура - состав полей задается в функции ЗапасыСервер.ПараметрыУчетаНоменклатуры
//
// Возвращаемое значение
//   Строка - текст запроса
//
Функция ТекстЗапросаРасчетаСтатусовУчетаНоменклатуры(ПараметрыУчетаНоменклатуры) Экспорт
	
	Возврат ЗапасыСервер.ТекстЗапросаРасчетаСтатусовУчетаНоменклатуры(ПараметрыУчетаНоменклатуры);
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Склад)
	|	И ЗначениеРазрешено(Отделение)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Проведение
#Область Проведение

// Инициализирует таблицы значений, содержащие данные для проведения документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицыДвиженийДляПроведения(ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	ОсновныеДанныеДокумента = ПодготовитьОсновныеДанныеДляПроведения(ДополнительныеСвойства);
	
	ИнициализироватьКлючиАналитикиВидаУчета(ОсновныеДанныеДокумента);
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(ОсновныеДанныеДокумента);
	
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтТаблицаТовары());
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаТоварыНаСкладахВОтделениях(), Метаданные.РегистрыНакопления.ТоварыНаСкладахВОтделениях);
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаСвободныеОстатки(), Метаданные.РегистрыНакопления.СвободныеОстатки);
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтАналитика());
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаСебестоимостьТоваров(), Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаДвиженияНоменклатураНоменклатура(), Метаданные.РегистрыНакопления.ДвиженияНоменклатураНоменклатура);
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаТоварыКОформлениюИзлишковНедостач(), Метаданные.РегистрыНакопления.ТоварыКОформлениюИзлишковНедостач);
	
	Запрос = Новый Запрос(ПроведениеБольничнаяАптека.ПолучитьТекстЗапросаДвижений(ДополнительныеСвойства, Регистры));
	
	Для Каждого ДанныеДокумента Из ОсновныеДанныеДокумента Цикл
		Запрос.УстановитьПараметр(ДанныеДокумента.Ключ, ДанныеДокумента.Значение);
	КонецЦикла;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ПроведениеБольничнаяАптека.ЗаполнитьТаблицыДвижений(ДополнительныеСвойства, Запрос.ВыполнитьПакет(), Регистры);
	
КонецПроцедуры

Функция ПодготовитьОсновныеДанныеДляПроведения(ДополнительныеСвойства)
	
	ЗапрашиваемыеДанные = Новый Структура;
	ЗапрашиваемыеДанные.Вставить("Ссылка");
	ЗапрашиваемыеДанные.Вставить("Период", "Дата");
	ЗапрашиваемыеДанные.Вставить("Организация");
	ЗапрашиваемыеДанные.Вставить("Склад");
	ЗапрашиваемыеДанные.Вставить("ПриходоватьТоварыПоСебестоимостиСписания");
	ЗапрашиваемыеДанные.Вставить("СтатьяДоходов");
	ЗапрашиваемыеДанные.Вставить("АналитикаДоходов");
	ЗапрашиваемыеДанные.Вставить("СтатьяРасходов");
	ЗапрашиваемыеДанные.Вставить("АналитикаРасходов");
	ЗапрашиваемыеДанные.Вставить("ДокументОснование", "ИнвентаризацияТоваровВОтделении");
	
	ОсновныеДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ПроведениеБольничнаяАптека.ПолучитьСсылкуНаДокументДляПроведения(ДополнительныеСвойства),
		ЗапрашиваемыеДанные);
	
	ОсновныеДанныеДокумента.Вставить("ВестиУчетПоИсточникамФинансирования", ПолучитьФункциональнуюОпцию("ИспользоватьИсточникиФинансирования"));
	ОсновныеДанныеДокумента.Вставить("ИспользоватьМестаХранения", ПолучитьФункциональнуюОпцию("ИспользоватьМестаХранения", Новый Структура("Склад", ОсновныеДанныеДокумента.Склад)));
	ОсновныеДанныеДокумента.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПересортицаТоваров);
	ОсновныеДанныеДокумента.Вставить("ЗаполненДокументОснование", ЗначениеЗаполнено(ОсновныеДанныеДокумента.ДокументОснование));
	
	ЗапасыСервер.ПриПодготовкеОсновныхДанныхДляПроведения(ДополнительныеСвойства, ОсновныеДанныеДокумента);
	
	Возврат ОсновныеДанныеДокумента;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаТовары()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                                        КАК НомерСтроки,
	|	&Организация                                                     КАК Организация,
	|	&Склад                                                           КАК Склад,
	|	ВЫБОР
	|		КОГДА &ИспользоватьМестаХранения
	|			ТОГДА ТаблицаТовары.МестоХранения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.МестаХранения.ПустаяСсылка)
	|	КОНЕЦ                                                            КАК МестоХранения,
	|	ТаблицаТовары.Номенклатура                                       КАК Номенклатура,
	|	ТаблицаТовары.НоменклатураОприходование                          КАК НоменклатураОприходование,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетПоСериям, &СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                            КАК СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерийОприходование В (&СтатусУчетПоСериям, &СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатурыОприходование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                            КАК СерияНоменклатурыОприходование,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетПоПартиям, &СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                            КАК Партия,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартийОприходование В (&СтатусУчетПоПартиям, &СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.ПартияОприходование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                            КАК ПартияОприходование,
	|	ВЫБОР 
	|		КОГДА &ВестиУчетПоИсточникамФинансирования
	|			ТОГДА ТаблицаТовары.ИсточникФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПустаяСсылка)
	|	КОНЕЦ                                                            КАК ИсточникФинансирования,
	|	ТаблицаТовары.Количество                                         КАК Количество,
	|	ТаблицаТовары.КоличествоОприходование                            КАК КоличествоОприходование,
	|	ТаблицаТовары.Сумма                                              КАК Сумма,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                            КАК СерияНоменклатурыДляСебестоимости,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерийОприходование В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатурыОприходование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                            КАК СерияНоменклатурыОприходованиеДляСебестоимости,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                            КАК ПартияДляСебестоимости,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартийОприходование В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.ПартияОприходование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                            КАК ПартияОприходованиеДляСебестоимости
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	Документ.ПересортицаТоваровВОтделении.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТоварыНаСкладахВОтделениях()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	1                                       КАК Порядок,
	|	ТаблицаТовары.НомерСтроки               КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ТаблицаТовары.Организация               КАК Организация,
	|	ТаблицаТовары.Склад                     КАК Склад,
	|	ТаблицаТовары.МестоХранения             КАК МестоХранения,
	|	ТаблицаТовары.Номенклатура              КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры         КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия                    КАК Партия,
	|	ТаблицаТовары.ИсточникФинансирования    КАК ИсточникФинансирования,
	|	ТаблицаТовары.Количество                КАК Количество
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                             КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                     КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)        КАК ВидДвижения,
	|	&Период                                       КАК Период,
	|	ТаблицаТовары.Организация                     КАК Организация,
	|	ТаблицаТовары.Склад                           КАК Склад,
	|	ТаблицаТовары.МестоХранения                   КАК МестоХранения,
	|	ТаблицаТовары.НоменклатураОприходование       КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатурыОприходование  КАК СерияНоменклатуры,
	|	ТаблицаТовары.ПартияОприходование             КАК Партия,
	|	ТаблицаТовары.ИсточникФинансирования          КАК ИсточникФинансирования,
	|	ТаблицаТовары.КоличествоОприходование         КАК Количество
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСвободныеОстатки()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	1                                       КАК Порядок,
	|	ТаблицаТовары.НомерСтроки               КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ТаблицаТовары.Организация               КАК Организация,
	|	ТаблицаТовары.Склад                     КАК Склад,
	|	ТаблицаТовары.МестоХранения             КАК МестоХранения,
	|	ТаблицаТовары.Номенклатура              КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры         КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия                    КАК Партия,
	|	ТаблицаТовары.ИсточникФинансирования    КАК ИсточникФинансирования,
	|	ТаблицаТовары.Количество                КАК ВНаличии
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                             КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                     КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)        КАК ВидДвижения,
	|	&Период                                       КАК Период,
	|	ТаблицаТовары.Организация                     КАК Организация,
	|	ТаблицаТовары.Склад                           КАК Склад,
	|	ТаблицаТовары.МестоХранения                   КАК МестоХранения,
	|	ТаблицаТовары.НоменклатураОприходование       КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатурыОприходование  КАК СерияНоменклатуры,
	|	ТаблицаТовары.ПартияОприходование             КАК Партия,
	|	ТаблицаТовары.ИсточникФинансирования          КАК ИсточникФинансирования,
	|	ТаблицаТовары.КоличествоОприходование         КАК ВНаличии
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Номенклатура                                                 КАК Номенклатура,
	|	ТаблицаТовары.НоменклатураОприходование                                    КАК НоменклатураОприходование,
	|	ТаблицаТовары.СерияНоменклатурыДляСебестоимости                            КАК СерияНоменклатуры,
	|	ТаблицаТовары.ПартияДляСебестоимости                                       КАК Партия,
	|	ТаблицаТовары.СерияНоменклатурыОприходованиеДляСебестоимости               КАК СерияНоменклатурыОприходование,
	|	ТаблицаТовары.ПартияОприходованиеДляСебестоимости                          КАК ПартияОприходование,
	|	ТаблицаТовары.ИсточникФинансирования                                       КАК ИсточникФинансирования,
	|	АналитикаУчетаНоменклатуры.КлючАналитики                                   КАК АналитикаУчетаНоменклатуры,
	|	АналитикаВидаУчета.КлючАналитики                                           КАК АналитикаВидаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВОтделениях)  КАК РазделУчета,
	|	АналитикаУчетаНоменклатурыПолучатель.КлючАналитики                         КАК АналитикаУчетаНоменклатурыПолучатель
	|ПОМЕСТИТЬ ВтАналитика
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|		ПО
	|			ТаблицаТовары.Номенклатура = АналитикаУчетаНоменклатуры.Номенклатура
	|			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = АналитикаУчетаНоменклатуры.СерияНоменклатуры
	|			И ТаблицаТовары.ПартияДляСебестоимости = АналитикаУчетаНоменклатуры.Партия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаВидаУчета КАК АналитикаВидаУчета
	|		ПО
	|			АналитикаВидаУчета.Организация = ТаблицаТовары.Организация
	|			И АналитикаВидаУчета.Склад = ТаблицаТовары.Склад
	|			И АналитикаВидаУчета.ИсточникФинансирования = ТаблицаТовары.ИсточникФинансирования
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатурыПолучатель
	|		ПО
	|			ТаблицаТовары.НоменклатураОприходование = АналитикаУчетаНоменклатурыПолучатель.Номенклатура
	|			И ТаблицаТовары.СерияНоменклатурыОприходованиеДляСебестоимости = АналитикаУчетаНоменклатурыПолучатель.СерияНоменклатуры
	|			И ТаблицаТовары.ПартияОприходованиеДляСебестоимости = АналитикаУчетаНоменклатурыПолучатель.Партия
	|ГДЕ
	|	АналитикаУчетаНоменклатуры.КлючАналитики <> АналитикаУчетаНоменклатурыПолучатель.КлючАналитики
	|	ИЛИ НЕ &ПриходоватьТоварыПоСебестоимостиСписания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСебестоимостьТоваров()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	1                                               КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                       КАК НомерСтроки,
	|	&Период                                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	Аналитика.АналитикаУчетаНоменклатуры            КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.АналитикаВидаУчета                    КАК АналитикаВидаУчета,
	|	Аналитика.РазделУчета                           КАК РазделУчета,
	|	ТаблицаТовары.Количество                        КАК Количество,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма
	|	КОНЕЦ                                           КАК Стоимость,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма
	|	КОНЕЦ                                           КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма
	|	КОНЕЦ                                           КАК СтоимостьРегл,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА &ХозяйственнаяОперация
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой)
	|	КОНЕЦ                                           КАК ХозяйственнаяОперация,
	|	&Ссылка                                         КАК ДокументДвижения,
	|	Аналитика.АналитикаУчетаНоменклатурыПолучатель  КАК КорАналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА Аналитика.АналитикаВидаУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                           КАК КорАналитикаВидаУчета,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА Аналитика.РазделУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                           КАК КорРазделУчета,
	|	ТаблицаТовары.КоличествоОприходование           КАК КорКоличество,
	|
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ &СтатьяДоходов
	|	КОНЕЦ                                    КАК СтатьяДоходов,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ &АналитикаДоходов
	|	КОНЕЦ                                    КАК АналитикаДоходов,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ &СтатьяРасходов
	|	КОНЕЦ                                    КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ &АналитикаРасходов
	|	КОНЕЦ                                    КАК АналитикаРасходов
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ВтАналитика КАК Аналитика
	|		ПО
	|			ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	|			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = Аналитика.СерияНоменклатуры
	|			И ТаблицаТовары.ПартияДляСебестоимости = Аналитика.Партия
	|			И ТаблицаТовары.НоменклатураОприходование = Аналитика.НоменклатураОприходование
	|			И ТаблицаТовары.СерияНоменклатурыОприходованиеДляСебестоимости = Аналитика.СерияНоменклатурыОприходование
	|			И ТаблицаТовары.ПартияОприходованиеДляСебестоимости = Аналитика.ПартияОприходование
	|			И ТаблицаТовары.ИсточникФинансирования = Аналитика.ИсточникФинансирования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                               КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                       КАК НомерСтроки,
	|	&Период                                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	Аналитика.АналитикаУчетаНоменклатурыПолучатель  КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.АналитикаВидаУчета                    КАК АналитикаВидаУчета,
	|	Аналитика.РазделУчета                           КАК РазделУчета,
	|	ТаблицаТовары.КоличествоОприходование           КАК Количество,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма
	|	КОНЕЦ                                           КАК Стоимость,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма
	|	КОНЕЦ                                           КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма
	|	КОНЕЦ                                           КАК СтоимостьРегл,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА &ХозяйственнаяОперация
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой)
	|	КОНЕЦ                                           КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                    КАК ДокументДвижения,
	|	НЕОПРЕДЕЛЕНО                                    КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                    КАК КорАналитикаВидаУчета,
	|	НЕОПРЕДЕЛЕНО                                    КАК КорРазделУчета,
	|	0                                               КАК КорКоличество,
	|
	|	НЕОПРЕДЕЛЕНО                                    КАК СтатьяДоходов,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                    КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаРасходов
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ВтАналитика КАК Аналитика
	|		ПО
	|			ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	|			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = Аналитика.СерияНоменклатуры
	|			И ТаблицаТовары.ПартияДляСебестоимости = Аналитика.Партия
	|			И ТаблицаТовары.НоменклатураОприходование = Аналитика.НоменклатураОприходование
	|			И ТаблицаТовары.СерияНоменклатурыОприходованиеДляСебестоимости = Аналитика.СерияНоменклатурыОприходование
	|			И ТаблицаТовары.ПартияОприходованиеДляСебестоимости = Аналитика.ПартияОприходование
	|			И ТаблицаТовары.ИсточникФинансирования = Аналитика.ИсточникФинансирования
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияНоменклатураНоменклатура()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                           КАК НомерСтроки,
	|	&Период                                             КАК Период,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА &ХозяйственнаяОперация
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой)
	|	КОНЕЦ                                               КАК ХозяйственнаяОперация,
	|	Аналитика.АналитикаВидаУчета                        КАК АналитикаВидаУчета,
	|	Аналитика.АналитикаУчетаНоменклатуры                КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.АналитикаВидаУчета                        КАК КорАналитикаВидаУчета,
	|	Аналитика.АналитикаУчетаНоменклатурыПолучатель      КАК КорАналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.Количество                            КАК Количество,
	|	ТаблицаТовары.КоличествоОприходование               КАК КорКоличество,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма
	|	КОНЕЦ                                               КАК Стоимость,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма
	|	КОНЕЦ                                               КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА &ПриходоватьТоварыПоСебестоимостиСписания
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма
	|	КОНЕЦ                                               КАК СтоимостьРегл,
	|	&Ссылка                                             КАК ДокументДвижения
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ВтАналитика КАК Аналитика
	|		ПО
	|			ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	|			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = Аналитика.СерияНоменклатуры
	|			И ТаблицаТовары.ПартияДляСебестоимости = Аналитика.Партия
	|			И ТаблицаТовары.НоменклатураОприходование = Аналитика.НоменклатураОприходование
	|			И ТаблицаТовары.СерияНоменклатурыОприходованиеДляСебестоимости = Аналитика.СерияНоменклатурыОприходование
	|			И ТаблицаТовары.ПартияОприходованиеДляСебестоимости = Аналитика.ПартияОприходование
	|			И ТаблицаТовары.ИсточникФинансирования = Аналитика.ИсточникФинансирования
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТоварыКОформлениюИзлишковНедостач()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	1                                       КАК Порядок,
	|	&Период                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	ТаблицаТовары.Организация               КАК Организация,
	|	&Склад                                  КАК Склад,
	|	&ДокументОснование                      КАК ДокументОснование,
	|	ТаблицаТовары.НомерСтроки               КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура              КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры         КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия                    КАК Партия,
	|	ТаблицаТовары.ИсточникФинансирования    КАК ИсточникФинансирования,
	|	0                                       КАК КОформлениюОприходования,
	|	ТаблицаТовары.Количество                КАК КОформлениюСписания
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	&ЗаполненДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                             КАК Порядок,
	|	&Период                                       КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)        КАК ВидДвижения,
	|	ТаблицаТовары.Организация                     КАК Организация,
	|	&Склад                                        КАК Склад,
	|	&ДокументОснование                            КАК ДокументОснование,
	|	ТаблицаТовары.НомерСтроки                     КАК НомерСтроки,
	|	ТаблицаТовары.НоменклатураОприходование       КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатурыОприходование  КАК СерияНоменклатуры,
	|	ТаблицаТовары.ПартияОприходование             КАК Партия,
	|	ТаблицаТовары.ИсточникФинансирования          КАК ИсточникФинансирования,
	|	ТаблицаТовары.КоличествоОприходование         КАК КОформлениюОприходования,
	|	0                                             КАК КОформлениюСписания
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	&ЗаполненДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Реквизиты)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Номенклатура                КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                     КАК СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                     КАК Партия
	|ПОМЕСТИТЬ втТаблицаАналитики
	|ИЗ
	|	Документ.ПересортицаТоваровВОтделении.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.НоменклатураОприходование   КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерийОприходование В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатурыОприходование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                     КАК СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартийОприходование В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.ПартияОприходование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                     КАК Партия
	|ИЗ
	|	Документ.ПересортицаТоваровВОтделении.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СерияНоменклатуры,
	|	Партия
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("СтатусУчетСебестоимостиПоСериям", Реквизиты.СтатусУчетСебестоимостиПоСериям);
	Запрос.УстановитьПараметр("СтатусУчетСебестоимостиПоПартиям", Реквизиты.СтатусУчетСебестоимостиПоПартиям);
	Запрос.Выполнить();
	
	Справочники.КлючиАналитикиУчетаНоменклатуры.ИнициализироватьКлючиАналитики(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиВидаУчета(Реквизиты)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Организация                      КАК Организация,
	|	&Склад                            КАК Склад,
	|	ВЫБОР
	|		КОГДА &ВестиУчетПоИсточникамФинансирования
	|			ТОГДА ТаблицаТовары.ИсточникФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПустаяСсылка)
	|	КОНЕЦ                             КАК ИсточникФинансирования
	|ПОМЕСТИТЬ втТаблицаАналитики
	|ИЗ
	|	Документ.ПересортицаТоваровВОтделении.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Склад,
	|	ИсточникФинансирования
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Склад", Реквизиты.Склад);
	Запрос.УстановитьПараметр("ВестиУчетПоИсточникамФинансирования", Реквизиты.ВестиУчетПоИсточникамФинансирования);
	Запрос.Выполнить();
	
	Справочники.КлючиАналитикиВидаУчета.ИнициализироватьКлючиАналитики(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

#КонецОбласти // Проведение

////////////////////////////////////////////////////////////////////////////////
// Печать
#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	УправлениеПечатьюБольничнаяАптека.ДобавитьКомандыПечати(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыПечати);
	
КонецПроцедуры

// Возвращает список доступных печатных форм документа
//
Функция ДоступныеПечатныеФормы() Экспорт
	
	МетаданныеДокумента = ПустаяСсылка().Метаданные();
	МенеджерПечати      = МетаданныеДокумента.ПолноеИмя();
	МетаданныеМакетов   = МетаданныеДокумента.Макеты;
	
	ПечатныеФормы = УправлениеПечатьюБольничнаяАптека.СоздатьКоллекциюДоступныхПечатныхФорм();
	
	ПечатнаяФорма = УправлениеПечатьюБольничнаяАптека.ДобавитьПечатнуюФорму(ПечатныеФормы, "Накладная", МенеджерПечати);
	ПечатнаяФорма.Представление = МетаданныеМакетов.ПФ_MXL_Накладная.Представление();
	ПечатнаяФорма.ПутьКМакету = ФормированиеПечатныхФормБольничнаяАптека.ПутьКМакету(МетаданныеМакетов.ПФ_MXL_Накладная);
	УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	
	Возврат ПечатныеФормы;
	
КонецФункции

Функция ПолучитьТекстЗапросаДанныеДляПечати()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Документ.Ссылка                                    КАК Ссылка,
	|	Документ.Номер                                     КАК НомерДокумента,
	|	Документ.Дата                                      КАК ДатаДокумента,
	|	Документ.Организация                               КАК Организация,
	|	Документ.Отделение                                 КАК Подразделение,
	|	Документ.Отделение.Представление                   КАК ПодразделениеПредставление,
	|	Документ.Склад                                     КАК Склад,
	|	Документ.Склад.Представление                       КАК СкладПредставление,
	|	Документ.ПриходоватьТоварыПоСебестоимостиСписания  КАК ПриходоватьТоварыПоСебестоимостиСписания,
	|	Документ.ИнвентаризацияТоваровВОтделении           КАК Основание,
	|	Документ.Товары.(
	|		НомерСтроки                                  КАК НомерСтроки,
	|		Номенклатура                                 КАК Номенклатура,
	|		Номенклатура.НаименованиеПолное              КАК ТоварНаименование,
	|		Номенклатура.Код                             КАК ТоварКод,
	|		СерияНоменклатуры                            КАК Серия,
	|		Партия                                       КАК Партия,
	|		ЕдиницаИзмерения                             КАК ЕдиницаИзмерения,
	|		ЕдиницаИзмерения.Представление               КАК ЕдиницаИзмеренияПредставление,
	|		ЕдиницаИзмерения.КодОКЕИ                     КАК КодПоОКЕИ,
	|		НоменклатураОприходование                    КАК НоменклатураОприходование,
	|		НоменклатураОприходование.НаименованиеПолное КАК ТоварНаименованиеОприходование,
	|		НоменклатураОприходование.Код                КАК ТоварКодОприходование,
	|		СерияНоменклатурыОприходование               КАК СерияОприходование,
	|		ПартияОприходование                          КАК ПартияОприходование,
	|		ЕдиницаИзмеренияОприходование                КАК ЕдиницаИзмеренияОприходование,
	|		ЕдиницаИзмеренияОприходование.Представление  КАК ЕдиницаИзмеренияОприходованиеПредставление,
	|		ЕдиницаИзмеренияОприходование.КодОКЕИ        КАК КодПоОКЕИОприходование,
	|		Цена                                         КАК Цена,
	|		Сумма                                        КАК Сумма,
	|		КоличествоВЕдиницахИзмерения                 КАК Количество,
	|	)
	|ИЗ
	|	Документ.ПересортицаТоваровВОтделении КАК Документ
	|
	|ГДЕ
	|	Документ.Ссылка В (&ТекущийДокумент)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПечатьНакладная(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб        = Истина;
	
	ПолноеИмяМакета = ФормированиеПечатныхФормБольничнаяАптека.ПутьКМакету(ПустаяСсылка().Метаданные().Макеты.ПФ_MXL_Накладная);
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_" + ПолноеИмяМакета;
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПолноеИмяМакета);
	
	МассивВыводимыхОбластей = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаДанныеДляПечати();
	Запрос.УстановитьПараметр("ТекущийДокумент", МассивОбъектов);
	
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	ОбластьПодвалТаблицы  = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ОбластьПодписи        = Макет.ПолучитьОбласть("Подписи");
	ОбластьСуммаПрописью  = Макет.ПолучитьОбласть("СуммаПрописью");
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	Пока Шапка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(Шапка.Ссылка, ТабличныйДокумент, Макет);
	
		ПараметрыИзШапки = ПолучитьПараметрыШапкаНакладная(Шапка);
		
		ФормированиеПечатныхФормБольничнаяАптека.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Заголовок", ПараметрыИзШапки);
		
		Если ЗначениеЗаполнено(ПараметрыИзШапки.Основание) Тогда 
			ФормированиеПечатныхФормБольничнаяАптека.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Основание", ПараметрыИзШапки);
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		ВыборкаСтрокТовары = Шапка.Товары.Выбрать();
		
		ДанныеТекущейСтроки = Новый Структура(ФормированиеПечатныхФормБольничнаяАптека.ПолучитьИменаКолонокТаблицы(ВыборкаСтрокТовары));
		ДанныеТекущейСтроки.Вставить("НоменклатураПредставление");
		ДанныеТекущейСтроки.Вставить("НоменклатураОприходованиеПредставление");
		
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		
		КоличествоСтрок = ВыборкаСтрокТовары.Количество();
		НомерСтроки = 0;
		Пока ВыборкаСтрокТовары.Следующий() Цикл
			
			НомерСтроки = НомерСтроки + 1;
			
			ЗаполнитьЗначенияСвойств(ДанныеТекущейСтроки, ВыборкаСтрокТовары);
			ДанныеТекущейСтроки.НоменклатураПредставление = ОбщегоНазначенияБольничнаяАптека.ПолучитьПредставлениеНоменклатурыДляПечати(
				ВыборкаСтрокТовары.ТоварНаименование,
				ВыборкаСтрокТовары.Серия,
				ВыборкаСтрокТовары.Партия);
			ДанныеТекущейСтроки.НоменклатураОприходованиеПредставление = ОбщегоНазначенияБольничнаяАптека.ПолучитьПредставлениеНоменклатурыДляПечати(
				ВыборкаСтрокТовары.ТоварНаименованиеОприходование,
				ВыборкаСтрокТовары.СерияОприходование,
				ВыборкаСтрокТовары.ПартияОприходование);
			ОбластьСтрока.Параметры.Заполнить(ПараметрыИзШапки);
			ОбластьСтрока.Параметры.Заполнить(ДанныеТекущейСтроки);
			
			МассивВыводимыхОбластей.Очистить();
			МассивВыводимыхОбластей.Добавить(ОбластьСтрока);
			Если НомерСтроки = КоличествоСтрок Тогда
				МассивВыводимыхОбластей.Добавить(ОбластьПодвалТаблицы);
				МассивВыводимыхОбластей.Добавить(ОбластьПодписи);
				МассивВыводимыхОбластей.Добавить(ОбластьСуммаПрописью);
			КонецЕсли;
			
			Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, МассивВыводимыхОбластей) Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
		
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ОбластьПодвалТаблицы);
		
		ПараметрыИзШапки.Вставить("ИтоговаяСтрока", СтрЗаменить(НСтр("ru = 'Всего наименований %ВсегоНаименований%'"), "%ВсегоНаименований%", НомерСтроки));
		ФормированиеПечатныхФормБольничнаяАптека.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "СуммаПрописью", ПараметрыИзШапки);
		
		ФормированиеПечатныхФормБольничнаяАптека.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Подписи", ПараметрыИзШапки);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьПараметрыШапкаНакладная(Шапка)
	
	КлючиПараметров = ФормированиеПечатныхФормБольничнаяАптека.ПолучитьИменаКолонокТаблицы(Шапка);
	
	Параметры = Новый Структура(КлючиПараметров);
	ЗаполнитьЗначенияСвойств(Параметры, Шапка);
	
	// Данные шапки
	НомерДокумента       = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.НомерДокумента);
	ШаблонТекстЗаголовка = НСтр("ru = 'Акт о пересортице товаров № %1 от %2'");
	ТекстЗаголовка       = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонТекстЗаголовка, НомерДокумента, Формат(Шапка.ДатаДокумента, "ДЛФ=DD"));
	
	СведенияОбОрганизации    = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента);
	ОрганизацияПредставление  = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование");
	
	Параметры.Вставить("ТекстЗаголовка"          , ТекстЗаголовка);
	Параметры.Вставить("ОрганизацияПредставление", ОрганизацияПредставление);
	Если ЗначениеЗаполнено(Шапка.Основание) Тогда
		Параметры.Вставить("ОснованиеПредставление", ОбщегоНазначения.ПредметСтрокой(Шапка.Основание));
	КонецЕсли;
	
	Кладовщик = РегистрыСведений.МатериальноОтветственныеЛица.ПолучитьДанныеОтветственного(Шапка.Склад, Шапка.ДатаДокумента);
	Параметры.Вставить("Кладовщик", Кладовщик.ФИО);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти // Печать

////////////////////////////////////////////////////////////////////////////////
// Команды формы
#Область КомандыФормы

// Заполняет список команд ввода на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании, НастройкиФормы) Экспорт
	
	ВводНаОснованииБольничнаяАптека.ДобавитьКомандыСозданияНаОсновании(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыСоздатьНаОсновании, НастройкиФормы);
	
КонецПроцедуры

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, НастройкиФормы) Экспорт
	
	МенюОтчетыБольничнаяАптека.ДобавитьОбщиеКоманды(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыОтчетов, НастройкиФормы);
	
КонецПроцедуры

#КонецОбласти // КомандыФормы

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти // СтандартныеПодсистемы

#КонецЕсли