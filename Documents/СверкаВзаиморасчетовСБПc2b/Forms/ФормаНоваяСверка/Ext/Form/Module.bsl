///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкаПодключенияНачалоВыбора(
		Элемент,
		ДанныеВыбора,
		СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Ссылка", НастройкиСверкиВзаиморасчетов());
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"НастройкаИнтеграцииНачалоВыбораЗавершение",
		ЭтотОбъект);
	
	ОткрытьФорму(
		"Справочник.НастройкиИнтеграцииСПлатежнымиСистемами.ФормаВыбора",
		ПараметрыФормы,
		ЭтотОбъект,
		ЭтотОбъект.УникальныйИдентификатор,
		,
		,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОписаниеОшибкиОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "open:log" Тогда
		СтандартнаяОбработка = Ложь;
		ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройки Тогда
		СформироватьОтчет();
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОшибка
			Или Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершение Тогда
		ЭтотОбъект.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройки;
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериода(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВыборПериодЗавершение",
		ЭтотОбъект);
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода;
	Диалог.Период = Новый СтандартныйПериод(ДатаНачала, ДатаОкончания);
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьОтчет()
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Поле ""Настройка интеграции"" не заполнено'"),
			,
			"НастройкаПодключения",
			,
			Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаНачала)
		Или Не ЗначениеЗаполнено(ДатаОкончания) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Поле ""Период"" не заполнено'"),
			,
			"Период",
			,
			Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация;
	УстановитьВидимостьДоступность();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	РезультатВыполнения = СформироватьОтчетНаСервере(
		НастройкаПодключения,
		ДатаНачала,
		ДатаОкончания,
		ЭтотОбъект.УникальныйИдентификатор);
	
	ИдентификаторЗаданияФормированияQRКода = РезультатВыполнения.ИдентификаторЗадания;
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"СформироватьОтчетЗавершение",
		ЭтотОбъект);
	
	Если РезультатВыполнения.Статус = "Выполнено" Тогда
		СформироватьОтчетЗавершение(РезультатВыполнения, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьОтчетНаСервере(
		НастройкаИнтеграции,
		ДатаНачала,
		ДатаОкончания,
		УникальныйИдентификатор)
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("НастройкаПодключения", НастройкаИнтеграции);
	ПараметрыПроцедуры.Вставить("НачалоПериода", ДатаНачала);
	ПараметрыПроцедуры.Вставить("КонецПериода", ДатаОкончания);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка отчета сверки оборотов'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"СверкаВзаиморасчетовСБПc2b.СверкаВзаиморасчетовПоОборотамИнтерактивныйЗапрос",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			Завершена = Ложь;
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОшибка;
			УстановитьВидимостьДоступность(
				РезультатОперации.СообщениеОбОшибке);
		Иначе
				Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершение;
				УстановитьВидимостьДоступность();
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОшибка;
		УстановитьВидимостьДоступность(
			Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность(ОписаниеОшибки = "")
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация Тогда
		Элементы.КнопкаНазад.Видимость = Ложь;
		Элементы.КнопкаДалее.Видимость = Ложь;
		Элементы.Отмена.Видимость      = Истина;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройки Тогда
		Элементы.КнопкаНазад.Видимость = Ложь;
		Элементы.КнопкаДалее.Видимость  = Истина;
		Элементы.Отмена.Видимость      = Ложь;
		Элементы.КнопкаДалее.Заголовок = НСтр("ru = 'Далее >'");
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершение Тогда
		Элементы.КнопкаНазад.Видимость = Ложь;
		Элементы.КнопкаДалее.Видимость = Истина;
		Элементы.Отмена.Видимость      = Ложь;
		Элементы.КнопкаДалее.Заголовок = НСтр("ru = 'ОК'");
		Элементы.ДекорацияНадписьУспешноеЗавершение.Заголовок = НСтр("ru = 'Отчет сверки оборотов запрошен, ожидается завершение подготовки.'");
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОшибка Тогда
		Элементы.ДекорацияОписаниеОшибки.Заголовок = ОписаниеОшибки;
		Элементы.КнопкаНазад.Видимость = Ложь;
		Элементы.КнопкаДалее.Видимость = Ложь;
		Элементы.Отмена.Видимость      = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкиСверкиВзаиморасчетов()
	
	Возврат ИнтеграцияСПлатежнымиСистемамиСлужебный.НастройкиСверкиВзаиморасчетов();
	
КонецФункции

&НаКлиенте
Процедура НастройкаИнтеграцииНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	НастройкаПодключения = Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериодЗавершение(РезультатВыбора, Контекст) Экспорт 
	
	Если РезультатВыбора = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДатаНачала = НачалоДня(РезультатВыбора.ДатаНачала);
	ДатаОкончания = КонецДня(РезультатВыбора.ДатаОкончания);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВыбранноеЗначение = КонецДня(ВыбранноеЗначение);
	
КонецПроцедуры


#КонецОбласти
