
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДанныеФайла = ПолучитьДанныеПрисоединенногоФайлаТранспортногоКонтейнераНаСервере(ПараметрКоманды,
		ПараметрыВыполненияКоманды.Источник.УникальныйИдентификатор);
	
	Если ДанныеФайла <> Неопределено Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПолучитьФайлПродолжить", ЭтотОбъект, ДанныеФайла);
		
		Шаблон = НСтр("ru='Открыть или сохранить файл?
			|
			|%1'");
		
		ТекстВопроса = СтрШаблон(Шаблон, ДанныеФайла.ИмяФайла);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить("Открыть");
		Кнопки.Добавить("Сохранить");
		Кнопки.Добавить("Отмена");
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки,, "Отмена", НСтр("ru = 'Получить файл'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьДанныеПрисоединенногоФайлаТранспортногоКонтейнераНаСервере(Знач ТранспортныйКонтейнер, Знач УникальныйИдентификатор)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТранспортныйКонтейнерЭДОПрисоединенныеФайлы.Ссылка
	|ИЗ
	|	Справочник.ТранспортныйКонтейнерЭДОПрисоединенныеФайлы КАК ТранспортныйКонтейнерЭДОПрисоединенныеФайлы
	|ГДЕ
	|	ТранспортныйКонтейнерЭДОПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла";
	
	Запрос.УстановитьПараметр("ВладелецФайла", ТранспортныйКонтейнер);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	РезультатЗапроса.Следующий();
	
	Если Не ЗначениеЗаполнено(РезультатЗапроса.Ссылка) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru='Не обнаружен файл транспортного контейнера.'"));
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыДанныхФайла = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ПараметрыДанныхФайла.ИдентификаторФормы = УникальныйИдентификатор;
	ДанныеФайла = РаботаСФайлами.ДанныеФайла(РезультатЗапроса.Ссылка, ПараметрыДанныхФайла);
	
	Возврат ДанныеФайла;
	
КонецФункции

#Область ОбработчикиАсинхронныхДиалогов

&НаКлиенте
Процедура ПолучитьФайлПродолжить(Результат, ДанныеФайла) Экспорт
	
	Если Результат = "Открыть" Тогда
		РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла);
	ИначеЕсли Результат = "Сохранить" Тогда
		РаботаСФайламиКлиент.СохранитьФайлКак(ДанныеФайла);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
