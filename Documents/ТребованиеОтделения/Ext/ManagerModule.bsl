#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Получает непоставленные стоки требования отделения
//
// Возвращаемое значение
//  РезультатЗапроса
//
Функция ПолучитьРезультатЗапросаПоОстаткам(ТребованиеОтделения, Документ = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заказ"   , ТребованиеОтделения);
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЗаказыОстатки.ЗаказНаПеремещение  КАК Заказ,
	|	ЗаказыОстатки.КодСтроки           КАК КодСтроки,
	|	ЗаказыОстатки.ОтборНоменклатуры   КАК НоменклатураЗаказа,
	|	ЗаказыОстатки.ЕдиницаИзмерения    КАК ЕдиницаИзмеренияЗаказа,
	|	ЗаказыОстатки.КОформлениюОстаток  КАК КОформлению
	|ПОМЕСТИТЬ ТаблицаЗаказы
	|ИЗ
	|	РегистрНакопления.ЗаказыНаПеремещение.Остатки(, ЗаказНаПеремещение = &Заказ) КАК ЗаказыОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказыДвижения.ЗаказНаПеремещение КАК Заказ,
	|	ЗаказыДвижения.КодСтроки          КАК КодСтроки,
	|	ЗаказыДвижения.ОтборНоменклатуры  КАК НоменклатураЗаказа,
	|	ЗаказыДвижения.ЕдиницаИзмерения   КАК ЕдиницаИзмеренияЗаказа,
	|	ВЫБОР
	|		КОГДА ЗаказыДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ЗаказыДвижения.КОформлению
	|		ИНАЧЕ ЗаказыДвижения.КОформлению
	|	КОНЕЦ                             КАК КОформлению
	|ИЗ
	|	РегистрНакопления.ЗаказыНаПеремещение КАК ЗаказыДвижения
	|ГДЕ
	|	ЗаказыДвижения.Регистратор = &Документ
	|	И ЗаказыДвижения.ЗаказНаПеремещение = &Заказ
	|	И ЗаказыДвижения.Активность
	|
	|;
	|ВЫБРАТЬ
	|	ТаблицаЗаказы.Заказ                                КАК Заказ,
	|	ТаблицаЗаказы.КодСтроки                            КАК КодСтроки,
	|	ТаблицаЗаказы.НоменклатураЗаказа                   КАК НоменклатураЗаказа,
	|	ТаблицаЗаказы.ЕдиницаИзмеренияЗаказа               КАК ЕдиницаИзмеренияЗаказа,
	|	АналитикаНоменклатурыЗаказа.Номенклатура           КАК Номенклатура,
	|	АналитикаНоменклатурыЗаказа.ТорговоеНаименование   КАК ТорговоеНаименование,
	|	АналитикаНоменклатурыЗаказа.ДействующиеВеществаМНН КАК ДействующиеВеществаМНН,
	|	АналитикаНоменклатурыЗаказа.ФормаВыпуска           КАК ФормаВыпуска,
	|	СУММА(ТаблицаЗаказы.КОформлению)                   КАК КОформлению
	|ИЗ
	|	ТаблицаЗаказы КАК ТаблицаЗаказы
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаОписанийОтборовНоменклатуры КАК АналитикаНоменклатурыЗаказа
	|			ПО
	|				ТаблицаЗаказы.НоменклатураЗаказа = АналитикаНоменклатурыЗаказа.КлючАналитики
	|	
	|СГРУППИРОВАТЬ ПО
	|	Заказ,
	|	КодСтроки,
	|	НоменклатураЗаказа,
	|	ЕдиницаИзмеренияЗаказа,
	|	Номенклатура,
	|	ТорговоеНаименование,
	|	ДействующиеВеществаМНН,
	|	ФормаВыпуска
	|	
	|	ИМЕЮЩИЕ
	|		СУММА(ТаблицаЗаказы.КОформлению) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Заказ,
	|	КодСтроки
	|";
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Запрос.Выполнить();
	
КонецФункции

// Возвращает параметры для расчета состояния выполнения заказов.
// Вызывается из РегистрСведений.СостоянияВнутреннихЗаказов.
//
// Возвращаемое значение:
//  Структура
//
Функция ПараметрыРасчетаСостоянияЗаказов() Экспорт
	
	НеСогласован = Новый Массив;
	НеСогласован.Добавить(Перечисления.СтатусыТребованийОтделений.НеСогласован);
	НеСогласован.Добавить(Перечисления.СтатусыТребованийОтделений.НаСогласовании);
	
	КВыполнению = Новый Массив;
	КВыполнению.Добавить(Перечисления.СтатусыТребованийОтделений.КВыполнению);
	КВыполнению.Добавить(Перечисления.СтатусыТребованийОтделений.Исполнен);
	
	Параметры = Новый Структура;
	Параметры.Вставить("СтатусНеСогласован", НеСогласован);
	Параметры.Вставить("СтатусСогласован"  , Перечисления.СтатусыТребованийОтделений.Согласован);
	Параметры.Вставить("СтатусКВыполнению" , КВыполнению);
	Параметры.Вставить("СтатусЗакрыт"      , Перечисления.СтатусыТребованийОтделений.Закрыт);
	
	Возврат Параметры;
	
КонецФункции

// Определяет необходимость контроля закрытия заказа
//
Функция КонтролироватьВыполнениеЗаказа() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("НеЗакрыватьТребованияОтделенийБезПолнойОтгрузки");
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Работа со статусами документа
#Область Статусы

// Проверяет возможность ручного изменения статуса
//
// Возвращаемое значение
//	Булево - Истина -ручное изменение разрешено
//
Функция РазрешеноРучноеИзменениеСтатуса() Экспорт
	
	Возврат Не ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыТребованийОтделений")
	       Или Пользователи.РолиДоступны("РучноеИзменениеСтатусаТребованийОтделений");
	
КонецФункции

// Формирует запрос проверки при смене статуса списка документов
//
// Параметры:
//  МассивДокументов - Массив - Массив ссылок на документы, которые надо проверять
//  НовыйСтатус - Строка - Имя нового статуса
//  ДополнительныеПараметры - Структура - Структура дополнительных параметров
//
// Возвращаемое значение:
//  Запрос - Запрос проверки перед сменой статуса
//
Функция СформироватьЗапросПроверкиПриСменеСтатуса(МассивДокументов, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	ЗначениеНовогоСтатуса = Перечисления.СтатусыТребованийОтделений[НовыйСтатус];
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Ссылка                КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Ссылка) КАК Представление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Статус) КАК ПредставлениеТекущегоСтатуса,
	|	ПРЕДСТАВЛЕНИЕ(&Статус)                  КАК ПредставлениеНовогоСтатуса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Статус = &Статус
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                   КАК СтатусСовпадает,
	|	ТаблицаДокументов.Проведен              КАК Проведен,
	|	ТаблицаДокументов.ПометкаУдаления       КАК ПометкаУдаления,
	|	ИСТИНА                                  КАК ЗаписьПроведением
	|ПОМЕСТИТЬ втДокументы
	|ИЗ
	|	Документ.ТребованиеОтделения КАК ТаблицаДокументов
	|ГДЕ
	|	ТаблицаДокументов.Ссылка В(&МассивДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	
	Если ДополнительныеПараметры <> Неопределено И ДополнительныеПараметры.Свойство("КонтрольВыполненияЗаказа") Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ЗаказыНаПеремещениеОстатки.ЗаказНаПеремещение КАК ЗаказНаПеремещение
		|ПОМЕСТИТЬ ОстатокПоЗаказу
		|ИЗ
		|	РегистрНакопления.ЗаказыНаПеремещение.Остатки(, ЗаказНаПеремещение В (&МассивДокументов)) КАК ЗаказыНаПеремещениеОстатки
		|ГДЕ
		|	ЗаказыНаПеремещениеОстатки.КОформлениюОстаток > 0
		|	ИЛИ ЗаказыНаПеремещениеОстатки.ЗаказаноОстаток > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	*,
		|	ВЫБОР
		|		КОГДА ОстатокПоЗаказу.ЗаказНаПеремещение ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьОстаткиПоЗаказу
		|ИЗ
		|	втДокументы КАК втДокументы
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ОстатокПоЗаказу КАК ОстатокПоЗаказу
		|	ПО
		|		втДокументы.Ссылка = ОстатокПоЗаказу.ЗаказНаПеремещение
		|";
		
	Иначе
		
		ТекстЗапроса = ТекстЗапроса + "ВЫБРАТЬ * ИЗ втДокументы КАК втДокументы";
		
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Статус"          , ЗначениеНовогоСтатуса);
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Возврат Запрос;
	
КонецФункции

// Возвращает результат проверки при смене статуса документа
//
// Параметры:
// 		ВыборкаПроверки - ВыборкаИзРезультатаЗапроса - Текущая строка выборки
// 		НовыйСтатус - Перечисление - Новый статус
// 		ДополнительныеПараметры - Структура - Структура дополнительных параметров
//
// Возвращаемое значение:
// 		Булево - Истина, в случае успешного завершения проверки
//
Функция ПроверкаПередСменойСтатуса(ВыборкаПроверки, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	Отказ = Ложь;
	
	Если ДополнительныеПараметры = Неопределено Тогда
		Возврат Не Отказ;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("КонтрольВыполненияЗаказа")
	   И ВыборкаПроверки.ЕстьОстаткиПоЗаказу Тогда
		
		ТекстОшибки = НСтр("ru='У документа %Документ% статус ""%Статус%"" не установлен, т.к. требование выполнено не полностью'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ВыборкаПроверки.Ссылка);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Статус%", ВыборкаПроверки.ПредставлениеНовогоСтатуса);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ВыборкаПроверки.Ссылка);
		Отказ = Истина;
		
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

#КонецОбласти // Статусы

////////////////////////////////////////////////////////////////////////////////
// Работа с отменой строк документа
#Область ОтменаСтрок

// Возвращает признак использования причин отмены строк заказа
//
// Возвращаемое значение
//  Булево - Истина, если используются причины отмены
//
Функция ИспользоватьПричиныОтмены() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьПричиныОтменыТребованийОтделений");
	
КонецФункции

// Возвращает полное имя объекта метаданных причин отмены
//
// Возвращаемое значение
//   Строка
//
Функция ИмяОбъектаПричиныОтмены() Экспорт
	
	Возврат Метаданные.Справочники.ПричиныОтменыТребованийОтделений.ПолноеИмя();
	
КонецФункции

#КонецОбласти // ОтменаСтрок

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И (ЗначениеРазрешено(ПодразделениеОрганизации, Отключено КАК Ложь)
	|		ИЛИ ЗначениеРазрешено(Отделение, Отключено КАК Ложь))
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Отделение, Отключено КАК Ложь)
	|";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Проведение
#Область Проведение

// Инициализирует таблицы значений, содержащие данные для проведения документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицыДвиженийДляПроведения(ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	ОсновныеДанныеДокумента = ПодготовитьОсновныеДанныеДляПроведения(ДополнительныеСвойства);
	
	ИнициализироватьКлючиАналитикиОписанийОтборовНоменклатуры(ОсновныеДанныеДокумента);
	
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтТаблицаТовары());
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтАналитика());
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаЗаказыНаПеремещение(), Метаданные.РегистрыНакопления.ЗаказыНаПеремещение);
	
	Запрос = Новый Запрос(ПроведениеБольничнаяАптека.ПолучитьТекстЗапросаДвижений(ДополнительныеСвойства, Регистры));
	
	Для Каждого ДанныеДокумента Из ОсновныеДанныеДокумента Цикл
		Запрос.УстановитьПараметр(ДанныеДокумента.Ключ, ДанныеДокумента.Значение);
	КонецЦикла;
	
	ПроведениеБольничнаяАптека.ЗаполнитьТаблицыДвижений(ДополнительныеСвойства, Запрос.ВыполнитьПакет(), Регистры);
	
КонецПроцедуры

Функция ПодготовитьОсновныеДанныеДляПроведения(ДополнительныеСвойства)
	
	ЗапрашиваемыеДанные = Новый Структура;
	ЗапрашиваемыеДанные.Вставить("Ссылка");
	ЗапрашиваемыеДанные.Вставить("Период", "Дата");
	ЗапрашиваемыеДанные.Вставить("Организация");
	ЗапрашиваемыеДанные.Вставить("Статус");
	ЗапрашиваемыеДанные.Вставить("Отделение");
	ЗапрашиваемыеДанные.Вставить("ПодразделениеОрганизации");
	
	ОсновныеДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ПроведениеБольничнаяАптека.ПолучитьСсылкуНаДокументДляПроведения(ДополнительныеСвойства),
		ЗапрашиваемыеДанные);
	
	ОсновныеДанныеДокумента.Вставить("ВестиУчетПоИсточникамФинансирования", ПолучитьФункциональнуюОпцию("ИспользоватьИсточникиФинансирования"));
	
	Возврат ОсновныеДанныеДокумента;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаТовары()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                   КАК НомерСтроки,
	|	ТаблицаТовары.КодСтроки                     КАК КодСтроки,
	|	&Организация                                КАК Организация,
	|	&ПодразделениеОрганизации                   КАК ПодразделениеОрганизации,
	|	ТаблицаТовары.ДействующиеВеществаМНН        КАК ДействующиеВеществаМНН,
	|	ТаблицаТовары.ТорговоеНаименование          КАК ТорговоеНаименование,
	|	ТаблицаТовары.ФормаВыпуска                  КАК ФормаВыпуска,
	|	ТаблицаТовары.ЕдиницаИзмерения              КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.Номенклатура                  КАК Номенклатура,
	|	ТаблицаТовары.КоличествоВЕдиницахИзмерения  КАК Количество,
	|	ТаблицаТовары.Отменено                      КАК Отменено,
	|	ТаблицаТовары.ПричинаОтмены                 КАК ПричинаОтмены
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	Документ.ТребованиеОтделения.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки       КАК НомерСтроки,
	|	ТаблицаТовары.КодСтроки         КАК КодСтроки,
	|	&Период                         КАК Период,
	|	&Ссылка                         КАК ТребованиеОтделения,
	|	АналитикаОтборов.КлючАналитики  КАК АналитикаОписанияОтбораНоменклатуры,
	|	ТаблицаТовары.ЕдиницаИзмерения  КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.Количество        КАК Количество,
	|	ТаблицаТовары.Отменено          КАК Отменено,
	|	ТаблицаТовары.ПричинаОтмены     КАК ПричинаОтмены
	|ПОМЕСТИТЬ ВтАналитика
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаОписанийОтборовНоменклатуры КАК АналитикаОтборов
	|		ПО
	|			ТаблицаТовары.ДействующиеВеществаМНН = АналитикаОтборов.ДействующиеВеществаМНН
	|			И ТаблицаТовары.ТорговоеНаименование = АналитикаОтборов.ТорговоеНаименование
	|			И ТаблицаТовары.ФормаВыпуска         = АналитикаОтборов.ФормаВыпуска
	|			И ТаблицаТовары.Номенклатура         = АналитикаОтборов.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЗаказыНаПеремещение()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	1                                                  КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                          КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)             КАК ВидДвижения,
	|	ТаблицаТовары.Период                               КАК Период,
	|	ТаблицаТовары.КодСтроки                            КАК КодСтроки,
	|	ТаблицаТовары.ТребованиеОтделения                  КАК ЗаказНаПеремещение,
	|	ТаблицаТовары.АналитикаОписанияОтбораНоменклатуры  КАК ОтборНоменклатуры,
	|	ТаблицаТовары.ЕдиницаИзмерения                     КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.Количество                           КАК Заказано,
	|	ВЫБОР
	|		КОГДА (НЕ ТаблицаТовары.Отменено)
	|			И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыТребованийОтделений.КВыполнению),
	|				ЗНАЧЕНИЕ(Перечисление.СтатусыТребованийОтделений.Исполнен),
	|				ЗНАЧЕНИЕ(Перечисление.СтатусыТребованийОтделений.Закрыт))
	|			ТОГДА ТаблицаТовары.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ                                              КАК КОформлению,
	|	НЕОПРЕДЕЛЕНО                                       КАК ПричинаОтмены
	|ИЗ
	|	ВтАналитика КАК ТаблицаТовары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                                  КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                          КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)             КАК ВидДвижения,
	|	ТаблицаТовары.Период                               КАК Период,
	|	ТаблицаТовары.КодСтроки                            КАК КодСтроки,
	|	ТаблицаТовары.ТребованиеОтделения                  КАК ЗаказНаПеремещение,
	|	ТаблицаТовары.АналитикаОписанияОтбораНоменклатуры  КАК ОтборНоменклатуры,
	|	ТаблицаТовары.ЕдиницаИзмерения                     КАК ЕдиницаИзмерения,
	|	-ТаблицаТовары.Количество                          КАК Заказано,
	|	0                                                  КАК КОформлению,
	|	ТаблицаТовары.ПричинаОтмены                        КАК ПричинаОтмены
	|ИЗ
	|	ВтАналитика КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Отменено
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ИнициализироватьКлючиАналитикиОписанийОтборовНоменклатуры(Реквизиты)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.ДействующиеВеществаМНН КАК ДействующиеВеществаМНН,
	|	ТаблицаТовары.ТорговоеНаименование КАК ТорговоеНаименование,
	|	ТаблицаТовары.ФормаВыпуска КАК ФормаВыпуска,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ втТаблицаАналитики
	|ИЗ
	|	Документ.ТребованиеОтделения.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	ТорговоеНаименование,
	|	ДействующиеВеществаМНН,
	|	ФормаВыпуска
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.Выполнить();
	
	Справочники.КлючиАналитикиОписанийОтборовНоменклатуры.ИнициализироватьКлючиАналитики(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

#КонецОбласти // Проведение

////////////////////////////////////////////////////////////////////////////////
// Электронная подпись
#Область ЭлектроннаяПодпись

// Возвращает описание ключевых полей для подписи.
//
// Возвращаемое значение:
//  Структура - описание ключевых полей см. ЭлектроннаяПодписьБольничнаяАптека.ПустоеОписаниеКлючевыхПолей().
//
Функция ОписаниеКлючевыхПолей(Версия = Неопределено) Экспорт
	
	МетаданныеОбъекта = ПустаяСсылка().Метаданные();
	
	ОписаниеКлючевыхПолей = ЭлектроннаяПодписьБольничнаяАптека.ПустоеОписаниеКлючевыхПолей();
	
	ПоляШапки = МетаданныеОбъекта.Реквизиты;
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьОписаниеРеквизита(ОписаниеКлючевыхПолей, ПоляШапки.Организация.Имя);
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьОписаниеРеквизита(ОписаниеКлючевыхПолей, ПоляШапки.Отделение.Имя);
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьОписаниеРеквизита(ОписаниеКлючевыхПолей, ПоляШапки.СкладПолучатель.Имя);
	
	ЭлектроннаяПодписьБольничнаяАптека.ВключитьФайлыВОписаниеКлючевыхПолей(ОписаниеКлючевыхПолей);
	
	// Товары
	ИмяТаблицы = МетаданныеОбъекта.ТабличныеЧасти.Товары.Имя;
	ТаблицаТовары = ЭлектроннаяПодписьБольничнаяАптека.ДобавитьПустоеОписаниеТабличнойЧасти(ОписаниеКлючевыхПолей, ИмяТаблицы);
	
	ПоляТовары = МетаданныеОбъекта.ТабличныеЧасти.Товары.Реквизиты;
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьГруппируемоеПолеТабличнойЧасти(ТаблицаТовары, ПоляТовары.Номенклатура.Имя);
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьГруппируемоеПолеТабличнойЧасти(ТаблицаТовары, ПоляТовары.ТорговоеНаименование.Имя);
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьГруппируемоеПолеТабличнойЧасти(ТаблицаТовары, ПоляТовары.ДействующиеВеществаМНН.Имя);
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьГруппируемоеПолеТабличнойЧасти(ТаблицаТовары, ПоляТовары.ФормаВыпуска.Имя);
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьГруппируемоеПолеТабличнойЧасти(ТаблицаТовары, ПоляТовары.ЕдиницаИзмерения.Имя);
	
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьПолеРесурсаТабличнойЧасти(ТаблицаТовары, ПоляТовары.КоличествоВЕдиницахИзмерения.Имя);
	
	// Пациенты
	ИмяТаблицы =  МетаданныеОбъекта.ТабличныеЧасти.Пациенты.Имя;
	ТаблицаПациентов = ЭлектроннаяПодписьБольничнаяАптека.ДобавитьПустоеОписаниеТабличнойЧасти(ОписаниеКлючевыхПолей, ИмяТаблицы);
	
	ПоляПациенты = МетаданныеОбъекта.ТабличныеЧасти.Пациенты.Реквизиты;
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьГруппируемоеПолеТабличнойЧасти(ТаблицаПациентов, ПоляПациенты.Пациент.Имя);
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьГруппируемоеПолеТабличнойЧасти(ТаблицаПациентов, ПоляПациенты.КлючСвязиПациентов.Имя);
	
	ЭлектроннаяПодписьБольничнаяАптека.ДобавитьПолеРесурсаТабличнойЧасти(ТаблицаПациентов, ПоляПациенты.Количество.Имя);
	
	Возврат ОписаниеКлючевыхПолей;
	
КонецФункции

#КонецОбласти // ЭлектроннаяПодпись

////////////////////////////////////////////////////////////////////////////////
// Печать
#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	УправлениеПечатьюБольничнаяАптека.ДобавитьКомандыПечати(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыПечати);
	
КонецПроцедуры

// Возвращает список доступных печатных форм документа
//
Функция ДоступныеПечатныеФормы() Экспорт
	
	ПечатныеФормы = УправлениеПечатьюБольничнаяАптека.СоздатьКоллекциюДоступныхПечатныхФорм();
	
	Обработки.ПечатьЗаказНаПеремещение.ДобавитьПечатнуюФорму(ПечатныеФормы);
	
	// ТребованиеНакладная0504204
	ПечатнаяФорма = Обработки.ПечатьТребованиеНакладная0504204.ДобавитьПечатнуюФорму(ПечатныеФормы);
	ПечатнаяФорма.ДополнительныеПараметры.ВыводитьПациентов = Истина;
	УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	
	КомандаПечати = УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	КомандаПечати.Представление = КомандаПечати.Представление + " " + НСтр("ru = 'на лат.'");
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзДействующегоВещества = Истина;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзТорговогоНаименования = Ложь;
	
	КомандаПечати = УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	КомандаПечати.Представление = КомандаПечати.Представление + " " + НСтр("ru = 'на англ.'");
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзДействующегоВещества = Ложь;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзТорговогоНаименования = Истина;
	
	КомандаПечати = УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	КомандаПечати.Представление = КомандаПечати.Представление + " " + НСтр("ru = 'с исполнением'");
	КомандаПечати.ДополнительныеПараметры.ВыводитьИсполнение = Истина;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзДействующегоВещества = Истина;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзТорговогоНаименования = Ложь;
	
	// ТребованиеНакладная0510451
	ПечатнаяФорма = Обработки.ПечатьТребованиеНакладная0510451.ДобавитьПечатнуюФорму(ПечатныеФормы);
	ПечатнаяФорма.ДополнительныеПараметры.ВыводитьПациентов = Истина;
	УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	
	КомандаПечати = УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	КомандаПечати.Представление = КомандаПечати.Представление + " " + НСтр("ru = 'на лат.'");
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзДействующегоВещества = Истина;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзТорговогоНаименования = Ложь;
	
	КомандаПечати = УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	КомандаПечати.Представление = КомандаПечати.Представление + " " + НСтр("ru = 'на англ.'");
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзДействующегоВещества = Ложь;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзТорговогоНаименования = Истина;
	
	КомандаПечати = УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	КомандаПечати.Представление = КомандаПечати.Представление + " " + НСтр("ru = 'с исполнением'");
	КомандаПечати.ДополнительныеПараметры.ВыводитьИсполнение = Истина;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзДействующегоВещества = Истина;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзТорговогоНаименования = Ложь;
	
	// М11
	ПечатнаяФорма = Обработки.ПечатьМ11.ДобавитьПечатнуюФорму(ПечатныеФормы);
	ПечатнаяФорма.ДополнительныеПараметры.ВыводитьПациентов = Истина;
	УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	
	КомандаПечати = УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	КомандаПечати.Представление = КомандаПечати.Представление + " " + НСтр("ru = 'на лат.'");
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзДействующегоВещества = Истина;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзТорговогоНаименования = Ложь;
	
	КомандаПечати = УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	КомандаПечати.Представление = КомандаПечати.Представление + " " + НСтр("ru = 'на англ.'");
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзДействующегоВещества = Ложь;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзТорговогоНаименования = Истина;
	
	КомандаПечати = УправлениеПечатьюБольничнаяАптека.ДобавитьКомандуПечати(ПечатнаяФорма);
	КомандаПечати.Представление = КомандаПечати.Представление + " " + НСтр("ru = 'с исполнением'");
	КомандаПечати.ДополнительныеПараметры.ВыводитьИсполнение = Истина;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзДействующегоВещества = Истина;
	КомандаПечати.ДополнительныеПараметры.ПредставлениеИзТорговогоНаименования = Ложь;
	
	Возврат ПечатныеФормы;
	
КонецФункции

Функция ПолучитьДанныеДляПечати(МассивОбъектов, ПараметрыПечати = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныеДляПечати();
	Запрос.УстановитьПараметр("ТекущийДокумент"                    , МассивОбъектов);
	Запрос.УстановитьПараметр("ИспользоватьИсточникиФинансирования", ПолучитьФункциональнуюОпцию("ИспользоватьИсточникиФинансирования"));
	ЗапасыСервер.УстановитьСтатусыПараметровУчетаВПараметрахЗапроса(Запрос);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке"          , РезультатыЗапросов[РезультатыЗапросов.ВГраница() - 1]);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти" , РезультатыЗапросов[РезультатыЗапросов.ВГраница()]);
	
	ВыводитьИсполнение = Ложь;
	Если ПараметрыПечати <> Неопределено И ПараметрыПечати.Свойство("ВыводитьИсполнение", ВыводитьИсполнение) И ВыводитьИсполнение Тогда
		
		ПолучатьЦены = Ложь;
		ПолучатьЦеныПоНастройкамСклада = Ложь;
		
		Если ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда
			ПолучатьЦеныПоНастройкамСклада = ПараметрыПечати.Свойство("ПолучатьЦеныПоНастройкамСклада");
			ПолучатьЦены = ПолучатьЦеныПоНастройкамСклада Или ПараметрыПечати.Свойство("ПолучатьЦены");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапросаИсполнения(ПолучатьЦены, ПолучатьЦеныПоНастройкамСклада);
		Запрос.УстановитьПараметр("ПолучатьЦеныПоНастройкамСклада", ПолучатьЦеныПоНастройкамСклада);
		РезультатыЗапросов = Запрос.ВыполнитьПакет();
		
		ДанныеДляПечати.Вставить("ТаблицаИсполнения", РезультатыЗапросов[РезультатыЗапросов.ВГраница()].Выгрузить());
		
	КонецЕсли;
	
	Возврат ДанныеДляПечати;
	
КонецФункции

Функция ТекстЗапросаДанныеДляПечати()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Документ.Ссылка                                                    КАК Ссылка,
	|	Документ.Номер                                                     КАК НомерДокумента,
	|	Документ.Дата                                                      КАК ДатаДокумента,
	|	Документ.Организация                                               КАК Организация,
	|	Документ.Организация.КодПоОКПО                                     КАК КодОКПО,
	|	Документ.Отделение                                                 КАК Отделение,
	|	Документ.Отделение                                                 КАК Получатель,
	|	Документ.Отделение.Наименование                                    КАК ПолучательПредставление,
	|	Документ.Отделение.Представление                                   КАК ОтделениеПредставление,
	|	Документ.Отделение.ТекущийРуководитель.Наименование                КАК ЗаведующийОтделением,
	|	Документ.Отделение.ТекущийРуководитель.Наименование                КАК Затребовал,
	|	Документ.Отделение.ТекущаяДолжностьРуководителя                    КАК ДолжностьЗаведующегоОтделением,
	|	Документ.Отделение.ТекущаяДолжностьРуководителя                    КАК ЗатребовалДолжность,
	|	Документ.Отделение.ТекущаяСтаршаяМедСестра.Наименование            КАК СтаршаяМедСестра,
	|	Документ.ПодразделениеОрганизации                                  КАК ПодразделениеОрганизации,
	|	Документ.ПодразделениеОрганизации                                  КАК Отправитель,
	|	Документ.ПодразделениеОрганизации.Наименование                     КАК ОтправительПредставление,
	|	Документ.ПодразделениеОрганизации.Представление                    КАК ПодразделениеПредставление,
	|	Документ.ПодразделениеОрганизации.ТекущийРуководитель.Наименование КАК ЗаведующийАптекой,
	|	Документ.ПодразделениеОрганизации.ТекущаяДолжностьРуководителя     КАК ДолжностьЗаведующегоАптекой,
	|	Документ.ИсточникФинансирования                                    КАК ИсточникФинансирования,
	|	Документ.СкладПолучатель                                           КАК СкладПолучатель,
	|	Документ.СкладОтправитель                                          КАК СкладОтправитель,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)                   КАК Разрешил,
	|	""""                                                               КАК РазрешилДолжность,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)                          КАК ВидЦены,
	|	Документ.Ответственный                                             КАК Менеджер,
	|	Документ.Пациенты.(
	|		НомерСтроки        КАК НомерСтроки,
	|		КлючСвязиПациентов КАК КлючСвязиПациентов,
	|		Пациент            КАК Пациент,
	|		Количество         КАК Затребовано
	|	)
	|ИЗ
	|	Документ.ТребованиеОтделения КАК Документ
	|ГДЕ
	|	Документ.Ссылка В(&ТекущийДокумент)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Документ.Пациенты.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка                                        КАК Документ,
	|	Товары.ДействующиеВеществаМНН                        КАК ДействующиеВеществаМНН,
	|	Товары.ТорговоеНаименование                          КАК ТорговоеНаименование,
	|	Товары.ФормаВыпуска                                  КАК ФормаВыпуска,
	|	Товары.Номенклатура                                  КАК Номенклатура,
	|	КлючиАналитики.КлючАналитики                         КАК ОтборНоменклатуры,
	|	Товары.Номенклатура.НаименованиеПолное               КАК ТоварНаименование,
	|	""""                                                 КАК ТоварКод,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)  КАК СерияНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК Партия,
	|	Товары.КодСтроки                                     КАК КодСтроки,
	|	Товары.ЕдиницаИзмерения                              КАК ЕдиницаИзмерения,
	|	Товары.ЕдиницаИзмерения.КодОКЕИ                      КАК КодПоОКЕИ,
	|	Товары.ЕдиницаИзмерения.ТипЕдиницы                   КАК ТипЕдиницы,
	|	Товары.ЕдиницаИзмерения.НаименованиеПолное           КАК ЕдиницаИзмеренияНаименование,
	|	Товары.КоличествоВЕдиницахИзмерения                  КАК Затребовано,
	|	0                                                    КАК Отпущено,
	|	0                                                    КАК Сумма,
	|	Товары.КлючСвязиПациентов                            КАК КлючСвязиПациентов,
	|	ЛОЖЬ                                                 КАК Отменено
	|ИЗ
	|	Документ.ТребованиеОтделения.Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаОписанийОтборовНоменклатуры КАК КлючиАналитики
	|		ПО
	|			Товары.Номенклатура = КлючиАналитики.Номенклатура
	|			И Товары.ТорговоеНаименование = КлючиАналитики.ТорговоеНаименование
	|			И Товары.ДействующиеВеществаМНН = КлючиАналитики.ДействующиеВеществаМНН
	|			И Товары.ФормаВыпуска = КлючиАналитики.ФормаВыпуска
	|ГДЕ
	|	Товары.Ссылка В(&ТекущийДокумент)
	|	И НЕ Товары.Отменено
	|УПОРЯДОЧИТЬ ПО
	|	Документ,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Документ
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаИсполнения(ПолучатьЦены = Ложь, ПолучатьЦеныПоНастройкамСклада = Ложь)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Документ.Ссылка                                  КАК Ссылка,
	|	Документ.ТребованиеОтделения                     КАК ДокументТребование,
	|	Документ.Дата                                    КАК ДатаДокумента,
	|	Документ.Организация                             КАК Организация,
	|	Документ.СкладОтправитель                        КАК Склад,
	|	ВЫБОР
	|		КОГДА &ПолучатьЦеныПоНастройкамСклада
	|			ТОГДА Документ.СкладОтправитель.УчетныйВидЦены
	|		ИНАЧЕ Документ.ВидЦены
	|	КОНЕЦ                                            КАК ВидЦены,
	|	ВЫБОР
	|		КОГДА &ПолучатьЦеныПоНастройкамСклада
	|			ТОГДА Документ.СкладОтправитель.ИсточникИнформацииОЦенахДляПечати
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|	КОНЕЦ                                            КАК ИсточникИнформацииОЦенах,
	|	РасчетСебестоимостиТоваров.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ПОМЕСТИТЬ втШапка
	|ИЗ
	|	Документ.ОтпускТоваровВОтделение КАК Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Документ.РасчетСебестоимостиТоваров КАК РасчетСебестоимостиТоваров
	|		ПО
	|			(&ПолучатьЦеныПоНастройкамСклада)
	|			И (РасчетСебестоимостиТоваров.Дата МЕЖДУ НАЧАЛОПЕРИОДА(Документ.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(Документ.Дата, МЕСЯЦ))
	|			И (РасчетСебестоимостиТоваров.Проведен)
	|			И Документ.Организация = РасчетСебестоимостиТоваров.Организация
	|ГДЕ
	|	Документ.ТребованиеОтделения В(&ТекущийДокумент)
	|	И Документ.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка                                                           КАК Документ,
	|	Шапка.ДокументТребование                                                КАК ДокументТребование,
	|	ВЫБОР
	|		КОГДА Товары.КодСтроки <> 0
	|			ТОГДА Товары.НоменклатураЗаказа
	|		ИНАЧЕ КлючиАналитики.КлючАналитики
	|	КОНЕЦ                                                                   КАК ОтборНоменклатуры,
	|	Товары.ЕдиницаИзмеренияЗаказа                                           КАК ЕдиницаИзмерения,
	|	Товары.КодСтроки                                                        КАК КодСтроки,
	|	МАКСИМУМ(Товары.НомерСтроки)                                            КАК НомерСтроки,
	|	КОНЕЦПЕРИОДА(Шапка.ДатаДокумента, ДЕНЬ)                                 КАК ДатаПолученияЦены,
	|	Шапка.Организация                                                       КАК Организация,
	|	Шапка.Склад                                                             КАК Склад,
	|	Шапка.ВидЦены                                                           КАК ВидЦены,
	|	Шапка.ИсточникИнформацииОЦенах                                          КАК ИсточникИнформацииОЦенах,
	|	Шапка.ПредварительныйРасчет                                             КАК ПредварительныйРасчет,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
	|	ВЫБОР
	|		КОГДА &ИспользоватьИсточникиФинансирования
	|			ТОГДА Товары.ИсточникФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПустаяСсылка)
	|	КОНЕЦ                                                                   КАК ИсточникФинансирования,
	|	Товары.СерияНоменклатуры                                                КАК СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА Товары.СтатусУказанияСерийОтправитель В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА Товары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                                   КАК СерияНоменклатурыДляСебестоимости,
	|	Товары.Партия                                                           КАК Партия,
	|	ВЫБОР
	|		КОГДА Товары.СтатусУказанияПартийОтправитель В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА Товары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                                   КАК ПартияДляСебестоимости,
	|	Товары.Номенклатура                                                     КАК Номенклатура,
	|	Товары.Номенклатура.Наименование                                        КАК ТоварНаименование,
	|	Товары.Номенклатура.Код                                                 КАК ТоварКод,
	|	Товары.ЕдиницаИзмерения                                                 КАК Упаковка,
	|	Товары.ЕдиницаИзмерения.КодОКЕИ                                         КАК КодПоОКЕИ,
	|	Товары.ЕдиницаИзмерения.ТипЕдиницы                                      КАК ТипЕдиницы,
	|	Товары.ЕдиницаИзмерения.Наименование                                    КАК ЕдиницаИзмеренияНаименование,
	|	Товары.Коэффициент                                                      КАК Коэффициент,
	|	СУММА(Товары.КоличествоВЕдиницахИзмерения)                              КАК Количество,
	|	СУММА(Товары.КоличествоВЕдиницахЗаказа)                                 КАК КоличествоЗаказа,
	|	СУММА(Товары.КоличествоВЕдиницахИзмерения)                              КАК Отпущено
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.ОтпускТоваровВОтделение.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			втШапка КАК Шапка
	|		ПО
	|			Товары.Ссылка = Шапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаОписанийОтборовНоменклатуры КАК КлючиАналитики
	|		ПО
	|			Товары.Номенклатура = КлючиАналитики.Номенклатура
	|			И КлючиАналитики.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			И КлючиАналитики.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			И КлючиАналитики.ФормаВыпуска = ЗНАЧЕНИЕ(Справочник.ФормыВыпуска.ПустаяСсылка)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка,
	|	Шапка.ДокументТребование,
	|	ВЫБОР
	|		КОГДА Товары.КодСтроки <> 0
	|			ТОГДА Товары.НоменклатураЗаказа
	|		ИНАЧЕ КлючиАналитики.КлючАналитики
	|	КОНЕЦ,
	|	Товары.ЕдиницаИзмеренияЗаказа,
	|	Товары.КодСтроки,
	|	КОНЕЦПЕРИОДА(Шапка.ДатаДокумента, ДЕНЬ),
	|	Шапка.Организация,
	|	Шапка.Склад,
	|	Шапка.ВидЦены,
	|	Шапка.ИсточникИнформацииОЦенах,
	|	Шапка.ПредварительныйРасчет,
	|	ВЫБОР
	|		КОГДА &ИспользоватьИсточникиФинансирования
	|			ТОГДА Товары.ИсточникФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПустаяСсылка)
	|	КОНЕЦ,
	|	Товары.СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА Товары.СтатусУказанияСерийОтправитель В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА Товары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	Товары.Партия,
	|	ВЫБОР
	|		КОГДА Товары.СтатусУказанияПартийОтправитель В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА Товары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	Товары.Номенклатура,
	|	Товары.ЕдиницаИзмерения,
	|	Товары.Коэффициент
	|";
	
	ПараметрыПолученияЦен = ФормированиеПечатныхФормБольничнаяАптека.ПараметрыПолученияЦен();
	
	Если ПолучатьЦены Тогда
		
		ПараметрыПолученияЦен.ИспользоватьЦеныПоВидуЦен = Истина;
		Если ПолучатьЦеныПоНастройкамСклада Тогда
			ПараметрыПолученияЦен.ИспользоватьЦеныПоСебестоимости = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстЗапроса = ФормированиеПечатныхФормБольничнаяАптека.ТекстЗапросаСЦенами(ТекстЗапроса, ПараметрыПолученияЦен);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти // Печать

////////////////////////////////////////////////////////////////////////////////
// Формирование отчетов
#Область ФормированиеОтчетов

// Возвращает данные для заполнения отчета Состояние выполнения документов.
//
// Параметры:
//  ВходящиеДокументы - СписокЗначений - ссылки документов Требование отделения,
//                                       для которых получается состояние выполнения.
//
// Возвращаемое значение:
//  ДанныеОтчета - Структура - структурированные данные для заполнения отчета.
//
Функция ПолучитьДанныеОтчетаСостоянияВыполненияДокументов(ВходящиеДокументы) Экспорт
	
	Запрос = Новый Запрос(ТекстЗапросаСостоянияВыполненияДокументов());
	Запрос.УстановитьПараметр("ЗаказНаПеремещение", ВходящиеДокументы);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ДанныеОтчета = Новый Структура;
	ДанныеОтчета.Вставить("ЗаказаноБезСумм"     , РезультатыЗапросов[4].Выгрузить());
	ДанныеОтчета.Вставить("ВПроцессеОтгрузки"   , РезультатыЗапросов[5].Выгрузить());
	ДанныеОтчета.Вставить("ВПроцессеПоступления", РезультатыЗапросов[6].Выгрузить());
	ДанныеОтчета.Вставить("ПоступилоБезСумм"    , РезультатыЗапросов[7].Выгрузить());
	ДанныеОтчета.Вставить("ОтмененоБезСумм"     , РезультатыЗапросов[8].Выгрузить());
	
	Возврат ДанныеОтчета;
	
КонецФункции

Функция ТекстЗапросаСостоянияВыполненияДокументов()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документы.Ссылка КАК Документ
	|ПОМЕСТИТЬ Документы
	|ИЗ
	|	Документ.ТребованиеОтделения КАК Документы
	|ГДЕ
	|	Документы.Ссылка В (&ЗаказНаПеремещение)
	|	И Документы.Проведен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОборотыЗаказовНаПеремещение.Регистратор         КАК Регистратор,
	|	ОборотыЗаказовНаПеремещение.ЗаказНаПеремещение  КАК ЗаказНаПеремещение,
	|	ОборотыЗаказовНаПеремещение.ОтборНоменклатуры   КАК ОтборНоменклатуры,
	|	ОборотыЗаказовНаПеремещение.ЕдиницаИзмерения    КАК ЕдиницаИзмерения,
	|	ОборотыЗаказовНаПеремещение.КодСтроки           КАК КодСтроки,
	|	ВЫБОР
	|		КОГДА ОборотыЗаказовНаПеремещение.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ОборотыЗаказовНаПеремещение.Заказано
	|		ИНАЧЕ 0
	|	КОНЕЦ                                           КАК Заказано,
	|	ВЫБОР
	|		КОГДА ОборотыЗаказовНаПеремещение.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ОборотыЗаказовНаПеремещение.КОформлению
	|		ИНАЧЕ 0
	|	КОНЕЦ                                           КАК КОтгрузке,
	|	ВЫБОР
	|		КОГДА ОборотыЗаказовНаПеремещение.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ОборотыЗаказовНаПеремещение.КОформлению
	|		ИНАЧЕ 0
	|	КОНЕЦ                                           КАК Отгружено,
	|	ВЫБОР
	|		КОГДА ОборотыЗаказовНаПеремещение.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ОборотыЗаказовНаПеремещение.КОформлению
	|		ИНАЧЕ 0
	|	КОНЕЦ                                           КАК Принято,
	|	ОборотыЗаказовНаПеремещение.ПричинаОтмены       КАК ПричинаОтмены
	|ПОМЕСТИТЬ ОборотыЗаказовНаПеремещение
	|ИЗ
	|	РегистрНакопления.ЗаказыНаПеремещение КАК ОборотыЗаказовНаПеремещение
	|ГДЕ
	|	ОборотыЗаказовНаПеремещение.ЗаказНаПеремещение В (ВЫБРАТЬ Документ ИЗ Документы)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОборотыЗаказовНаПеремещение.ЗаказНаПеремещение  КАК ЗаказНаПеремещение,
	|	ОборотыЗаказовНаПеремещение.ОтборНоменклатуры   КАК ОтборНоменклатуры,
	|	ОборотыЗаказовНаПеремещение.ЕдиницаИзмерения    КАК ЕдиницаИзмерения,
	|	ОборотыЗаказовНаПеремещение.Заказано            КАК Заказано,
	|	ОборотыЗаказовНаПеремещение.КОтгрузке           КАК КОтгрузке,
	|	ОборотыЗаказовНаПеремещение.КОтгрузке           КАК ОсталосьОтгрузить,
	|	0                                               КАК Отгружено,
	|	0                                               КАК КПоступлению,
	|	0                                               КАК ОсталосьПринять,
	|	0                                               КАК Принято
	|ПОМЕСТИТЬ ЗаказыНаПеремещение
	|ИЗ
	|	ОборотыЗаказовНаПеремещение КАК ОборотыЗаказовНаПеремещение
	|ГДЕ
	|	ОборотыЗаказовНаПеремещение.Регистратор = ОборотыЗаказовНаПеремещение.ЗаказНаПеремещение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОборотыЗаказовНаПеремещение.ЗаказНаПеремещение  КАК ЗаказНаПеремещение,
	|	ОборотыЗаказовНаПеремещение.ОтборНоменклатуры   КАК ОтборНоменклатуры,
	|	ОборотыЗаказовНаПеремещение.ЕдиницаИзмерения    КАК ЕдиницаИзмерения,
	|	0                                               КАК Заказано,
	|	0                                               КАК КОтгрузке,
	|	-ОборотыЗаказовНаПеремещение.Отгружено          КАК ОсталосьОтгрузить,
	|	ОборотыЗаказовНаПеремещение.Отгружено           КАК Отгружено,
	|	ОборотыЗаказовНаПеремещение.Отгружено           КАК КПоступлению,
	|	ОборотыЗаказовНаПеремещение.Отгружено           КАК ОсталосьПринять,
	|	0                                               КАК Принято
	|ИЗ
	|	ОборотыЗаказовНаПеремещение КАК ОборотыЗаказовНаПеремещение
	|ГДЕ
	|	ОборотыЗаказовНаПеремещение.Регистратор <> ОборотыЗаказовНаПеремещение.ЗаказНаПеремещение
	|	И ОборотыЗаказовНаПеремещение.Регистратор.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.КОтгрузке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОборотыЗаказовНаПеремещение.ЗаказНаПеремещение  КАК ЗаказНаПеремещение,
	|	ОборотыЗаказовНаПеремещение.ОтборНоменклатуры   КАК ОтборНоменклатуры,
	|	ОборотыЗаказовНаПеремещение.ЕдиницаИзмерения    КАК ЕдиницаИзмерения,
	|	0                                               КАК Заказано,
	|	0                                               КАК КОтгрузке,
	|	0                                               КАК ОсталосьОтгрузить,
	|	0                                               КАК Отгружено,
	|	0                                               КАК КПоступлению,
	|	-ОборотыЗаказовНаПеремещение.Принято            КАК ОсталосьПринять,
	|	ОборотыЗаказовНаПеремещение.Принято             КАК Принято
	|ИЗ
	|	ОборотыЗаказовНаПеремещение КАК ОборотыЗаказовНаПеремещение
	|ГДЕ
	|	ОборотыЗаказовНаПеремещение.Регистратор <> ОборотыЗаказовНаПеремещение.ЗаказНаПеремещение
	|	И ОборотыЗаказовНаПеремещение.Регистратор.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.Принято)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СводныеДанныеПоЗаказам.ЗаказНаПеремещение        КАК ЗаказНаПеремещение,
	|	СводныеДанныеПоЗаказам.ОтборНоменклатуры         КАК ОтборНоменклатуры,
	|	СводныеДанныеПоЗаказам.ЕдиницаИзмерения          КАК ЕдиницаИзмерения,
	|	СУММА(СводныеДанныеПоЗаказам.Заказано)           КАК Заказано,
	|	СУММА(СводныеДанныеПоЗаказам.КОтгрузке)          КАК КОтгрузке,
	|	СУММА(СводныеДанныеПоЗаказам.ОсталосьОтгрузить)  КАК ОсталосьОтгрузить,
	|	СУММА(СводныеДанныеПоЗаказам.Отгружено)          КАК Отгружено,
	|	СУММА(СводныеДанныеПоЗаказам.КПоступлению)       КАК КПоступлению,
	|	СУММА(СводныеДанныеПоЗаказам.ОсталосьПринять)    КАК ОсталосьПринять,
	|	СУММА(СводныеДанныеПоЗаказам.Принято)            КАК Принято
	|ПОМЕСТИТЬ СводныеДанныеПоЗаказам
	|ИЗ
	|	ЗаказыНаПеремещение КАК СводныеДанныеПоЗаказам
	|
	|СГРУППИРОВАТЬ ПО
	|	СводныеДанныеПоЗаказам.ЗаказНаПеремещение,
	|	СводныеДанныеПоЗаказам.ОтборНоменклатуры,
	|	СводныеДанныеПоЗаказам.ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	
	// Заказано
	
	|ВЫБРАТЬ
	|	СводныеДанныеПоЗаказам.ЗаказНаПеремещение  КАК Документ,
	|	СводныеДанныеПоЗаказам.ОтборНоменклатуры   КАК Номенклатура,
	|	СводныеДанныеПоЗаказам.ЕдиницаИзмерения    КАК ЕдиницаИзмерения,
	|	СводныеДанныеПоЗаказам.Заказано            КАК Заказано
	|ИЗ
	|	СводныеДанныеПоЗаказам КАК СводныеДанныеПоЗаказам
	|ГДЕ
	|	СводныеДанныеПоЗаказам.Заказано > 0
	|	И СводныеДанныеПоЗаказам.Отгружено = 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ,
	|	Номенклатура,
	|	ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	
	// В процессе отгрузки
	
	|ВЫБРАТЬ
	|	СводныеДанныеПоЗаказам.ЗаказНаПеремещение  КАК Документ,
	|	СводныеДанныеПоЗаказам.ОтборНоменклатуры   КАК Номенклатура,
	|	СводныеДанныеПоЗаказам.ЕдиницаИзмерения    КАК ЕдиницаИзмерения,
	|	СводныеДанныеПоЗаказам.КОтгрузке           КАК КОтгрузке,
	|	СводныеДанныеПоЗаказам.ОсталосьОтгрузить   КАК ОсталосьОтгрузить,
	|	СводныеДанныеПоЗаказам.Отгружено           КАК Отгружено
	|ИЗ
	|	СводныеДанныеПоЗаказам КАК СводныеДанныеПоЗаказам
	|ГДЕ
	|	СводныеДанныеПоЗаказам.ОсталосьОтгрузить > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ,
	|	Номенклатура,
	|	ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	
	// В процессе поступления
	
	|ВЫБРАТЬ
	|	СводныеДанныеПоЗаказам.ЗаказНаПеремещение  КАК Документ,
	|	СводныеДанныеПоЗаказам.ОтборНоменклатуры   КАК Номенклатура,
	|	СводныеДанныеПоЗаказам.ЕдиницаИзмерения    КАК ЕдиницаИзмерения,
	|	СводныеДанныеПоЗаказам.КПоступлению        КАК КПоступлению,
	|	СводныеДанныеПоЗаказам.ОсталосьПринять     КАК ОсталосьПринять,
	|	СводныеДанныеПоЗаказам.Принято             КАК Принято
	|ИЗ
	|	СводныеДанныеПоЗаказам КАК СводныеДанныеПоЗаказам
	|ГДЕ
	|	СводныеДанныеПоЗаказам.ОсталосьПринять > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ,
	|	Номенклатура,
	|	ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	
	// Поступило полностью
	
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтпускаемыеТовары.Ссылка.ТребованиеОтделения                КАК Документ,
	|	ОтпускаемыеТовары.Номенклатура                              КАК Номенклатура,
	|	ОтпускаемыеТовары.Номенклатура.ОсновнаяЕдиницаУчета         КАК ЕдиницаИзмерения,
	|	СУММА(ОтпускаемыеТовары.Количество / Упаковки.Коэффициент)  КАК Принято
	|ИЗ
	|	Документ.ОтпускТоваровВОтделение.Товары КАК ОтпускаемыеТовары
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ЕдиницыИзмеренияНоменклатуры КАК Упаковки
	|		ПО
	|			ОтпускаемыеТовары.Номенклатура = Упаковки.Номенклатура
	|			И ОтпускаемыеТовары.Номенклатура.ОсновнаяЕдиницаУчета = Упаковки.ЕдиницаИзмерения
	|ГДЕ
	|	ОтпускаемыеТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.Принято)
	|	И ОтпускаемыеТовары.Ссылка В (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ОборотыЗаказовНаПеремещение.Регистратор КАК Регистратор
	|		ИЗ
	|			ОборотыЗаказовНаПеремещение КАК ОборотыЗаказовНаПеремещение)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтпускаемыеТовары.Ссылка,
	|	ОтпускаемыеТовары.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ,
	|	Номенклатура,
	|	ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	
	// Отменено
	
	|ВЫБРАТЬ
	|	ОтмененныеЗаказы.ЗаказНаПеремещение  КАК Документ,
	|	ОтмененныеЗаказы.ОтборНоменклатуры   КАК Номенклатура,
	|	ОтмененныеЗаказы.ЕдиницаИзмерения    КАК ЕдиницаИзмерения,
	|	ОтмененныеЗаказы.КодСтроки           КАК КодСтроки,
	|	ОтмененныеЗаказы.ПричинаОтмены       КАК ПричинаОтмены,
	|	-ОтмененныеЗаказы.Заказано           КАК Отменено
	|ИЗ
	|	ОборотыЗаказовНаПеремещение КАК ОтмененныеЗаказы
	|ГДЕ
	|	ОтмененныеЗаказы.Заказано < 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ,
	|	Номенклатура,
	|	ЕдиницаИзмерения
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти // ФормированиеОтчетов

////////////////////////////////////////////////////////////////////////////////
// Команды формы
#Область КомандыФормы

// Заполняет список команд ввода на основании.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании, НастройкиФормы) Экспорт
	
	ВводНаОснованииБольничнаяАптека.ДобавитьКомандыСозданияНаОсновании(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыСоздатьНаОсновании, НастройкиФормы);
	
	МетаданныеДокумента = ПустаяСсылка().Метаданные();
	Команда = БизнесПроцессы.ОбработкаВнутреннегоДокумента.ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании, НастройкиФормы);
	Команда.ФункциональныеОпции = Метаданные.ФункциональныеОпции.ИспользоватьБизнесПроцессыТребованийОтделений.Имя;
	Команда.Представление = НСтр("ru = 'Согласовать и исполнить'");
	Команда.Порядок = "10";
	Команда.ПараметрыФормы.ЗакрытьФормуВладельцаПоИмени = МетаданныеДокумента.Формы.ФормаДокумента.ПолноеИмя();
	
	Команда = БизнесПроцессы.Согласование.ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании, НастройкиФормы);
	Команда.ФункциональныеОпции = Метаданные.ФункциональныеОпции.ИспользоватьБизнесПроцессыТребованийОтделений.Имя;
	Команда.Порядок = "20";
	Команда.ПараметрыФормы.ЗакрытьФормуВладельцаПоИмени = МетаданныеДокумента.Формы.ФормаДокумента.ПолноеИмя();
	
	Команда = БизнесПроцессы.Исполнение.ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании, НастройкиФормы);
	Команда.ФункциональныеОпции = Метаданные.ФункциональныеОпции.ИспользоватьБизнесПроцессыТребованийОтделений.Имя;
	Команда.Порядок = "30";
	Команда.ПараметрыФормы.ЗакрытьФормуВладельцаПоИмени = МетаданныеДокумента.Формы.ФормаДокумента.ПолноеИмя();
	
КонецПроцедуры

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, НастройкиФормы) Экспорт
	
	МенюОтчетыБольничнаяАптека.ДобавитьОбщиеКоманды(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыОтчетов, НастройкиФормы);
	
	Отчеты.СостояниеВыполненияДокументов.ДобавитьКомандуОтчета(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти // КомандыФормы

////////////////////////////////////////////////////////////////////////////////
// Бизнес-процессы и задачи
#Область БизнесПроцессыИЗадачи

// Обработчик события изменения статуса предмета при прохождении бизнес-процесса
//
Процедура ПриИзмененииСтатусаПредмета(Предмет, НовыйСтатус, ДополнительныеПараметры, СтандартнаяОбработка) Экспорт
	
	Если НовыйСтатус = "Закрыт" Тогда
		Если ОбщегоНазначенияБольничнаяАптека.ИспользоватьСтатусы(Предмет) Тогда
			Если ДополнительныеПараметры = Неопределено Тогда
				ДополнительныеПараметры = Новый Структура("КонтрольВыполненияЗаказа");
			КонецЕсли;
		Иначе
			СтандартнаяОбработка = Ложь;
			Если Не Документы.ТребованиеОтделения.ПолучитьРезультатЗапросаПоОстаткам(Предмет).Пустой() Тогда
				
				Если ПолучитьФункциональнуюОпцию("ИспользоватьПричиныОтменыТребованийОтделений") Тогда
					ПричинаОтмены = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ОтменаНеотработанныхСтрок");
				Иначе
					ПричинаОтмены = Справочники.ПричиныОтменыТребованийОтделений.ПустаяСсылка();
				КонецЕсли;
				
				Если ПричинаОтмены = Неопределено Тогда
					ВызватьИсключение НСтр("ru = 'Не указана причина отмены непоставленных строк.'");
				КонецЕсли;
				
				Требование = Предмет.ПолучитьОбъект();
				ЗаблокироватьДанныеДляРедактирования(Требование.Ссылка);
				Требование.ОтменитьНепоставленныеСтроки(ПричинаОтмены, Истина);
				Если Требование.ПроверитьЗаполнение() Тогда
					Требование.Записать(РежимЗаписиДокумента.Проведение);
				Иначе
					ВызватьИсключение СтрЗаменить(НСтр("ru = 'При закрытии %Требование% возникли ошибки.'"), "%Требование%", Предмет);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // БизнесПроцессыИЗадачи

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

#Область ТекущиеДела

// Заполняет список текущих дел пользователя.
// Описание параметров процедуры см. в ТекущиеДелаСлужебный.НоваяТаблицаТекущихДел()
//
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	ОбщиеПараметрыЗапросов = ТекущиеДелаСервер.ОбщиеПараметрыЗапросов();
	
	Доступность =
		ПолучитьФункциональнуюОпцию("ВестиУчетВОтделениях")
		И (ОбщиеПараметрыЗапросов.ЭтоПолноправныйПользователь
			Или ПравоДоступа("Редактирование", ПустаяСсылка().Метаданные()));
	
	Если Не Доступность Тогда
		Возврат;
	КонецЕсли;
	
	// Расчет показателей
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументЗаказ.Ссылка) КАК ЗаказыВсегоВРаботе,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|		КОГДА СостоянияВнутреннихЗаказов.ДатаСобытия <> ДАТАВРЕМЯ(1, 1, 1)
	|				И &ДатаАктуальности >= СостоянияВнутреннихЗаказов.ДатаСобытия ТОГДА
	|			СостоянияВнутреннихЗаказов.Заказ
	|		КОНЕЦ) КАК ЗаказыНаСегодня,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|		КОГДА СостоянияВнутреннихЗаказов.ДатаСобытия <> ДАТАВРЕМЯ(1, 1, 1)
	|				И &ДатаАктуальности > СостоянияВнутреннихЗаказов.ДатаСобытия
	|				И СостоянияВнутреннихЗаказов.Состояние В (
	|					ЗНАЧЕНИЕ(Перечисление.СостоянияВнутреннихЗаказов.ОжидаетсяСогласование),
	|					ЗНАЧЕНИЕ(Перечисление.СостоянияВнутреннихЗаказов.ГотовКОтгрузке),
	|					ЗНАЧЕНИЕ(Перечисление.СостоянияВнутреннихЗаказов.ВПроцессеОтгрузки)) ТОГДА
	|			СостоянияВнутреннихЗаказов.Заказ
	|		КОНЕЦ) КАК ЗаказыПросроченоИсполнение
	|ИЗ
	|	Документ.ТребованиеОтделения КАК ДокументЗаказ
	|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияВнутреннихЗаказов КАК СостоянияВнутреннихЗаказов
	|		ПО (СостоянияВнутреннихЗаказов.Заказ = ДокументЗаказ.Ссылка)}
	|ГДЕ
	|	(НЕ (СостоянияВнутреннихЗаказов.Заказ ЕСТЬ NULL) ИЛИ НЕ ДокументЗаказ.Проведен)
	|	И ДокументЗаказ.Ответственный = &Пользователь
	|	И ЕСТЬNULL(СостоянияВнутреннихЗаказов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияВнутреннихЗаказов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СостоянияВнутреннихЗаказов.Закрыт)
	|	И (НЕ ДокументЗаказ.ПометкаУдаления)
	|";
	
	
	ИмяФормы = Метаданные.Документы.ТребованиеОтделения.Формы.ФормаСписка.ПолноеИмя();
	
	Результат = ТекущиеДелаСервер.ЧисловыеПоказателиТекущихДел(Запрос, ОбщиеПараметрыЗапросов);
	
	// Заполнение дел.
	// ЗаказыПоставщикам
	ДелоРодитель = ТекущиеДела.Добавить();
	ДелоРодитель.Идентификатор  = "ТребованияОтделений";
	ДелоРодитель.Представление  = НСтр("ru = 'Требования отделений'");
	ДелоРодитель.Владелец       = Метаданные.Подсистемы.УчетВОтделениях;
	
	// ЗаказыПоставщикамВсегоВРаботе
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Состояние"    , "ВсеОткрытые");
	ПараметрыОтбора.Вставить("Актуальность" , "");
	ПараметрыОтбора.Вставить("ДатаСобытия"  , ОбщиеПараметрыЗапросов.ПустаяДата);
	ПараметрыОтбора.Вставить("Ответственный", ОбщиеПараметрыЗапросов.Пользователь);
	ПараметрыОтбора.Вставить("Приоритет"    , Неопределено);
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ТребованияОтделенийВсегоВРаботе";
	Дело.ЕстьДела       = Результат.ЗаказыВсегоВРаботе > 0;
	Дело.Представление  = НСтр("ru = 'Всего заказов в работе'");
	Дело.Количество     = Результат.ЗаказыВсегоВРаботе;
	Дело.Важное         = Ложь;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = Новый Структура("СтруктураБыстрогоОтбора", ПараметрыОтбора);
	Дело.Владелец       = "ТребованияОтделений";
	
	// ЗаказыПоставщикамНаСегодня
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Состояние"    , "ВсеОткрытые");
	ПараметрыОтбора.Вставить("Актуальность" , "Сегодня");
	ПараметрыОтбора.Вставить("ДатаСобытия"  , ОбщиеПараметрыЗапросов.ПустаяДата);
	ПараметрыОтбора.Вставить("Ответственный", ОбщиеПараметрыЗапросов.Пользователь);
	ПараметрыОтбора.Вставить("Приоритет"    , Неопределено);
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ТребованияОтделенийНаСегодня";
	Дело.ЕстьДела       = Результат.ЗаказыНаСегодня > 0;
	Дело.Представление  = НСтр("ru = 'На сегодня'");
	Дело.Количество     = Результат.ЗаказыНаСегодня;
	Дело.Важное         = Ложь;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = Новый Структура("СтруктураБыстрогоОтбора", ПараметрыОтбора);
	Дело.Владелец       = "ТребованияОтделений";
	
	// ЗаказыПоставщикамПросроченоИсполнение
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Состояние"    , "ВсеОжидающиеИсполнения");
	ПараметрыОтбора.Вставить("Актуальность" , "Просрочен");
	ПараметрыОтбора.Вставить("ДатаСобытия"  , ОбщиеПараметрыЗапросов.ПустаяДата);
	ПараметрыОтбора.Вставить("Ответственный", ОбщиеПараметрыЗапросов.Пользователь);
	ПараметрыОтбора.Вставить("Приоритет"    , Неопределено);
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ТребованияОтделенийПросроченоИсполнение";
	Дело.ЕстьДела       = Результат.ЗаказыПросроченоИсполнение > 0;
	Дело.Представление  = НСтр("ru = 'Просрочено исполнение заказов'");
	Дело.Количество     = Результат.ЗаказыПросроченоИсполнение;
	Дело.Важное         = Истина;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = Новый Структура("СтруктураБыстрогоОтбора", ПараметрыОтбора);
	Дело.Владелец       = "ТребованияОтделений";
	
	Если Результат.ЗаказыВсегоВРаботе > 0 Тогда
		ДелоРодитель.ЕстьДела = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ТекущиеДела

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти // СтандартныеПодсистемы

#КонецЕсли