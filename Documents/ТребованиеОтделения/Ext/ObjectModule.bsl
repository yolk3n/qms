#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Отменяет все строки, по которым не было документально оформлено поступление
//
Функция ОтменитьНепоставленныеСтроки(ПричинаОтмены, Знач ПроверятьОстатки = Ложь) Экспорт
	
	КоличествоОтмененныхСтрок = 0;
	
	Если ПроверятьОстатки Тогда
		
		Выборка = Документы.ТребованиеОтделения.ПолучитьРезультатЗапросаПоОстаткам(Ссылка).Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			// Найдем отменяемую строку с нужным кодом строки
			
			Отбор = Новый Структура;
			Отбор.Вставить("КодСтроки", Выборка.КодСтроки);
			Отбор.Вставить("Номенклатура", Выборка.Номенклатура);
			Отбор.Вставить("ТорговоеНаименование", Выборка.ТорговоеНаименование);
			Отбор.Вставить("ДействующиеВеществаМНН", Выборка.ДействующиеВеществаМНН);
			Отбор.Вставить("ФормаВыпуска", Выборка.ФормаВыпуска);
			Отбор.Вставить("ЕдиницаИзмерения", Выборка.ЕдиницаИзмеренияЗаказа);
			Отбор.Вставить("Отменено", Ложь);
			
			НайденныеСтроки = Товары.НайтиСтроки(Отбор);
			КОформлениюОстаток = Выборка.КОформлению;
			
			Для Каждого ТекСтрока Из НайденныеСтроки Цикл
				
				Если КОформлениюОстаток = 0 Тогда
					Прервать;
				КонецЕсли;
				
				Если ТекСтрока.КоличествоВЕдиницахИзмерения <= КОформлениюОстаток Тогда
					
					ТекСтрока.Отменено    = Истина;
					ТекСтрока.ПричинаОтмены = ПричинаОтмены;
					
					КОформлениюОстаток = КОформлениюОстаток - ТекСтрока.КоличествоВЕдиницахИзмерения;
					
				Иначе
					
					// Если поступила только часть строки, разобьем строку на две
					НоваяСтрока = Товары.Добавить();
					
					КопируемыеПоля = "Номенклатура, ТорговоеНаименование, ДействующиеВеществаМНН, ФормаВыпуска, ЕдиницаИзмерения, ДатаПоступления";
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока, КопируемыеПоля);
					НоваяСтрока.Отменено      = Истина;
					НоваяСтрока.ПричинаОтмены = ПричинаОтмены;
					НоваяСтрока.КоличествоВЕдиницахИзмерения = КОформлениюОстаток;
					
					ТекКоличество = ТекСтрока.КоличествоВЕдиницахИзмерения;
					ТекСтрока.КоличествоВЕдиницахИзмерения = ТекКоличество - КОформлениюОстаток;
					
					КОформлениюОстаток = 0;
					
				КонецЕсли;
				
				КоличествоОтмененныхСтрок = КоличествоОтмененныхСтрок + 1;
				
			КонецЦикла;
			
		КонецЦикла;
		
	Иначе
		
		Для Каждого ТекСтрока Из Товары Цикл
			
			Если Не ТекСтрока.Отменено Тогда
				ТекСтрока.Отменено = Истина;
				ТекСтрока.ПричинаОтмены = ПричинаОтмены;
				КоличествоОтмененныхСтрок = КоличествоОтмененныхСтрок + 1;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат КоличествоОтмененныхСтрок;
	
КонецФункции

// Устанавливает статус для объекта документа
//
// Параметры:
// 		НовыйСтатус - Строка - Имя статуса, который будет установлен у заказов
// 		ДополнительныеПараметры - Структура - Структура дополнительных параметров установки статуса
//
// Возвращаемое значение:
// 		Булево - Истина, в случае успешной установки нового статуса
//
Функция УстановитьСтатус(НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	ЗначениеНовогоСтатуса = Перечисления.СтатусыТребованийОтделений[НовыйСтатус];
	
	Если ЗначениеНовогоСтатуса = Перечисления.СтатусыТребованийОтделений.НеСогласован Тогда
		
		Если Согласован Тогда
			Согласован = Ложь;
		КонецЕсли;
		
	ИначеЕсли ЗначениеНовогоСтатуса = Перечисления.СтатусыТребованийОтделений.КВыполнению
	      ИЛИ ЗначениеНовогоСтатуса = Перечисления.СтатусыТребованийОтделений.Исполнен
	      ИЛИ ЗначениеНовогоСтатуса = Перечисления.СтатусыТребованийОтделений.Закрыт Тогда
		
		ЗаполнитьДатыПоступления();
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		ПричинаОтмены = Неопределено;
		Если ДополнительныеПараметры.Свойство("ОтменаНеотработанныхСтрок", ПричинаОтмены) Тогда
			
			ПроверятьОстатки = Статус = Перечисления.СтатусыТребованийОтделений.КВыполнению
			               Или Статус = Перечисления.СтатусыТребованийОтделений.Исполнен
			               Или Статус = Перечисления.СтатусыТребованийОтделений.Закрыт;
			КоличествоСтрок = ОтменитьНепоставленныеСтроки(ПричинаОтмены, ПроверятьОстатки);
			
		КонецЕсли;
	КонецЕсли;
	
	Статус = ЗначениеНовогоСтатуса;
	
	Возврат ПроверитьЗаполнение();
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
	ЗаполнитьПоЗначениямАвтозаполнения();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Согласован = Ложь;
	Статус = Метаданные().Реквизиты.Статус.ЗначениеЗаполнения;
	ЖелаемаяДатаПоступления = Дата(1,1,1);
	ДатаПоступления = Дата(1,1,1);
	ДатаСогласования = Дата(1,1,1);
	МаксимальныйКодСтроки = 0;
	
	ПодписанЭП = Ложь;
	ЭлектронныеПодписи.Очистить();
	
	Для Каждого ТекущаяСтрока Из Товары Цикл
		
		ТекущаяСтрока.КодСтроки = 0;
		ТекущаяСтрока.ДатаПоступления = Дата(1,1,1);
		ТекущаяСтрока.Отменено = Ложь;
		ТекущаяСтрока.ПричинаОтмены = Справочники.ПричиныОтменыТребованийОтделений.ПустаяСсылка();
		
	КонецЦикла;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	// Срок согласования заказа должен быть не меньше даты документа
	Если Статус = Перечисления.СтатусыЗаказовПоставщикам.НеСогласован
	   И ЗначениеЗаполнено(ДатаСогласования) И ДатаСогласования < НачалоДня(Дата) Тогда
		
		ТекстОшибки = НСтр("ru='Дата согласования должна быть не меньше даты документа %Дата%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Дата%", Формат(Дата, "ДЛФ=DD"));
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			"ДатаСогласования",
			,
			Отказ);
		
	КонецЕсли;
	
	// Дата поступления в шапке должна быть не меньше даты документа
	Если ЗначениеЗаполнено(ЖелаемаяДатаПоступления) И ЖелаемаяДатаПоступления < НачалоДня(Дата) Тогда
		
		ТекстОшибки = НСтр("ru='Желаемая дата поступления должна быть не меньше даты документа %Дата%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Дата%", Формат(Дата,"ДЛФ=DD"));
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			"ЖелаемаяДатаПоступления",
			,
			Отказ);
		
	КонецЕсли;
	
	ВсеСтрокиОтменены = (Товары.НайтиСтроки(Новый Структура("Отменено", Ложь)).Количество() = 0);
	
	Если Не ПоступлениеОднойДатой
	 Или ПоступлениеОднойДатой
	   И Не(Статус = Перечисления.СтатусыТребованийОтделений.КВыполнению
	    Или Статус = Перечисления.СтатусыТребованийОтделений.Исполнен
	    Или Статус = Перечисления.СтатусыТребованийОтделений.Закрыт)
	 Или ПоступлениеОднойДатой И ВсеСтрокиОтменены Тогда
		НепроверяемыеРеквизиты.Добавить("ДатаПоступления");
	КонецЕсли;
	
	// Дата поступления в шапке должна быть не меньше даты документа
	Если ПоступлениеОднойДатой
	   И ЗначениеЗаполнено(ДатаПоступления)
	   И ДатаПоступления < НачалоДня(Дата)
	   И Не ВсеСтрокиОтменены Тогда
		
		ТекстОшибки = НСтр("ru='Дата поступления должна быть не меньше даты документа %Дата%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Дата%", Формат(Дата,"ДЛФ=DD"));
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			"ДатаПоступления",
			,
			Отказ);
		
	КонецЕсли;
	
	НепроверяемыеРеквизиты.Добавить("Товары.ПричинаОтмены");
	НепроверяемыеРеквизиты.Добавить("Товары.ДатаПоступления");
	
	АдресОшибки = " " + НСтр("ru='в строке %1 списка ""%ИмяТабличнойЧасти%""'");
	АдресОшибки = СтрЗаменить(АдресОшибки, "%ИмяТабличнойЧасти%", Метаданные().ТабличныеЧасти.Товары.Синоним);
	
	Для Каждого СтрокаТребования Из Товары Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТребования.ДействующиеВеществаМНН)
		   И Не ЗначениеЗаполнено(СтрокаТребования.ТорговоеНаименование)
		   И Не ЗначениеЗаполнено(СтрокаТребования.Номенклатура) Тогда
			
			ТекстОшибки =
				НСтр("ru='Не заполнена колонка ""Действующие вещества (МНН)"" или ""Торговое наименование"" или ""Номенклатура""'")
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(АдресОшибки, СтрокаТребования.НомерСтроки);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект, 
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТребования.НомерСтроки, "ДействующиеВеществаМНН"),
				,
				Отказ);
			
		КонецЕсли;
		
		Если (ЗначениеЗаполнено(СтрокаТребования.ДействующиеВеществаМНН) Или ЗначениеЗаполнено(СтрокаТребования.ТорговоеНаименование))
		   И Не ЗначениеЗаполнено(СтрокаТребования.ФормаВыпуска) Тогда
			
			ТекстОшибки =
				НСтр("ru='Не заполнена колонка ""Форма выпуска""'")
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(АдресОшибки, СтрокаТребования.НомерСтроки);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект, 
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТребования.НомерСтроки, "ФормаВыпуска"),
				,
				Отказ);
			
		КонецЕсли;
		
		// Дата поступления обязательна к заполнению во всех статусах кроме НеСогласован.
		Если Не ПоступлениеОднойДатой
		   И (    Статус = Перечисления.СтатусыТребованийОтделений.КВыполнению
		      Или Статус = Перечисления.СтатусыТребованийОтделений.Исполнен
		      Или Статус = Перечисления.СтатусыТребованийОтделений.Закрыт)
		   И Не СтрокаТребования.Отменено
		   И Не ЗначениеЗаполнено(СтрокаТребования.ДатаПоступления) Тогда
			
			ТекстОшибки =
				НСтр("ru='Не заполнена колонка ""Дата поступления""'")
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(АдресОшибки, СтрокаТребования.НомерСтроки);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТребования.НомерСтроки, "ДатаПоступления"),
				,
				Отказ);
			
		КонецЕсли;
		
		// Дата поступления должна быть не меньше даты документа
		Если ЗначениеЗаполнено(СтрокаТребования.ДатаПоступления) И СтрокаТребования.ДатаПоступления < НачалоДня(Дата) Тогда
			
			ТекстОшибки =
				НСтр("ru='Дата поступления должна быть не меньше даты документа %Дата%'")
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(АдресОшибки, СтрокаТребования.НомерСтроки);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Дата%", Формат(Дата,"ДЛФ=DD"));
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТребования.НомерСтроки, "ДатаПоступления"),
				,
				Отказ);
			
		КонецЕсли;
		
		// Причина отмены обязательна для заполнения в строках с признаком Отменено
		Если СтрокаТребования.Отменено
		   И ПолучитьФункциональнуюОпцию("ИспользоватьПричиныОтменыТребованийОтделений")
		   И Не ЗначениеЗаполнено(СтрокаТребования.ПричинаОтмены) Тогда
			
			ТекстОшибки =
				НСтр("ru='Необходимо указать причину отмены'")
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(АдресОшибки, СтрокаТребования.НомерСтроки);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТребования.НомерСтроки, "ПричинаОтмены"),
				,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеБольничнаяАптека.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ЗаказыСервер.УстановитьКлючВСтрокахТабличнойЧасти(ЭтотОбъект, "Товары");
	
	НоваяДатаПоступления = Дата(1,1,1);
	Если Товары.Количество() > 0 Тогда
		
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыТребованийОтделений")
		 Или Статус = Перечисления.СтатусыТребованийОтделений.Согласован
		 Или Статус = Перечисления.СтатусыТребованийОтделений.КВыполнению Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Отменено", Ложь);
			ПодтвержденныеСтроки = Товары.НайтиСтроки(ПараметрыОтбора);
			
			Если ПодтвержденныеСтроки.Количество() > 0 Тогда
				
				ТаблицаПодтвержденныхСтрок = Товары.Выгрузить(ПодтвержденныеСтроки, "ДатаПоступления");
				ТаблицаПодтвержденныхСтрок.Сортировать("ДатаПоступления Возр");
				НоваяДатаПоступления = ТаблицаПодтвержденныхСтрок[0].ДатаПоступления;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДатаПервогоПоступления = НоваяДатаПоступления;
	
	МассивСтатусовНеСогласован = Новый Массив();
	МассивСтатусовНеСогласован.Добавить(Перечисления.СтатусыТребованийОтделений.НеСогласован);
	МассивСтатусовНеСогласован.Добавить(Перечисления.СтатусыТребованийОтделений.НаСогласовании);
	
	ДокументБылСогласован = Согласован;
	
	ОбщегоНазначенияБольничнаяАптека.ИзменитьПризнакСогласованностиДокумента(
		ЭтотОбъект,
		РежимЗаписи,
		МассивСтатусовНеСогласован);
	
	// Установим дату согласования, если документ согласован
	Если Не ДокументБылСогласован И Согласован Тогда
		ДатаСогласования = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ОчиститьВДокументеНеиспользуемыхПациентов(Товары, Пациенты);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеБольничнаяАптека.СформироватьДвиженияПоРегистрам(ЭтотОбъект, Отказ, РежимПроведения);
	
	РегистрыСведений.СостоянияВнутреннихЗаказов.ОтразитьСостояниеЗаказа(Ссылка);
	
	ЗаказыСервер.ВыполнитьКонтрольЗаказаПослеПроведения(Ссылка, Отказ);
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеБольничнаяАптека.СформироватьДвиженияПоРегистрам(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Инициализация и заполнение документа
#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент()
	
	Автор = Пользователи.ТекущийПользователь();
	Ответственный = Пользователи.ТекущийПользователь();
	
	Если ОбщегоНазначенияБольничнаяАптека.ИспользоватьСтатусы(Ссылка) Тогда
		Статус = Перечисления.СтатусыТребованийОтделений.НеСогласован;
	КонецЕсли;
	
	ПоступлениеОднойДатой = Истина;
	
	Приоритет = Справочники.Приоритеты.ПолучитьПриоритетПоУмолчанию(Приоритет);
	Организация = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	ПодразделениеОрганизации = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьПодразделениеПоУмолчанию(ПодразделениеОрганизации, Организация, Истина);
	Отделение = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОтделениеПоУмолчанию(Отделение, Организация);
	СкладПолучатель = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьСкладОтделенияПоУмолчанию(СкладПолучатель, Отделение);
	СкладОтправитель = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьСкладАптекиПоУмолчанию(СкладОтправитель, ПодразделениеОрганизации);
	
КонецПроцедуры

Процедура ЗаполнитьПоЗначениямАвтозаполнения()
	
	ОбщегоНазначенияБольничнаяАптека.ЗаполнитьПоЗначениямАвтозаполнения(ЭтотОбъект, Неопределено, "Организация, СкладПолучатель, СкладОтправитель");
	ОбщегоНазначенияБольничнаяАптека.ЗаполнитьПоЗначениямАвтозаполнения(ЭтотОбъект, Неопределено, "ПодразделениеОрганизации, Отделение", "Организация");
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоОтбору(ДанныеЗаполнения)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	
	ОбщегоНазначенияБольничнаяАптека.ПроверитьЗаполнениеПодразделенияОрганизации(ЭтотОбъект, "Отделение");
	ОбщегоНазначенияБольничнаяАптека.ПроверитьЗаполнениеПодразделенияОрганизации(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // ИнициализацияИЗаполнение

////////////////////////////////////////////////////////////////////////////////
// Прочее
#Область Прочее

Функция СписокРегистровДляКонтроля() Экспорт
	
	РегистрыДляКонтроля = Новый Массив;
	РегистрыДляКонтроля.Добавить(Движения.ЗаказыНаПеремещение);
	
	Возврат РегистрыДляКонтроля;
	
КонецФункции

// Очищает строки из таблицы пациентов с ключами связи, отсутствующими в таблице товаров
//
Процедура ОчиститьВДокументеНеиспользуемыхПациентов(ТЧТовары, ТЧПациенты)
	
	СоответствиеКлючей = Новый Соответствие;
	Количество = ТЧПациенты.Количество();
	Для Итератор = 1 По Количество Цикл
		
		ТекущаяСтрока = ТЧПациенты[Количество - Итератор];
		НомерНеИспользуется = СоответствиеКлючей[ТекущаяСтрока.КлючСвязиПациентов];
		Если НомерНеИспользуется = Неопределено Тогда
			
			НомерНеИспользуется = (ТЧТовары.Найти(ТекущаяСтрока.КлючСвязиПациентов, "КлючСвязиПациентов") = Неопределено);
			
			СоответствиеКлючей.Вставить(ТекущаяСтрока.КлючСвязиПациентов, НомерНеИспользуется);
			
		КонецЕсли;
		
		Если НомерНеИспользуется Тогда
			ТЧПациенты.Удалить(ТекущаяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДатыПоступления()
	
	Если Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьПустыеДатыПоступления() Тогда
		Если ПоступлениеОднойДатой Тогда
			ДатаПоступления = ОпределитьЖелаемуюДатуПоступления();
			ЗаполнитьДатыПоступленияВТаблице(ДатаПоступления);
		Иначе
			ЗаполнитьПустыеДатыПоступленияВТаблице(ОпределитьЖелаемуюДатуПоступления());
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьПустыеДатыПоступления()
	
	Для Каждого ТекущаяСтрока Из Товары Цикл
		Если Не ТекущаяСтрока.Отменено
		   И Не ЗначениеЗаполнено(ТекущаяСтрока.ДатаПоступления) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ОпределитьЖелаемуюДатуПоступления()
	
	Если ЗначениеЗаполнено(ЖелаемаяДатаПоступления) И ЖелаемаяДатаПоступления >= НачалоДня(Дата) Тогда
		ДатаПоступления = ЖелаемаяДатаПоступления;
	Иначе
		ДатаПоступления = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Возврат ДатаПоступления;
	
КонецФункции

Процедура ЗаполнитьДатыПоступленияВТаблице(ДатаПоступления)
	
	Для Каждого ТекущаяСтрока Из Товары Цикл
		Если Не ТекущаяСтрока.Отменено Тогда
			ТекущаяСтрока.ДатаПоступления = ДатаПоступления;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПустыеДатыПоступленияВТаблице(ДатаПоступления)
	
	Для Каждого ТекущаяСтрока Из Товары Цикл
		Если Не ТекущаяСтрока.Отменено
		   И Не ЗначениеЗаполнено(ТекущаяСтрока.ДатаПоступления) Тогда
			ТекущаяСтрока.ДатаПоступления = ДатаПоступления;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // Прочее

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли