
////////////////////////////////////////////////////////////////////////////////
// ОПИСАНИЕ ПЕРЕМЕННЫХ
#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения;

&НаКлиенте
Перем ОчередьОбработкиШтрихкодов;

#КонецОбласти // ОписаниеПеременных

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Печать
	ОбщегоНазначенияБольничнаяАптекаКлиентСервер.УстановитьОтображениеТолькоВоВсехДействиях(Элементы.ПодменюПечать, Истина);
	// Конец СтандартныеПодсистемы.Печать
	
	// ПодключаемоеОборудование
	ПодключаемоеОборудованиеСервер.НастроитьФормуДляИспользованияПодключаемогоОборудования(ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
	// ИнтеграцияМДЛП.КонтрольКМ
	КонтрольКодовМаркировкиМДЛП.ПриСозданииНаСервере(ЭтотОбъект, Отказ);
	// Конец ИнтеграцияМДЛП.КонтрольКМ
	
	// ИнтеграцияСМобильнымПриложением
	ИнтеграцияСМобильнымПриложением.СоздатьКомандуЗагрузкиДанныхИзМобильногоПриложенияНаФорме(ЭтотОбъект, "Товары", Элементы.ГруппаТоварыКоманднаяПанель.Имя);
	// Конец ИнтеграцияСМобильнымПриложением
	
	КонтрольНаСкладеОтключен = Не Константы.КонтролироватьОстаткиПриПробитииЧековККМ.Получить();
	
	ДанныеФискальнойОперации = РозничныеПродажи.ДанныеФискальнойОперации();
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииНовогоПриЧтенииНаСервере();
	КонецЕсли;
	
	РозничныеПродажи.ПодписатьГорячиеКлавишиНаКнопках(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриСозданииНовогоПриЧтенииНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ИнтеграцияМДЛП.КонтрольКМ
	КонтрольКодовМаркировкиМДЛП.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтеграцияМДЛП.КонтрольКМ
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	ПоддерживаемыеТипыПодключаемогоОборудования = "СканерШтрихкода,ДисплейПокупателя,СчитывательМагнитныхКарт";
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, ПоддерживаемыеТипыПодключаемогоОборудования);
	// Конец ПодключаемоеОборудование
	
	СтрокаДисплеяПокупателя = НСтр("ru = 'Здравствуйте!'");
	ПересчитатьДокументНаКлиенте();
	
	Если ПринудительнаяАвторизация Тогда
		ПодключитьОбработчикОжидания("ОткрытьОкноАвторизации", 0.5, Истина);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОчередьОбработкиШтрихкодов = Новый Массив;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если ПодключаемоеОборудованиеКлиент.ОбрабатыватьОповещение(ЭтотОбъект, Источник) Тогда
		Если ПодключаемоеОборудованиеКлиент.ОбработатьПолучениеДанныхОтСканераШтрихкода(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
			
			// Штрихкоды помещаются в очередь, т.к. может использоваться сканер с памятью.
			ОчередьОбработкиШтрихкодов.Добавить(ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ПолучитьДанныеШтрихкода(Параметр, 1));
			ПодключитьОбработчикОжидания("ОбработатьШтрихкодыОтложенно", 0.1, Истина);
			
			
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	// ИнтеграцияМДЛП.КонтрольКМ
	Если ИмяСобытия = "СохранениеРезультатовВыборочногоКонтроляМДЛП" И Параметр = Объект.Ссылка Тогда
		Прочитать();
	КонецЕсли;
	// Конец ИнтеграцияМДЛП.КонтрольКМ
	
	Если ИмяСобытия = "ВведенШтрихкод" И Источник = УникальныйИдентификатор Тогда
		
		// Штрихкоды помещаются в очередь, т.к. может использоваться сканер с памятью.
		ОчередьОбработкиШтрихкодов.Добавить(ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ПолучитьДанныеШтрихкода(Параметр, 1));
		ПодключитьОбработчикОжидания("ОбработатьШтрихкодыОтложенно", 0.1, Истина);
		
	КонецЕсли;
	
	Если ИмяСобытия = "АвторизованПользователь" Тогда
		
		Если Параметр.Режим = "СменитьПрава" Тогда
			НастроитьПраваДляПользователя(Параметр.Пользователь);
			ПересчитатьДокументНаКлиенте();
		ИначеЕсли Параметр.Режим = "ЗакрытьЧекККМ" Тогда
			ПринудительноеЗавершениеРаботы = Истина;
			Если Открыта() Тогда
				Закрыть();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменениеКассовойСмены" Тогда
		СостояниеКассовойСмены = РозничныеПродажиВызовСервера.СостояниеКассовойСмены(Объект.КассаККМ);
		ЗаполнитьЗначенияСвойств(Объект, СостояниеКассовойСмены);
		Объект.КассоваяСмена = СостояниеКассовойСмены.ОтчетОРозничныхПродажах;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ПодборТоваровКлиент.ОбработатьПодборТоваровВДокументСписания(ЭтотОбъект, ИсточникВыбора) Тогда
		ОбработатьПодбор(ВыбранноеЗначение.АдресТоваровВХранилище, КэшированныеЗначения);
		// Нужен обработчик ожидания, т.к. в ином случае сообщения будут привязаны к форме подбора.
		ПодключитьОбработчикОжидания("ПроверитьЗаполнениеМаркированнойПродукции", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОценкаПроизводительностиБольничнаяАптекаКлиент.НачатьЗамерПроведенияДокумента(Объект.Ссылка, Отказ, ПараметрыЗаписи);
	
	Если ВерсияФФД >= "1.2" Тогда
		Объект.ИспользоватьЛьготы              = Ложь;
		Объект.ПроцентЛьготы                   = Неопределено;
		Объект.ДатаРегистрацииЛьготногоРецепта = Неопределено;
		Объект.НомерСерииЛьготногоРецепта      = Неопределено;
		Объект.НомерЛьготногоРецепта           = Неопределено;
		Объект.ДатаРегистрацииРецепта          = Неопределено;
		Объект.НомерСерииРецепта               = Неопределено;
		Объект.НомерРецепта                    = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ИнтеграцияМДЛП.КонтрольКМ
	КонтрольКодовМаркировкиМДЛП.ПриЗаписиНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияМДЛП.КонтрольКМ
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьСлужебныеРеквизиты();
	
	// ИнтеграцияМДЛП.КонтрольКМ
	КонтрольКодовМаркировкиМДЛП.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияМДЛП.КонтрольКМ
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.Пробит") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПринудительноеЗавершениеРаботы Или ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПередЗакрытиемЧека(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоискПоШтрихкодуВыполнить()
	
	ОбработкаТабличнойЧастиКлиент.ПоказатьВводШтрихкода(УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьЗагрузкуДанныхИзТСД", ЭтотОбъект);
	ОборудованиеТерминалыСбораДанныхКлиент.НачатьЗагрузкуДанныеИзТСД(Оповещение, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВес(Команда)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru='Необходимо выбрать строку, для которой необходимо получить вес.'"));
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьПолучениеВеса", ЭтотОбъект, ТекущаяСтрока);
	МенеджерОборудованияКлиент.НачатьПолученияВесаСЭлектронныхВесов(Оповещение, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьТовары(Команда)
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("Документ", Объект.Ссылка);
	ПараметрыПодбора.Вставить("Организация", Объект.Организация);
	ПараметрыПодбора.Вставить("МестоХраненияОстатка", "Склад");
	ПараметрыПодбора.Вставить("Склад", Объект.Склад);
	ПараметрыПодбора.Вставить("ИсточникФинансирования", Объект.ИсточникФинансирования);
	ПараметрыПодбора.Вставить("ВидЦены", Объект.ВидЦены);
	ПараметрыПодбора.Вставить("ЗапретитьИзменениеЦены", Истина);
	
	ПодборТоваровКлиент.ОткрытьПодборТоваровВДокументСписания(ЭтотОбъект, ПараметрыПодбора);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйЧек(Команда)
	
	ЗагрузитьНовыйЧек();
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйЛьготныйЧек(Команда)
	
	ЗагрузитьНовыйЧек(, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратТовара(Команда)
	
	Оповестить = Новый ОписаниеОповещения("ВозвратТовараОбработкаОповещения", ЭтотОбъект);
	РозничныеПродажиКлиент.ОбработатьСостояниеСмены(ЭтотОбъект, Оповестить);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтложенныйЧек(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("КассаККМ", Объект.КассаККМ);
	
	Оповестить = Новый ОписаниеОповещения("ОтложенныйЧекОбработкаВыбора", ЭтотОбъект);
	ОткрытьФорму("Документ.ЧекККМ.Форма.ВыборОтложенногоЧека", ПараметрыОткрытия, ЭтотОбъект,,,, Оповестить);
	
КонецПроцедуры

&НаКлиенте
Процедура Заблокировать(Команда)
	
	ОткрытьОкноАвторизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОплатыПлатежнымиКартами(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ВыполнитьОбработкуОповещения(
		Новый ОписаниеОповещения("ОтменитьОплатыПлатежнымиКартами", ЭтотОбъект, ДополнительныеПараметры));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьМенюОперацииСККМ(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("КассаККМ", Объект.КассаККМ);
	ПараметрыОткрытияФормы.Вставить("Кассир",   Объект.Кассир);
	ПараметрыОткрытияФормы.Вставить("ИзменитьКассуККМ", Истина);
	
	ОткрытьФорму("Документ.ЧекККМ.Форма.МенюОперацииСККМ", ПараметрыОткрытияФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьНаличными(Команда)
	
	ОчиститьСообщения();
	
	Объект.Дата = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	Оповестить = Новый ОписаниеОповещения("ОплатитьНаличнымиОбработкаОповещения", ЭтотОбъект);
	РозничныеПродажиКлиент.ОбработатьСостояниеСмены(ЭтотОбъект, Оповестить);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьПлатежнойКартой(Команда)
	
	ОчиститьСообщения();
	
	Объект.Дата = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	РозничныеПродажиКлиент.ОбработатьСостояниеСмены(
		ЭтотОбъект,
		Новый ОписаниеОповещения("ОплатитьПлатежнойКартойОбработкаОповещения", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура СмешаннаяОплата(Команда)
	
	ОчиститьСообщения();
	
	Объект.Дата = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	РозничныеПродажиКлиент.ОбработатьСостояниеСмены(
		ЭтотОбъект,
		Новый ОписаниеОповещения("СмешаннаяОплатаОбработкаОповещения", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура НапечататьПоследнийСлипЧек(Команда)
	
	Если ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Подключаемое оборудование не используется.'"));
		Возврат;
	КонецЕсли;
	
	ПоследнийСлипЧек = ОборудованиеПлатежныеСистемыКлиент.ПоследнийСлипЧек();
	Если Не ПустаяСтрока(ПоследнийСлипЧек) Тогда
		
		ПараметрыОперации = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыПечатиТекста(ПоследнийСлипЧек);
		
		Обработчик = Новый ОписаниеОповещения("ПослеЗавершенияПечатиСлипЧека", ЭтотОбъект);
		ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьПечатьТекста(
			Обработчик,
			УникальныйИдентификатор,
			ПараметрыКассыККМ.ИдентификаторУстройства,
			ПараметрыОперации);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Слип-чек отсутствует.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборочныйКонтрольМДЛП(Команда)
	
	// ИнтеграцияМДЛП.КонтрольКМ
	КонтрольКодовМаркировкиМДЛПКлиент.ОткрытьФормуВыборочногоКонтроляКМИзФормыВладельца(ЭтотОбъект);
	// Конец ИнтеграцияМДЛП.КонтрольКМ
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРецепт(Команда)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ВыделенныеСтроки = Элементы.Товары.ВыделенныеСтроки;
	
	Если ТекущиеДанные <> Неопределено Или ВыделенныеСтроки.Количество() > 0 Тогда
		ВыбратьРецептИлиЛьготуСтроки(ТекущиеДанные, ВыделенныеСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	// ИнтеграцияСМобильнымПриложением
	Оповестить = Новый ОписаниеОповещения("ОбработатьЗагрузкуДанныхИзТСД", ЭтотОбъект);
	ИнтеграцияСМобильнымПриложениемКлиент.ВыполнитьКомандуЗагрузкиДанныхИзМобильногоПриложения(ЭтотОбъект, Команда, Оповестить);
	// Конец ИнтеграцияСМобильнымПриложением
	
	ОбщегоНазначенияБольничнаяАптекаКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЛьготаНажатие(Элемент)
	
	ВыбратьРецептИлиЛьготуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РецептНажатие(Элемент)
	
	ВыбратьРецептИлиЛьготуДокумента();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Список "Товары"
#Область Товары

&НаКлиенте
Процедура ТоварыПриАктивизацииСтроки(Элемент)
	
	ОбновитьДанныеИнформационнойПанели(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииЯчейки(Элемент)
	
	Если Элемент.ТекущийЭлемент <> Элементы.ТоварыШтрихкод Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Элементы.ТоварыШтрихкод.СписокВыбора.Очистить();
		Возврат;
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("Номенклатура"     , ТекущиеДанные.Номенклатура);
	Структура.Вставить("СерияНоменклатуры", ТекущиеДанные.СерияНоменклатуры);
	Структура.Вставить("Партия"           , ТекущиеДанные.Партия);
	Структура.Вставить("ЕдиницаИзмерения" , ТекущиеДанные.ЕдиницаИзмерения);
	
	Элементы.ТоварыШтрихкод.СписокВыбора.ЗагрузитьЗначения(РозничныеПродажиВызовСервера.ПолучитьШтрихкодыНоменклатуры(Структура));
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не ПраваДоступа.КорректировкаСтрок Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Копирование И Элементы.Товары.ТекущиеДанные.СерияПромаркированаДляЦелейМДЛП Тогда
		Текст = НСтр("ru = 'Копирование строк маркированных товаров недоступно.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(Текст,,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	ПересчитатьДокументНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Не ПраваДоступа.КорректировкаСтрок Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если Поле = Элементы.ТоварыКоличествоВЕдиницахИзмерения
	 Или Поле = Элементы.ТоварыЕдиницаИзмерения
	 Или Поле = Элементы.ТоварыЕдиницаИзмеренияДополнительно Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Номенклатура"                   , ТекущиеДанные.Номенклатура);
		ПараметрыФормы.Вставить("ЕдиницаИзмерения"               , ТекущиеДанные.ЕдиницаИзмерения);
		ПараметрыФормы.Вставить("КоличествоВЕдиницахИзмерения"   , ТекущиеДанные.КоличествоВЕдиницахИзмерения);
		ПараметрыФормы.Вставить("СерияПромаркированаДляЦелейМДЛП", ТекущиеДанные.СерияПромаркированаДляЦелейМДЛП);
		
		Оповестить = Новый ОписаниеОповещения("ЗавершитьВводКоличестваВЕдиницахИзмерения", ЭтотОбъект);
		ОткрытьФорму("Документ.ЧекККМ.Форма.ФормаЗапросаКоличества", ПараметрыФормы,,,,, Оповестить);
		
	ИначеЕсли Поле = Элементы.ТоварыСуммаЛьготы И Не ЗначениеЗаполнено(Объект.ПроцентЛьготы) Тогда
		
		Если ВерсияФФД >= "1.2" Тогда
			ВыбратьРецептИлиЛьготуСтроки(ТекущиеДанные, Элемент.ВыделенныеСтроки);
		Иначе
			ВыбратьРецептИлиЛьготуДокумента();
		КонецЕсли;
		
	ИначеЕсли Поле = Элементы.ТоварыРецепт Тогда
		
		ВыбратьРецептИлиЛьготуСтроки(ТекущиеДанные, Элемент.ВыделенныеСтроки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	Если Не ПраваДоступа.КорректировкаСтрок Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	
	ПересчитатьДокументНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	ТекущаяСтрока.Штрихкод = "";
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ПроверитьСериюНоменклатурыПоВладельцу(), ТекущаяСтрока.СерияНоменклатуры);
	СтруктураДействий.Вставить(Действия.Действие_ПроверитьУпаковкуПоВладельцу(), ТекущаяСтрока.ЕдиницаИзмерения);
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЕдиницуИзмерения(), НоменклатураКлиентСервер.ВидЕдиницы_ПотребительскаяУпаковка());
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЦенуПродажи(), Действия.ПолучитьПараметрыЗаполненияЦены(Объект));
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьПараметрыУчета(), ПараметрыУчетаНоменклатуры);
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЗабраковкуСерий());
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьКоэффициент());
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьКоличествоЕдиниц());
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСумму());
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьСтавкуНДС(), Объект.НалогообложениеНДС);
	ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСерияНоменклатурыПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЦенуПродажи(), Действия.ПолучитьПараметрыЗаполненияЦены(Объект));
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЗабраковкуСерий());
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСумму());
	ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСумму());
	ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьЦену());
	ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПартияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущаяСтрока = Элемент.Родитель.ТекущиеДанные;
	ОтборПартий = Новый Структура;
	ОтборПартий.Вставить("Документ"     , Объект.Ссылка);
	ОтборПартий.Вставить("Организация"  , Объект.Организация);
	ОтборПартий.Вставить("Склад"        , Объект.Склад);
	
	ПараметрыВыбораПартии = ОбработкаТабличнойЧастиКлиент.ПолучитьПараметрыВыбораПартии(ОтборПартий, ТекущаяСтрока);
	ОбработкаТабличнойЧастиКлиент.ВыбратьПартиюНоменклатуры(ЭтотОбъект, Элемент, ПараметрыВыбораПартии, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПартияПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЦенуПродажи(), Действия.ПолучитьПараметрыЗаполненияЦены(Объект));
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСумму());
	ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

#КонецОбласти // Товары

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНовогоПриЧтенииНаСервере()
	
	СостояниеКассовойСмены = РозничныеПродажиВызовСервера.СостояниеКассовойСмены(Объект.КассаККМ);
	ЗаполнитьЗначенияСвойств(Объект, СостояниеКассовойСмены);
	Объект.КассоваяСмена = СостояниеКассовойСмены.ОтчетОРозничныхПродажах;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ПроцентЛьготы                   = Неопределено;
		Объект.ДатаРегистрацииЛьготногоРецепта = Неопределено;
		Объект.НомерСерииЛьготногоРецепта      = Неопределено;
		Объект.НомерЛьготногоРецепта           = Неопределено;
		Объект.ДатаРегистрацииРецепта          = Неопределено;
		Объект.НомерСерииРецепта               = Неопределено;
		Объект.НомерРецепта                    = Неопределено;
	КонецЕсли;
	
	ПараметрыУчетаНоменклатуры = Новый ФиксированнаяСтруктура(ЗапасыСервер.ПолучитьПараметрыУчетаНоменклатуры(Объект));
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Склад", Объект.Склад));
	
	Если ДанныеФискальнойОперации <> Неопределено Тогда
		ДанныеФискальнойОперации.ВариантОтправкиЭлектронногоЧека = Неопределено;
		ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека = Неопределено;
	КонецЕсли;
	
	НастроитьРМК();
	
	ЗаполнитьСлужебныеРеквизиты();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ОбработкаТабличнойЧастиСервер.УстановитьОформлениеЦенаВключаетНДС(ЭтотОбъект);
	ОбработкаТабличнойЧастиСервер.УстановитьОформлениеСуммаНДС(ЭтотОбъект);
	ОбработкаТабличнойЧастиСервер.УстановитьОформлениеИсточниковФинансирования(ЭтотОбъект);
	
	ОбработкаТабличнойЧастиСервер.УстановитьОформлениеСерийНоменклатуры(ЭтотОбъект);
	ОбработкаТабличнойЧастиСервер.УстановитьОформлениеПартий(ЭтотОбъект);
	
	ОбработкаТабличнойЧастиСервер.УстановитьОформлениеЗабракованнойСерии(ЭтотОбъект);
	
	// ОтметкаНезаполненного, Отображать поля ТоварыСерияНоменклатурыДополнительно.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСерияНоменклатурыДополнительно.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Объект.Товары.СтатусУказанияСерий", ВидСравненияКомпоновкиДанных.Равно, 0);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	// ОтметкаНезаполненного, Отображать поля ТоварыПартияДополнительно.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПартияДополнительно.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Объект.Товары.СтатусУказанияПартий", ВидСравненияКомпоновкиДанных.Равно, 0);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	// Формат поля ТоварыКоличествоВЕдиницахИзмерения.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКоличествоВЕдиницахИзмерения.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Объект.Товары.СерияПромаркированаДляЦелейМДЛП", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Формат", "ЧДЦ=0");
	
	// Только просмотр полей строки табличной части Товары.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыШтрихкод.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНомерКиЗ.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКодСтатусаПроверкиКМ.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПояснениеСтатусаПроверкиКМ.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНоменклатура.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСерияНоменклатурыДополнительно.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПартияДополнительно.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСерияНоменклатуры.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПартия.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыИсточникФинансирования.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Объект.Товары.СерияПромаркированаДляЦелейМДЛП", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// Видимость поля ТоварыШтрихкод строки табличной части Товары.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыШтрихкод.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Объект.Товары.СерияПромаркированаДляЦелейМДЛП", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Видимость поля ТоварыНомерКиЗ строки табличной части Товары.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНомерКиЗ.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКодСтатусаПроверкиКМ.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПояснениеСтатусаПроверкиКМ.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Объект.Товары.СерияПромаркированаДляЦелейМДЛП", ВидСравненияКомпоновкиДанных.НеРавно, Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Отметка незаполненного поля Цена табличной части Товары.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСуммаЛьготы.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыЦена.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"ВерсияФФД", ВидСравненияКомпоновкиДанных.Меньше, "1.2");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Объект.ПроцентЛьготы", ВидСравненияКомпоновкиДанных.Равно, Справочники.ПроцентыЛьгот.Льгота_100);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// Видимость рецепта в табличной части для ФФД 1.2.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыРецепт.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"ВерсияФФД", ВидСравненияКомпоновкиДанных.Меньше, "1.2");
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Видимость суммы льготы в табличной части для ФФД ниже 1.2.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСуммаЛьготы.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"ВерсияФФД", ВидСравненияКомпоновкиДанных.Меньше, "1.2");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Объект.ИспользоватьЛьготы", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Отметка незаполненного поля ТоварыСуммаЛьготы табличной части Товары.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСуммаЛьготы.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"ВерсияФФД", ВидСравненияКомпоновкиДанных.БольшеИлиРавно, "1.2");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Объект.Товары.ПроцентЛьготы", ВидСравненияКомпоновкиДанных.Равно, ПредопределенноеЗначение("Справочник.ПроцентыЛьгот.ПустаяСсылка"));
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизиты()
	
	Если ВерсияФФД >= "1.2" Тогда
		Для Каждого ТекущиеДанные Из Объект.Товары Цикл
			ТекущиеДанные.Рецепт = ОписаниеРецептаИлиЛьготы(ТекущиеДанные);
		КонецЦикла;
	Иначе
		ОписаниеРецептаИлиЛьготы = ОписаниеРецептаИлиЛьготы(Объект);
		Если Объект.ИспользоватьЛьготы Тогда
			Если ЗначениеЗаполнено(ОписаниеРецептаИлиЛьготы) Тогда
				Элементы.Льгота.Заголовок = ОписаниеРецептаИлиЛьготы;
			Иначе
				Элементы.Льгота.Заголовок = НСтр("ru = 'Льгота:'");
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(ОписаниеРецептаИлиЛьготы) Тогда
				Элементы.Рецепт.Заголовок = ОписаниеРецептаИлиЛьготы;
			Иначе
				Элементы.Рецепт.Заголовок = НСтр("ru = 'Рецепт:'");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЗабраковкуСерий());
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьПризнакСерияПромаркированаДляЦелейМДЛП());
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТабличнуюЧасть(Объект.Товары, СтруктураДействий, Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокФормы()
	
	Заголовок = НСтр("ru = 'Продажа (Кассир: %Кассир%)'");
	Заголовок = СтрЗаменить(Заголовок, "%Кассир%", Объект.Кассир);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьРМК()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПолучитьСсылкиНаОборудование();
	
	НастроитьПраваДляПользователя(Объект.Кассир);
	
	ПринудительнаяАвторизация = Константы.ИспользоватьАвторизациюРМК.Получить();
	Элементы.Заблокировать.Видимость = ПринудительнаяАвторизация;
	
	ДоступныеВидыОплаты = ДоступныеВидыОплаты(ЭтотОбъект);
	Элементы.ОплатитьНаличными.Видимость = ДоступныеВидыОплаты.Наличные;
	Элементы.ОплатитьКартой.Видимость    = ДоступныеВидыОплаты.ПлатежныеКарты;
	Элементы.СмешаннаяОплата.Видимость   = ДоступныеВидыОплаты.ПлатежныеКарты;
	
	ВерсияФФД = ОборудованиеЧекопечатающиеУстройстваВызовСервера.ФискальноеУстройствоПоддерживаетВерсиюФФД(ПараметрыКассыККМ.ИдентификаторУстройства);
	
	НастроитьРецептИЛьготыДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДокументНаКлиенте()
	
	ОбновитьДанныеИнформационнойПанели(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("ВывестиИнформациюНаДисплейПокупателя", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗагрузкуДанныхИзТСД(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		ОбработатьШтрихкоды(РезультатВыполнения.ТаблицаТоваров, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолучениеВеса(РезультатВыполнения, ТекущаяСтрока) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		ТекущаяСтрока.КоличествоВЕдиницахИзмерения = РезультатВыполнения.Вес;
		ПриИзмененииКоличестваВСтрокеСпискаТовары(ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКоличестваВСтрокеСпискаТовары(ТекущаяСтрока)
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьКоличествоЕдиниц());
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСумму());
	ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(
		ТекущаяСтрока,
		СтруктураДействий,
		КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВводКоличестваВЕдиницахИзмерения(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, РезультатЗакрытия);
		
		Действия = ОбработкаТабличнойЧастиКлиентСервер;
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить(Действия.Действие_ПересчитатьКоэффициент());
		СтруктураДействий.Вставить(Действия.Действие_ПересчитатьКоличествоЕдиниц());
		Если ТекущиеДанные.Коэффициент > 0 Тогда
			Если ТекущиеДанные.СерияПромаркированаДляЦелейМДЛП И ТекущиеДанные.КоличествоВЕдиницахИзмерения Тогда
				СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЦенуПродажи(), Действия.ПолучитьПараметрыЗаполненияЦены(Объект));
			Иначе
				СтруктураДействий.Вставить(Действия.Действие_ПересчитатьЦенуЗаУпаковку(), Действия.ПолучитьПараметрыПересчетаЦеныЗаУпаковку(ТекущиеДанные.Коэффициент,, "Коэффициент"));
			КонецЕсли;
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСумму());
			ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
			
			Если ВерсияФФД >= "1.2" Тогда
				СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ТекущиеДанные.ПроцентЛьготыЧислом));
			Иначе
				СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
			КонецЕсли;
		КонецЕсли;
		
		ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
		
		ПересчитатьДокументНаКлиенте();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДанныеИнформационнойПанели(Форма)
	
	ИнформацияОбОплате = ИнформацияОбОплате(Форма);
	
	НаименованиеТовара = Новый ФорматированнаяСтрока("");
	РасчетСуммы = Новый ФорматированнаяСтрока("");
	
	ОтобразитьСдачу = Форма.ТолькоПросмотр = Истина;
	Если ОтобразитьСдачу Тогда
		
		Оплачено = ИнформацияОбОплате.ИтогоОплачено;
		КОплате = ИнформацияОбОплате.СуммаКОплате;
		
		Сдача = Оплачено - КОплате;
		
		Если Сдача < 0 Тогда
			РасчетСуммы = Новый ФорматированнаяСтрока(
				СтрЗаменить(НСтр("ru = 'Осталось оплатить: %1'"), "%1", Формат(-Сдача, "ЧДЦ=2; ЧН=0.00")));
		Иначе
			РасчетСуммы = Новый ФорматированнаяСтрока(
				СтрЗаменить(НСтр("ru = 'Сдача: %1'"), "%1", Формат(Сдача, "ЧДЦ=2; ЧН=0.00")));
		КонецЕсли;
		
	Иначе
		
		КоличествоСтрок = Форма.Объект.Товары.Количество();
		
		ТекущаяСтрока = Форма.Элементы.Товары.ТекущаяСтрока;
		Если ТекущаяСтрока = Неопределено И КоличествоСтрок > 0 Тогда
			ТекущиеДанные = Форма.Объект.Товары[КоличествоСтрок - 1];
			Форма.Элементы.Товары.ТекущаяСтрока = ТекущиеДанные.ПолучитьИдентификатор();
		ИначеЕсли ТекущаяСтрока = Неопределено И КоличествоСтрок = 0 Тогда
			ТекущиеДанные = Неопределено;
		Иначе
			ТекущиеДанные = Форма.Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрока);
		КонецЕсли;
		
		Если ТекущиеДанные <> Неопределено Тогда
			Если ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
				НаименованиеТовара = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
					ТекущиеДанные.Номенклатура,
					ТекущиеДанные.СерияНоменклатуры,
					ТекущиеДанные.Партия);
				НаименованиеТовара = Новый ФорматированнаяСтрока(НаименованиеТовара);
			КонецЕсли;
			
			Если ТекущиеДанные.СуммаСНДС <> 0 Тогда
				
				// Количество x Цена
				ФорматКоличество = ФорматнаяСтрока(ТекущиеДанные.Количество, 3);
				ФорматЦена       = ФорматнаяСтрока(ТекущиеДанные.Цена, 2);
				ЦенаКоличество = СтрШаблон("%1 x %2", Формат(ТекущиеДанные.КоличествоВЕдиницахИзмерения, ФорматКоличество), Формат(ТекущиеДанные.Цена, ФорматЦена));
				
				// Сумма НДС
				ФорматСуммаНДС = ФорматнаяСтрока(ТекущиеДанные.СуммаНДС, 2);
				СуммаНДС = ?(Не Форма.Объект.ЦенаВключаетНДС, СтрШаблон(" + %1 ( %2 )", Формат(ТекущиеДанные.СуммаНДС, ФорматСуммаНДС), СокрЛП(ТекущиеДанные.СтавкаНДС)), "");
				
				// Льготы
				ФорматСуммаЛьготы = ФорматнаяСтрока(ТекущиеДанные.СуммаЛьготы, 2);
				Льготы = "";
				Если Форма.ВерсияФФД >= "1.2" И ЗначениеЗаполнено(ТекущиеДанные.ПроцентЛьготы) Тогда
					Льготы = ?(ТекущиеДанные.СуммаЛьготы > 0, СтрШаблон(" - %1 ( %2 )", Формат(ТекущиеДанные.СуммаЛьготы, ФорматСуммаЛьготы), СокрЛП(ТекущиеДанные.ПроцентЛьготы)), "");
				ИначеЕсли Форма.ВерсияФФД < "1.2" И ЗначениеЗаполнено(Форма.Объект.ПроцентЛьготы) Тогда
					Льготы = ?(ТекущиеДанные.СуммаЛьготы > 0, СтрШаблон(" - %1 ( %2 )", Формат(ТекущиеДанные.СуммаЛьготы, ФорматСуммаЛьготы), СокрЛП(Форма.Объект.ПроцентЛьготы)), "");
				КонецЕсли;
				
				// Сумма с НДС
				СуммаСНДС = ТекущиеДанные.СуммаСНДС - ТекущиеДанные.СуммаЛьготы;
				ФорматСуммаСНДС = ФорматнаяСтрока(СуммаСНДС, 2);
				СуммаСНДС = " = " + Формат(СуммаСНДС, ФорматСуммаСНДС);
				
				РасчетСуммы = Новый ФорматированнаяСтрока(ЦенаКоличество, СуммаНДС, Льготы, СуммаСНДС);
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Форма.ИнформационнаяПанельРасчетСуммы             = РасчетСуммы;
	Форма.ИнформационнаяПанельНаименованиеТовара      = НаименованиеТовара;
	
	Форма.ИнформационнаяПанельСуммаБезСкидки = ИнформацияОбОплате.СуммаБезСкидки;
	Форма.ИнформационнаяПанельСуммаЛьготы    = ИнформацияОбОплате.СуммаЛьготы;
	Форма.ИнформационнаяПанельСуммаКОплате   = ИнформацияОбОплате.СуммаКОплате;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ФорматнаяСтрока(Число, РазрядностьДробнойЧасти)
	
	Если Цел(Число) = Число Тогда
		Возврат "ЧН=0; ЧДЦ=;";
	Иначе
		Возврат "ЧН=0; ЧДЦ=" + РазрядностьДробнойЧасти;
	КонецЕсли
	
КонецФункции

&НаСервере
Процедура НастроитьПраваДляПользователя(Пользователь)
	
	ПраваДоступа = РозничныеПродажи.ПраваДоступаРМК(Пользователь);
	
	Элементы.ВозвратТовара.Доступность     = ПраваДоступа.ВозвратТовара;
	
	Элементы.ТоварыДобавить.Доступность    = ПраваДоступа.КорректировкаСтрок;
	Элементы.ТоварыСкопировать.Доступность = ПраваДоступа.КорректировкаСтрок;
	Элементы.ТоварыУдалить.Доступность     = ПраваДоступа.КорректировкаСтрок;
	
	Элементы.ТоварыКонтекстноеМенюДобавить.Доступность    = ПраваДоступа.КорректировкаСтрок;
	Элементы.ТоварыКонтекстноеМенюСкопировать.Доступность = ПраваДоступа.КорректировкаСтрок;
	Элементы.ТоварыКонтекстноеМенюУдалить.Доступность = ПраваДоступа.КорректировкаСтрок;
	
	Элементы.ТоварыШтрихкод.ТолькоПросмотр          = Не ПраваДоступа.КорректировкаСтрок;
	Элементы.ТоварыНоменклатура.ТолькоПросмотр      = Не ПраваДоступа.КорректировкаСтрок;
	Элементы.ТоварыСерияНоменклатуры.ТолькоПросмотр = Не ПраваДоступа.КорректировкаСтрок;
	Элементы.ТоварыПартия.ТолькоПросмотр            = Не ПраваДоступа.КорректировкаСтрок;
	
	Объект.Кассир = Пользователь;
	РеквизитыКассира = РозничныеПродажи.РеквизитыКассира(Пользователь);
	
	ОбновитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущийЭлементНаКнопкуНовыйЧек()
	
	ТекущийЭлемент = Элементы.НовыйЧек;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтложенныйЧекОбработкаВыбора(ОтложенныйЧек, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ОтложенныйЧек) И Объект.Ссылка <> ОтложенныйЧек Тогда
		ЗагрузитьНовыйЧек(ОтложенныйЧек);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОкноАвторизации()
	
	ОткрытьФорму("Документ.ЧекККМ.Форма.Авторизация", Новый Структура("Режим", "СменитьПрава"), ЭтотОбъект);
	
КонецПроцедуры

// Вызывается из формы МенюОперацииСККМ
&НаКлиенте
Процедура ИзменитьКассуККМЗавершение(КассаККМ, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(КассаККМ) Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьКассуККМ(КассаККМ);
	
	ПересчитатьДокументНаКлиенте();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьКассуККМ(КассаККМ)
	
	Объект.КассаККМ = КассаККМ;
	ПараметрыКассыККМ = Новый ФиксированнаяСтруктура(Справочники.КассыККМ.ПараметрыКассыККМ(Объект.КассаККМ));
	
	ВерсияФФД = ОборудованиеЧекопечатающиеУстройстваВызовСервера.ФискальноеУстройствоПоддерживаетВерсиюФФД(ПараметрыКассыККМ.ИдентификаторУстройства);
	
	НовыйЧекНаСервере(, Объект.ИспользоватьЛьготы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратТовараОбработкаОповещения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормыВозврата = Новый Структура;
	ПараметрыФормыВозврата.Вставить("КассаККМ", Объект.КассаККМ);
	ПараметрыФормыВозврата.Вставить("ВозвратТовараТолькоТекущейСмены", ПраваДоступа.ВозвратТовараТолькоТекущейСмены);
	
	ОткрытьФорму("Документ.ЧекККМ.Форма.ОформлениеВозвратаДеньВДень", ПараметрыФормыВозврата, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияПечатиСлипЧека(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'При печати слип-чека возникла ошибка:
			           |""%1"".'"),
			РезультатВыполнения.ОписаниеОшибки);
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеМаркированнойПродукции()
	
	ПроверитьЗаполнениеМаркированнойПродукцииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнениеМаркированнойПродукцииНаСервере()
	
	Документы.ЧекККМ.ПроверитьЗаполнениеМаркированнойПродукции(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьОшибкуВЖурналРегистрации(ИнформацияОбОшибке)
	
	ИмяСобытия = НСтр("ru = 'Проведение документа Чек ККМ.'", ОбщегоНазначения.КодОсновногоЯзыка());
	Текст = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.ЧекККМ,, Текст);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Льготы и рецепты
#Область ЛьготыИРецепты

&НаСервере
Процедура НастроитьРецептИЛьготыДокумента()
	
	Если ВерсияФФД >= "1.2" Тогда
		
		Элементы.НовыйЛьготныйЧек.Видимость      = Ложь;
		Элементы.ТоварыЗаполнитьРецепт.Видимость = Истина;
		
		Элементы.ГруппаЛьготы.Видимость      = Истина;
		Элементы.Льгота.Заголовок            = НСтр("ru = 'Сумма льгот:'");
		Элементы.Льгота.Гиперссылка          = Ложь;
		
		Элементы.ГруппаРецепт.Видимость      = Ложь;
		
		Элементы.ТоварыСуммаЛьготы.Видимость = Истина;
		
		Объект.ВидЦены         = СостояниеКассовойСмены.ВидЦены;
		Объект.ЦенаВключаетНДС = СостояниеКассовойСмены.ЦенаВключаетНДС;
		
		Объект.ВидЦеныЛьготныхТоваров         = СостояниеКассовойСмены.ВидЦеныЛьготныхТоваров;
		Объект.ЦенаВключаетНДСЛьготныхТоваров = СостояниеКассовойСмены.ЦенаВключаетНДСЛьготныхТоваров;
		
	Иначе
		
		Элементы.НовыйЛьготныйЧек.Видимость      = Истина;
		Элементы.ТоварыЗаполнитьРецепт.Видимость = Ложь;
		
		Если Объект.ИспользоватьЛьготы Тогда
			Объект.ВидЦены         = СостояниеКассовойСмены.ВидЦеныЛьготныхТоваров;
			Объект.ЦенаВключаетНДС = СостояниеКассовойСмены.ЦенаВключаетНДСЛьготныхТоваров;
			
			Элементы.ГруппаЛьготы.Видимость      = Истина;
			Элементы.Льгота.Гиперссылка          = Истина;
			Элементы.ГруппаРецепт.Видимость      = Ложь;
			
			Элементы.ТоварыСуммаЛьготы.Видимость = Истина;
			
		Иначе
			Объект.ВидЦены         = СостояниеКассовойСмены.ВидЦены;
			Объект.ЦенаВключаетНДС = СостояниеКассовойСмены.ЦенаВключаетНДС;
			
			Элементы.ГруппаЛьготы.Видимость      = Ложь;
			Элементы.ГруппаРецепт.Видимость      = Истина;
			
			Элементы.ТоварыСуммаЛьготы.Видимость = Ложь;
	КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеРецептаИлиЛьготы(Знач Источник)
	
	Если ЗначениеЗаполнено(Источник.ПроцентЛьготы) Тогда
		ОписаниеРецептаВид   = НСтр("ru = 'Льгота %1'");
		ОписаниеРецептаВид   = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеРецептаВид, Источник.ПроцентЛьготы);
		ОписаниеРецептаДата  = Формат(Источник.ДатаРегистрацииЛьготногоРецепта, "ДЛФ=D");
		ОписаниеРецептаСерия = СокрЛП(Источник.НомерСерииЛьготногоРецепта);
		ОписаниеРецептаНомер = СокрЛП(Источник.НомерЛьготногоРецепта);
	ИначеЕсли ЗначениеЗаполнено(Источник.ДатаРегистрацииРецепта) Тогда
		ОписаниеРецептаВид   = НСтр("ru = 'Рецепт'");
		ОписаниеРецептаДата  = Формат(Источник.ДатаРегистрацииРецепта, "ДЛФ=D");
		ОписаниеРецептаСерия = СокрЛП(Источник.НомерСерииРецепта);
		ОписаниеРецептаНомер = СокрЛП(Источник.НомерРецепта);
	Иначе
		Возврат "";
	КонецЕсли;
	
	ШаблонОписанияРецепта = НСтр("ru = '%1 от %2 %3 %4'");
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонОписанияРецепта,
		ОписаниеРецептаВид,
		ОписаниеРецептаДата,
		ОписаниеРецептаСерия,
		ОписаниеРецептаНомер);
	
КонецФункции


&НаКлиенте
Процедура ВыбратьРецептИлиЛьготуДокумента()

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВерсияФФД"                       , ВерсияФФД);
	ПараметрыФормы.Вставить("ИспользоватьЛьготы"              , Объект.ИспользоватьЛьготы);
	ПараметрыФормы.Вставить("ПроцентЛьготы"                   , Объект.ПроцентЛьготы);
	ПараметрыФормы.Вставить("НомерСерииЛьготногоРецепта"      , Объект.НомерСерииЛьготногоРецепта);
	ПараметрыФормы.Вставить("НомерЛьготногоРецепта"           , Объект.НомерЛьготногоРецепта);
	ПараметрыФормы.Вставить("ДатаРегистрацииЛьготногоРецепта" , Объект.ДатаРегистрацииЛьготногоРецепта);
	ПараметрыФормы.Вставить("НомерСерииРецепта"               , Объект.НомерСерииРецепта);
	ПараметрыФормы.Вставить("НомерРецепта"                    , Объект.НомерРецепта);
	ПараметрыФормы.Вставить("ДатаРегистрацииРецепта"          , Объект.ДатаРегистрацииРецепта);
	
	Если Объект.ИспользоватьЛьготы Тогда
		Оповестить = Новый ОписаниеОповещения("ЗавершитьВыборЛьготыДокумента", ЭтотОбъект);
	Иначе
		Оповестить = Новый ОписаниеОповещения("ЗавершитьВыборРецептаДокумента", ЭтотОбъект);
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЧекККМ.Форма.ФормаЗапросаРецепта", ПараметрыФормы,,,,, Оповестить);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыборЛьготыДокумента(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Льгота.Заголовок = НСтр("ru = 'Льгота:'");
	
	ПроцентЛьготы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "ПроцентЛьготы");
	
	Если ЗначениеЗаполнено(ПроцентЛьготы) Тогда
		Объект.НомерСерииЛьготногоРецепта      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "НомерСерииЛьготногоРецепта");
		Объект.НомерЛьготногоРецепта           = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "НомерЛьготногоРецепта");
		Объект.ДатаРегистрацииЛьготногоРецепта = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "ДатаРегистрацииЛьготногоРецепта");
		
		Если Объект.ПроцентЛьготы <> ПроцентЛьготы Тогда
			Объект.ПроцентЛьготы = ПроцентЛьготы;
			
			ПроцентЛьготыЧислом = ПроцентЛьготыЧислом(ПроцентЛьготы);
			
			Элементы.Льгота.Заголовок = ОписаниеРецептаИлиЛьготы(Объект);
			
			Действия = ОбработкаТабличнойЧастиКлиентСервер;
			
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
			
			Для Каждого ТекущиеДанные Из Объект.Товары Цикл
				ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
			КонецЦикла;
			
			ПересчитатьДокументНаКлиенте();
		КонецЕсли;
		
		Объект.НомерСерииРецепта               = Неопределено;
		Объект.НомерРецепта                    = Неопределено;
		Объект.ДатаРегистрацииРецепта          = Неопределено;
	Иначе
		Объект.НомерСерииЛьготногоРецепта      = Неопределено;
		Объект.НомерЛьготногоРецепта           = Неопределено;
		Объект.ДатаРегистрацииЛьготногоРецепта = Неопределено;
		
		Объект.НомерСерииРецепта               = Неопределено;
		Объект.НомерРецепта                    = Неопределено;
		Объект.ДатаРегистрацииРецепта          = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыборРецептаДокумента(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Рецепт.Заголовок = НСтр("ru = 'Рецепт:'");
	
	ДатаРегистрацииРецепта = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "ДатаРегистрацииРецепта");
	
	Если ЗначениеЗаполнено(ДатаРегистрацииРецепта) Тогда
		
		Объект.НомерСерииРецепта      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "НомерСерииРецепта");
		Объект.НомерРецепта           = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "НомерРецепта");
		Объект.ДатаРегистрацииРецепта = ДатаРегистрацииРецепта;
		
		Элементы.Рецепт.Заголовок = ОписаниеРецептаИлиЛьготы(Объект);
		
		Объект.НомерСерииЛьготногоРецепта      = Неопределено;
		Объект.НомерЛьготногоРецепта           = Неопределено;
		Объект.ДатаРегистрацииЛьготногоРецепта = Неопределено;
		
	Иначе
		
		Объект.НомерСерииРецепта               = Неопределено;
		Объект.НомерРецепта                    = Неопределено;
		Объект.ДатаРегистрацииРецепта          = Неопределено;
		
		Объект.НомерСерииЛьготногоРецепта      = Неопределено;
		Объект.НомерЛьготногоРецепта           = Неопределено;
		Объект.ДатаРегистрацииЛьготногоРецепта = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ВыбратьРецептИлиЛьготуСтроки(ТекущиеДанные, ВыделенныеСтроки)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВерсияФФД"                       , ВерсияФФД);
	ПараметрыФормы.Вставить("ИспользоватьЛьготы"              , Ложь);
	ПараметрыФормы.Вставить("ПроцентЛьготы"                   , ТекущиеДанные.ПроцентЛьготы);
	ПараметрыФормы.Вставить("НомерСерииЛьготногоРецепта"      , ТекущиеДанные.НомерСерииЛьготногоРецепта);
	ПараметрыФормы.Вставить("НомерЛьготногоРецепта"           , ТекущиеДанные.НомерЛьготногоРецепта);
	ПараметрыФормы.Вставить("ДатаРегистрацииЛьготногоРецепта" , ТекущиеДанные.ДатаРегистрацииЛьготногоРецепта);
	ПараметрыФормы.Вставить("НомерСерииРецепта"               , ТекущиеДанные.НомерСерииРецепта);
	ПараметрыФормы.Вставить("НомерРецепта"                    , ТекущиеДанные.НомерРецепта);
	ПараметрыФормы.Вставить("ДатаРегистрацииРецепта"          , ТекущиеДанные.ДатаРегистрацииРецепта);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВыделенныеСтроки", ВыделенныеСтроки);
	
	Оповестить = Новый ОписаниеОповещения("ЗавершитьВыборРецептаИлиЛьготыСтроки", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Документ.ЧекККМ.Форма.ФормаЗапросаРецепта", ПараметрыФормы,,,,, Оповестить);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыборРецептаИлиЛьготыСтроки(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ПроцентЛьготы           = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "ПроцентЛьготы");
	ДатаРегистрацииРецепта  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "ДатаРегистрацииРецепта");
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	ПараметрыЗаполненияЦены = Действия.ПолучитьПараметрыЗаполненияЦены(Объект);
	ПараметрыПересчета      = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	
	ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
	
	Если ЗначениеЗаполнено(ПроцентЛьготы) Тогда
		
		ПараметрыЗаполненияЦены.ВидЦены    = Объект.ВидЦеныЛьготныхТоваров;
		ПараметрыПересчета.ЦенаВключаетНДС = Объект.ЦенаВключаетНДСЛьготныхТоваров;
		
		Для Каждого ТекущаяСтрока Из ВыделенныеСтроки Цикл
			
			ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрока);
			ТекущиеДанные.ПроцентЛьготы                   = ПроцентЛьготы;
			ТекущиеДанные.НомерСерииЛьготногоРецепта      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "НомерСерииЛьготногоРецепта");
			ТекущиеДанные.НомерЛьготногоРецепта           = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "НомерЛьготногоРецепта");
			ТекущиеДанные.ДатаРегистрацииЛьготногоРецепта = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "ДатаРегистрацииЛьготногоРецепта");
			ТекущиеДанные.НомерСерииРецепта               = Неопределено;
			ТекущиеДанные.НомерРецепта                    = Неопределено;
			ТекущиеДанные.ДатаРегистрацииРецепта          = Неопределено;
			
			ТекущиеДанные.ПроцентЛьготыЧислом = ПроцентЛьготыЧислом(ПроцентЛьготы);
			ТекущиеДанные.Рецепт = ОписаниеРецептаИлиЛьготы(ТекущиеДанные);
			
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЦенуПродажи(), ПараметрыЗаполненияЦены);
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСумму());
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ТекущиеДанные.ПроцентЛьготыЧислом));
			
			ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
			
		КонецЦикла;
		
		ПересчитатьДокументНаКлиенте();
		
	ИначеЕсли ЗначениеЗаполнено(ДатаРегистрацииРецепта) Тогда
		
		Для Каждого ТекущаяСтрока Из ВыделенныеСтроки Цикл
			
			ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрока);
			ТекущиеДанные.ПроцентЛьготы                   = Неопределено;
			ТекущиеДанные.НомерСерииЛьготногоРецепта      = Неопределено;
			ТекущиеДанные.НомерЛьготногоРецепта           = Неопределено;
			ТекущиеДанные.ДатаРегистрацииЛьготногоРецепта = Неопределено;
			ТекущиеДанные.НомерСерииРецепта               = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "НомерСерииРецепта");
			ТекущиеДанные.НомерРецепта                    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "НомерРецепта");
			ТекущиеДанные.ДатаРегистрацииРецепта          = ДатаРегистрацииРецепта;
			
			ТекущиеДанные.Рецепт = ОписаниеРецептаИлиЛьготы(ТекущиеДанные);
			
			Действия = ОбработкаТабличнойЧастиКлиентСервер;
			
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЦенуПродажи(), ПараметрыЗаполненияЦены);
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСумму());
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
			СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(0));
			
			ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
			
		КонецЦикла;
		
		ПересчитатьДокументНаКлиенте();
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПроцентЛьготыЧислом(Знач ПроцентЛьготыСсылка)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроцентЛьготыСсылка, "Процент");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоЛьгота100(ПроцентЛьготы)
	
	Возврат ПроцентЛьготы = ПредопределенноеЗначение("Справочник.ПроцентыЛьгот.Льгота_100");
	
КонецФункции

#КонецОбласти // ЛьготыИРецепты

////////////////////////////////////////////////////////////////////////////////
// Обработка штрихкодов
#Область ОбработкаШтрихкодов

&НаКлиенте
Процедура ОбработатьШтрихкодыОтложенно()
	
	ОтключитьОбработчикОжидания("ОбработатьШтрихкодыОтложенно");
	
	// Обрабатывать штрихкоды из очереди будем по одному.
	Если ОчередьОбработкиШтрихкодов.Количество() > 0 Тогда
		ДанныеШтрихкода = ОчередьОбработкиШтрихкодов[0];
		ОчередьОбработкиШтрихкодов.Удалить(0);
		ОбработатьШтрихкоды(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеШтрихкода));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьШтрихкоды(ДанныеШтрихкодов, ЗагрузкаИзТСД = Ложь)
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	ДействияСДобавленнымиСтроками = Новый Структура;
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ЗаполнитьЕдиницуИзмерения(), НоменклатураКлиентСервер.ВидЕдиницы_ПотребительскаяУпаковка());
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ЗаполнитьПараметрыУчета(), ПараметрыУчетаНоменклатуры);
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ЗаполнитьЗабраковкуСерий());
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ЗаполнитьПризнакСерияПромаркированаДляЦелейМДЛП());
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ПересчитатьКоэффициент());
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ПересчитатьКоличествоЕдиниц());
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ЗаполнитьЦенуПродажи(), Действия.ПолучитьПараметрыЗаполненияЦены(Объект));
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ПересчитатьСумму());
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ЗаполнитьСтавкуНДС(), Объект.НалогообложениеНДС);
	ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
	ДействияСДобавленнымиСтроками.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
	
	ДействияСИзмененнымиСтроками = Новый Структура;
	ДействияСИзмененнымиСтроками.Вставить(Действия.Действие_ПересчитатьКоличествоЕдиниц());
	ДействияСИзмененнымиСтроками.Вставить(Действия.Действие_ПересчитатьСумму());
	ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	ДействияСИзмененнымиСтроками.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
	ДействияСИзмененнымиСтроками.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
	ДействияСИзмененнымиСтроками.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
	
	ИзменятьКоличество = Не ТолькоПросмотр;
	ПараметрыДействия = ОбработкаТабличнойЧастиКлиент.ПолучитьПараметрыОбработкиШтрихкодов(ДанныеШтрихкодов, ДействияСДобавленнымиСтроками, ДействияСИзмененнымиСтроками);
	ПараметрыДействия.ИзменятьКоличество = ИзменятьКоличество;
	ПараметрыДействия.ПропускатьНенайденныеШтрихкоды = Истина;
	ПараметрыДействия.ПараметрыУчетаНоменклатуры = ПараметрыУчетаНоменклатуры;
	ПараметрыДействия.ШтрихкодыВТЧ = Истина;
	ПараметрыДействия.ПроверятьУникальностьУпаковокМДЛП = Истина;
	ПараметрыДействия.ЕстьКолонкаНомерКиЗ = Истина;
	
	ПараметрыДействия.ЕстьКолонкаШтрихкодBase64             = Истина;
	
	ПараметрыДействия.ЕстьКолонкаРезультатПроверкиКМ        = Истина;
	ПараметрыДействия.ЕстьКолонкаКодСтатусаПроверкиКМ       = Истина;
	ПараметрыДействия.ЕстьКолонкаПояснениеСтатусаПроверкиКМ = Истина;
	
	Доступность = Ложь;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПродолжитьОбработкуШтрихкодов", ЭтотОбъект, ПараметрыДействия);
	КонтрольКодовМаркировкиМДЛПКлиент.НачатьПроверкуКМНаФорме(ЭтотОбъект, Объект, ДанныеШтрихкодов, ОповещениеОЗавершении, ЗагрузкаИзТСД);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьОбработкуШтрихкодов(Результат, ПараметрыДействия) Экспорт
	
	Доступность = Истина;
	
	ЗаполнитьДокументПоПолученнымШтрихкодам(ПараметрыДействия);
	
	// Когда была обработана очередная порция данных, можно обрабатывать следующую.
	ПодключитьОбработчикОжидания("ОбработатьШтрихкодыОтложенно", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДокументПоПолученнымШтрихкодам(ПараметрыДействия)
	
	ОбработатьШтрихкодыНаСервере(ПараметрыДействия, КэшированныеЗначения);
	
	ОбработкаТабличнойЧастиКлиент.СообщитьОНеизвестныхШтрихкодах(ПараметрыДействия);
	
	Если ПараметрыДействия.ТекущаяСтрока <> Неопределено Тогда
		Элементы.Товары.ТекущаяСтрока = ПараметрыДействия.ТекущаяСтрока;
	КонецЕсли;
	
	ПересчитатьДокументНаКлиенте();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьШтрихкодыНаСервере(ПараметрыДействия, КэшированныеЗначения)
	
	ОбработкаТабличнойЧастиСервер.ОбработатьШтрихкоды(ЭтотОбъект, Объект, ПараметрыДействия, КэшированныеЗначения);
	ПроверитьЗаполнениеМаркированнойПродукцииНаСервере();
	
КонецПроцедуры

#КонецОбласти // ОбработкаШтрихкодов

////////////////////////////////////////////////////////////////////////////////
// Проверка КМ
#Область ПроверкаКМ

&НаКлиенте
Процедура НачатьПроверкуКМПередФискализациейЧека(ПараметрыОперацииФискализацииЧека, ОповещениеОЗавершении)
	
	ДанныеДляПроверки = Новый Массив;
	Для Каждого ПозицияЧека Из ПараметрыОперацииФискализацииЧека.ПозицииЧека Цикл
		
		Если Не ЗначениеЗаполнено(ПозицияЧека.КонтрольнаяМарка) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементДанных = КонтрольКодовМаркировкиСредствамиККТМДЛПКлиент.ЭлементДанныхДляПроверкиКМ();
		//ЭлементДанных.КодМаркировки         = ПозицияЧека.SGTIN;
		ЭлементДанных.ПолныйКодМаркировки   = ПозицияЧека.КонтрольнаяМарка;
		ЭлементДанных.ИдентификаторЭлемента = ПозицияЧека.КонтрольнаяМарка;
		ЭлементДанных.ДробноеКоличество     = ПозицияЧека.ДробноеКоличество;
		
		Если ЗначениеЗаполнено(ЭлементДанных.ДробноеКоличество)
		   И ЗначениеЗаполнено(ЭлементДанных.ДробноеКоличество.Числитель)
		   И ЗначениеЗаполнено(ЭлементДанных.ДробноеКоличество.Знаменатель) Тогда
			ЭлементДанных.ПланируемыйСтатусТовара = ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.МерныйТоварВСтадииРеализации");
		КонецЕсли;
		
		ДанныеДляПроверки.Добавить(ЭлементДанных);
		
	КонецЦикла;
	
	Если ДанныеДляПроверки.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении);
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершенииПроверкиКМ = Новый ОписаниеОповещения("ЗакончитьПроверкуКМПередФискализациейЧека", ЭтотОбъект, ОповещениеОЗавершении);
	
	ПараметрыПроверкиКМ = КонтрольКодовМаркировкиСредствамиККТМДЛПКлиент.ПараметрыПроверкиКМ(ЭтотОбъект);
	ПараметрыПроверкиКМ.ОповещениеОЗавершении = ОповещениеОЗавершенииПроверкиКМ;
	ПараметрыПроверкиКМ.ККТФФД12МДЛП          = ПараметрыКассыККМ.ИдентификаторУстройства;
	ПараметрыПроверкиКМ.ДанныеДляПроверки     = ДанныеДляПроверки;
	
	// Проверка КМ перед фискализацией чека выполняется всегда, т.к. нужно добавить КМ в буфер кассы.
	ПараметрыПроверкиКМ.ТолькоПодготовитьДанныеДляВыборочногоКонтроляКМ = Ложь;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыПроверкиКМ,
		КонтрольКодовМаркировкиСредствамиККТМДЛПКлиент.ПараметрыПроверкиКМ_РежимПроверкаКМНепосредственноВоВремяПробитияЧекаККТСИгнорированием(),
		Истина);
	
	КонтрольКодовМаркировкиСредствамиККТМДЛПКлиент.НачатьПроверкуКМ(ПараметрыПроверкиКМ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьПроверкуКМПередФискализациейЧека(РезультатПроверки, ОповещениеОЗавершении) Экспорт
	
	Если ЗначениеЗаполнено(РезультатПроверки) И РезультатПроверки.ЕстьОшибки Тогда
		
		// Закрыть сессию проверки КМ на ККТ, если была критическая ошибка.
		ЗакрытьСессиюПроверкиКМ();
		
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьСессиюПроверкиКМ()
	
	Если МенеджерОборудованияКлиент.СессияПроверкиКодовМаркировки(ПараметрыКассыККМ.ИдентификаторУстройства) <> Неопределено Тогда
		ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьЗакрытииСессииРегистрацииКМ(
			Новый ОписаниеОповещения("ЗакрытьСессиюПроверкиКМЗавершение", ЭтотОбъект),
			УникальныйИдентификатор,
			ПараметрыКассыККМ.ИдентификаторУстройства);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьСессиюПроверкиКМЗавершение(РезультатЗакрытияСессии, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытияСессии) И Не РезультатЗакрытияСессии.Результат Тогда
		ТекстОшибки = НСтр("ru = 'Не удалось закрыть сессию проверки КМ по причине:'");
		ТекстОшибки = ТекстОшибки + Символы.ПС + РезультатЗакрытияСессии.ОписаниеОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗапомнитьРезультатыПроверкиКМ(Объект)
	
	РезультатыПроверкиКМ = Новый Соответствие;
	Для Индекс = 0 По Объект.Товары.Количество() - 1 Цикл
		РезультатПроверкиКМ = Новый Структура("РезультатПроверкиКМ, КодСтатусаПроверкиКМ, ПояснениеСтатусаПроверкиКМ");
		ЗаполнитьЗначенияСвойств(РезультатПроверкиКМ, Объект.Товары[Индекс]);
		РезультатыПроверкиКМ.Вставить(Индекс, РезультатПроверкиКМ);
	КонецЦикла;
	
	Возврат РезультатыПроверкиКМ;
	
КонецФункции

&НаСервере
Процедура ВосстановитьРезультатыПроверкиКМ(РезультатыПроверкиКМ)
	
	Для Индекс = 0 По Объект.Товары.Количество() - 1 Цикл
		РезультатПроверкиКМ = РезультатыПроверкиКМ.Получить(Индекс);
		Если РезультатПроверкиКМ <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(Объект.Товары[Индекс], РезультатПроверкиКМ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// Обработка подбора
#Область ОбработкаПодбора

&НаСервере
Процедура ОбработатьПодбор(Знач АдресТоваровВХранилище, КэшированныеЗначения)
	
	СписокТоваров = ПолучитьИзВременногоХранилища(АдресТоваровВХранилище);
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьПараметрыУчета(), ПараметрыУчетаНоменклатуры);
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЗабраковкуСерий());
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьПризнакСерияПромаркированаДляЦелейМДЛП());
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьСтавкуНДС(), Объект.НалогообложениеНДС);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСумму());
	ПараметрыПересчета = Действия.ПолучитьПараметрыПересчетаСуммыНДС(Объект);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуСНДС(), ПараметрыПересчета);
	СтруктураДействий.Вставить(Действия.Действие_ПересчитатьСуммуЛьготы(), Действия.ПолучитьПараметрыПересчетаСуммыЛьготы(ПроцентЛьготыЧислом));
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшированныхЗначений();
	
	Для Каждого СтрокаТовара Из СписокТоваров Цикл
		
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовара);
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТабличнойЧасти(НоваяСтрока, СтруктураДействий, КэшированныеЗначения);
		
	КонецЦикла;
	
	ОбновитьДанныеИнформационнойПанели(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // ОбработкаПодбора

////////////////////////////////////////////////////////////////////////////////
// Операции с чеком
#Область ОперацииСЧеком

&НаКлиенте
Процедура ЗагрузитьНовыйЧек(Ссылка = Неопределено, ИспользоватьЛьготы = Ложь)
	
	Если Объект.Статус <> ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.Пробит") Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Ссылка", Ссылка);
		ДополнительныеПараметры.Вставить("ИспользоватьЛьготы", ИспользоватьЛьготы);
		
		Отказ = Ложь;
		Оповестить = Новый ОписаниеОповещения("ЗагрузитьНовыйЧекЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПередЗакрытиемЧека(Отказ, Оповестить);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
	Иначе
		
		НовыйЧекНаСервере(Ссылка, ИспользоватьЛьготы);
		ПересчитатьДокументНаКлиенте();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНовыйЧекЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	НовыйЧекНаСервере(ДополнительныеПараметры.Ссылка, ДополнительныеПараметры.ИспользоватьЛьготы);
	ПересчитатьДокументНаКлиенте();
	
КонецПроцедуры

&НаСервере
Процедура НовыйЧекНаСервере(Ссылка = Неопределено, ИспользоватьЛьготы = Ложь)
	
	Если Ссылка = Неопределено Тогда
		НовыйЧек = Документы.ЧекККМ.СоздатьДокумент();
		НовыйЧек.Заполнить(Новый Структура("КассаККМ, ИспользоватьЛьготы", Объект.КассаККМ, ИспользоватьЛьготы));
	Иначе
		НовыйЧек = Ссылка.ПолучитьОбъект();
	КонецЕсли;
	
	НовыйЧек.Дата = ТекущаяДатаСеанса();
	
	РазблокироватьДанныеФормыДляРедактирования();
	ЗначениеВРеквизитФормы(НовыйЧек, "Объект");
	
	ТолькоПросмотр = Ложь;
	Модифицированность = Ложь;
	
	ПриСозданииНовогоПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитьЧек()
	
	ЕстьОшибки = Ложь;
	
	ОчиститьСообщения();
	
	Если Объект.ПометкаУдаления Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Документ помечен на удаление'"), Объект.Ссылка,,, ЕстьОшибки);
	КонецЕсли;
	
	Объект.Дата = ОбщегоНазначенияКлиент.ДатаСеанса();
	Модифицированность = Истина;
	
	Если Не ЕстьОшибки И ПроверитьЗаполнение() Тогда
		Оповестить = Новый ОписаниеОповещения("ПробитьЧекПослеПроведения", ЭтотОбъект);
		ВыполнитьДействиеПослеЗаписи(Оповестить,
			НСтр("ru = 'Перед выполнением операции пробития чека не удалось провести документ.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитьЧекПослеПроведения(Проведен, ДополнительныеПараметры) Экспорт
	
	Если Не Проведен Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПробитьЧекЗавершение", ЭтотОбъект);
	Оповещение = Новый ОписаниеОповещения("ПробитьЧекВыполнитьЗавершение", ЭтотОбъект, Новый Структура("ОписаниеОповещения", ОписаниеОповещения));
	
	Если Не ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования Тогда
		
		ПараметрыОперацииФискализацииЧека = ПараметрыОперацииФискализацииЧека();
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПробитьЧекПослеПроверкиКМ", ЭтотОбъект, ПараметрыОперацииФискализацииЧека);
		НачатьПроверкуКМПередФискализациейЧека(ПараметрыОперацииФискализацииЧека, ОповещениеОЗавершении);
		
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, Новый Структура("Результат", Истина));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитьЧекПослеПроверкиКМ(Результат, ПараметрыОперацииФискализацииЧека) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПробитьЧекЗавершение", ЭтотОбъект);
	Оповещение = Новый ОписаниеОповещения("ПробитьЧекВыполнитьЗавершение", ЭтотОбъект, Новый Структура("ОписаниеОповещения", ОписаниеОповещения));
	
	ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФискализациюЧекаНаФискальномУстройстве(
		Оповещение,
		УникальныйИдентификатор,
		ПараметрыКассыККМ.ИдентификаторУстройства,
		ПараметрыОперацииФискализацииЧека);
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитьЧекВыполнитьЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	ИзмененныеДанныеЗаписаны = Ложь;
	ВыполненаОперацияНаУстройстве = Ложь;
	
	Если РезультатВыполнения.Результат Тогда
		
		Для Каждого СтрокаТовара Из Объект.Товары Цикл
			// Очистка полного кода маркировки после успешного пробития чека.
			СтрокаТовара.ШтрихкодBase64 = Неопределено;
		КонецЦикла;
		
		Объект.Дата   = ОбщегоНазначенияКлиент.ДатаСеанса();
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.Пробит");
		
		ДанныеДляЖурналаРегистрации = Новый Структура;
		ДанныеДляЖурналаРегистрации.Вставить("Дата"  , Объект.Дата);
		ДанныеДляЖурналаРегистрации.Вставить("Статус", Объект.Статус);
		
		ПараметрыФискализации = Неопределено;
		Если РезультатВыполнения.Свойство("ВыходныеПараметры")
		   И РезультатВыполнения.ВыходныеПараметры.Количество() > 1 Тогда
			
			ДанныеДляЖурналаРегистрации.Вставить("НомерЧекаККМ", РезультатВыполнения.ВыходныеПараметры[1]);
			
			// Если данные в регистр ФискальныеОперации не были записаны - произошла ошибка при записи.
			Если РезультатВыполнения.ВыходныеПараметры.Количество() > 8 Тогда
				ПараметрыФискализации = РезультатВыполнения.ВыходныеПараметры[8];
			КонецЕсли;
			
		КонецЕсли;
		
		ВыполненаОперацияНаУстройстве = Истина;
		Модифицированность = Истина;
		
		ТребуетсяПовторнаяПопыткаЗаписи = Ложь;
		ИзмененныеДанныеЗаписаны = ЗаписатьФискальнуюОперациюНаКлиенте(ТребуетсяПовторнаяПопыткаЗаписи, ПараметрыФискализации);
		Если Не ИзмененныеДанныеЗаписаны Тогда
			
			ДополнительныеПараметрыПовторЗаписи = Новый Структура;
			ДополнительныеПараметрыПовторЗаписи.Вставить("ПараметрыФискализации", ПараметрыФискализации);
			ДополнительныеПараметрыПовторЗаписи.Вставить("ОписаниеОповещения", ДополнительныеПараметры.ОписаниеОповещения);
			ДополнительныеПараметрыПовторЗаписи.Вставить("ТекстСообщения", НСтр("ru = 'После пробития чека на ФР не удалось сохранить документ.'"));
			ДополнительныеПараметрыПовторЗаписи.Вставить("ВозвращатьРезультатФункции", Ложь);
			ДополнительныеПараметрыПовторЗаписи.Вставить("РезультатПриУспешномПроведении", Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны", Истина, Истина));
			ДополнительныеПараметрыПовторЗаписи.Вставить("РезультатПриОтмене", Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны", Истина, Ложь));
			ДополнительныеПараметрыПовторЗаписи.Вставить("ИмяПроцедуры", "ЗаписатьФискальнуюОперациюНаСервере");
			ДополнительныеПараметрыПовторЗаписи.Вставить("РезультатОперации", ИзмененныеДанныеЗаписаны);
			
			Оповестить = Новый ОписаниеОповещения("ОшибкаПриПроведенииЧекаВопросЗавершение", ЭтотОбъект, ДополнительныеПараметрыПовторЗаписи);
			Если Не ТребуетсяПовторнаяПопыткаЗаписи Тогда
				ВыполнитьОбработкуОповещения(Оповестить, КодВозвратаДиалога.Отмена);
			Иначе
				ПоказатьВопрос(
					Оповестить,
					ДополнительныеПараметрыПовторЗаписи.ТекстСообщения,
					РежимДиалогаВопрос.ПовторитьОтмена,
					10,
					КодВозвратаДиалога.Повторить,
					,
					КодВозвратаДиалога.Повторить);
			КонецЕсли;
			
			Возврат;
			
		КонецЕсли;
		
	Иначе
		
		ШаблонСообщенияОбОшибке = НСтр(
			"ru='При печати чека произошла ошибка.
				|Чек не напечатан на фискальном регистраторе.
				|Дополнительное описание:
				|%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ШаблонСообщенияОбОшибке, "%ДополнительноеОписание%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		// Закрыть сессию проверки КМ на ККТ, если была критическая ошибка.
		ЗакрытьСессиюПроверкиКМ();
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОписаниеОповещения,
		Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны",
			ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны));
	
КонецПроцедуры

&НаКлиенте
Функция ЗаписатьФискальнуюОперациюНаКлиенте(ТребуетсяПовторнаяПопыткаЗаписи, ПараметрыФискализации)
	
	Результат = ЗаписатьФискальнуюОперациюНаСервере(ТребуетсяПовторнаяПопыткаЗаписи, ПараметрыФискализации);
	
	Если Результат Тогда
		Оповестить("Запись_ЧекККМ", Новый Структура, Неопределено);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗаписатьФискальнуюОперациюНаСервере(ТребуетсяПовторнаяПопыткаЗаписи, Знач ПараметрыФискализации)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить();
		ЭлементБлокировки.Область = "Документ.ЧекККМ";
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Объект.Ссылка);
		Блокировка.Заблокировать();
		
		Если ПараметрыФискализации <> Неопределено
		   И Не ПараметрыФискализации.ОперацияЗаписана Тогда
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить();
			ЭлементБлокировки.Область = "РегистрСведений.ФискальныеОперации";
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			ЭлементБлокировки.УстановитьЗначение("ДокументОснование", ПараметрыФискализации.ДокументОснование);
			Блокировка.Заблокировать();
			
			ОборудованиеЧекопечатающиеУстройстваВызовСервера.ЗаписатьФискальнуюОперацию(ПараметрыФискализации);
			
		КонецЕсли;
		
		Результат = Записать(Новый Структура("РежимЗаписи, РежимПроведения", РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный));
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписатьОшибкуВЖурналРегистрации(ИнформацияОбОшибке());
		ТребуетсяПовторнаяПопыткаЗаписи = Истина;
		Возврат Ложь;
		
	КонецПопытки;
	
	Если Не Результат Тогда
		ЗаполнитьСлужебныеРеквизиты();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПробитьЧекЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.ВыполненаОперацияНаУстройстве
	   И Результат.ИзмененныеДанныеЗаписаны Тогда
		
		ТолькоПросмотр = Истина;
		
		ПодключитьОбработчикОжидания("УстановитьТекущийЭлементНаКнопкуНовыйЧек", 0.1, Истина);
		
	Иначе
		
		Если Результат.ВыполненаОперацияНаУстройстве И Не Результат.ИзмененныеДанныеЗаписаны Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Данные", Объект.Ссылка);
			ПараметрыФормы.Вставить("ДанныеДляЖурналаРегистрации", ДанныеДляЖурналаРегистрации);
			ПараметрыФормы.Вставить("ТекстСообщения",
				НСтр("ru = 'ВНИМАНИЕ! Произошла исключительная ситуация:
				|Чек ККМ пробит, но не зафиксирован в системе.'"));
			
			ОткрытьФорму("Документ.ЧекККМ.Форма.ОшибкаЗаписи", ПараметрыФормы, ЭтотОбъект);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УдалитьЧек(Ссылка)
	
	Результат = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	ЧекОбъект = Ссылка.ПолучитьОбъект();
	
	Если ЧекОбъект.Статус = Перечисления.СтатусыЧековККМ.Пробит Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Пробитый чек не может быть удален'"));
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		ЧекОбъект.Удалить();
		Результат = Истина;
	Исключение
		Результат = Ложь;
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЧека(Отказ, ОповещениеОЗакрытии = Неопределено)
	
	Кнопки = Новый СписокЗначений;
	
	Если Не ТолькоПросмотр И (Модифицированность Или Не Объект.Ссылка.Пустая()) Тогда
		
		ТекстВопроса = НСтр("ru = 'Данные чека ККМ были изменены.'");
		
		Если КонтрольНаСкладеОтключен Тогда
			Если ПраваДоступа.Отложить Тогда
				Кнопки.Добавить(1, НСтр("ru = 'Отложить'"));
			КонецЕсли;
		Иначе
			Если ПраваДоступа.Отложить Тогда
				Кнопки.Добавить(1, НСтр("ru = 'Отложить без резервирования'"));
			КонецЕсли;
			Если ПраваДоступа.Зарезервировать Тогда
				Кнопки.Добавить(2, НСтр("ru = 'Зарезервировать до...'"));
			КонецЕсли;
		КонецЕсли;
		
		Если Не Объект.Ссылка.Пустая() И ПраваДоступа.КорректировкаСтрок Тогда
			Кнопки.Добавить(4, НСтр("ru = 'Удалить чек'"));
		КонецЕсли;
		
		Если ПраваДоступа.КорректировкаСтрок Или Объект.Ссылка.Пустая()Тогда
			Если Модифицированность Тогда
				Кнопки.Добавить(3, НСтр("ru = 'Закрыть без сохранения'"));
			Иначе
				Кнопки.Добавить(3, НСтр("ru = 'Закрыть'"));
			КонецЕсли;
		КонецЕсли;
		
		Кнопки.Добавить(99, НСтр("ru = 'Отмена'"));
		
	КонецЕсли;
	
	Если Кнопки.Количество() > 0 Тогда
		
		Отказ = Истина;
		ОповещениеПродолжения = Новый ОписаниеОповещения("ПередЗакрытиемЧекаПродолжение", ЭтотОбъект, Новый Структура("ОповещенияОЗавершении", ОповещениеОЗакрытии));
		ПоказатьВопрос(ОповещениеПродолжения, ТекстВопроса, Кнопки);
		
	Иначе
		
		Если ОповещениеОЗакрытии <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеОЗакрытии, Неопределено);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЧекаПродолжение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ОповещениеПродолжения = Новый ОписаниеОповещения("ПередЗакрытиемЧекаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Если РезультатВопроса = 1 Тогда
		
		// Отложить чек без резервирования
		ОтложитьНаКлиенте(ОповещениеПродолжения);
		
	ИначеЕсли РезультатВопроса = 2 Тогда
		
		// Отложить чек с резервированием
		
		ОповещениеОтложитьДо = Новый ОписаниеОповещения("ЗарезервироватьНаКлиенте", ЭтотОбъект, Новый Структура("ОповещениеОЗавершении", ОповещениеПродолжения));
		РезервироватьДо = ОбщегоНазначенияКлиент.ДатаСеанса() + 3*60*60;
		ПоказатьВводДаты(ОповещениеОтложитьДо, РезервироватьДо, НСтр("ru = 'Зарезервировать до:'"), ЧастиДаты.ДатаВремя);
		
	ИначеЕсли РезультатВопроса = 3 Тогда
		
		// Закрыть без сохранения.
		ВыполнитьОбработкуОповещения(ОповещениеПродолжения, Истина);
		
	ИначеЕсли РезультатВопроса = 4 Тогда
		
		// Удалить чек.
		УдалениеВыполнено = УдалитьЧек(Объект.Ссылка);
		Если УдалениеВыполнено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеПродолжения, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарезервироватьНаКлиенте(ОтложенДо, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ОтложенДо) Тогда
		Возврат;
	КонецЕсли;
	
	ТребуетсяПовторнаяПопыткаЗаписи = Ложь;
	
	Объект.ОтложенДо = ОтложенДо;
	Результат = ЗарезервироватьНаСервере(ТребуетсяПовторнаяПопыткаЗаписи);
	
	Если Не Результат Тогда
		
		ДополнительныеПараметрыПовторЗаписи = Новый Структура;
		ДополнительныеПараметрыПовторЗаписи.Вставить("ОписаниеОповещения", ДополнительныеПараметры.ОповещениеОЗавершении);
		ДополнительныеПараметрыПовторЗаписи.Вставить("ТекстСообщения", НСтр("ru = 'После выполнения операции отмены оплаты не удалось записать документ.'"));
		ДополнительныеПараметрыПовторЗаписи.Вставить("ВозвращатьРезультатФункции", Истина);
		ДополнительныеПараметрыПовторЗаписи.Вставить("ИмяПроцедуры", "ЗарезервироватьНаСервере");
		ДополнительныеПараметрыПовторЗаписи.Вставить("РезультатОперации", Результат);
		
		Оповестить = Новый ОписаниеОповещения("ОшибкаПриПроведенииЧекаВопросЗавершение", ЭтотОбъект, ДополнительныеПараметрыПовторЗаписи);
		
		Если ТребуетсяПовторнаяПопыткаЗаписи Тогда
			ПоказатьВопрос(
				Оповестить,
				ДополнительныеПараметрыПовторЗаписи.ТекстСообщения,
				РежимДиалогаВопрос.ПовторитьОтмена,
				10,
				КодВозвратаДиалога.Повторить,
				,
				КодВозвратаДиалога.Повторить);
		Иначе
			ВыполнитьОбработкуОповещения(Оповестить, КодВозвратаДиалога.Отмена);
		КонецЕсли;
		
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗарезервироватьНаСервере(ТребуетсяПовторнаяПопыткаЗаписи)
	
	РезервированиеВыполнено = Ложь;
	
	СтарыйСтатус = Объект.Статус;
	Объект.Статус = Перечисления.СтатусыЧековККМ.ТоварЗарезервирован;
	Объект.Дата = ТекущаяДатаСеанса();
	
	Попытка
		
		РезервированиеВыполнено = ЗаписатьНаСервере(ТребуетсяПовторнаяПопыткаЗаписи);
		
		Если Не РезервированиеВыполнено Тогда
			Объект.Статус = СтарыйСтатус;
		КонецЕсли;
		
	Исключение
		
		Объект.Статус = СтарыйСтатус;
		РезервированиеВыполнено = Ложь;
		
	КонецПопытки;
	
	Возврат РезервированиеВыполнено;
	
КонецФункции

&НаКлиенте
Процедура ОтложитьНаКлиенте(ОповещениеПриЗавершении)
	
	Объект.ОтложенДо = Дата(1,1,1);
	Модифицированность = Истина;
	
	ТребуетсяПовторнаяПопыткаЗаписи = Ложь;
	Результат = ОтложитьНаСервере(ТребуетсяПовторнаяПопыткаЗаписи);
	
	Если Не Результат Тогда
		
		ДополнительныеПараметрыПопытка = Новый Структура;
		ДополнительныеПараметрыПопытка.Вставить("ОписаниеОповещения", ОповещениеПриЗавершении);
		ДополнительныеПараметрыПопытка.Вставить("ТекстСообщения", НСтр("ru = 'После выполнения операции отмены оплаты не удалось записать документ.'"));
		ДополнительныеПараметрыПопытка.Вставить("ВозвращатьРезультатФункции", Истина);
		ДополнительныеПараметрыПопытка.Вставить("ИмяПроцедуры", "ОтложитьНаСервере");
		ДополнительныеПараметрыПопытка.Вставить("РезультатОперации", Результат);
		
		Если Не ТребуетсяПовторнаяПопыткаЗаписи Тогда
			ОшибкаПриПроведенииЧекаВопросЗавершение(КодВозвратаДиалога.Отмена, ДополнительныеПараметрыПопытка);
			Возврат;
		КонецЕсли;
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ОшибкаПриПроведенииЧекаВопросЗавершение", ЭтотОбъект, ДополнительныеПараметрыПопытка),
			ДополнительныеПараметрыПопытка.ТекстСообщения,
			РежимДиалогаВопрос.ПовторитьОтмена, 10, КодВозвратаДиалога.Повторить,,КодВозвратаДиалога.Повторить);
		Возврат;
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОтложитьНаСервере(ТребуетсяПовторнаяПопыткаЗаписи)
	
	ОтложитьВыполнено = Истина;
	
	СтарыйСтатус  = Объект.Статус;
	Объект.Статус = Перечисления.СтатусыЧековККМ.Отложен;
	Объект.Дата   = ТекущаяДатаСеанса();
	
	Попытка
		
		ОтложитьВыполнено = ЗаписатьНаСервере(ТребуетсяПовторнаяПопыткаЗаписи);
		
		Если Не ОтложитьВыполнено Тогда
			Объект.Статус = СтарыйСтатус;
		КонецЕсли;
		
	Исключение
		
		Объект.Статус = СтарыйСтатус;
		ОтложитьВыполнено = Ложь;
		
	КонецПопытки;
	
	Возврат ОтложитьВыполнено;
	
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЧекаЗавершение(РезультатОперации, ДополнительныеПараметры) Экспорт
	
	Оповестить("Запись_ЧекККМ", Новый Структура, Неопределено);
	
	Если РезультатОперации Тогда
		
		Если ДополнительныеПараметры.ОповещенияОЗавершении = Неопределено Тогда
			ПринудительноеЗавершениеРаботы = Истина;
			Закрыть(Неопределено);
		Иначе
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияОЗавершении, Неопределено);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействиеПослеЗаписи(ОповещениеПриЗавершении, ТекстСообщения)
	
	ТребуетсяПовторнаяПопыткаЗаписи = Ложь;
	ИзмененныеДанныеЗаписаны = ЗаписатьНаКлиенте(ТребуетсяПовторнаяПопыткаЗаписи);
	Если Не ИзмененныеДанныеЗаписаны Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОповещениеПриЗавершении);
		ДополнительныеПараметры.Вставить("ТекстСообщения", ТекстСообщения);
		ДополнительныеПараметры.Вставить("ВозвращатьРезультатФункции", Истина);
		ДополнительныеПараметры.Вставить("ИмяПроцедуры", "ЗаписатьНаСервере");
		ДополнительныеПараметры.Вставить("РезультатОперации", ИзмененныеДанныеЗаписаны);
		
		Если Не ТребуетсяПовторнаяПопыткаЗаписи Тогда
			ОшибкаПриПроведенииЧекаВопросЗавершение(КодВозвратаДиалога.Отмена, ДополнительныеПараметры);
			Возврат;
		КонецЕсли;
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ОшибкаПриПроведенииЧекаВопросЗавершение", ЭтотОбъект, ДополнительныеПараметры),
			ДополнительныеПараметры.ТекстСообщения,
			РежимДиалогаВопрос.ПовторитьОтмена,
			10,
			КодВозвратаДиалога.Повторить,
			,
			КодВозвратаДиалога.Повторить);
		Возврат;
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаписатьНаКлиенте(ТребуетсяПовторнаяПопыткаЗаписи = Неопределено)
	
	Если Модифицированность Тогда
		
		Результат = ЗаписатьНаСервере(ТребуетсяПовторнаяПопыткаЗаписи);
		
		Если Результат Тогда
			Оповестить("Запись_ЧекККМ", Новый Структура, Неопределено);
		КонецЕсли;
		
		Возврат Результат;
		
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ЗаписатьНаСервере(ТребуетсяПовторнаяПопыткаЗаписи = Неопределено)
	
	Если Объект.Ссылка.Пустая() Тогда
		РезультатыПроверкиКМ = ЗапомнитьРезультатыПроверкиКМ(Объект);
		Значение = РеквизитФормыВЗначение("Объект");
		Значение.УстановитьСсылкуНового(Документы.ЧекККМ.ПолучитьСсылку());
		ЗначениеВРеквизитФормы(Значение, "Объект");
		ВосстановитьРезультатыПроверкиКМ(РезультатыПроверкиКМ);
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить();
		ЭлементБлокировки.Область = "Документ.ЧекККМ";
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Объект.Ссылка);
		Блокировка.Заблокировать();
		
		Результат = Записать(Новый Структура("РежимЗаписи, РежимПроведения", РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный));
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписатьОшибкуВЖурналРегистрации(ИнформацияОбОшибке());
		ТребуетсяПовторнаяПопыткаЗаписи = Истина;
		Возврат Ложь;
		
	КонецПопытки;
	
	Если Не Результат Тогда
		ЗаполнитьСлужебныеРеквизиты();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОшибкаПриПроведенииЧекаВопросЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Отмена Тогда
		
		ТребуетсяПовторнаяПопыткаЗаписи = Ложь;
		Если ДополнительныеПараметры.ИмяПроцедуры = "ЗаписатьНаСервере" Тогда
			РезультатОперации = ЗаписатьНаСервере(ТребуетсяПовторнаяПопыткаЗаписи);
		ИначеЕсли ДополнительныеПараметры.ИмяПроцедуры = "ЗаписатьФискальнуюОперациюНаСервере" Тогда
			РезультатОперации = ЗаписатьФискальнуюОперациюНаСервере(ТребуетсяПовторнаяПопыткаЗаписи, ДополнительныеПараметры.ПараметрыФискализации);
		ИначеЕсли ДополнительныеПараметры.ИмяПроцедуры = "ОтложитьНаСервере" Тогда
			РезультатОперации = ОтложитьНаСервере(ТребуетсяПовторнаяПопыткаЗаписи);
		ИначеЕсли ДополнительныеПараметры.ИмяПроцедуры = "ЗарезервироватьНаСервере" Тогда
			РезультатОперации = ЗарезервироватьНаСервере(ТребуетсяПовторнаяПопыткаЗаписи);
		КонецЕсли;
		
		Если РезультатОперации Тогда
			
			ВыполнитьОбработкуОповещения(
				ДополнительныеПараметры.ОписаниеОповещения,
				?(ДополнительныеПараметры.ВозвращатьРезультатФункции, РезультатОперации, ДополнительныеПараметры.РезультатПриУспешномПроведении));
			
		Иначе
			
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ОшибкаПриПроведенииЧекаВопросЗавершение", ЭтотОбъект, ДополнительныеПараметры),
				ДополнительныеПараметры.ТекстСообщения,
				РежимДиалогаВопрос.ПовторитьОтмена,
				10,
				КодВозвратаДиалога.Повторить,
				,
				КодВозвратаДиалога.Повторить);
			
		КонецЕсли;
		
	Иначе
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОписаниеОповещения,
			?(ДополнительныеПараметры.ВозвращатьРезультатФункции, ДополнительныеПараметры.РезультатОперации, ДополнительныеПараметры.РезультатПриОтмене));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыОперацииФискализацииЧека()
	
	ПараметрыФискализацииЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека();
	
	ДанныеПоТоварам = Документы.ЧекККМ.ДанныеПоТоварам(Объект.Ссылка);
	
	ЕстьСерииПромаркированныеДляЦелейМДЛП = Ложь;
	Для Каждого СтрокаТЧ Из ДанныеПоТоварам Цикл
		
		НаименованиеТовара = Строка(СтрокаТЧ.Номенклатура)
			+ ?(ЗначениеЗаполнено(СтрокаТЧ.СерияНоменклатуры), " (" + Строка(СтрокаТЧ.СерияНоменклатуры) + ")", "");
		
		Если СтрокаТЧ.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
			ЗначениеСтавкиНДС = Неопределено;
		Иначе
			СтавкаНДС = ЦенообразованиеБольничнаяАптекаКлиентСервер.ПолучитьСтавкуНДСЧислом(СтрокаТЧ.СтавкаНДС);
			ЗначениеСтавкиНДС = СтавкаНДС * 100;
		КонецЕсли;
		
		СтрокаПозицииЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыФискальнойСтрокиЧека();
		СтрокаПозицииЧека.НомерСтрокиТовара = СтрокаТЧ.НомерСтроки;
		СтрокаПозицииЧека.Наименование   = НаименованиеТовара;
		СтрокаПозицииЧека.Количество     = СтрокаТЧ.КоличествоВЕдиницахИзмерения;
		СтрокаПозицииЧека.Цена           = СтрокаТЧ.Цена;
		СтрокаПозицииЧека.Сумма          = СтрокаТЧ.СуммаСНДС;
		СтрокаПозицииЧека.ЦенаСоСкидками = Окр(СтрокаПозицииЧека.Сумма / СтрокаПозицииЧека.Количество, 2, 1); // Обязательно, Цена с учетом скидок и наценок (Необходимо начиная с ФФД 1.0.5 и выше)
		СтрокаПозицииЧека.СтавкаНДС      = ЗначениеСтавкиНДС;
		СтрокаПозицииЧека.СуммаНДС       = СтрокаТЧ.СуммаНДС;
		СтрокаПозицииЧека.НомерСекции    = 1;
		СтрокаПозицииЧека.Штрихкод       = СтрокаТЧ.Штрихкод;
		СтрокаПозицииЧека.КодЕдиницыИзмерения = "796"; // пока шт. См. разъяснительное письме ФНС от 28.07.2021 № АБ-4-20/10633@
		
		СтрокаПозицииЧека.ПризнакСпособаРасчета  = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой; // Признак способа расчета. Обязательно для ФФД 1.0.5 и выше.
		СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Товар; // Признак предмета расчета. Обязательно для ФФД 1.1 и выше.
		
		Если СтрокаТЧ.СерияПромаркированаДляЦелейМДЛП Тогда
			
			ЕстьСерииПромаркированныеДляЦелейМДЛП = Истина;
			
			Если ВерсияФФД >= "1.2" Тогда
				// ФФД 1.2
				СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ТоварМаркируемыйСИИмеющийКМ; // Для ФФД 1.2.
				СтрокаПозицииЧека.КонтрольнаяМарка    = СтрокаТЧ.ШтрихкодBase64;
				СтрокаПозицииЧека.ШтрихкодBase64      = СтрокаТЧ.ШтрихкодBase64;
				СтрокаПозицииЧека.КодЕдиницыИзмерения = "796"; // шт
				СтрокаПозицииЧека.ОтраслевойРеквизит  = ОтраслевойРеквизитМДЛП(СтрокаТЧ); // Тег 1260
				СтрокаПозицииЧека.ДробноеКоличество   = ДробноеКоличествоТовараМДЛП(СтрокаТЧ); // Тег 1291
			Иначе
				// ФФД 1.1 или ФФД 1.0.5
				СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры = ИнтеграцияМДЛПКлиентСервер.СформироватьПараметрДанныеКодаТоварнойНоменклатуры(СтрокаТЧ.НомерКИЗ); // Тег 1162
				СтрокаПозицииЧека.ДополнительныйРеквизит = ДополнительныйРеквизитПредметаРасчетаМДЛП(СтрокаТЧ); // Тег 1191
				СтрокаПозицииЧека.ШтрихкодBase64 = СтрЗаменить(СтрЗаменить(СтрокаТЧ.ШтрихкодBase64, Символы.ПС, ""), Символы.ВК, "");
			КонецЕсли;
			
			СтрокаПозицииЧека.Количество = 1; // Тег 1023. Согласно Примечанию 18 к таблице 20 Приказа ФНС от 21 марта 2017 г. № ММВ-7-20/229@.
			
		Иначе
			
			ОписаниеПолучаемыхДанных = Новый Структура("КодВидаНоменклатурнойКлассификации", "КлассификацияМедицинскихИзделийПоВидам.КодВидаНоменклатурнойКлассификации");
			ДанныеНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТЧ.Номенклатура, ОписаниеПолучаемыхДанных);
			Если ЗначениеЗаполнено(ДанныеНоменклатуры.КодВидаНоменклатурнойКлассификации) Тогда
				СтрокаПозицииЧека.КодВидаНоменклатурнойКлассификации = ДанныеНоменклатуры.КодВидаНоменклатурнойКлассификации;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЭтоЛьгота100(Объект.ПроцентЛьготы) Тогда
			СтрокаПозицииЧека.ЦенаСоСкидками = 0; // Тег 1079.
			СтрокаПозицииЧека.Сумма          = 0; // Тег 1043.
		КонецЕсли;
		
		ПараметрыФискализацииЧека.ПозицииЧека.Добавить(СтрокаПозицииЧека);
		
	КонецЦикла;
	
	ПараметрыФискализацииЧека.ТипРасчета = ПредопределенноеЗначение("Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств");
	
	ЕстьЭлектроннаяОтправка = Ложь;
	Если ДанныеФискальнойОперации.ВариантОтправкиЭлектронногоЧека = ПредопределенноеЗначение("Перечисление.ВариантыОтправкиЭлектронногоЧекаПокупателю.ОтправитьEmail")
	   И ЗначениеЗаполнено(ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека) Тогда
		ПараметрыФискализацииЧека.Отправляет1СEmail = Не ДанныеФискальнойОперации.ОтправлятьEmailЧерезОФД;
		ПараметрыФискализацииЧека.ПокупательEmail = ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека;
		ЕстьЭлектроннаяОтправка = Истина;
	КонецЕсли;
	Если ДанныеФискальнойОперации.ВариантОтправкиЭлектронногоЧека = ПредопределенноеЗначение("Перечисление.ВариантыОтправкиЭлектронногоЧекаПокупателю.ОтправитьSMS")
	   И ЗначениеЗаполнено(ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека) Тогда
		ПараметрыФискализацииЧека.Отправляет1СSMS = Не ДанныеФискальнойОперации.ОтправлятьSMSЧерезОФД;
		ПараметрыФискализацииЧека.ПокупательНомер = "+7" + РозничныеПродажиКлиентСервер.НомерТелефонаВФормате10Знаков(ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека);
		ЕстьЭлектроннаяОтправка = Истина;
	КонецЕсли;
	
	ПараметрыФискализацииЧека.ОтправительEmail = ДанныеФискальнойОперации.ОтправительEmail;
	
	ПараметрыФискализацииЧека.Электронно = ЕстьЭлектроннаяОтправка И ДанныеФискальнойОперации.НеПечататьФискальныйЧек;
	
	ПараметрыФискализацииЧека.СерийныйНомер = ПараметрыКассыККМ.СерийныйНомер;
	
	ПараметрыФискализацииЧека.ДокументОснование = Объект.Ссылка;
	
	// Параметры необходимые для чека ЕНВД на принтере чеков
	ПараметрыФискализацииЧека.Кассир = РеквизитыКассира.Наименование;
	ПараметрыФискализацииЧека.Вставить("ИмяКассира", РеквизитыКассира.Наименование);
	ПараметрыФискализацииЧека.КассирИНН = РеквизитыКассира.ИНН;
	
	ПараметрыФискализацииЧека.Организация         = СостояниеКассовойСмены.Организация;
	ПараметрыФискализацииЧека.ОрганизацияНазвание = СостояниеКассовойСмены.СведенияООрганизации.ОфициальноеНаименование;
	ПараметрыФискализацииЧека.ОрганизацияИНН      = СостояниеКассовойСмены.СведенияООрганизации.ИНН;
	ПараметрыФискализацииЧека.ОрганизацияКПП      = СостояниеКассовойСмены.СведенияООрганизации.КПП;
	ПараметрыФискализацииЧека.НомерКассы          = "00001";
	ПараметрыФискализацииЧека.НомерЧека           = 1;
	ПараметрыФискализацииЧека.НомерСмены          = 1;
	
	ПараметрыФискализацииЧека.ТорговыйОбъект         = СостояниеКассовойСмены.КассаККМ;
	ПараметрыФискализацииЧека.АдресМагазина          = СостояниеКассовойСмены.АдресСклада;
	ПараметрыФискализацииЧека.НаименованиеМагазина   = Строка(СостояниеКассовойСмены.Склад);
	ПараметрыФискализацииЧека.СистемаНалогообложения = СостояниеКассовойСмены.СистемаНалогообложения;
	
	// Подготовка таблицы оплат
	ИнформацияОбОплате = ИнформацияОбОплате(ЭтотОбъект);
	
	// Наличные (тег 1031)
	Если ИнформацияОбОплате.Наличные > 0 Тогда
		СтрокаОплаты = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыСтрокиОплаты();
		СтрокаОплаты.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Наличные");
		СтрокаОплаты.Сумма = ИнформацияОбОплате.Наличные;
		ПараметрыФискализацииЧека.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	// Безналичные (тег 1081)
	Если ИнформацияОбОплате.ПлатежныеКарты > 0 Тогда
		СтрокаОплаты = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыСтрокиОплаты();
		СтрокаОплаты.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Электронно");
		СтрокаОплаты.Сумма = ИнформацияОбОплате.ПлатежныеКарты;
		ПараметрыФискализацииЧека.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	// Встречное предоставление (тег 1217)
	Если ИнформацияОбОплате.СуммаЛьготы > 0 И Не ЭтоЛьгота100(Объект.ПроцентЛьготы) Тогда
		СтрокаОплаты = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыСтрокиОплаты();
		СтрокаОплаты.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.ВстречноеПредоставление");
		СтрокаОплаты.Сумма = ИнформацияОбОплате.СуммаЛьготы;
		ПараметрыФискализацииЧека.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	Если ЕстьСерииПромаркированныеДляЦелейМДЛП Тогда
		Если ВерсияФФД < "1.2" Тогда
			ПараметрыФискализацииЧека.ДополнительныйРеквизитПользователя = ДополнительныйРеквизитПользователя(); // Тег 1084
		КонецЕсли;
	КонецЕсли;
	
	// При необходимости будет проведен формато-логический контроль
	ПараметрыФискализацииЧека.СпособФорматноЛогическогоКонтроля = ПараметрыКассыККМ.СпособФорматноЛогическогоКонтроля;
	ПараметрыФискализацииЧека.ДопустимоеРасхождениеФорматноЛогическогоКонтроля = ПараметрыКассыККМ.ДопустимоеРасхождениеФорматноЛогическогоКонтроля;
	Если ФорматноЛогическийКонтрольКлиентСервер.НуженФорматноЛогическийКонтроль(ПараметрыФискализацииЧека) Тогда
		ФорматноЛогическийКонтрольКлиентСервер.ПровестиФорматноЛогическийКонтроль(ПараметрыФискализацииЧека);
	КонецЕсли;
	
	Возврат ПараметрыФискализацииЧека;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ФФД 1.2
#Область ФФД_1_2

&НаСервере
Функция ОтраслевойРеквизитМДЛП(ДанныеПредметаРасчета)
	
	ОписаниеДанных = ИнтеграцияМДЛПКлиентСервер.ОписаниеДанныхПараметраЗначениеОтраслевогоРеквизитаМДЛП();
	
	ПроцентЛьготыСтрокиЧислом = 
		?(ЗначениеЗаполнено(ДанныеПредметаРасчета.ПроцентЛьготы), 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеПредметаРасчета.ПроцентЛьготы, "Процент"), 
			0);
			
	ОписаниеДанных.ПроцентЛьготы                   = ПроцентЛьготыСтрокиЧислом;
	ОписаниеДанных.СуммаЛьготы                     = ДанныеПредметаРасчета.СуммаЛьготы;
	ОписаниеДанных.НомерСерииЛьготногоРецепта      = ДанныеПредметаРасчета.НомерСерииЛьготногоРецепта;
	ОписаниеДанных.НомерЛьготногоРецепта           = ДанныеПредметаРасчета.НомерЛьготногоРецепта;
	ОписаниеДанных.ДатаРегистрацииЛьготногоРецепта = ДанныеПредметаРасчета.ДатаРегистрацииЛьготногоРецепта;
	
	ОписаниеДанных.НомерСерииРецепта               = ДанныеПредметаРасчета.НомерСерииРецепта;
	ОписаниеДанных.НомерРецепта                    = ДанныеПредметаРасчета.НомерРецепта;
	ОписаниеДанных.ДатаРегистрацииРецепта          = ДанныеПредметаРасчета.ДатаРегистрацииРецепта;
	
	ОписаниеДанных.ИдентификаторМестаДеятельностиМДЛП = СостояниеКассовойСмены.ИдентификаторМестаДеятельностиМДЛП;
	
	ЗначениеОтраслевогоРеквизита = ИнтеграцияМДЛПКлиентСервер.СформироватьПараметрЗначениеОтраслевогоРеквизитаМДЛП(ОписаниеДанных);
	
	Возврат ИнтеграцияМДЛПКлиентСервер.СформироватьПараметрОтраслевойРеквизитМДЛП(ЗначениеОтраслевогоРеквизита);
	
КонецФункции

&НаСервере
Функция ДробноеКоличествоТовараМДЛП(ДанныеПредметаРасчета)
	
	ОписаниеДанных = ИнтеграцияМДЛПКлиентСервер.ОписаниеДанныхПараметраДробноеКоличествоТовараМДЛП();
	
	Если ДанныеПредметаРасчета.КоэфициентЕдиницыИзмеренияВДокументе <> ДанныеПредметаРасчета.КоэфициентПотребительскойУпаковки Тогда
		
		Если ДанныеПредметаРасчета.КоэфициентПотребительскойУпаковки = 0 Или ДанныеПредметаРасчета.КоэффициентПервичнойУпаковки = 0 Тогда
			ВызватьИсключение НСтр("ru = 'Ошибка при заполнении параметров пробития чека:
				|Недопустимое значение тега 1291:
				|Коэффициент потребительской или первичной упаковки не заполнен.'");
		КонецЕсли;
		
		Числитель = ДанныеПредметаРасчета.КоэфициентЕдиницыИзмеренияВДокументе * ДанныеПредметаРасчета.КоличествоВЕдиницахИзмерения;
		Знаменатель = ДанныеПредметаРасчета.КоэфициентПотребительскойУпаковки;
		Если Числитель % ДанныеПредметаРасчета.КоэффициентПервичнойУпаковки = 0 И Знаменатель % ДанныеПредметаРасчета.КоэффициентПервичнойУпаковки = 0 Тогда
			Числитель   = Числитель / ДанныеПредметаРасчета.КоэффициентПервичнойУпаковки;
			Знаменатель = Знаменатель / ДанныеПредметаРасчета.КоэффициентПервичнойУпаковки;
		КонецЕсли;
		
		Если Цел(Знаменатель) <> Знаменатель Или Цел(Числитель) <> Числитель Тогда
			ВызватьИсключение НСтр("ru = 'Ошибка при заполнении параметров пробития чека:
				|Недопустимое значение тега 1291:
				|Коэффициенты упаковок имеют дробное значение.
				|При частичной продаже доступна продажа только целой первичной упаковки из потребительской.'");
		КонецЕсли;
		
		ОписаниеДанных.КоличествоОтпущенныхПервичныхУпаковок           = Числитель;
		ОписаниеДанных.КоличествоПервичныхУпаковокВМаркируемойУпаковке = Знаменатель;
		
	КонецЕсли;
	
	Возврат ИнтеграцияМДЛПКлиентСервер.СформироватьПараметрыДробногоКоличестваТовараМДЛП(ОписаниеДанных);
	
КонецФункции

#КонецОбласти // ФФД_1_2

////////////////////////////////////////////////////////////////////////////////
// ФФД 1.1 и ФФД 1.0.5
#Область ФФД_1_1_ФФД_1_0_5

&НаСервере
Функция ДополнительныйРеквизитПредметаРасчетаМДЛП(ДанныеПредметаРасчета)
	
	ОписаниеДанных = ИнтеграцияМДЛПКлиентСервер.ОписаниеДанныхПараметраДополнительныйРеквизитПредметаРасчетаМДЛП();
	
	Если ДанныеПредметаРасчета.КоэфициентЕдиницыИзмеренияВДокументе <> ДанныеПредметаРасчета.КоэфициентПотребительскойУпаковки Тогда
		
		Если ДанныеПредметаРасчета.КоэфициентПотребительскойУпаковки = 0 Или ДанныеПредметаРасчета.КоэффициентПервичнойУпаковки = 0 Тогда
			ВызватьИсключение НСтр("ru = 'Ошибка при заполнении параметров пробития чека:
				|Недопустимое значение знаменателя реквизита ""sp"" тега 1191:
				|Коэффициент потребительской или первичной упаковки не заполнен.'");
		КонецЕсли;
		
		Числитель = ДанныеПредметаРасчета.КоэфициентЕдиницыИзмеренияВДокументе * ДанныеПредметаРасчета.КоличествоВЕдиницахИзмерения;
		Знаменатель = ДанныеПредметаРасчета.КоэфициентПотребительскойУпаковки;
		Если Числитель % ДанныеПредметаРасчета.КоэффициентПервичнойУпаковки = 0 И Знаменатель % ДанныеПредметаРасчета.КоэффициентПервичнойУпаковки = 0 Тогда
			Числитель   = Числитель / ДанныеПредметаРасчета.КоэффициентПервичнойУпаковки;
			Знаменатель = Знаменатель / ДанныеПредметаРасчета.КоэффициентПервичнойУпаковки;
		КонецЕсли;
		
		Если Цел(Знаменатель) <> Знаменатель Или Цел(Числитель) <> Числитель Тогда
			ВызватьИсключение НСтр("ru = 'Ошибка при заполнении параметров пробития чека:
				|Недопустимое значение реквизита ""sp"" тега 1191:
				|Коэффициенты упаковок имеют дробное значение.
				|При частичной продаже доступна продажа только целой первичной упаковки из потребительской.'");
		КонецЕсли;
		
		ОписаниеДанных.КоличествоОтпущенныхПервичныхУпаковок           = Числитель;
		ОписаниеДанных.КоличествоПервичныхУпаковокВМаркируемойУпаковке = Знаменатель;
		
	КонецЕсли;
	
	ОписаниеДанных.ПроцентЛьготы = ПроцентЛьготыЧислом;
	ОписаниеДанных.СуммаЛьготы = ДанныеПредметаРасчета.СуммаЛьготы;
	
	Возврат ИнтеграцияМДЛПКлиентСервер.СформироватьПараметрДополнительныйРеквизитПредметаРасчетаМДЛП(ОписаниеДанных);
	
КонецФункции

&НаСервере
Функция ДополнительныйРеквизитПользователя()
	
	ОписаниеДанных = ИнтеграцияМДЛПКлиентСервер.ОписаниеДанныхПараметраДополнительныйРеквизитПользователяМДЛП();
	
	ОписаниеДанных.ПроцентЛьготы                   = ПроцентЛьготыЧислом;
	ОписаниеДанных.НомерСерииЛьготногоРецепта      = Объект.НомерСерииЛьготногоРецепта;
	ОписаниеДанных.НомерЛьготногоРецепта           = Объект.НомерЛьготногоРецепта;
	ОписаниеДанных.ДатаРегистрацииЛьготногоРецепта = Объект.ДатаРегистрацииЛьготногоРецепта;
	
	ОписаниеДанных.НомерСерииРецепта               = Объект.НомерСерииРецепта;
	ОписаниеДанных.НомерРецепта                    = Объект.НомерРецепта;
	ОписаниеДанных.ДатаРегистрацииРецепта          = Объект.ДатаРегистрацииРецепта;
	
	ОписаниеДанных.ИдентификаторМестаДеятельностиМДЛП = СостояниеКассовойСмены.ИдентификаторМестаДеятельностиМДЛП;
	
	Возврат ИнтеграцияМДЛПКлиентСервер.СформироватьПараметрДополнительныйРеквизитПользователяМДЛП(ОписаниеДанных);
	
КонецФункции

#КонецОбласти // ФФД_1_1_ФФД_1_0_5

#КонецОбласти // ОперацииСЧеком

////////////////////////////////////////////////////////////////////////////////
// Операции по оплате
#Область Оплата

&НаКлиенте
Процедура ОплатитьНаличнымиОбработкаОповещения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ФормаОплаты = ПредопределенноеЗначение("Перечисление.ФормыОплаты.Наличная");
	Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.ТоварЗарезервирован");
	
	Оповестить = Новый ОписаниеОповещения("ОткрытьФормуОплатыНаличными", ЭтотОбъект);
	ВыполнитьДействиеПослеЗаписи(Оповестить,
		НСтр("ru = 'Перед открытием формы оплаты не удалось записать документ.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОплатыНаличными(ИзмененныеДанныеЗаписаны, ДополнительныеПараметры) Экспорт
	
	Если Не ИзмененныеДанныеЗаписаны Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ИнформацияОбОплате"    , ИнформацияОбОплате(ЭтотОбъект));
	ПараметрыОткрытияФормы.Вставить("ОплатаНаличными"       , Истина);
	ПараметрыОткрытияФормы.Вставить("ДоступнаПередачаДанных", ПараметрыКассыККМ.ДоступнаПередачаДанных);
	ПараметрыОткрытияФормы.Вставить("ВариантОтправкиЭлектронногоЧека" , ДанныеФискальнойОперации.ВариантОтправкиЭлектронногоЧека);
	ПараметрыОткрытияФормы.Вставить("КонтактныеДанныеЭлектронногоЧека", ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека);
	
	ОткрытьФорму(
		"Документ.ЧекККМ.Форма.ФормаОплаты",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("ЗавершениеОплаты", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьПлатежнойКартойОбработкаОповещения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ФормаОплаты = ПредопределенноеЗначение("Перечисление.ФормыОплаты.ПлатежнаяКарта");
	Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.ТоварЗарезервирован");
	
	ВыполнитьОбработкуОповещения(
		Новый ОписаниеОповещения("ДобавитьОплатуКартой", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура СмешаннаяОплатаОбработкаОповещения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиКОплатеНаДисплейПокупателя = Истина;
	ПодключитьОбработчикОжидания("ВывестиИнформациюНаДисплейПокупателя", 0.1, Истина);
	
	Объект.ФормаОплаты = ПредопределенноеЗначение("Перечисление.ФормыОплаты.ПустаяСсылка");
	Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.ТоварЗарезервирован");
	
	Оповестить = Новый ОписаниеОповещения("ОткрытьФормуСмешаннойОплаты", ЭтотОбъект);
	ВыполнитьДействиеПослеЗаписи(Оповестить,
		НСтр("ru = 'Перед открытием формы оплаты не удалось записать документ.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСмешаннойОплаты(ИзмененныеДанныеЗаписаны, ДоступныеВидыОплаты) Экспорт
	
	Если Не ИзмененныеДанныеЗаписаны Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиКОплатеНаДисплейПокупателя = Истина;
	ПодключитьОбработчикОжидания("ВывестиИнформациюНаДисплейПокупателя", 0.1, Истина);
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ИнформацияОбОплате", ИнформацияОбОплате(ЭтотОбъект));
	Если ДоступныеВидыОплаты <> Неопределено Тогда
		ПараметрыОткрытияФормы.ИнформацияОбОплате.ДоступныеВидыОплаты = ДоступныеВидыОплаты;
	КонецЕсли;
	ПараметрыОткрытияФормы.Вставить("ДоступнаПередачаДанных", ПараметрыКассыККМ.ДоступнаПередачаДанных);
	ПараметрыОткрытияФормы.Вставить("ВариантОтправкиЭлектронногоЧека" , ДанныеФискальнойОперации.ВариантОтправкиЭлектронногоЧека);
	ПараметрыОткрытияФормы.Вставить("КонтактныеДанныеЭлектронногоЧека", ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека);
	
	ОткрытьФорму(
		"Документ.ЧекККМ.Форма.ФормаОплаты",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("ЗавершениеОплаты", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеОплаты(РезультатОплаты, ДополнительныеПараметры) Экспорт
	
	Объект.ПолученоНаличными = 0;
	Если ТипЗнч(РезультатОплаты) = Тип("Структура") Тогда
		
		Объект.ПолученоНаличными = РезультатОплаты.ПолученоНаличными;
		
		ДанныеФискальнойОперации.ВариантОтправкиЭлектронногоЧека  = РезультатОплаты.ДанныеЭлектронногоЧека.ВариантОтправкиЭлектронногоЧека;
		ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека = РезультатОплаты.ДанныеЭлектронногоЧека.КонтактныеДанныеЭлектронногоЧека;
		
	КонецЕсли;
	
	ОбработатьДобавлениеОплаты(ДополнительныеПараметры);
	
КонецПроцедуры

// Вызывается из формы сложной оплаты
&НаКлиенте
Процедура ДобавитьОплатуКартой(Результат, ПараметрыЗавершения) Экспорт
	
	Если ПараметрыЗавершения <> Неопределено Тогда
		Объект.ПолученоНаличными = ПараметрыЗавершения.ПолученоНаличными;
	КонецЕсли;
	
	ВывестиКОплатеНаДисплейПокупателя = Истина;
	ПодключитьОбработчикОжидания("ВывестиИнформациюНаДисплейПокупателя", 0.1, Истина);
	
	ИнформацияОбОплате = ИнформацияОбОплате(ЭтотОбъект);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения("ДобавитьОплатуКартойЗавершение", ЭтотОбъект, ПараметрыЗавершения));
	
	ДополнительныеПараметры.Вставить("Форма"                       , ЭтотОбъект);
	ДополнительныеПараметры.Вставить("ЭквайринговыеТерминалы"      , ЭквайринговыеТерминалы);
	ДополнительныеПараметры.Вставить("ПараметрыКассыККМ"           , ПараметрыКассыККМ);
	ДополнительныеПараметры.Вставить("ФормаАвторизации_Сумма"      , ИнформацияОбОплате.СуммаКОплате - ИнформацияОбОплате.ИтогоОплачено);
	ДополнительныеПараметры.Вставить("ФормаАвторизации_ПределСуммы", ИнформацияОбОплате.СуммаКОплате - ИнформацияОбОплате.ИтогоОплачено + ИнформацияОбОплате.Наличные);
	ДополнительныеПараметры.Вставить("Валюта"                      , Объект.Валюта);
	ДополнительныеПараметры.Вставить("СтруктураЭквайринговыйТерминал");
	ДополнительныеПараметры.Вставить("ИдентификаторУстройстваЭТ");
	
	Оповестить = Новый ОписаниеОповещения("ДобавитьОплатуКартой", РозничныеПродажиКлиент, ДополнительныеПараметры);
	ВыполнитьДействиеПослеЗаписи(Оповестить,
		НСтр("ru = 'Перед оплатой платежной картой не удалось записать документ.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОплатуКартойЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ОбработатьДобавлениеОплаты(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	// Сохранить в таблице данные оплаты картой
	СтрокаОплатыКартой = Объект.ОплатаПлатежнымиКартами.Добавить();
	
	СтрокаОплатыКартой.ЭквайринговыйТерминал = Результат.СтруктураЭквайринговыйТерминал.Ссылка;
	СтрокаОплатыКартой.НомерПлатежнойКарты   = Результат.НомерКарты; // Возможна запись пустого номера карты или номера вида "****************"
	СтрокаОплатыКартой.Сумма                 = Результат.СуммаОперации;
	СтрокаОплатыКартой.СсылочныйНомер        = Результат.СсылочныйНомер;
	СтрокаОплатыКартой.НомерЧекаЭТ           = Результат.НомерЧека;
	СтрокаОплатыКартой.КодАвторизации        = Результат.КодАвторизации;
	ВидыПлатежныхКарт = Результат.ВидыПлатежныхКарт;
	СтрокаОплатыКартой.ВидПлатежнойКарты = ВидыПлатежныхКарт.НайтиПоЗначению(Результат.ТипКарты).Представление;
	
	ДанныеДляЖурналаРегистрации = Новый Структура;
	ДанныеДляЖурналаРегистрации.Вставить("СуммаОперации",  Результат.СуммаОперации);
	ДанныеДляЖурналаРегистрации.Вставить("СсылочныйНомер", Результат.СсылочныйНомер);
	ДанныеДляЖурналаРегистрации.Вставить("НомерКарты",     Результат.НомерКарты);
	ДанныеДляЖурналаРегистрации.Вставить("НомерЧекаЭТ",    Результат.НомерЧека);
	ДанныеДляЖурналаРегистрации.Вставить("КодАвторизации", Результат.КодАвторизации);
	
	Модифицированность = Истина;
	
	ПересчитатьДокументНаКлиенте();
	
	ТребуетсяПовторнаяПопыткаЗаписи = Ложь;
	ИзмененныеДанныеЗаписаны = ЗаписатьНаКлиенте(ТребуетсяПовторнаяПопыткаЗаписи);
	Если Не ИзмененныеДанныеЗаписаны Тогда
		
		ДополнительныеПараметрыЗавершение = Новый Структура;
		ДополнительныеПараметрыЗавершение.Вставить("ОписаниеОповещения", Новый ОписаниеОповещения("ДобавитьОплатуКартойЗаписьЗавершение", ЭтотОбъект, ДополнительныеПараметры));
		ДополнительныеПараметрыЗавершение.Вставить("ТекстСообщения", НСтр("ru = 'После проведения оплаты платежной картой не удалось сохранить документ.'"));
		ДополнительныеПараметрыЗавершение.Вставить("ВозвращатьРезультатФункции", Ложь);
		ДополнительныеПараметрыЗавершение.Вставить("РезультатПриУспешномПроведении", Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны", Истина, Истина));
		ДополнительныеПараметрыЗавершение.Вставить("РезультатПриОтмене", Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны", Истина, Ложь));
		ДополнительныеПараметрыЗавершение.Вставить("ИмяПроцедуры", "ЗаписатьНаСервере");
		ДополнительныеПараметрыЗавершение.Вставить("РезультатОперации", ИзмененныеДанныеЗаписаны);
		
		Если Не ТребуетсяПовторнаяПопыткаЗаписи Тогда
			ОшибкаПриПроведенииЧекаВопросЗавершение(КодВозвратаДиалога.Отмена, ДополнительныеПараметрыЗавершение);
			Возврат;
		КонецЕсли;
	
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ОшибкаПриПроведенииЧекаВопросЗавершение", ЭтотОбъект, ДополнительныеПараметрыЗавершение),
			ДополнительныеПараметрыЗавершение.ТекстСообщения,
			РежимДиалогаВопрос.ПовторитьОтмена, 10, КодВозвратаДиалога.Повторить,,КодВозвратаДиалога.Повторить);
		Возврат;
		
	Иначе
		
		ОбработатьДобавлениеОплаты(ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОплатуКартойЗаписьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОбработатьДобавлениеОплаты(ДополнительныеПараметры);
	
	Если Результат.ВыполненаОперацияНаУстройстве И Не Результат.ИзмененныеДанныеЗаписаны Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Данные", Объект.Ссылка);
		ПараметрыФормы.Вставить("ДанныеДляЖурналаРегистрации", ДанныеДляЖурналаРегистрации);
		ПараметрыФормы.Вставить("ТекстСообщения",
			НСтр("ru = 'ВНИМАНИЕ! Произошла исключительная ситуация:
			|Оплата произведена, но не зафиксирована в системе.'"));
		
		ОткрытьФорму("Документ.ЧекККМ.Форма.ОшибкаЗаписи", ПараметрыФормы, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДобавлениеОплаты(ДополнительныеПараметры)
	
	ВывестиКОплатеНаДисплейПокупателя = Ложь;
	ПодключитьОбработчикОжидания("ВывестиИнформациюНаДисплейПокупателя", 0.1, Истина);
	
	ИнформацияОбОплате = ИнформацияОбОплате(ЭтотОбъект);
	// Команда оплаты картой вызвана из формы РМК
	Если ДополнительныеПараметры = Неопределено Тогда
		
		Если ИнформацияОбОплате.ИтогоОплачено >= ИнформацияОбОплате.СуммаКОплате
		   И (Не ПараметрыКассыККМ.ДоступнаПередачаДанных Или ЗначениеЗаполнено(ДанныеФискальнойОперации.ВариантОтправкиЭлектронногоЧека)) Тогда
			
			ВывестиКОплатеНаДисплейПокупателя = Истина;
			
			ОбновитьДанныеИнформационнойПанели(ЭтотОбъект);
			
			ПробитьЧек();
			
		ИначеЕсли ИнформацияОбОплате.ИтогоОплачено > 0 Тогда
			
			Оповестить = Новый ОписаниеОповещения("ОткрытьФормуСмешаннойОплаты", ЭтотОбъект);
			ВыполнитьДействиеПослеЗаписи(Оповестить,
				НСтр("ru = 'Перед открытием формы оплаты не удалось записать документ.'"));
			
		КонецЕсли;
		
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеВФормуОплаты, ИнформацияОбОплате);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтменитьОплатыПлатежнымиКартами(Результат, ДополнительныеПараметры) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресВХранилище", ПоместитьТабличнуюЧастьОплатыПлатежнымиКартамиВХранилище());
	
	Если ДополнительныеПараметры.Свойство("ПолученоНаличными") Тогда
		Объект.ПолученоНаличными = ДополнительныеПараметры.ПолученоНаличными;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("Форма", ЭтотОбъект);
	ДополнительныеПараметры.Вставить("Валюта", Объект.Валюта);
	
	ОткрытьФорму(
		"Документ.ЧекККМ.Форма.ТабличнаяЧастьОплатаПлатежнымиКартами",
		ПараметрыФормы,
		,
		,
		,
		,
		Новый ОписаниеОповещения("ОтменитьОплатыПлатежнымиКартамиВыборСтрокиОплатыЗавершение", РозничныеПродажиКлиент, ДополнительныеПараметры),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОплатыПлатежнымиКартамиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.ВыполненаОперацияНаУстройстве И Не Результат.ИзмененныеДанныеЗаписаны Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Данные", ДополнительныеПараметры.Форма.Объект.Ссылка);
		ПараметрыФормы.Вставить("ДанныеДляЖурналаРегистрации", ДополнительныеПараметры.Форма.ДанныеДляЖурналаРегистрации);
		ПараметрыФормы.Вставить("ТекстСообщения",
			НСтр("ru = 'ВНИМАНИЕ! Произошла исключительная ситуация:
			|Отмена оплаты не зафиксирована в системе.'"));
		
		ОткрытьФорму("Документ.ЧекККМ.Форма.ОшибкаЗаписи", ПараметрыФормы, ДополнительныеПараметры.Форма);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ОповещениеВФормуОплаты") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеВФормуОплаты, ИнформацияОбОплате(ЭтотОбъект));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТабличнуюЧастьОплатыПлатежнымиКартамиВХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.ОплатаПлатежнымиКартами.Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ОтменитьОплатуПлатежнымиКартами(ОповещениеОЗавершении)
	
	СтрокиОплатыПлатежнымиКартами = Новый Массив;
	Для каждого СтрокаОплаты Из Объект.ОплатаПлатежнымиКартами Цикл
		СтрокиОплатыПлатежнымиКартами.Добавить(СтрокаОплаты);
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении", ОповещениеОЗавершении);
	ДополнительныеПараметры.Вставить("СтрокиОплатыПлатежнымиКартами", СтрокиОплатыПлатежнымиКартами);
	
	ДополнительныеПараметры.Вставить("Форма", ЭтотОбъект);
	ДополнительныеПараметры.Вставить("Валюта", Объект.Валюта);
	
	ОтменитьОплатуПлатежнымиКартамиПоследовательно(Неопределено, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОплатуПлатежнойКартой(Результат, ДополнительныеПараметры) Экспорт
	
	Оповестить = Новый ОписаниеОповещения("ОтменитьОплатуКартой", РозничныеПродажиКлиент, ДополнительныеПараметры);
	ВыполнитьДействиеПослеЗаписи(Оповестить,
		НСтр("ru = 'Перед открытием формы оплаты не удалось записать документ.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОплатуПлатежнымиКартамиПоследовательно(Результат, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.СтрокиОплатыПлатежнымиКартами.Количество() > 0 Тогда
		
		СтрокаОплаты = ДополнительныеПараметры.СтрокиОплатыПлатежнымиКартами[ДополнительныеПараметры.СтрокиОплатыПлатежнымиКартами.Количество() - 1];
		ДополнительныеПараметры.СтрокиОплатыПлатежнымиКартами.Удалить(ДополнительныеПараметры.СтрокиОплатыПлатежнымиКартами.Найти(СтрокаОплаты));
		
		ПараметрыОтмены = Новый Структура;
		ПараметрыОтмены.Вставить("ОповещениеПриЗавершении", Новый ОписаниеОповещения("ОтменитьОплатуПлатежнымиКартамиПоследовательно", ЭтотОбъект, ДополнительныеПараметры));
		ПараметрыОтмены.Вставить("ТекущиеДанные", СтрокаОплаты);
		ПараметрыОтмены.Вставить("Форма", ДополнительныеПараметры.Форма);
		ПараметрыОтмены.Вставить("Валюта", ДополнительныеПараметры.Валюта);
		
		ВыполнитьОбработкуОповещения(
			Новый ОписаниеОповещения("ОтменитьОплатуПлатежнойКартой", ДополнительныеПараметры.Форма, ПараметрыОтмены));
		
	Иначе
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПриЗавершении, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОплатуПлатежнойКартойЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДанныеДляЖурналаРегистрации = Новый Структура;
	ДанныеДляЖурналаРегистрации.Вставить("СуммаОперации" , ДополнительныеПараметры.СуммаОперации);
	ДанныеДляЖурналаРегистрации.Вставить("СсылочныйНомер", ДополнительныеПараметры.СсылочныйНомер);
	ДанныеДляЖурналаРегистрации.Вставить("НомерЧекаЭТ"   , ДополнительныеПараметры.НомерЧекаЭТ);
	
	Объект.ОплатаПлатежнымиКартами.Удалить(Объект.ОплатаПлатежнымиКартами.Индекс(ДополнительныеПараметры.СтрокаОплаты));
	Модифицированность = Истина;
	
	ПересчитатьДокументНаКлиенте();
	
	ТребуетсяПовторнаяПопыткаЗаписи = Ложь;
	ИзмененныеДанныеЗаписаны = ЗаписатьНаКлиенте(ТребуетсяПовторнаяПопыткаЗаписи);
	Если Не ИзмененныеДанныеЗаписаны Тогда
		
		ДополнительныеПараметрыПовторЗаписи = Новый Структура;
		ДополнительныеПараметрыПовторЗаписи.Вставить("ОписаниеОповещения", ДополнительныеПараметры.ОповещениеПриЗавершении);
		ДополнительныеПараметрыПовторЗаписи.Вставить("ТекстСообщения", НСтр("ru = 'После выполнения операции отмены оплаты не удалось записать документ.'"));
		ДополнительныеПараметрыПовторЗаписи.Вставить("ВозвращатьРезультатФункции", Ложь);
		ДополнительныеПараметрыПовторЗаписи.Вставить("РезультатПриУспешномПроведении", Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны", Истина, Истина));
		ДополнительныеПараметрыПовторЗаписи.Вставить("РезультатПриОтмене", Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны", Истина, Ложь));
		ДополнительныеПараметрыПовторЗаписи.Вставить("ИмяПроцедуры", "ЗаписатьНаСервере");
		ДополнительныеПараметрыПовторЗаписи.Вставить("РезультатОперации", ИзмененныеДанныеЗаписаны);
		
		Если Не ТребуетсяПовторнаяПопыткаЗаписи Тогда
			ОшибкаПриПроведенииЧекаВопросЗавершение(КодВозвратаДиалога.Отмена, ДополнительныеПараметрыПовторЗаписи);
			Возврат;
		КонецЕсли;
	
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ОшибкаПриПроведенииЧекаВопросЗавершение", ЭтотОбъект, ДополнительныеПараметрыПовторЗаписи),
			ДополнительныеПараметрыПовторЗаписи.ТекстСообщения,
			РежимДиалогаВопрос.ПовторитьОтмена, 10, КодВозвратаДиалога.Повторить,,КодВозвратаДиалога.Повторить);
		Возврат;
		
	Иначе
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПриЗавершении,
			Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны",
				Истина, Истина));
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается из формы оплаты
&НаКлиенте
Процедура ОтменитьОплату(Результат, ДополнительныеПараметры) Экспорт
	
	Объект.ПолученоНаличными = 0;
	
	ОтменитьОплатуПлатежнымиКартами(
		Новый ОписаниеОповещения("ОтменитьОплатуЗавершение", ЭтотОбъект, ДополнительныеПараметры));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОплатуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеВФормуОплаты, ИнформацияОбОплате(ЭтотОбъект));
	
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
Функция ИнформацияОбОплате(Форма)
	
	СуммаДокумента = Форма.Объект.Товары.Итог("СуммаСНДС");
	СуммаЛьготы    = Форма.Объект.Товары.Итог("СуммаЛьготы");
	
	СуммаСкидки = 0;
	СуммаБезСкидки = СуммаДокумента + СуммаСкидки;
	СуммаКОплате = СуммаБезСкидки - СуммаСкидки - СуммаЛьготы;
	
	ИнформацияОбОплате = Новый Структура;
	ИнформацияОбОплате.Вставить("Документ"               , Форма.Объект.Ссылка);
	
	ИнформацияОбОплате.Вставить("Наличные"               , Форма.Объект.ПолученоНаличными);
	ИнформацияОбОплате.Вставить("ПлатежныеКарты"         , Форма.Объект.ОплатаПлатежнымиКартами.Итог("Сумма"));
	ИнформацияОбОплате.Вставить("СуммаЛьготы"            , СуммаЛьготы);
	
	ИнформацияОбОплате.Вставить("СуммаДокумента"         , СуммаДокумента);
	ИнформацияОбОплате.Вставить("СуммаБезСкидки"         , СуммаБезСкидки);
	ИнформацияОбОплате.Вставить("СуммаКОплате"           , СуммаКОплате);
	ИнформацияОбОплате.Вставить("СуммаСкидки"            , СуммаСкидки);
	ИнформацияОбОплате.Вставить("ИтогоОплачено"          , ИнформацияОбОплате.Наличные + ИнформацияОбОплате.ПлатежныеКарты);
	
	ИнформацияОбОплате.Вставить("ДоступныеВидыОплаты", ДоступныеВидыОплаты(Форма));
	
	Возврат ИнформацияОбОплате;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДоступныеВидыОплаты(Форма)
	
	ОплатаПлатежнымиКартами = Ложь;
	Если Форма.ЭквайринговыеТерминалы.Количество() > 0 Тогда
		ОплатаПлатежнымиКартами = Истина;
	КонецЕсли;
	
	ДоступныеВидыОплаты = Новый Структура;
	ДоступныеВидыОплаты.Вставить("Наличные",       Истина);
	ДоступныеВидыОплаты.Вставить("ПлатежныеКарты", ОплатаПлатежнымиКартами);
	
	Возврат ДоступныеВидыОплаты;
	
КонецФункции

#КонецОбласти // Оплата

////////////////////////////////////////////////////////////////////////////////
// Подключаемое оборудование
#Область ПодключаемоеОборудование

&НаСервере
Процедура ПолучитьСсылкиНаОборудование()
	
	ЭквайринговыеТерминалы.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЭквайринговыеТерминалы.Ссылка                                  КАК Ссылка,
	|	ЭквайринговыеТерминалы.ИспользоватьБезПодключенияОборудования  КАК ИспользоватьБезПодключенияОборудования,
	|	ЭквайринговыеТерминалы.ПодключаемоеОборудование                КАК ПодключаемоеОборудование,
	|	
	|	ЭквайринговыеТерминалы.ВидыПлатежныхКарт.(
	|		ВидыПлатежныхКарт.ВидПлатежнойКарты КАК ВидПлатежнойКарты
	|	) КАК ВидыПлатежныхКарт
	|	
	|ИЗ
	|	Справочник.ЭквайринговыеТерминалы КАК ЭквайринговыеТерминалы
	|ГДЕ
	|	ЭквайринговыеТерминалы.Касса = &КассаККМ
	|	И ((ЭквайринговыеТерминалы.ПодключаемоеОборудование <> ЗНАЧЕНИЕ(Справочник.ПодключаемоеОборудование.ПустаяСсылка)
	|		И ЭквайринговыеТерминалы.ПодключаемоеОборудование.РабочееМесто = &РабочееМесто)
	|		ИЛИ ЭквайринговыеТерминалы.ИспользоватьБезПодключенияОборудования)
	|";
	
	Запрос.УстановитьПараметр("КассаККМ", Объект.КассаККМ);
	
	Если ПодключаемоеОборудованиеКлиентСервер.ИспользоватьПодключаемоеОборудование(ЭтотОбъект) Тогда
		РабочееМесто = МенеджерОборудованияВызовСервера.РабочееМестоКлиента();
	Иначе
		РабочееМесто = Справочники.РабочиеМеста.ПустаяСсылка();
	КонецЕсли;
	
	Запрос.УстановитьПараметр("РабочееМесто", РабочееМесто);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПараметрыЭТ = Новый Структура;
		ПараметрыЭТ.Вставить("Ссылка",                                 Выборка.Ссылка);
		ПараметрыЭТ.Вставить("ПодключаемоеОборудование",               Выборка.ПодключаемоеОборудование);
		ПараметрыЭТ.Вставить("ВидыПлатежныхКарт",                      Новый СписокЗначений);
		ПараметрыЭТ.Вставить(
			"ИспользоватьБезПодключенияОборудования",
			Выборка.ИспользоватьБезПодключенияОборудования
				Или Не ПодключаемоеОборудованиеКлиентСервер.ИспользоватьПодключаемоеОборудование(ЭтотОбъект));
		
		ВыборкаВидыКарт = Выборка.ВидыПлатежныхКарт.Выбрать();
		Пока ВыборкаВидыКарт.Следующий() Цикл
			ПараметрыЭТ.ВидыПлатежныхКарт.Добавить(ВыборкаВидыКарт.ВидПлатежнойКарты);
		КонецЦикла;
		
		ЭквайринговыеТерминалы.Добавить(ПараметрыЭТ);
		
	КонецЦикла;
	
	Если ПодключаемоеОборудованиеКлиентСервер.ИспользоватьПодключаемоеОборудование(ЭтотОбъект) Тогда
		СписокОтбор = МенеджерОборудованияКлиентСервер.СписокОборудованияОтбор();
		СписокОтбор.ТипыПО = "ДисплейПокупателя";
		СписокОтбор.РабочееМесто = РабочееМесто;
		СписокОтбор.СетевоеОборудование = Истина;
		Дисплеи.ЗагрузитьЗначения(МенеджерОборудованияВызовСервера.СписокОборудования(СписокОтбор));
	КонецЕсли;
	
	ПараметрыКассыККМ = Новый ФиксированнаяСтруктура(Справочники.КассыККМ.ПараметрыКассыККМ(Объект.КассаККМ));
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиИнформациюНаДисплейПокупателя()
	
	Если Не ВводДоступен() Тогда
		Возврат;
	КонецЕсли;
	
	ДПТекст1 = "";
	ДПТекст2 = "";
	
	Если ВывестиКОплатеНаДисплейПокупателя Тогда
		
		ИнформацияОбОплате = ИнформацияОбОплате(ЭтотОбъект);
		ДПТекст1 = РозничныеПродажиКлиент.ПодготовитьСтрокуКВыводуНаДисплейПокупателя(
			НСтр("ru = 'К оплате:'"), ИнформацияОбОплате.СуммаКОплате);
		
	Иначе
		
		КоличествоСтрокВТЧ = Объект.Товары.Количество();
		
		ТекущаяСтрока = Элементы.Товары.ТекущаяСтрока;
		Если ТекущаяСтрока = Неопределено И КоличествоСтрокВТЧ > 0 Тогда
			ТекущиеДанные = Объект.Товары[КоличествоСтрокВТЧ - 1];
			Элементы.Товары.ТекущаяСтрока = ТекущиеДанные.ПолучитьИдентификатор();
		ИначеЕсли ТекущаяСтрока = Неопределено И КоличествоСтрокВТЧ = 0 Тогда
			ТекущиеДанные = Неопределено;
		Иначе
			ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрока);
		КонецЕсли;
		
		Если ТекущиеДанные <> Неопределено Тогда
			ДПТекст1 = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ТекущиеДанные.Номенклатура,
				ТекущиеДанные.СерияНоменклатуры,
				ТекущиеДанные.Партия);
		
			ДПТекст2 = РозничныеПродажиКлиент.ПодготовитьСтрокуКВыводуНаДисплейПокупателя(
				НСтр("ru='Итог:'"), Объект.СуммаДокумента);
		Иначе
			ДПТекст1 = НСтр("ru = 'Здравствуйте!'");
		КонецЕсли;
		
	КонецЕсли;
	
	ВыводимыйТекст = ДПТекст1 + Символы.ПС + ДПТекст2;
	
	Для Каждого ЭлементСписка Из Дисплеи Цикл
		
		Дисплей = ЭлементСписка.Значение;
		Оповещение = Новый ОписаниеОповещения("ВывестиИнформациюНаДисплейПокупателяЗавершение", ЭтотОбъект);
		
		ПараметрыВыводаНаДисплей = ОборудованиеДисплеиПокупателяКлиент.ПараметрыОперацииДисплейПокупателя(ВыводимыйТекст);
		ОборудованиеДисплеиПокупателяКлиент.НачатьВыводИнформацииНаДисплейПокупателя(Оповещение, УникальныйИдентификатор, Дисплей.ИдентификаторУстройства, ПараметрыВыводаНаДисплей);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиИнформациюНаДисплейПокупателяЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		ШаблонСообщенияОбОшибке = НСтр(
			"ru='При использовании дисплея покупателя произошла ошибка.
				|Дополнительное описание:
				|%ОписаниеОшибки%'");
		ТекстСообщения = СтрЗаменить(ШаблонСообщенияОбОшибке, "%ОписаниеОшибки%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ПодключаемоеОборудование

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти // СтандартныеПодсистемы
