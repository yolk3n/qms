
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВерсияФФД                       = Параметры.ВерсияФФД;
	ИспользоватьЛьготы              = Параметры.ИспользоватьЛьготы;
	
	ПроцентЛьготы                   = Параметры.ПроцентЛьготы;
	ДатаРегистрацииЛьготногоРецепта = Параметры.ДатаРегистрацииЛьготногоРецепта;
	НомерСерииЛьготногоРецепта      = Параметры.НомерСерииЛьготногоРецепта;
	НомерЛьготногоРецепта           = Параметры.НомерЛьготногоРецепта;
	
	ДатаРегистрацииРецепта          = Параметры.ДатаРегистрацииРецепта;
	НомерСерииРецепта               = Параметры.НомерСерииРецепта;
	НомерРецепта                    = Параметры.НомерРецепта;
	
	ТаблицаДанныхПроцентовЛьгот     = ДанныеПроцентовЛьгот();
	
	Элементы.ПроцентЛьготы.СписокВыбора.ЗагрузитьЗначения(ТаблицаДанныхПроцентовЛьгот.ВыгрузитьКолонку("ПроцентЛьготы"));
	
	Если ВерсияФФД >= "1.2" Тогда
		Элементы.ПроцентЛьготы.СписокВыбора.Вставить(0, Справочники.ПроцентыЛьгот.ПустаяСсылка(), НСтр("ru = 'без льгот'"));
	КонецЕсли;
	
	НастроитьФорму();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если ЗначениеЗаполнено(ПроцентЛьготы) Тогда
		НепроверяемыеРеквизиты.Добавить("ДатаРегистрацииРецепта");
	Иначе
		НепроверяемыеРеквизиты.Добавить("НомерСерииЛьготногоРецепта");
		НепроверяемыеРеквизиты.Добавить("НомерЛьготногоРецепта");
		НепроверяемыеРеквизиты.Добавить("ДатаРегистрацииЛьготногоРецепта");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомендаОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ПроцентЛьготы"                  , ПроцентЛьготы);
	РезультатВыбора.Вставить("НомерСерииЛьготногоРецепта"     , НомерСерииЛьготногоРецепта);
	РезультатВыбора.Вставить("НомерЛьготногоРецепта"          , НомерЛьготногоРецепта);
	РезультатВыбора.Вставить("ДатаРегистрацииЛьготногоРецепта", ДатаРегистрацииЛьготногоРецепта);
	
	РезультатВыбора.Вставить("НомерСерииРецепта"              , НомерСерииРецепта);
	РезультатВыбора.Вставить("НомерРецепта"                   , НомерРецепта);
	РезультатВыбора.Вставить("ДатаРегистрацииРецепта"         , ДатаРегистрацииРецепта);
	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПроцентЛьготыПриИзменении(Элемент)
	
	Если ВерсияФФД >= "1.2" Тогда
		НастроитьФорму();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьФорму()
	
	Если ВерсияФФД >= "1.2" Тогда
		Если ЗначениеЗаполнено(ПроцентЛьготы) И ЗначениеЗаполнено(ПроцентЛьготы.Процент) Тогда
			Элементы.ГруппаЛьготы.Видимость  = Истина;
			Элементы.ГруппаРецепт.Видимость  = Ложь;
		Иначе
			Элементы.ГруппаЛьготы.Видимость  = Ложь;
			Элементы.ГруппаРецепт.Видимость  = Истина;
		КонецЕсли;
	Иначе
		Если ИспользоватьЛьготы Тогда
			Элементы.ГруппаРецепт.Видимость  = Ложь;
		Иначе
			Элементы.ПроцентЛьготы.Видимость = Ложь;
			Элементы.ГруппаЛьготы.Видимость  = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеПроцентовЛьгот()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ПроцентыЛьгот.Ссылка  КАК ПроцентЛьготы
	|ИЗ
	|	Справочник.ПроцентыЛьгот КАК ПроцентыЛьгот
	|ГДЕ
	|	НЕ ПроцентыЛьгот.ПометкаУдаления
	|УПОРЯДОЧИТЬ ПО
	|	Процент
	|");
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
