
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СпособОпределенияСебестоимости = Параметры.СпособОпределенияСебестоимости;
	Организация = Параметры.Организация;
	ДокументРеализации = Параметры.ДокументРеализации;
	
	УстановитьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если СпособОпределенияСебестоимости <> Перечисления.СпособыОпределенияСебестоимости.ИзДокументаПродажи Тогда
		НепроверяемыеРеквизиты.Добавить("ДокументРеализации");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОКЗакрыть(Команда)
	
	ОчиститьСообщения();
	Если ПроверитьЗаполнение() Тогда
		ПеренестиДанныеВДокумент();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СпособОпределенияСебестоимостиПриИзменении(Элемент)
	
	УстановитьДоступность(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПеренестиДанныеВДокумент()
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("СпособОпределенияСебестоимости", СпособОпределенияСебестоимости);
	Если СпособОпределенияСебестоимости = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСебестоимости.ИзДокументаПродажи") Тогда
		ПараметрыЗакрытия.Вставить("ДокументРеализации", ДокументРеализации);
	Иначе
		ПараметрыЗакрытия.Вставить("ДокументРеализации", Неопределено);
	КонецЕсли;
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступность(Форма)
	
	Форма.Элементы.ДокументРеализации.Доступность = Форма.СпособОпределенияСебестоимости = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСебестоимости.ИзДокументаПродажи");
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
