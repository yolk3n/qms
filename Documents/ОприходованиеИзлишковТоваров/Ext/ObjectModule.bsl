#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ИнвентаризацияТоваровНаСкладе") Тогда
		
		ИнвентаризацияТоваровНаСкладе = ДанныеЗаполнения;
		ЗаполнитьДокументНаОснованииИнвентаризацииТоваровНаСкладе();
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("Структура") Тогда
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
	ЗаполнитьПоЗначениямАвтозаполнения();
	
	ОбщегоНазначенияБольничнаяАптека.ЗаполнитьРеквизитыПоСкладу(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
	
	ЗапасыСервер.ЗаполнитьСтатусыУчетаНоменклатуры(ЭтотОбъект, ЗапасыСервер.ПолучитьПараметрыУчетаНоменклатуры(ЭтотОбъект));
	
	ОбщегоНазначенияБольничнаяАптека.ЗаполнитьРеквизитыПоСкладу(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	ОбработкаТабличнойЧастиСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, НепроверяемыеРеквизиты, Отказ);
	
	ПараметрыУчетаНоменклатуры = ЗапасыСервер.ПолучитьПараметрыУчетаНоменклатуры(ЭтотОбъект);
	ЗапасыСервер.ПроверитьЗаполнениеСерийНоменклатуры(ЭтотОбъект, ПараметрыУчетаНоменклатуры, НепроверяемыеРеквизиты, Отказ);
	ЗапасыСервер.ПроверитьЗаполнениеПартийНоменклатуры(ЭтотОбъект, ПараметрыУчетаНоменклатуры, НепроверяемыеРеквизиты, Отказ);
	
	ПланыВидовХарактеристик.СтатьиДоходов.ПроверитьЗаполнениеАналитик(ЭтотОбъект,, НепроверяемыеРеквизиты, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеБольничнаяАптека.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ОбработкаТабличнойЧастиСервер.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи);
	
	СуммаДокумента = ЦенообразованиеБольничнаяАптекаКлиентСервер.ПолучитьСуммуДокумента(Товары);
	
	ЗапасыСервер.ОчиститьНеиспользуемыеРеквизиты(ЭтотОбъект, ЗапасыСервер.ПолучитьПараметрыУчетаНоменклатуры(ЭтотОбъект));
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеБольничнаяАптека.СформироватьДвиженияПоРегистрам(ЭтотОбъект, Отказ, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеБольничнаяАптека.СформироватьДвиженияПоРегистрам(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Инициализация и заполнение документа
#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент()
	
	Автор = Пользователи.ТекущийПользователь();
	Ответственный = Пользователи.ТекущийПользователь();
	
	ЗаполнитьПоляПоУмолчанию();
	
КонецПроцедуры

Процедура ЗаполнитьПоляПоУмолчанию()
	
	Организация = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	ПодразделениеОрганизации = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьПодразделениеПоУмолчанию(ПодразделениеОрганизации, Организация);
	Склад = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьСкладПоУмолчанию(Склад, ПодразделениеОрганизации);
	
КонецПроцедуры

Процедура ЗаполнитьПоЗначениямАвтозаполнения()
	
	ОбщегоНазначенияБольничнаяАптека.ЗаполнитьПоЗначениямАвтозаполнения(ЭтотОбъект, Неопределено, "Организация, Склад, СтатьяДоходов");
	ОбщегоНазначенияБольничнаяАптека.ЗаполнитьПоЗначениямАвтозаполнения(ЭтотОбъект, Неопределено, "ПодразделениеОрганизации", "Организация");
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоОтбору(ДанныеЗаполнения)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	ОбщегоНазначенияБольничнаяАптека.ПроверитьЗаполнениеПодразделенияОрганизации(ЭтотОбъект);
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииИнвентаризацииТоваровНаСкладе() Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Документ.Ссылка                  КАК Ссылка,
	|	Документ.Организация             КАК Организация,
	|	Документ.Склад                   КАК Склад,
	|	НЕ Документ.Проведен             КАК ЕстьОшибкиПроведен,
	|	Документ.Статус                  КАК Статус,
	|	ВЫБОР
	|		КОГДА Документ.Статус В (&ДоступныеСтатусы)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                            КАК ЕстьОшибкиСтатус
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Инвентаризация
	|");
	
	Запрос.УстановитьПараметр("Инвентаризация", ИнвентаризацияТоваровНаСкладе);
	ДоступныеСтатусы = Новый Массив;
	ДоступныеСтатусы.Добавить(Перечисления.СтатусыИнвентаризацииТоваров.Выполнено);
	Запрос.УстановитьПараметр("ДоступныеСтатусы", ДоступныеСтатусы);
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	ОбщегоНазначенияБольничнаяАптека.ПроверитьВозможностьВводаНаОсновании(
		Шапка.Ссылка,
		Шапка.ЕстьОшибкиПроведен,
		Шапка.Статус,
		Шапка.ЕстьОшибкиСтатус,
		ДоступныеСтатусы);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Шапка);
	
	ЗаполнитьТабличнуюЧастьТовары();
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьТовары() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ИсточникФинансирования", ?(ЗначениеЗаполнено(ИсточникФинансирования), ИсточникФинансирования, Неопределено));
	Запрос.УстановитьПараметр("ДокументОснование", ИнвентаризацияТоваровНаСкладе);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлению.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ТоварыКОформлению.Партия КАК Партия,
	|	ТоварыКОформлению.ИсточникФинансирования КАК ИсточникФинансирования,
	|	СУММА(ТоварыКОформлению.КОформлениюОприходованияОстаток) КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыКОформлению.Номенклатура КАК Номенклатура,
	|		ТоварыКОформлению.СерияНоменклатуры КАК СерияНоменклатуры,
	|		ТоварыКОформлению.Партия КАК Партия,
	|		ТоварыКОформлению.ИсточникФинансирования КАК ИсточникФинансирования,
	|		ТоварыКОформлению.КОформлениюОприходованияОстаток КАК КОформлениюОприходованияОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыКОформлениюИзлишковНедостач.Остатки(
	|				,
	|				ДокументОснование = &ДокументОснование
	|					И Склад = &Склад
	|					И Организация = &Организация
	|					И (&ИсточникФинансирования = НЕОПРЕДЕЛЕНО ИЛИ ИсточникФинансирования = &ИсточникФинансирования)) КАК ТоварыКОформлению
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКОформлению.Номенклатура,
	|		ТоварыКОформлению.СерияНоменклатуры,
	|		ТоварыКОформлению.Партия,
	|		ТоварыКОформлению.ИсточникФинансирования,
	|		ТоварыКОформлению.КОформлениюОприходования
	|	ИЗ
	|		РегистрНакопления.ТоварыКОформлениюИзлишковНедостач КАК ТоварыКОформлению
	|	ГДЕ
	|		ТоварыКОформлению.Регистратор = &Ссылка
	|		И ТоварыКОформлению.ДокументОснование = &ДокументОснование
	|		И ТоварыКОформлению.Активность = ИСТИНА
	|		И (&ИсточникФинансирования = НЕОПРЕДЕЛЕНО ИЛИ ИсточникФинансирования = &ИсточникФинансирования)) КАК ТоварыКОформлению
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|		ТоварыКОформлению.СерияНоменклатуры,
	|		ТоварыКОформлению.Партия,
	|		ТоварыКОформлению.ИсточникФинансирования
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.КОформлениюОприходованияОстаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.СерияНоменклатуры КАК СерияНоменклатуры,
	|	Товары.Партия КАК Партия,
	|	Товары.ИсточникФинансирования КАК ИсточникФинансирования,
	|	МАКСИМУМ(Товары.ЕдиницаИзмерения) КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ ЕдиницыИзмеренияТовара
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ТоварыКОформлению КАК ТоварыКОформлению
	|		ПО
	|			ТоварыКОформлению.Номенклатура = Товары.Номенклатура
	|			И ТоварыКОформлению.СерияНоменклатуры = Товары.СерияНоменклатуры
	|			И ТоварыКОформлению.Партия = Товары.Партия
	|			И ТоварыКОформлению.ИсточникФинансирования = Товары.ИсточникФинансирования
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.СерияНоменклатуры,
	|	Товары.Партия,
	|	Товары.ИсточникФинансирования
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(Товары.ЕдиницаИзмерения) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлению.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ТоварыКОформлению.Партия КАК Партия,
	|	ТоварыКОформлению.ИсточникФинансирования КАК ИсточникФинансирования,
	|	ТоварыКОформлению.Количество КАК Количество,
	|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.ЕдиницаИзмерения,
	|		ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ЕдиницыИзмеренияТовара КАК ЕдиницыИзмеренияТовара
	|		ПО
	|			ЕдиницыИзмеренияТовара.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И ЕдиницыИзмеренияТовара.СерияНоменклатуры = ТоварыКОформлению.СерияНоменклатуры
	|			И ЕдиницыИзмеренияТовара.Партия = ТоварыКОформлению.Партия
	|			И ЕдиницыИзмеренияТовара.ИсточникФинансирования = ТоварыКОформлению.ИсточникФинансирования
	|";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьИсточникиФинансирования") Тогда
			ТаблицаТоваров = Результат.Выгрузить();
			ИсточникиФинансирования = ТаблицаТоваров.Скопировать(, "ИсточникФинансирования");
			ИсточникиФинансирования.Свернуть("ИсточникФинансирования");
			Если ИсточникиФинансирования.Количество() = 1 Тогда
				Товары.Загрузить(ТаблицаТоваров);
				Если Не ЗначениеЗаполнено(ИсточникФинансирования) Тогда
					ИсточникФинансирования = ИсточникиФинансирования[0].ИсточникФинансирования;
				КонецЕсли;
			Иначе
				Возврат;
			КонецЕсли;
		Иначе
			Товары.Загрузить(Результат.Выгрузить());
		КонецЕсли;
		
		Действия = ОбработкаТабличнойЧастиКлиентСервер;
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЕдиницуИзмерения());
		СтруктураДействий.Вставить(Действия.Действие_ПересчитатьКоэффициент());
		СтруктураДействий.Вставить(Действия.Действие_ПересчитатьКоличествоУпаковок());
		
		ОбработкаТабличнойЧастиСервер.ОбработатьТабличнуюЧасть(Товары, СтруктураДействий, Неопределено);
		ЗапасыСервер.ЗаполнитьСтатусыУчетаНоменклатуры(ЭтотОбъект, ЗапасыСервер.ПолучитьПараметрыУчетаНоменклатуры(ЭтотОбъект));
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Нет данных для заполнения по основанию ""%ДокументОснование%"" .'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДокументОснование%", ИнвентаризацияТоваровНаСкладе);
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ИнициализацияИЗаполнение

////////////////////////////////////////////////////////////////////////////////
// Прочее
#Область Прочее

Функция СписокРегистровДляКонтроля() Экспорт
	
	РегистрыДляКонтроля = Новый Массив;
	
	Если Не ДополнительныеСвойства.ЭтоНовый Тогда
		// Приходы в регистр (сторно расхода из регистра) контролируем при перепроведении и отмене проведения
		РегистрыДляКонтроля.Добавить(Движения.СебестоимостьТоваров);
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		РегистрыДляКонтроля.Добавить(Движения.ТоварыКОформлениюИзлишковНедостач);
	КонецЕсли;
	
	Возврат РегистрыДляКонтроля;
	
КонецФункции

#КонецОбласти // Прочее

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли