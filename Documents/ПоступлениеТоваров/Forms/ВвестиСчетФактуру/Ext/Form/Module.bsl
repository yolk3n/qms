
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПредъявленСчетФактура = Истина;
	Если Параметры.ПредъявленСчетФактура Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "НомерВходящегоСчетаФактуры, ДатаВходящегоСчетаФактуры");
	КонецЕсли;
	
	НастроитьОтображениеФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не ПредъявленСчетФактура Тогда
		НепроверяемыеРеквизиты.Добавить("НомерВходящегоСчетаФактуры");
		НепроверяемыеРеквизиты.Добавить("ДатаВходящегоСчетаФактуры");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповестить = Новый ОписаниеОповещения("СохранитьИзмененияПриЗакрытии", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповестить, Отказ, ЗавершениеРаботы, ТекстПредупреждения);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСохранить(Команда)
	
	СохранитьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СохранитьИзмененияПриЗакрытии(Результат, ДополнительныеПараметры) Экспорт
	
	СохранитьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыСчетаФактуры = Новый Структура("ПредъявленСчетФактура, НомерВходящегоСчетаФактуры, ДатаВходящегоСчетаФактуры");
	Если ПредъявленСчетФактура Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыСчетаФактуры, ЭтотОбъект);
	Иначе
		РеквизитыСчетаФактуры.ПредъявленСчетФактура = Ложь;
	КонецЕсли;
	
	Модифицированность = Ложь;
	Закрыть(РеквизитыСчетаФактуры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредъявленСчетФактураПриИзменении(Элемент)
	
	НастроитьОтображениеФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьОтображениеФормы(Форма)
	
	Форма.Элементы.НомерВходящегоСчетаФактуры.Доступность = Форма.ПредъявленСчетФактура;
	Форма.Элементы.ДатаВходящегоСчетаФактуры.Доступность = Форма.ПредъявленСчетФактура;
	
	Форма.Элементы.НомерВходящегоСчетаФактуры.АвтоОтметкаНезаполненного = Форма.ПредъявленСчетФактура;
	Форма.Элементы.ДатаВходящегоСчетаФактуры.АвтоОтметкаНезаполненного = Форма.ПредъявленСчетФактура;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
