
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ДокументВозврата = Параметры.Документ;
	Дата = Параметры.Дата;
	Валюта = Параметры.Валюта;
	ЦенаВключаетНДС = Параметры.ЦенаВключаетНДС;
	Партнер = Параметры.Партнер;
	Организация = Параметры.Организация;
	Контрагент = Параметры.Контрагент;
	ДоговорКонтрагента = Параметры.ДоговорКонтрагента;
	Склад = Параметры.Склад;
	
	Если Период.ДатаОкончания = Дата(1, 1, 1) Тогда
		Период.Вариант = ВариантСтандартногоПериода.Месяц;
	КонецЕсли; 
	
	ЗаполнитьТаблицуТоваров();
	
	ПодборТоваровСервер.УстановитьЗаголовокФормыПодбора(ЭтотОбъект);
	СформироватьИнформационнуюНадписьОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ТоварыВыбраноКоличество > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Подобранные товары не перенесены в документ. Перенести?'");
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы, ТекстВопроса, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПеренестиВДокументДанныеВыбора();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТаблицаТоваровВыбранПриИзменении(Элемент)
	
	Данные = Элементы.ТаблицаТоваров.ТекущиеДанные;
	
	ЗнакСложения = ?(Данные.Выбран, 1, -1);
	ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + ЗнакСложения;
	ТоварыВыбраноСумма = ТоварыВыбраноСумма + Данные.СуммаСНДС * ЗнакСложения;
	
	СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокументВыполнить(Команда)
	
	ПеренестиВДокументДанныеВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТовары(Команда)
	
	Для Каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
		СтрокаТаблицы.Выбран = Истина;
	КонецЦикла;
	
	ТоварыВыбраноКоличество = ТаблицаТоваров.Количество();
	ТоварыВыбраноСумма = ТаблицаТоваров.Итог("СуммаСНДС");
	
	СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьТовары(Команда)
	
	Для Каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
		СтрокаТаблицы.Выбран = Ложь;
	КонецЦикла;
	
	ТоварыВыбраноКоличество = 0;
	ТоварыВыбраноСумма = 0;
	
	СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделенныеТовары(Команда)
	
	МассивСтрок = Элементы.ТаблицаТоваров.ВыделенныеСтроки;
	Для Каждого ИдентификаторСтроки Из МассивСтрок Цикл
		Строка = ТаблицаТоваров.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если Не Строка.Выбран Тогда
			Строка.Выбран = Истина;
			ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + 1;
			ТоварыВыбраноСумма = ТоварыВыбраноСумма + Строка.СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма, 
		Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВыделенныеТовары(Команда)
	
	МассивСтрок = Элементы.ТаблицаТоваров.ВыделенныеСтроки;
	Для Каждого ИдентификаторСтроки Из МассивСтрок Цикл
		Строка = ТаблицаТоваров.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если Строка.Выбран Тогда
			Строка.Выбран = Ложь;
			ТоварыВыбраноКоличество = ТоварыВыбраноКоличество - 1;
			ТоварыВыбраноСумма = ТоварыВыбраноСумма - Строка.СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТовары(Команда)
	
	ЗаполнитьТаблицуТоваров();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодВариантПриИзменении(Элемент)
	
	ЗаполнитьТаблицуТоваров();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	
	
КонецПроцедуры

#Область Прочее

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьИнформационнуюНадписьПодобранныхТоваров(ИнформационнаяНадпись, КоличествоТоваров, СуммаТоваров, Валюта)
	
	ИнформационнаяНадпись = НСтр("ru = 'Всего подобрано товаров [КоличествоТоваров] на сумму [СуммаТоваров] [Валюта]'");
	
	ВставляемыеЗначения = Новый Структура("КоличествоТоваров, СуммаТоваров, Валюта");
	
	ВставляемыеЗначения.КоличествоТоваров = Формат(КоличествоТоваров, "ЧН=0");
	ВставляемыеЗначения.СуммаТоваров      = Формат(СуммаТоваров, "ЧЦ=15; ЧДЦ=2; ЧН=0,00");
	ВставляемыеЗначения.Валюта            = Валюта;
	
	ИнформационнаяНадпись = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ИнформационнаяНадпись, ВставляемыеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокументДанныеВыбора()
	
	АдресТоваровВХранилище = ПоместитьТоварыВХранилище();
	ДанныеПодбора = Новый Структура("АдресТоваровВХранилище", АдресТоваровВХранилище);
	
	ЗакрыватьПриВыборе = Ложь;
	ОповеститьОВыборе(ДанныеПодбора);
	
	Модифицированность = Ложь;
	Закрыть(ДанныеПодбора);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТоварыВХранилище()
	
	Товары = ТаблицаТоваров.Выгрузить(ТаблицаТоваров.НайтиСтроки(Новый Структура("Выбран", Истина)));
	ВозвращаемаяСтруктура = Новый Структура("Товары", Товары);
	АдресТоваровВХранилище = ПоместитьВоВременноеХранилище(ВозвращаемаяСтруктура, УникальныйИдентификатор);
	
	Возврат АдресТоваровВХранилище;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуТоваров()
	
	ТаблицаТоваров.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВозвраты();
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Договор", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("ДокументВозврата", ДокументВозврата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если Валюта <> Выборка.ВалютаПоступления Тогда
			КурсНовойВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);
			КурсСтаройВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Выборка.ВалютаПоступления, Дата);
			
			НоваяСтрока.СуммаСНДС = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(НоваяСтрока.СуммаСНДС, КурсСтаройВалюты, КурсНовойВалюты);
			НоваяСтрока.СуммаНДС = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(НоваяСтрока.СуммаНДС, КурсСтаройВалюты, КурсНовойВалюты);
			
		КонецЕсли;
		
		НоваяСтрока.Коэффициент = НоменклатураСервер.КоэффициентЕдиницыИзмерения(НоваяСтрока.Номенклатура, НоваяСтрока.ЕдиницаИзмерения);
		НоваяСтрока.КоличествоВЕдиницахИзмерения = НоваяСтрока.Количество / НоваяСтрока.Коэффициент;
		
		НоваяСтрока.Сумма = НоваяСтрока.СуммаСНДС - ?(ЦенаВключаетНДС, 0, НоваяСтрока.СуммаНДС);
		Если НоваяСтрока.КоличествоВЕдиницахИзмерения <> 0 Тогда
			НоваяСтрока.Цена = НоваяСтрока.Сумма / НоваяСтрока.КоличествоВЕдиницахИзмерения;
		КонецЕсли;
		
	КонецЦикла;
	
	ТоварыВыбраноКоличество = 0;
	ТоварыВыбраноСумма = 0;
	
	СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьИнформационнуюНадписьОтборы()
	
	ТоварыИнформационнаяНадписьОтборы = НСтр("ru='Отбор по: %Организация%, %Партнер%, %Контрагент%, %Договор%, %Склад%'");
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПартнеров") Тогда
		ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,", %Партнер%", "");
	КонецЕсли;
	ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Организация%", Организация);
	ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Партнер%", Партнер);
	ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Контрагент%", Контрагент);
	ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Договор%", ДоговорКонтрагента);
	ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Склад%", Склад);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстЗапросаВозвраты()
	
	Возврат "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТовары.Ссылка                         КАК ДокументПоступления,
	|	ТаблицаТовары.Номенклатура                   КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры              КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия                         КАК Партия,
	|	ТаблицаТовары.Ссылка.ИсточникФинансирования  КАК ИсточникФинансирования,
	|	МАКСИМУМ(ТаблицаТовары.СтавкаНДС)            КАК СтавкаНДС,
	|	СУММА(ТаблицаТовары.Количество)              КАК Количество,
	|	СУММА(ТаблицаТовары.СуммаНДС)                КАК СуммаНДС,
	|	СУММА(ТаблицаТовары.СуммаСНДС)               КАК СуммаСНДС,
	|	МАКСИМУМ(ТаблицаТовары.ЕдиницаИзмерения)     КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ ДанныеПоступления
	|ИЗ
	|	Документ.ПоступлениеТоваров.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ТаблицаТовары.Ссылка.Проведен
	|	И ТаблицаТовары.Ссылка.Организация = &Организация
	|	И ТаблицаТовары.Ссылка.Контрагент = &Контрагент
	|	И ТаблицаТовары.Ссылка.ДоговорКонтрагента = &Договор
	|	И ТаблицаТовары.Ссылка.Партнер = &Партнер
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	СерияНоменклатуры,
	|	Партия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	СерияНоменклатуры,
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТовары.ДокументПоступления  КАК ДокументПоступления,
	|	ТаблицаТовары.Номенклатура         КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры    КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия               КАК Партия,
	|	СУММА(ТаблицаТовары.Количество)    КАК Количество,
	|	СУММА(ТаблицаТовары.СуммаНДС)      КАК СуммаНДС,
	|	СУММА(ТаблицаТовары.СуммаСНДС)     КАК СуммаСНДС
	|ПОМЕСТИТЬ ДанныеВозврата
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка.Проведен
	|	И ТаблицаТовары.Ссылка <> &ДокументВозврата
	|	И (ДокументПоступления, Номенклатура, СерияНоменклатуры, Партия) В
	|			(ВЫБРАТЬ
	|				ДокументПоступления, Номенклатура, СерияНоменклатуры, Партия
	|			ИЗ
	|				ДанныеПоступления)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	СерияНоменклатуры,
	|	Партия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	СерияНоменклатуры,
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПоступления.ДокументПоступления         КАК ДокументПоступления,
	|	ДанныеПоступления.ДокументПоступления.Валюта  КАК ВалютаПоступления,
	|	ДанныеПоступления.ДокументПоступления.Дата    КАК ДатаПоступления,
	|	ДанныеПоступления.ДокументПоступления.Номер   КАК НомерПоступления,
	|	ДанныеПоступления.Номенклатура                КАК Номенклатура,
	|	ДанныеПоступления.СерияНоменклатуры           КАК СерияНоменклатуры,
	|	ДанныеПоступления.Партия                      КАК Партия,
	|	ДанныеПоступления.ИсточникФинансирования      КАК ИсточникФинансирования,
	|	ДанныеПоступления.ЕдиницаИзмерения            КАК ЕдиницаИзмерения,
	|	ДанныеПоступления.СтавкаНДС                   КАК СтавкаНДС,
	|	ЕСТЬNULL(ДанныеПоступления.Количество, 0) - ЕСТЬNULL(ДанныеВозврата.Количество, 0)  КАК Количество,
	|	ДанныеПоступления.СуммаНДС - ЕСТЬNULL(ДанныеВозврата.СуммаНДС, 0)    КАК СуммаНДС,
	|	ДанныеПоступления.СуммаСНДС - ЕСТЬNULL(ДанныеВозврата.СуммаСНДС, 0)  КАК СуммаСНДС
	|ИЗ
	|	ДанныеПоступления КАК ДанныеПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ДанныеВозврата КАК ДанныеВозврата
	|			ПО
	|				ДанныеВозврата.ДокументПоступления = ДанныеПоступления.ДокументПоступления
	|				И ДанныеВозврата.Номенклатура = ДанныеПоступления.Номенклатура
	|				И ДанныеВозврата.СерияНоменклатуры = ДанныеПоступления.СерияНоменклатуры
	|				И ДанныеВозврата.Партия = ДанныеПоступления.Партия
	|ГДЕ
	|	ДанныеПоступления.Количество - ЕСТЬNULL(ДанныеВозврата.Количество,0) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПоступления УБЫВ
	|";
	
КонецФункции

#КонецОбласти

#КонецОбласти
