
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОчиститьСообщения();
	
	МассивДокументов = ПерезаполнитьОтчетыОРозничныхПродажахНаСервере(ПараметрКоманды);
	
	Оповестить("Запись_ОтчетОРозничныхПродажах", Новый Структура, ПараметрКоманды);
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Заполнение отчетов о розничных продажах выполнено'"));
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПерезаполнитьОтчетыОРозничныхПродажахНаСервере(ПараметрКоманды)
	
	ОтчетыОРозничныхПродажах = Новый Массив;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Документ.КассоваяСмена КАК КассоваяСмена
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК Документ
	|ГДЕ
	|	Документ.Проведен
	|	И Документ.Ссылка В (&Ссылка)
	|	И Документ.СтатусКассовойСмены <> ЗНАЧЕНИЕ(Перечисление.СтатусыКассовойСмены.Открыта)
	|	И Документ.КассоваяСмена <> ЗНАЧЕНИЕ(Документ.КассоваяСмена.ПустаяСсылка)
	|	И Документ.КассаККМ.ТипКассы = ЗНАЧЕНИЕ(Перечисление.ТипыКассККМ.ФискальныйРегистратор)
	|");
	
	Запрос.УстановитьПараметр("Ссылка", ПараметрКоманды);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ОписаниеОшибки = "";
		ОтчетОРозничныхПродажах = РозничныеПродажи.ЗаполнитьОтчетОРозничныхПродажахПоКассовойСмене(Выборка.КассоваяСмена, ОписаниеОшибки);
		
		Если ОтчетОРозничныхПродажах = Неопределено Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ОписаниеОшибки;
			Сообщение.Сообщить();
			
		Иначе
			ОтчетыОРозничныхПродажах.Добавить(ОтчетОРозничныхПродажах);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтчетыОРозничныхПродажах;
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
