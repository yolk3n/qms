#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	// ИнтеграцияМДЛП.Инвентаризация
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияМДЛП.Инвентаризация")
	   И ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИнвентаризацияПотребительскихУпаковокМДЛП") Тогда
		МодульИнвентаризацияМДЛП = ОбщегоНазначения.ОбщийМодуль("ИнвентаризацияМДЛП");
		МодульИнвентаризацияМДЛП.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		Возврат; // Переопределение см. в общем модуле ИнвентаризацияМДЛППереопределяемый.
	КонецЕсли;
	// Конец ИнтеграцияМДЛП.Инвентаризация
	
	ИнтеграцияМДЛППереопределяемый.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	СостояниеПодтверждения = Метаданные().Реквизиты.СостояниеПодтверждения.ЗначениеЗаполнения;
	НомераУпаковок.Очистить();
	
	ДокументИнвентаризации = Неопределено;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не ИнтеграцияМДЛП.ИспользоватьХарактеристикиНоменклатуры() Тогда
		НепроверяемыеРеквизиты.Добавить("Товары.Характеристика");
	КонецЕсли;
	
	Если Не ИнтеграцияМДЛП.ИспользоватьСерииНоменклатуры() Тогда
		НепроверяемыеРеквизиты.Добавить("Товары.Серия");
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Наименование документа"" в строке %1 списка ""Номера упаковок""'");
	
	НепроверяемыеРеквизиты.Добавить("НомераУпаковок.НаименованиеДокумента");
	Для Каждого ТекущаяСтрока Из ДокументыПродажи Цикл
		
		Если ТекущаяСтрока.ВидДокумента = Перечисления.ВидыДокументовПродажиМДЛП.Прочее
		   И Не ЗначениеЗаполнено(ТекущаяСтрока.НаименованиеДокумента) Тогда
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заолнение", НСтр("ru = 'Наименование документа'"), ТекущаяСтрока.НомерСтроки, НСтр("ru = 'Документы продажи'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("НомераУпаковок", ТекущаяСтрока.НомерСтроки, "НаименованиеДокумента")
				,
				,
				Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	ИнтеграцияМДЛП.ПроверитьЗаполнениеУпаковок(ЭтотОбъект, Отказ);
	
	ИнтеграцияМДЛППереопределяемый.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
	ИнтеграцияМДЛП.ПроверитьВозможностьЗаписиУведомления(ЭтотОбъект, РежимЗаписи);
	
	ИнтеграцияМДЛП.УбратьНезначащиеСимволы(ЭтотОбъект, "НомерДокумента");
	
	ИнтеграцияМДЛППереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияМДЛП.ЗаписатьСтатусДокументаПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СтандартнаяОбработка = Истина;
	ИнтеграцияМДЛППереопределяемый.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	Если Константы.ИспользоватьРеестрВыбытияУпаковокМДЛП.Получить() Тогда
		ИнтеграцияМДЛП.ПровестиДокументПоРееструВыбытияУпаковок(Ссылка,, Отказ);
	Иначе
		ПараметрыОперации = ИнтеграцияМДЛП.ПараметрыОперацииИзмененияСтатусаУпаковок();
		ПараметрыОперации.ИсходныйСтатус = Перечисления.СтатусыУпаковокМДЛП.ВОбороте;
		ПараметрыОперации.НовыйСтатус    = Перечисления.СтатусыУпаковокМДЛП.ВыбылаИзОборота;
		ПараметрыОперации.Владелец       = Справочники.ОрганизацииМДЛП.ПустаяСсылка();
		ПараметрыОперации.КонтролироватьОстаткиДолейВРеестреВыбытия = Истина;
		ИнтеграцияМДЛП.ПровестиДокументПоРегиструУпаковок(Ссылка, ПараметрыОперации, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	СтандартнаяОбработка = Истина;
	ИнтеграцияМДЛППереопределяемый.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	Если Константы.ИспользоватьРеестрВыбытияУпаковокМДЛП.Получить() Тогда
		ИнтеграцияМДЛП.ОтменитьПроведениеПоРееструВыбытияУпаковок(Ссылка);
	Иначе
		ИнтеграцияМДЛП.ОтменитьРезерв(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли