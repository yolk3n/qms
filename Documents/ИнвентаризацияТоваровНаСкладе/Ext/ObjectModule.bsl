#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения)
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
	ЗаполнитьПоЗначениямАвтозаполнения();
	
	ОбщегоНазначенияБольничнаяАптека.ЗаполнитьРеквизитыПоСкладу(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Статус = Перечисления.СтатусыИнвентаризацииТоваров.ВРаботе;
	
	ИнициализироватьДокумент();
	
	ПараметрыУчетаНоменклатуры = ЗапасыСервер.ПолучитьПараметрыУчетаНоменклатуры(ЭтотОбъект);
	ЗапасыСервер.ЗаполнитьСтатусыУчетаНоменклатуры(ЭтотОбъект, ПараметрыУчетаНоменклатуры);
	
	ОбщегоНазначенияБольничнаяАптека.ЗаполнитьРеквизитыПоСкладу(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	ОбработкаТабличнойЧастиСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, НепроверяемыеРеквизиты, Отказ);
	ОбработкаТабличнойЧастиСервер.ПроверитьЗаполнениеИсточникаФинансирования(ЭтотОбъект, НепроверяемыеРеквизиты, Отказ);
	
	ПараметрыУчетаНоменклатуры = ЗапасыСервер.ПолучитьПараметрыУчетаНоменклатуры(ЭтотОбъект);
	ЗапасыСервер.ПроверитьЗаполнениеСерийНоменклатуры(ЭтотОбъект, ПараметрыУчетаНоменклатуры, НепроверяемыеРеквизиты, Отказ);
	ЗапасыСервер.ПроверитьЗаполнениеПартийНоменклатуры(ЭтотОбъект, ПараметрыУчетаНоменклатуры, НепроверяемыеРеквизиты, Отказ);
	
	МетаданныеОбъекта = Метаданные();
	
	Для Каждого СтрокаТовара Из Товары Цикл
		
		Если СтрокаТовара.КоличествоВЕдиницахИзмерения = 0 И СтрокаТовара.КоличествоВЕдиницахИзмеренияПоДаннымУчета = 0 Тогда
			
			ТекстСообщения = НСтр("ru='Не заполнено количество ни ""По учету"" ни ""По факту"" в строке %НомерСтроки% списка ""%ИмяТабличнойЧасти%""'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", СтрокаТовара.НомерСтроки);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяТабличнойЧасти%", МетаданныеОбъекта.ТабличныеЧасти.Товары.Синоним);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект, 
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТовара.НомерСтроки, "КоличествоВЕдиницахИзмерения"),
				,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеБольничнаяАптека.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ОбработкаТабличнойЧастиСервер.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи);
	
	ПараметрыУчетаНоменклатуры = ЗапасыСервер.ПолучитьПараметрыУчетаНоменклатуры(ЭтотОбъект);
	ЗапасыСервер.ОчиститьНеиспользуемыеРеквизиты(ЭтотОбъект, ПараметрыУчетаНоменклатуры);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ОбработкаТабличнойЧастиСервер.ЗаполнитьИсточникФинансирования(ЭтотОбъект, Ложь);
		
		Если Не УчетныеДанныеЗаполнены Тогда
			КлючевыеРеквизиты = Новый Массив;
			КлючевыеРеквизиты.Добавить("Номенклатура");
			Если ПараметрыУчетаНоменклатуры.ИспользоватьСерии Тогда
				КлючевыеРеквизиты.Добавить("СерияНоменклатуры");
			КонецЕсли;
			Если ПараметрыУчетаНоменклатуры.ИспользоватьПартии Тогда
				КлючевыеРеквизиты.Добавить("Партия");
			КонецЕсли;
			Если ПолучитьФункциональнуюОпцию("ИспользоватьИсточникиФинансирования") Тогда
				КлючевыеРеквизиты.Добавить("ИсточникФинансирования");
			КонецЕсли;
			ОбработкаТабличнойЧастиСервер.ПроверитьНаличиеДублейСтрок(ЭтотОбъект, "Товары", КлючевыеРеквизиты, Отказ);
		КонецЕсли;
		
		Если Не УчетныеДанныеЗаполнены И Не Отказ Тогда
			ЗаполнитьКоличествоПоУчету();
		КонецЕсли;
		
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		УчетныеДанныеЗаполнены = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеБольничнаяАптека.СформироватьДвиженияПоРегистрам(ЭтотОбъект, Отказ, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеБольничнаяАптека.СформироватьДвиженияПоРегистрам(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Инициализация и заполнение документа
#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент()
	
	Дата = ТекущаяДатаСеанса();
	Автор = Пользователи.ТекущийПользователь();
	Ответственный = Пользователи.ТекущийПользователь();
	
	ЗаполнитьПоляПоУмолчанию();
	
КонецПроцедуры

Процедура ЗаполнитьПоляПоУмолчанию()
	
	Организация = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Склад = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьСкладПоУмолчанию(Склад);
	
КонецПроцедуры

Процедура ЗаполнитьПоЗначениямАвтозаполнения()
	
	ОбщегоНазначенияБольничнаяАптека.ЗаполнитьПоЗначениямАвтозаполнения(ЭтотОбъект, Неопределено, "Организация, Склад");
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоОтбору(ДанныеЗаполнения)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ЗаполнитьКоличествоПоУчету()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Таблица.Номенклатура                  КАК Номенклатура,
	|	Таблица.СерияНоменклатуры             КАК СерияНоменклатуры,
	|	Таблица.Партия                        КАК Партия,
	|	Таблица.СтатусУказанияСерий           КАК СтатусУказанияСерий,
	|	Таблица.СтатусУказанияПартий          КАК СтатусУказанияПартий,
	|	ВЫБОР
	|		КОГДА &ВестиУчетПоИсточникамФинансирования
	|			ТОГДА Таблица.ИсточникФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПустаяСсылка)
	|	КОНЕЦ                                 КАК ИсточникФинансирования,
	|	Таблица.ЕдиницаИзмерения              КАК ЕдиницаИзмерения,
	|	Таблица.Коэффициент                   КАК Коэффициент,
	|	Таблица.Количество                    КАК Количество,
	|	Таблица.КоличествоВЕдиницахИзмерения  КАК КоличествоВЕдиницахИзмерения,
	|	Таблица.НомерСтроки                   КАК НомерСтроки,
	|	Таблица.СтатусОбъекта                 КАК СтатусОбъекта,
	|	Таблица.ЦелеваяФункцияАктива          КАК ЦелеваяФункцияАктива,
	|	Таблица.КоличествоНеСоответствующееУсловиямАктива КАК КоличествоНеСоответствующееУсловиямАктива
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Номенклатура                  КАК Номенклатура,
	|	ТаблицаНоменклатуры.СерияНоменклатуры             КАК СерияНоменклатуры,
	|	ТаблицаНоменклатуры.Партия                        КАК Партия,
	|	ТаблицаНоменклатуры.СтатусУказанияСерий           КАК СтатусУказанияСерий,
	|	ТаблицаНоменклатуры.СтатусУказанияПартий          КАК СтатусУказанияПартий,
	|	ТаблицаНоменклатуры.ИсточникФинансирования        КАК ИсточникФинансирования,
	|	ТаблицаНоменклатуры.ЕдиницаИзмерения              КАК ЕдиницаИзмерения,
	|	ТаблицаНоменклатуры.Коэффициент                   КАК Коэффициент,
	|	ЕСТЬNULL(ТоварыНаСкладах.КоличествоОстаток, 0)    КАК КоличествоПоДаннымУчета,
	|	ВЫБОР 
	|		КОГДА ТаблицаНоменклатуры.Коэффициент = 0
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ТоварыНаСкладах.КоличествоОстаток, 0) / ТаблицаНоменклатуры.Коэффициент
	|	КОНЕЦ                                             КАК КоличествоВЕдиницахИзмеренияПоДаннымУчета,
	|	ТаблицаНоменклатуры.Количество                    КАК Количество,
	|	ТаблицаНоменклатуры.КоличествоВЕдиницахИзмерения  КАК КоличествоВЕдиницахИзмерения,
	|	ТаблицаНоменклатуры.СтатусОбъекта                 КАК СтатусОбъекта,
	|	ТаблицаНоменклатуры.ЦелеваяФункцияАктива          КАК ЦелеваяФункцияАктива,
	|	ТаблицаНоменклатуры.КоличествоНеСоответствующееУсловиямАктива КАК КоличествоНеСоответствующееУсловиямАктива
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				&ДатаОстатков,
	|				(Номенклатура, СерияНоменклатуры, Партия, ИсточникФинансирования, Организация, Склад) В
	|					(ВЫБРАТЬ
	|						Таблица.Номенклатура,
	|						Таблица.СерияНоменклатуры,
	|						Таблица.Партия,
	|						Таблица.ИсточникФинансирования,
	|						&Организация,
	|						&Склад
	|					ИЗ
	|						ТаблицаНоменклатуры КАК Таблица)) КАК ТоварыНаСкладах
	|		ПО
	|			ТаблицаНоменклатуры.Номенклатура = ТоварыНаСкладах.Номенклатура
	|			И ТаблицаНоменклатуры.СерияНоменклатуры = ТоварыНаСкладах.СерияНоменклатуры
	|			И ТаблицаНоменклатуры.Партия = ТоварыНаСкладах.Партия
	|			И ТаблицаНоменклатуры.ИсточникФинансирования = ТоварыНаСкладах.ИсточникФинансирования
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНоменклатуры.НомерСтроки
	|";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", Товары.Выгрузить());
	Запрос.УстановитьПараметр("ДатаОстатков", Дата);
	Запрос.УстановитьПараметр("ВестиУчетПоИсточникамФинансирования", ПолучитьФункциональнуюОпцию("ИспользоватьИсточникиФинансирования"));
	
	Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
	УчетныеДанныеЗаполнены = Истина;
	
КонецПроцедуры

#КонецОбласти // ИнициализацияИЗаполнение

////////////////////////////////////////////////////////////////////////////////
// Прочее
#Область Прочее

Функция СписокРегистровДляКонтроля() Экспорт
	
	РегистрыДляКонтроля = Новый Массив;
	РегистрыДляКонтроля.Добавить(Движения.ТоварыКОформлениюИзлишковНедостач);
	
	Возврат РегистрыДляКонтроля;
	
КонецФункции

#КонецОбласти // Прочее

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли