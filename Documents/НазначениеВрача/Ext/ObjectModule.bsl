#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
	ЗаполнитьПоЗначениямАвтозаполнения();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
	
	Для каждого СтрокаНазначения Из ЛС_Назначения Цикл
		СтрокаНазначения.УИДНазначения = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Для Каждого СтрокаНазначения Из ЛС_Назначения Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаНазначения.Номенклатура)
		   И Не ЗначениеЗаполнено(СтрокаНазначения.ТорговоеНаименование)
		   И Не ЗначениеЗаполнено(СтрокаНазначения.ДействующиеВеществаМНН) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не указан препарат в строке %1 списка назначений.'"), СтрокаНазначения.НомерСтроки),
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЛС_Назначения", СтрокаНазначения.НомерСтроки, "Номенклатура"),
				,
				Отказ);
		КонецЕсли;
		
		Если (ЗначениеЗаполнено(СтрокаНазначения.ТорговоеНаименование)
		      Или ЗначениеЗаполнено(СтрокаНазначения.ДействующиеВеществаМНН))
		   И Не ЗначениеЗаполнено(СтрокаНазначения.ФормаВыпуска) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не указана форма выпуска препарата в строке %1 списка назначений.'"), СтрокаНазначения.НомерСтроки),
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЛС_Назначения", СтрокаНазначения.НомерСтроки, "ФормаВыпуска"),
				,
				Отказ);
		КонецЕсли;
		
		ПериодыНазначения = РасписаниеНазначения.НайтиСтроки(Новый Структура("КлючСтроки", СтрокаНазначения.КлючСтроки));
		Для Каждого СтрокаПериодаНазначения Из ПериодыНазначения Цикл
			
			Если СтрокаПериодаНазначения.Количество - СтрокаПериодаНазначения.КоличествоИсполнено - СтрокаПериодаНазначения.КоличествоОтменено < 0 Тогда
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Превышено количество исполненного и отмененного на дату %2 в строке %1 списка назначений.'"),
					СтрокаНазначения.НомерСтроки,
					СтрокаПериодаНазначения.ДатаИсполнения);
				
				ИмяКолонки = "Дата" + Формат(СтрокаПериодаНазначения.ДатаИсполнения, "ДФ=yyyyMMdd") + "Количество";
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЛС_Назначения", СтрокаНазначения.НомерСтроки, ИмяКолонки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ОбработкаТабличнойЧастиСервер.ПроверитьЗаполнениеИсточникаФинансирования(ЭтотОбъект, НепроверяемыеРеквизиты, Отказ, "ЛС_Назначения");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеБольничнаяАптека.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ОбработкаТабличнойЧастиСервер.ЗаполнитьИсточникФинансирования(ЭтотОбъект, Истина, "ЛС_Назначения");
	КонецЕсли;
	
	ОчиститьНеиспользуемыеСтрокиРасписанияНазначения();
	
	Для Каждого ПериодНазначения Из РасписаниеНазначения Цикл
		Если ПериодНазначения.КоличествоДозИсполнено = 0 И ПериодНазначения.КоличествоИсполнено > 0 Тогда
			ПериодНазначения.КоличествоДозИсполнено = 1;
		ИначеЕсли ПериодНазначения.КоличествоДозИсполнено > 0 И ПериодНазначения.КоличествоИсполнено = 0 Тогда
			ПериодНазначения.КоличествоДозИсполнено = 0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеБольничнаяАптека.СформироватьДвиженияПоРегистрам(ЭтотОбъект, Отказ, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеБольничнаяАптека.СформироватьДвиженияПоРегистрам(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Инициализация и заполнение документа
#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент()
	
	Автор = Пользователи.ТекущийПользователь();
	Ответственный = Пользователи.ТекущийПользователь();
	
	ЗаполнитьПоляПоУмолчанию();
	
КонецПроцедуры

Процедура ЗаполнитьПоляПоУмолчанию()
	
	Организация = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Отделение = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОтделениеПоУмолчанию(Отделение, Организация);
	
КонецПроцедуры

Процедура ЗаполнитьПоЗначениямАвтозаполнения()
	
	ОбщегоНазначенияБольничнаяАптека.ЗаполнитьПоЗначениямАвтозаполнения(ЭтотОбъект, Неопределено, "Организация");
	ОбщегоНазначенияБольничнаяАптека.ЗаполнитьПоЗначениямАвтозаполнения(ЭтотОбъект, Неопределено, "Отделение", "Организация");
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоОтбору(ДанныеЗаполнения)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	ОбщегоНазначенияБольничнаяАптека.ПроверитьЗаполнениеПодразделенияОрганизации(ЭтотОбъект, "Отделение");
	
КонецПроцедуры

#КонецОбласти // ИнициализацияИЗаполнение

////////////////////////////////////////////////////////////////////////////////
// Прочее
#Область Прочее

Функция СписокРегистровДляКонтроля() Экспорт
	
	РегистрыДляКонтроля = Новый Массив;
	
	Если СкладыСервер.ВестиСкладскойУчетВОтделении(Отделение, Дата) Тогда
		РегистрыДляКонтроля.Добавить(Движения.РецептурныеНазначенияВрачей);
	КонецЕсли;
	
	Возврат РегистрыДляКонтроля;
	
КонецФункции

Процедура ОчиститьНеиспользуемыеСтрокиРасписанияНазначения()
	
	УдалитьСтроки = РасписаниеНазначения.НайтиСтроки(Новый Структура("Количество, КоличествоИсполнено, КоличествоОтменено", 0, 0, 0));
	Для Каждого УдалитьСтроку Из УдалитьСтроки Цикл
		РасписаниеНазначения.Удалить(УдалитьСтроку);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // Прочее

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли