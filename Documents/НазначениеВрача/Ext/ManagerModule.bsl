#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Отделение)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Проведение
#Область Проведение

// Инициализирует таблицы значений, содержащие данные для проведения документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицыДвиженийДляПроведения(ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	ОсновныеДанныеДокумента = ПодготовитьОсновныеДанныеДляПроведения(ДополнительныеСвойства);
	
	ИнициализироватьКлючиАналитикиОписанийОтборовНоменклатуры(ОсновныеДанныеДокумента);
	
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтТаблицаТовары());
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтСоставНазначения());
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтНазначенияВрачей());
	ПроведениеБольничнаяАптека.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаРецептурныеНазначенияВрачей(), Метаданные.РегистрыНакопления.РецептурныеНазначенияВрачей);
	
	Запрос = Новый Запрос(ПроведениеБольничнаяАптека.ПолучитьТекстЗапросаДвижений(ДополнительныеСвойства, Регистры));
	
	Для Каждого ДанныеДокумента Из ОсновныеДанныеДокумента Цикл
		Запрос.УстановитьПараметр(ДанныеДокумента.Ключ, ДанныеДокумента.Значение);
	КонецЦикла;
	
	ПроведениеБольничнаяАптека.ЗаполнитьТаблицыДвижений(ДополнительныеСвойства, Запрос.ВыполнитьПакет(), Регистры);
	
КонецПроцедуры

Функция ПодготовитьОсновныеДанныеДляПроведения(ДополнительныеСвойства)
	
	ЗапрашиваемыеДанные = Новый Структура;
	ЗапрашиваемыеДанные.Вставить("Ссылка");
	ЗапрашиваемыеДанные.Вставить("Период", "Дата");
	ЗапрашиваемыеДанные.Вставить("Организация");
	ЗапрашиваемыеДанные.Вставить("Отделение");
	
	ОсновныеДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ПроведениеБольничнаяАптека.ПолучитьСсылкуНаДокументДляПроведения(ДополнительныеСвойства),
		ЗапрашиваемыеДанные);
	
	ОсновныеДанныеДокумента.Вставить("ВестиУчетПоИсточникамФинансирования", ПолучитьФункциональнуюОпцию("ИспользоватьИсточникиФинансирования"));
	ОсновныеДанныеДокумента.Вставить("ВестиСкладскойУчетВОтделении", СкладыСервер.ВестиСкладскойУчетВОтделении(ОсновныеДанныеДокумента.Отделение, ОсновныеДанныеДокумента.Период));
	
	Возврат ОсновныеДанныеДокумента;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаТовары()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                       КАК НомерСтроки,
	|	ТаблицаТовары.КлючСтроки                        КАК КлючСтроки,
	|	&Организация                                    КАК Организация,
	|	ВЫБОР
	|		КОГДА &ВестиУчетПоИсточникамФинансирования
	|			ТОГДА ТаблицаТовары.ИсточникФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПустаяСсылка)
	|	КОНЕЦ                                           КАК ИсточникФинансирования,
	|	ТаблицаТовары.Номенклатура                      КАК Номенклатура,
	|	ТаблицаТовары.ТорговоеНаименование              КАК ТорговоеНаименование,
	|	ТаблицаТовары.ДействующиеВеществаМНН            КАК ДействующиеВеществаМНН,
	|	ТаблицаТовары.ФормаВыпуска                      КАК ФормаВыпуска,
	|	ТаблицаТовары.ЕдиницаИзмерения                  КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	Документ.НазначениеВрача.ЛС_Назначения КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтСоставНазначения()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки             КАК НомерСтроки,
	|	ТаблицаТовары.КлючСтроки              КАК КлючСтроки,
	|	&Период                               КАК Период,
	|	&Ссылка                               КАК НазначениеВрача,
	|	ТаблицаТовары.ИсточникФинансирования  КАК ИсточникФинансирования,
	|	АналитикаОтборов.КлючАналитики        КАК КлючАналитики,
	|	ТаблицаТовары.ЕдиницаИзмерения        КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ ЛСНазначенияВрачей
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаОписанийОтборовНоменклатуры КАК АналитикаОтборов
	|		ПО
	|			ТаблицаТовары.ДействующиеВеществаМНН = АналитикаОтборов.ДействующиеВеществаМНН
	|			И ТаблицаТовары.ТорговоеНаименование = АналитикаОтборов.ТорговоеНаименование
	|			И ТаблицаТовары.ФормаВыпуска         = АналитикаОтборов.ФормаВыпуска
	|			И ТаблицаТовары.Номенклатура         = АналитикаОтборов.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтНазначенияВрачей()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                 КАК НомерСтроки,
	|	ТаблицаТовары.Период                      КАК Период,
	|	ТаблицаТовары.НазначениеВрача             КАК НазначениеВрача,
	|	ТаблицаТовары.ИсточникФинансирования      КАК ИсточникФинансирования,
	|	ТаблицаТовары.КлючАналитики               КАК КлючАналитики,
	|	ТаблицаТовары.ЕдиницаИзмерения            КАК ЕдиницаИзмерения,
	|	РасписаниеНазначения.ДатаИсполнения       КАК ДатаИсполнения,
	|	РасписаниеНазначения.Количество           КАК Количество,
	|	РасписаниеНазначения.КоличествоИсполнено  КАК Исполнено,
	|	РасписаниеНазначения.КоличествоОтменено   КАК Отменено
	|ПОМЕСТИТЬ НазначенияВрачей
	|ИЗ
	|	ЛСНазначенияВрачей КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НазначениеВрача.РасписаниеНазначения КАК РасписаниеНазначения
	|		ПО
	|			ТаблицаТовары.КлючСтроки = РасписаниеНазначения.КлючСтроки
	|			И ТаблицаТовары.НазначениеВрача = РасписаниеНазначения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРецептурныеНазначенияВрачей()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	1                                       КАК Порядок,
	|	ТаблицаТовары.НомерСтроки               КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	ТаблицаТовары.ДатаИсполнения            КАК Период,
	|	ТаблицаТовары.НазначениеВрача           КАК НазначениеВрача,
	|	ТаблицаТовары.ИсточникФинансирования    КАК ИсточникФинансирования,
	|	ТаблицаТовары.КлючАналитики             КАК АналитикаОписанияОтбораНоменклатуры,
	|	ТаблицаТовары.ЕдиницаИзмерения          КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.Количество                КАК Назначено,
	|	0                                       КАК КОформлению
	|ИЗ
	|	НазначенияВрачей КАК ТаблицаТовары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                       КАК Порядок,
	|	ТаблицаТовары.НомерСтроки               КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	ТаблицаТовары.ДатаИсполнения            КАК Период,
	|	ТаблицаТовары.НазначениеВрача           КАК НазначениеВрача,
	|	ТаблицаТовары.ИсточникФинансирования    КАК ИсточникФинансирования,
	|	ТаблицаТовары.КлючАналитики             КАК АналитикаОписанияОтбораНоменклатуры,
	|	ТаблицаТовары.ЕдиницаИзмерения          КАК ЕдиницаИзмерения,
	|	-ТаблицаТовары.Отменено                 КАК Назначено,
	|	0                                       КАК КОформлению
	|ИЗ
	|	НазначенияВрачей КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Отменено <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3                                       КАК Порядок,
	|	ТаблицаТовары.НомерСтроки               КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	ТаблицаТовары.ДатаИсполнения            КАК Период,
	|	ТаблицаТовары.НазначениеВрача           КАК НазначениеВрача,
	|	ТаблицаТовары.ИсточникФинансирования    КАК ИсточникФинансирования,
	|	ТаблицаТовары.КлючАналитики             КАК АналитикаОписанияОтбораНоменклатуры,
	|	ТаблицаТовары.ЕдиницаИзмерения          КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.Исполнено                 КАК Назначено,
	|	0                                       КАК КОформлению
	|ИЗ
	|	НазначенияВрачей КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Исполнено <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4                                       КАК Порядок,
	|	ТаблицаТовары.НомерСтроки               КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	ТаблицаТовары.ДатаИсполнения            КАК Период,
	|	ТаблицаТовары.НазначениеВрача           КАК НазначениеВрача,
	|	ТаблицаТовары.ИсточникФинансирования    КАК ИсточникФинансирования,
	|	ТаблицаТовары.КлючАналитики             КАК АналитикаОписанияОтбораНоменклатуры,
	|	ТаблицаТовары.ЕдиницаИзмерения          КАК ЕдиницаИзмерения,
	|	0                                       КАК Назначено,
	|	ТаблицаТовары.Исполнено                 КАК КОформлению
	|ИЗ
	|	НазначенияВрачей КАК ТаблицаТовары
	|ГДЕ
	|	&ВестиСкладскойУчетВОтделении
	|	И ТаблицаТовары.Исполнено <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ИнициализироватьКлючиАналитикиОписанийОтборовНоменклатуры(Реквизиты)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.ДействующиеВеществаМНН  КАК ДействующиеВеществаМНН,
	|	ТаблицаТовары.ТорговоеНаименование    КАК ТорговоеНаименование,
	|	ТаблицаТовары.ФормаВыпуска            КАК ФормаВыпуска,
	|	ТаблицаТовары.Номенклатура            КАК Номенклатура
	|ПОМЕСТИТЬ втТаблицаАналитики
	|ИЗ
	|	Документ.НазначениеВрача.ЛС_Назначения КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	ТорговоеНаименование,
	|	ДействующиеВеществаМНН,
	|	ФормаВыпуска
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.Выполнить();
	
	Справочники.КлючиАналитикиОписанийОтборовНоменклатуры.ИнициализироватьКлючиАналитики(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

#КонецОбласти // Проведение

////////////////////////////////////////////////////////////////////////////////
// Печать
#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	УправлениеПечатьюБольничнаяАптека.ДобавитьКомандыПечати(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыПечати);
	
КонецПроцедуры

// Возвращает список доступных печатных форм документа
//
Функция ДоступныеПечатныеФормы() Экспорт
	
	ПечатныеФормы = УправлениеПечатьюБольничнаяАптека.СоздатьКоллекциюДоступныхПечатныхФорм();
	
	Возврат ПечатныеФормы;
	
КонецФункции

#КонецОбласти // Печать

////////////////////////////////////////////////////////////////////////////////
// Команды формы
#Область КомандыФормы

// Заполняет список команд ввода на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании, НастройкиФормы) Экспорт
	
	ВводНаОснованииБольничнаяАптека.ДобавитьКомандыСозданияНаОсновании(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыСоздатьНаОсновании, НастройкиФормы);
	
КонецПроцедуры

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, НастройкиФормы) Экспорт
	
	МенюОтчетыБольничнаяАптека.ДобавитьОбщиеКоманды(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыОтчетов, НастройкиФормы);
	
КонецПроцедуры

#КонецОбласти // КомандыФормы

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти // СтандартныеПодсистемы

#КонецЕсли