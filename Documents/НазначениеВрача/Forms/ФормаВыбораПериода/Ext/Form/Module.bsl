
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.ДатаНачала) И Не ЗначениеЗаполнено(Параметры.ДатаОкончания) Тогда
		ДатаНачала = ТекущаяДатаСеанса();
		ДатаОкончания = ТекущаяДатаСеанса();
	ИначеЕсли ЗначениеЗаполнено(Параметры.ДатаНачала) И Не ЗначениеЗаполнено(Параметры.ДатаОкончания)Тогда
		ДатаНачала = Параметры.ДатаНачала;
		ДатаОкончания = ДатаНачала;
	ИначеЕсли Не ЗначениеЗаполнено(Параметры.ДатаНачала) И ЗначениеЗаполнено(Параметры.ДатаОкончания)Тогда
		ДатаНачала = Параметры.ДатаОкончания;
		ДатаОкончания = ДатаНачала;
	Иначе
		Если Параметры.ДатаНачала > Параметры.ДатаОкончания Тогда
			ДатаНачала = Параметры.ДатаОкончания;
			ДатаОкончания = Параметры.ДатаНачала;
		Иначе
			ДатаНачала = Параметры.ДатаНачала;
			ДатаОкончания = Параметры.ДатаОкончания;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВыделение(Элементы.ДатаНачала, ДатаНачала);
	УстановитьВыделение(Элементы.ДатаОкончания, ДатаОкончания);
	
	ПредставлениеПериодаНазначения = ПредставлениеПериода(ДатаНачала, КонецДня(ДатаОкончания));
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ДатаНачала", ДатаНачала);
	РезультатВыбора.Вставить("ДатаОкончания", ДатаОкончания);
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	УстановитьВыделение(Элемент, ДатаНачала);
	
	Если ДатаОкончания < ДатаНачала Тогда
		ДатаОкончания = ДатаНачала;
		УстановитьВыделение(Элементы.ДатаОкончания, ДатаОкончания);
	КонецЕсли;
	
	ПредставлениеПериодаНазначения = ПредставлениеПериода(ДатаНачала, КонецДня(ДатаОкончания));
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	УстановитьВыделение(Элемент, ДатаОкончания);
	
	Если ДатаОкончания < ДатаНачала Тогда
		ДатаНачала = ДатаОкончания;
		УстановитьВыделение(Элементы.ДатаНачала, ДатаНачала);
	КонецЕсли;
	
	ПредставлениеПериодаНазначения = ПредставлениеПериода(ДатаНачала, КонецДня(ДатаОкончания));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВыделение(Элемент, Дата)
	
	Если Элемент.ВыделенныеДаты.Количество() > 1
	 Или Элемент.ВыделенныеДаты.Количество() = 1 И Элемент.ВыделенныеДаты[0] <> Дата Тогда
		Элемент.ВыделенныеДаты.Очистить();
	КонецЕсли;
	
	Если Элемент.ВыделенныеДаты.Количество() = 0 Тогда
		Элемент.ВыделенныеДаты.Добавить(Дата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы
