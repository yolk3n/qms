
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВалютаУпр = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Если Параметры.Свойство("Валюта") Тогда
		Валюта = Параметры.Валюта;
	Иначе
		Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	КонецЕсли;
	
	Параметры.Свойство("Организация", Организация);
	
	УстановитьЗаголовкиПолей();
	
	Если Параметры.Свойство("ТаблицаСтрок") Тогда
		ЗаполнитьРеквизитыДокумента();
	КонецЕсли;
	
	УстановитьПравилоРаспределения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПересчитатьОстатокКРаспределению();
	РассчитатьИтоговыеСуммы();
	ПроверитьЗаполнениеКоэффициентов();
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Если ОсталосьРаспределить <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'Необходимо дораспределить на документы поступления'") + " " + ОсталосьРаспределить + " " + Валюта;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		Закрыть(ПоместитьРезультатВоВременноеХранилище());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБазуРаспределения(Команда)
	
	МассивДокументов = Новый СписокЗначений();
	Для Каждого ТекСтрока Из СписокДокументов Цикл
		МассивДокументов.Добавить(ТекСтрока.АналитикаРасходов);
	КонецЦикла;
	
	ОткрытьФорму("Отчет.РасшифровкаПоступленийПоНоменклатуре.Форма",
		Новый Структура("Отбор, КлючВарианта, СформироватьПриОткрытии, ВидимостьКомандВариантовОтчетов",
			Новый Структура("МассивДокументов", МассивДокументов),
			"ПоступленияСоСтрокамиКонтекстный",
			Истина,
			Ложь));
	
КонецПроцедуры

&НаКлиенте
Процедура Распределить(Команда)
	
	Если ПравилоРаспределения = "ПоКоличеству" Тогда
		Итог = ИтогоКоличество;
		ИмяКоэффициента = "КоэффициентКоличество";
		КомментарийРаспределения = НСтр("ru='Распределена сумма %РаспределенаСумма% (%Валюта%) на %КоличествоСтрок% пропорционально количеству накладной'");
	Иначе
		Итог = ИтогоСумма;
		ИмяКоэффициента = "КоэффициентСумма";
		КомментарийРаспределения = НСтр("ru='Распределена сумма %РаспределенаСумма% (%Валюта%) на %КоличествоСтрок% пропорционально сумме накладной'");
	КонецЕсли;
	
	КомментарийРаспределения = СтрЗаменить(КомментарийРаспределения, "%РаспределенаСумма%", СуммаКРаспределению);
	КомментарийРаспределения = СтрЗаменить(КомментарийРаспределения, "%Валюта%", Валюта);
	ТекстКоличествоСтрок = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(НСтр("ru = ';%1 строку;;%1 строки;%1 строк;%1 строки'"), СписокДокументов.Количество());
	КомментарийРаспределения = СтрЗаменить(КомментарийРаспределения, "%КоличествоСтрок%", ТекстКоличествоСтрок);
	
	КРаспределению = СуммаКРаспределению;
	Для Каждого Строка Из СписокДокументов Цикл
		Если Итог * Строка[ИмяКоэффициента] = 0 Тогда
			Списать = 0;
		Иначе
			Списать = Окр(СуммаКРаспределению / Итог * Строка[ИмяКоэффициента], 2, РежимОкругления.Окр15как20);
		КонецЕсли;
		Строка.КомментарийРаспределения = КомментарийРаспределения;
		Если Списать > КРаспределению Тогда
			Строка.Сумма = КРаспределению;
			КРаспределению = 0;
			Прервать;
		Иначе
			Строка.Сумма = Списать;
			КРаспределению = КРаспределению - Списать;
		КонецЕсли;
	КонецЦикла;
	Если КРаспределению <> 0
	   И КРаспределению <> СуммаКРаспределению Тогда
		Строка.Сумма = Строка.Сумма + КРаспределению;
	КонецЕсли;
	
	ПересчитатьОстатокКРаспределению();
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВариантРаспределенияПриИзменении(Элемент)
	
	ПроверитьЗаполнениеКоэффициентов();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПриИзменении(Элемент)
	
	ПересчитатьОстатокКРаспределению();
	РассчитатьИтоговыеСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовДокументПриИзменении(Элемент)
	
	ИнформацияЗаполнена = ЗаполнитьИнформациюПоДокументуВТекущейСтроке();
	Если ИнформацияЗаполнена Тогда
		ПересчитатьОстатокКРаспределению();
		РассчитатьИтоговыеСуммы();
		ПроверитьЗаполнениеКоэффициентов();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовкиПолей()
	
	ОбщегоНазначенияБольничнаяАптекаКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "Валюта", "Заголовок", Валюта);
	ОбщегоНазначенияБольничнаяАптекаКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ВалютаНеРаспределено", "Заголовок", Валюта);
	ОбщегоНазначенияБольничнаяАптекаКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "СписокДокументовСумма", "Заголовок", НСтр("ru='Сумма'") +" (" + ВалютаУпр + ")");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыДокумента()
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Строки.НомерСтроки       КАК НомерСтроки,
	|	Строки.АналитикаРасходов КАК Ссылка,
	|	Строки.Содержание        КАК Содержание,
	|	Строки.Количество        КАК Количество,
	|	Строки.Сумма             КАК Сумма,
	|	Строки.СтавкаНДС         КАК СтавкаНДС,
	|	""""                     КАК Подразделение,
	|	Строки.СтатьяРасходов    КАК СтатьяРасходов
	|ПОМЕСТИТЬ МассивСтрок
	|ИЗ
	|	&МассивСтрок КАК Строки
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Строки.АналитикаРасходов) = ТИП(Документ.ПоступлениеТоваров)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Строки.Содержание     КАК Содержание,
	|	Строки.Количество     КАК Количество,
	|	Строки.СтавкаНДС      КАК СтавкаНДС,
	|	Строки.Подразделение  КАК Подразделение,
	|	Строки.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ СодержаниеРасходов
	|ИЗ
	|	МассивСтрок КАК Строки
	|ГДЕ
	|	Строки.Содержание <> НЕОПРЕДЕЛЕНО
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Строки.НомерСтроки  КАК НомерСтроки,
	|	Строки.Ссылка       КАК Ссылка,
	|	СУММА(Товары.КоличествоВЕдиницахИзмерения) КАК КоэффициентКоличество
	|ПОМЕСТИТЬ ДанныеПоКоличеству
	|ИЗ
	|	МассивСтрок КАК Строки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПоступлениеТоваров.Товары КАК Товары
	|	ПО
	|		Строки.Ссылка = Товары.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	Строки.НомерСтроки,
	|	Строки.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Строки.НомерСтроки  КАК НомерСтроки,
	|	Строки.Ссылка       КАК Ссылка,
	|	СУММА(Себестоимость.Стоимость) КАК КоэффициентСумма
	|ПОМЕСТИТЬ ДанныеПоСумме
	|ИЗ
	|	МассивСтрок КАК Строки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|	ПО
	|		Строки.Ссылка = Себестоимость.Регистратор
	|		И Себестоимость.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.НомерСтроки,
	|	Строки.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Строки.НомерСтроки                            КАК НомерСтроки,
	|	Строки.Ссылка                                 КАК АналитикаРасходов,
	|	Документ.Организация                          КАК Организация,
	|	Документ.Контрагент                           КАК Контрагент,
	|	Текст.Содержание                              КАК Содержание,
	|	Текст.Количество                              КАК Количество,
	|	Текст.СтавкаНДС                               КАК СтавкаНДС,
	|	Текст.Подразделение                           КАК Подразделение,
	|	Текст.СтатьяРасходов                          КАК СтатьяРасходов,
	|	ЕСТЬNULL(ДанныеПоКоличеству.КоэффициентКоличество, 0)  КАК КоэффициентКоличество,
	|	ЕСТЬNULL(ДанныеПоСумме.КоэффициентСумма, 0)             КАК КоэффициентСумма
	|ИЗ
	|	МассивСтрок КАК Строки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПоступлениеТоваров КАК Документ
	|	ПО
	|		Строки.Ссылка = Документ.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СодержаниеРасходов КАК Текст
	|	ПО
	|		ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ДанныеПоКоличеству КАК ДанныеПоКоличеству
	|	ПО
	|		Строки.Ссылка = ДанныеПоКоличеству.Ссылка
	|		И Строки.НомерСтроки = ДанныеПоКоличеству.НомерСтроки
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ДанныеПоСумме КАК ДанныеПоСумме
	|	ПО
	|		Строки.Ссылка = ДанныеПоСумме.Ссылка
	|		И Строки.НомерСтроки = ДанныеПоСумме.НомерСтроки
	|ГДЕ
	|	Документ.Проведен
	|	И ЕСТЬNULL(ДанныеПоКоличеству.КоэффициентКоличество, 0) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(Строки.Сумма) КАК Сумма
	|ИЗ
	|	МассивСтрок КАК Строки
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Строки.Содержание     КАК Содержание,
	|	Строки.Количество     КАК Количество,
	|	Строки.СтавкаНДС      КАК СтавкаНДС,
	|	Строки.Подразделение  КАК Подразделение,
	|	Строки.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	СодержаниеРасходов КАК Строки
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Строки.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	МассивСтрок КАК Строки
	|ГДЕ
	|	Строки.Ссылка = ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)
	|";
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("МассивСтрок", ПолучитьИзВременногоХранилища(Параметры.ТаблицаСтрок));
	Результат = Запрос.ВыполнитьПакет();
	
	ИндексПоследнегоРезультата = Результат.ВГраница();
	
	Выборка = Результат[ИндексПоследнегоРезультата - 2].Выбрать();
	Выборка.Следующий();
	СуммаКРаспределению = Выборка.Сумма;
	
	Выборка = Результат[ИндексПоследнегоРезультата - 1].Выбрать();
	Выборка.Следующий();
	
	Содержание = Выборка.Содержание;
	Количество = Выборка.Количество;
	СтавкаНДС = Выборка.СтавкаНДС;
	Подразделение = Выборка.Подразделение;
	СтатьяРасходов = Выборка.СтатьяРасходов;
	
	СтрокиКУдалению.Загрузить(Результат[ИндексПоследнегоРезультата].Выгрузить());
	СписокДокументов.Загрузить(Результат[ИндексПоследнегоРезультата - 3].Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПравилоРаспределения()
	
	ПравилоРаспределенияВСтатье = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "ПравилоРаспределенияНаСебестоимость");
	Если ПравилоРаспределенияВСтатье = Перечисления.ПравилаРаспределенияНаСебестоимостьТоваров.ПропорциональноКоличеству
	 Или ПравилоРаспределенияВСтатье = Перечисления.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка() Тогда
		ПравилоРаспределения = "ПоКоличеству";
	ИначеЕсли ПравилоРаспределенияВСтатье = Перечисления.ПравилаРаспределенияНаСебестоимостьТоваров.ПропорциональноСумме Тогда
		ПравилоРаспределения = "ПоСумме";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьИтоговыеСуммы()
	
	ИтогоСумма = СписокДокументов.Итог("КоэффициентСумма");
	ИтогоКоличество = СписокДокументов.Итог("КоэффициентКоличество");
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьОстатокКРаспределению()
	
	ОсталосьРаспределить = СуммаКРаспределению - СписокДокументов.Итог("Сумма")
	
КонецПроцедуры

&НаСервере
Функция ПоместитьРезультатВоВременноеХранилище()
	
	ВыгружаемыеПоля = Новый Структура;
	ВыгружаемыеПоля.Вставить("НомерСтроки");
	ВыгружаемыеПоля.Вставить("Сумма");
	ВыгружаемыеПоля.Вставить("Содержание");
	ВыгружаемыеПоля.Вставить("КомментарийРаспределения");
	ВыгружаемыеПоля.Вставить("Количество");
	ВыгружаемыеПоля.Вставить("СтавкаНДС");
	ВыгружаемыеПоля.Вставить("Подразделение");
	ВыгружаемыеПоля.Вставить("СтатьяРасходов");
	ВыгружаемыеПоля.Вставить("АналитикаРасходов");
	ВыгружаемыеПоля = ОбщегоНазначенияКлиентСервер.КлючиСтруктурыВСтроку(ВыгружаемыеПоля);
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("АдресВоВременномХранилище", ПоместитьВоВременноеХранилище(СписокДокументов.Выгрузить(, ВыгружаемыеПоля)));
	ПараметрыЗакрытия.Вставить("СтрокиКУдалению", ПоместитьВоВременноеХранилище(СтрокиКУдалению.Выгрузить()));
	
	Возврат ПараметрыЗакрытия;
	
КонецФункции

&НаСервере
Функция ЗаполнитьИнформациюПоДокументуВТекущейСтроке()
	
	ТекущаяСтрока = Элементы.СписокДокументов.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеСтроки = СписокДокументов.НайтиПоИдентификатору(ТекущаяСтрока);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	СУММА(Товары.КоличествоВЕдиницахИзмерения)  КАК КоэффициентКоличество
	|ПОМЕСТИТЬ ДанныеПоКоличеству
	|ИЗ
	|	Документ.ПоступлениеТоваров.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(Себестоимость.Стоимость)  КАК КоэффициентСумма
	|ПОМЕСТИТЬ ДанныеПоСумме
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|ГДЕ
	|	Себестоимость.Активность
	|	И Себестоимость.Регистратор = &Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Контрагент                       КАК Контрагент,
	|	ДанныеПоКоличеству.КоэффициентКоличество  КАК КоэффициентКоличество,
	|	ДанныеПоСумме.КоэффициентСумма            КАК КоэффициентСумма
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК Документ,
	|	ДанныеПоКоличеству КАК ДанныеПоКоличеству,
	|	ДанныеПоСумме КАК ДанныеПоСумме
	|ГДЕ
	|	Документ.Ссылка = &Ссылка
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеСтроки.АналитикаРасходов);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, Выборка);
	
	ДанныеСтроки.Количество = Количество;
	ДанныеСтроки.Содержание = Содержание;
	ДанныеСтроки.СтавкаНДС = СтавкаНДС;
	ДанныеСтроки.Подразделение = Подразделение;
	ДанныеСтроки.СтатьяРасходов = СтатьяРасходов;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеКоэффициентов()
	
	Если СписокДокументов.Количество() = 0 Тогда
		Элементы.ГруппаПредупреждение.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ПравилоРаспределения = "ПоКоличеству" Тогда
		ЕстьОшибки = ?(СписокДокументов.Итог("КоэффициентКоличество") = 0, Истина, Ложь);
	ИначеЕсли ПравилоРаспределения = "ПоСумме" Тогда
		ЕстьОшибки = ?(СписокДокументов.Итог("КоэффициентКоличество") = 0, Истина, Ложь);
	КонецЕсли;
	Элементы.ГруппаПредупреждение.Видимость = ЕстьОшибки;
	
КонецПроцедуры

#КонецОбласти
