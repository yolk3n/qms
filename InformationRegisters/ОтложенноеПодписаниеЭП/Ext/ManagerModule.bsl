#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обновляет список ожидаемых подписей для элемента справочника ФедеральныеВебСервисыЭМД.
Процедура АктуализироватьСписокОтложенногоПодписания(ЭМДСсылка) Экспорт

	Если Не ЗначениеЗаполнено(ЭМДСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	Набор_ = РегистрыСведений.ОтложенноеПодписаниеЭП.СоздатьНаборЗаписей();
	
	Набор_.Отбор.ПодписываемыйОбъект.Установить(ЭМДСсылка);
	
	РеквизитыЭМД_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЭМДСсылка, "ДокументИнформационнойБазы, ПометкаУдаления");
	
	Если РеквизитыЭМД_.ПометкаУдаления = Истина Тогда
		Набор_.Записать(Истина);
		Возврат;
	КонецЕсли;

	ТребуемыеПодписи_ = ПолучитьДолжностиДляПодписания(ЭМДСсылка);
	
	ИмеющиесяПодписи_ = ПолучитьСписокИмеющихсяПодписей(ЭМДСсылка);
	
	ДокументИнформационнойБазы_ = РеквизитыЭМД_.ДокументИнформационнойБазы;
	
	РолиСотрудников_ = ОбщиеМеханизмы.СоздатьТаблицуЗначений("Сотрудник, РольРЭМД");
	
	Если ЗначениеЗаполнено(ДокументИнформационнойБазы_) Тогда
		СвойстваДокумента_ = ФедеральныеВебСервисыРЭМД.ПолучитьСвойстваДокументаДляРЭМД(ДокументИнформационнойБазы_, "СписокСотрудниковОбязанныхПодписать");

		Если СвойстваДокумента_.Свойство("СписокСотрудниковОбязанныхПодписать")
			И ТипЗнч(СвойстваДокумента_.СписокСотрудниковОбязанныхПодписать) = Тип("Массив")
		Тогда
			УжеДобавленныеСотрудники_ = Новый Соответствие;
			Для Каждого Сотрудник_ Из СвойстваДокумента_.СписокСотрудниковОбязанныхПодписать Цикл
				Имеющиеся_ = ИмеющиесяПодписи_.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник_));
				
				Если Имеющиеся_.Количество() = 0 И УжеДобавленныеСотрудники_.Получить(Сотрудник_) <> Истина Тогда
					УжеДобавленныеСотрудники_.Вставить(Сотрудник_, Истина);
					
					НоваяЗапись_ = Набор_.Добавить();
					НоваяЗапись_.Сотрудник = Сотрудник_;
					НоваяЗапись_.ПодписываемыйОбъект = ЭМДСсылка;
					НоваяЗапись_.КоличествоПодписей = 0;
					
					НоваяСтрокаТЗ_ = РолиСотрудников_.Добавить();
					НоваяСтрокаТЗ_.Сотрудник = Сотрудник_;
					НоваяСтрокаТЗ_.РольРЭМД = ФедеральныеВебСервисыРЭМД.ПолучитьРольСотрудникаПриПодписанииПоДокументу(ДокументИнформационнойБазы_, Сотрудник_);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ТребуемаяПодпись_ Из ТребуемыеПодписи_ Цикл
		Имеющиеся_ = ИмеющиесяПодписи_.НайтиСтроки(Новый Структура("РольРЭМД", ТребуемаяПодпись_.РольРЭМД));
		ОтСотрудников_ = РолиСотрудников_.НайтиСтроки(Новый Структура("РольРЭМД", ТребуемаяПодпись_.РольРЭМД));
		
		Если ТребуемаяПодпись_.МинимальноеКоличествоПодписей >= (Имеющиеся_.Количество() + ОтСотрудников_.Количество()) Тогда
			ТребуемаяПодпись_.МинимальноеКоличествоПодписей = ТребуемаяПодпись_.МинимальноеКоличествоПодписей - Имеющиеся_.Количество() - ОтСотрудников_.Количество();
		Иначе
			ТребуемаяПодпись_.МинимальноеКоличествоПодписей = 0;
		КонецЕсли;
		
		Если ТребуемаяПодпись_.МинимальноеКоличествоПодписей > 0 Тогда
			НоваяЗапись_ = Набор_.Добавить();
			НоваяЗапись_.РольПриПодписании = ТребуемаяПодпись_.РольРЭМД;
			НоваяЗапись_.ПодписываемыйОбъект = ЭМДСсылка;
			НоваяЗапись_.КоличествоПодписей = ТребуемаяПодпись_.МинимальноеКоличествоПодписей;
		КонецЕсли;
		
	КонецЦикла;
	
	Набор_.Записать(Истина);
	
КонецПроцедуры

// Возвращает ТЧ ДолжностиДляПодписания из элемента справочника ТипыМДРегистрируемыхВРЭМД.
Функция ПолучитьДолжностиДляПодписания(ЭМДСсылка)

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ
		|	МАКСИМУМ(РолиПриПодписанииРЭМДПравилаПодписи.МинимальноеКоличествоПодписей) КАК МинимальноеКоличествоПодписей,
		|	МИНИМУМ(РолиПриПодписанииРЭМДПравилаПодписи.МаксимальноеКоличествоПодписей) КАК МаксимальноеКоличествоПодписей,
		|	РольПриПодписании.Ссылка КАК РольРЭМД
		|ИЗ
		|	Справочник.ФедеральныеВебСервисыЭМД КАК ЭМД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РолиПриПодписанииРЭМД.ПравилаПодписи КАК РолиПриПодписанииРЭМДПравилаПодписи
		|		ПО ЭМД.ТипРЭМД.КодМинздрава = РолиПриПодписанииРЭМДПравилаПодписи.КодМинздраваВидаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РолиПриПодписанииРЭМД КАК РольПриПодписании
		|		ПО (РольПриПодписании.Ссылка = РолиПриПодписанииРЭМДПравилаПодписи.Ссылка)
		|			И (НЕ РольПриПодписании.ПометкаУдаления)
		|ГДЕ
		|	ЭМД.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РольПриПодписании.Ссылка";
		
	Запрос_.УстановитьПараметр("Ссылка", ЭМДСсылка);
	Результат_ = Запрос_.Выполнить().Выгрузить();
	
	Возврат Результат_;
КонецФункции

Функция ПолучитьСписокИмеющихсяПодписей(ЭМДСсылка)
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ
		|	ФедеральныеВебСервисыЭМДЭлектронныеПодписиЭМД.РольРЭМД КАК РольРЭМД,
		|	ФедеральныеВебСервисыЭМДЭлектронныеПодписиЭМД.Сотрудник КАК Сотрудник
		|ИЗ
		|	Справочник.ФедеральныеВебСервисыЭМД.ЭлектронныеПодписиЭМД КАК ФедеральныеВебСервисыЭМДЭлектронныеПодписиЭМД
		|ГДЕ
		|	ФедеральныеВебСервисыЭМДЭлектронныеПодписиЭМД.Ссылка = &Ссылка
		|	И НЕ ФедеральныеВебСервисыЭМДЭлектронныеПодписиЭМД.ЭтоПодписьМО";
		
	Запрос_.УстановитьПараметр("Ссылка", ЭМДСсылка);
	Результат_ = Запрос_.Выполнить().Выгрузить();
	
	Возврат Результат_;
		
КонецФункции

#КонецЕсли