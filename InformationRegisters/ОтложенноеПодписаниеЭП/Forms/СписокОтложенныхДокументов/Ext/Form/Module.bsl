#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Отбор_ = Неопределено;
	Если Параметры.Свойство("Отбор", Отбор_) Тогда
		Если Отбор_.Свойство("Сотрудник") Тогда
			ЭтотОбъект.Сотрудник = Отбор_.Сотрудник;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Сотрудник) Тогда
		Попытка
			ЭтотОбъект.Сотрудник = ФедеральныеВебСервисыПереопределяемый.ПолучитьСотрудникаПользователя(Пользователи.ТекущийПользователь());
		Исключение
			Сообщить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЕсли;

	ЗаполнитьСписокВыбораПользователями();
	
	ЗаполнитьСписокОтложенногоПодписания();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПользовательФильтрПриИзменении(Элемент)
	
	ЗаполнитьСписокОтложенногоПодписания();
	
КонецПроцедуры

&НаКлиенте
Процедура Подписать(Команда)
	
	ПодписатьПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	СтандартнаяОбработка_ = Истина;
	ДокументИнформационнойБазы_ = Элементы.СписокОтложенного.ТекущиеДанные.ДокументИнформационнойБазы;
	
	ФедеральныеВебСервисыКлиентПереопределяемый.ОбработчикСобытияДокументИнформационнойБазыОткрытие(ЭтотОбъект, Элемент, ДокументИнформационнойБазы_, СтандартнаяОбработка_);
	
	Если СтандартнаяОбработка_ = Истина Тогда
		ОткрытьЗначение(ДокументИнформационнойБазы_);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокОтложенногоПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ЗаполнитьСписокОтложенногоПодписания();
КонецПроцедуры

&НаКлиенте
Процедура СписокОтложенногоПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти


&НаСервере
Процедура ЗаполнитьСписокВыбораПользователями()
	
	Элементы.ПользовательФильтр.СписокВыбора.ЗагрузитьЗначения(ФедеральныеВебСервисыПереопределяемый.ПолучитьСотрудниковПользователя(Пользователи.ТекущийПользователь()));
	
	Если ЗначениеЗаполнено(ЭтотОбъект.Сотрудник)
		И Элементы.ПользовательФильтр.СписокВыбора.НайтиПоЗначению(ЭтотОбъект.Сотрудник) = Неопределено
	Тогда
		Элементы.ПользовательФильтр.СписокВыбора.Добавить(ЭтотОбъект.Сотрудник);
	КонецЕсли;
	
	Элементы.ПользовательФильтр.СписокВыбора.СортироватьПоПредставлению();
КонецПроцедуры


#Область СлужебеныеПроцедурыИФункции

// Электронная подпись
//
#Область ЭлектроннаяПодпись

&НаКлиенте
Процедура ПодписатьПродолжение()
	
	МассивСтрок_ = Новый Массив;
	СписокСсылок_ = Новый СписокЗначений;
	
	Для Каждого ВыделеннаяСтрока_ Из Элементы.СписокОтложенного.ВыделенныеСтроки Цикл
		ТекущаяСтрока_ = Элементы.СписокОтложенного.ДанныеСтроки(ВыделеннаяСтрока_);
		
		Если СписокСсылок_.НайтиПоЗначению(ТекущаяСтрока_.ДокументИнформационнойБазы) = Неопределено Тогда
			МассивСтрок_.Добавить(ВыделеннаяСтрока_);
			СписокСсылок_.Добавить(ТекущаяСтрока_.ДокументИнформационнойБазы);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивСтрок_.Количество() > 0 Тогда
		
		НаборДанных_ = Новый Массив;

		Для Каждого ВыделеннаяСтрока_ Из МассивСтрок_ Цикл
			ТекущаяСтрока_ = Элементы.СписокОтложенного.ДанныеСтроки(ВыделеннаяСтрока_);
			
			ДД_ = ФедеральныеВебСервисыРЭМД.ПолучитьДвоичныеДанныеДокументаИзХранилища(ТекущаяСтрока_.ПодписываемыйОбъект);

			НаборДанных_.Добавить(
				Новый Структура("Данные, Представление, ДокументИнформационнойБазы, ПодписываемыйОбъект, РольПриПодписании", 
				ДД_,
				ТекущаяСтрока_.ДокументИнформационнойБазы,
				ТекущаяСтрока_.ДокументИнформационнойБазы,
				ТекущаяСтрока_.ПодписываемыйОбъект,
				ТекущаяСтрока_.РольПриПодписании)
			);
			
		КонецЦикла;

		ОписаниеДанных_ = Новый Структура;
		ОписаниеДанных_.Вставить("Операция",            НСтр("ru = 'Подписание документов'"));
		ОписаниеДанных_.Вставить("ЗаголовокДанных",     "Подписание документов");
		ОписаниеДанных_.Вставить("ПредставлениеНабора", "Документы (%1)");
		ОписаниеДанных_.Вставить("СписокПредставлений", СписокСсылок_);
		ОписаниеДанных_.Вставить(
			"НаборДанных",
			НаборДанных_
		);
		ОписаниеДанных_.Вставить("ПоказатьКомментарий", Истина);
		
		ОбработчикПродолжения_ = Новый ОписаниеОповещения("ПослеДобавленияПодписей", ЭтотОбъект, Новый Структура("СтандартнаяОбработка", Ложь));

		СтандартнаяОбработка_ = Истина;
		ФедеральныеВебСервисыКлиентПереопределяемый.ДобавитьПодписьДокументу(ОписаниеДанных_, ЭтотОбъект, ОбработчикПродолжения_, СтандартнаяОбработка_);
		
		Если СтандартнаяОбработка_ = Истина Тогда
			ОбработчикПродолжения_ = Новый ОписаниеОповещения("ПослеДобавленияПодписей", ЭтотОбъект, Новый Структура("СтандартнаяОбработка", Истина));
			
			ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных_, ЭтотОбъект, ОбработчикПродолжения_);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Завершение процедур ПодписатьФайлНаКлиенте.
Процедура ПослеДобавленияПодписей(Результат, ПараметрыВыполнения) Экспорт
	
	Если ПараметрыВыполнения.СтандартнаяОбработка = Истина Тогда
		Если Результат.Успех Тогда
			
			НаборДанных_ = Результат.НаборДанных;
			
			Для Каждого ЭлементНабораДанных_ Из НаборДанных_ Цикл
				
				СвойстваПодписи_ = Неопределено;
				
				Если ЭлементНабораДанных_.Свойство("СвойстваПодписи", СвойстваПодписи_) Тогда
					ДокументДД_ = ЭлементНабораДанных_.Данные;
					
					ВыбранныйСертификат_ = Результат.ВыбранныйСертификат;
					
					ДокументИнформационнойБазы_ = ЭлементНабораДанных_.ДокументИнформационнойБазы;
					
					РольПриПодписании_ = ЭлементНабораДанных_.РольПриПодписании;
					
					ДобавитьПодписьЭМД(ДокументИнформационнойБазы_, СвойстваПодписи_, ВыбранныйСертификат_, РольПриПодписании_);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	ЭтотОбъект.ПодключитьОбработчикОжидания("ОбновитьСписокОбработчикОжидания", 0.1, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьПодписьЭМД(ДокументИнформационнойБазы, СвойстваПодписи, ВыбранныйСертификат, РольПриПодписании)
	ДанныеЭлектронныхПодписей_ = ФедеральныеВебСервисыРЭМД.ПолучитьДанныеЭлектронныхПодписейИзРезультатаПодписания(
		ДокументИнформационнойБазы, Новый Структура("СвойстваПодписи, ВыбранныйСертификат", СвойстваПодписи, ВыбранныйСертификат),, РольПриПодписании);

	ФедеральныеВебСервисыРЭМД.ДобавитьПодписьДокументуВХранилищеЭМД(
		ДокументИнформационнойБазы,
		ДанныеЭлектронныхПодписей_
	);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОбновитьСписокОбработчикОжидания()
	ЗаполнитьСписокОтложенногоПодписания();
КонецПроцедуры

// Заполнение таблицы
//
#Область ЗаполнениеСпискаДокументов
&НаСервере
Процедура ЗаполнитьСписокОтложенногоПодписания()

	УстановитьПривилегированныйРежим(Истина);

	ЭтотОбъект.СписокОтложенного.Очистить();
	
	Если ЗначениеЗаполнено(ЭтотОбъект.Сотрудник) Тогда
	
		Запрос_ = Новый Запрос;
		Запрос_.Текст =
			"ВЫБРАТЬ
			|	ОтложенноеПодписаниеЭП.Сотрудник КАК Сотрудник,
			|	ОтложенноеПодписаниеЭП.ПодписываемыйОбъект КАК ПодписываемыйОбъект,
			|	ЭМД.ДокументИнформационнойБазы КАК ДокументИнформационнойБазы,
			|	ОтложенноеПодписаниеЭП.КоличествоПодписей КАК КоличествоПодписей,
			|	ЭМД.ДокументИнформационнойБазы.ПометкаУдаления КАК ПометкаУдаления,
			|	ОтложенноеПодписаниеЭП.РольПриПодписании КАК РольПриПодписании,
			|	ЭМД.ТипРЭМД КАК ТипРЭМД
			|ИЗ
			|	РегистрСведений.ОтложенноеПодписаниеЭП КАК ОтложенноеПодписаниеЭП
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФедеральныеВебСервисыЭМД КАК ЭМД
			|		ПО ОтложенноеПодписаниеЭП.ПодписываемыйОбъект = ЭМД.Ссылка
			|ГДЕ
			|	ОтложенноеПодписаниеЭП.Сотрудник = &Сотрудник";
			
		Запрос_.УстановитьПараметр("Сотрудник", ЭтотОбъект.Сотрудник);
		Выборка_ = Запрос_.Выполнить().Выбрать();
		
		Пока Выборка_.Следующий() Цикл
			НоваяСтрока_ = ЭтотОбъект.СписокОтложенного.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока_, Выборка_);
		КонецЦикла;

		// Показываем документы для которых роль выбранного сотрудника подходит для подписания.
		Запрос_ = Новый Запрос;
		Запрос_.Текст =
			"ВЫБРАТЬ
			|	ОтложенноеПодписаниеЭП.РольПриПодписании КАК РольПриПодписании,
			|	ОтложенноеПодписаниеЭП.ПодписываемыйОбъект КАК ПодписываемыйОбъект,
			|	ЭМД.ДокументИнформационнойБазы КАК ДокументИнформационнойБазы,
			|	ОтложенноеПодписаниеЭП.КоличествоПодписей КАК КоличествоПодписей,
			|	ЭМД.ДокументИнформационнойБазы.ПометкаУдаления КАК ПометкаУдаления,
			|	ЭМД.ТипРЭМД КАК ТипРЭМД
			|ИЗ
			|	РегистрСведений.ОтложенноеПодписаниеЭП КАК ОтложенноеПодписаниеЭП
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФедеральныеВебСервисыЭМД КАК ЭМД
			|		ПО ОтложенноеПодписаниеЭП.ПодписываемыйОбъект = ЭМД.Ссылка
			|ГДЕ
			|	(ЭМД.ТипРЭМД, ОтложенноеПодписаниеЭП.РольПриПодписании) В
			|			(ВЫБРАТЬ
			|				ТипыРЭМД.Ссылка,
			|				РолиПриПодписанииРЭМДПравилаПодписи.Ссылка КАК РольРЭМД
			|			ИЗ
			|				Справочник.РолиПриПодписанииРЭМД.ПравилаПодписи КАК РолиПриПодписанииРЭМДПравилаПодписи
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТипыМДРегистрируемыхВРЭМД КАК ТипыРЭМД
			|					ПО
			|						РолиПриПодписанииРЭМДПравилаПодписи.КодМинздраваВидаДокумента = ТипыРЭМД.КодМинздрава
			|							И ТипыРЭМД.Актуальность
			|							И НЕ ТипыРЭМД.ПометкаУдаления
			|			ГДЕ
			|				(РолиПриПодписанииРЭМДПравилаПодписи.КодМинздраваДолжности = &КодМинздраваДолжность
			|					ИЛИ РолиПриПодписанииРЭМДПравилаПодписи.КодМинздраваДолжности = &КодМинздраваГруппыДолжностей)
			|				И РолиПриПодписанииРЭМДПравилаПодписи.Ссылка.Актуальность
			|				И НЕ РолиПриПодписанииРЭМДПравилаПодписи.Ссылка.ПометкаУдаления)";
			
		Должность_ = ФедеральныеВебСервисыПереопределяемый.ПолучитьДанныеСотрудника(ЭтотОбъект.Сотрудник, "Должность").Должность;
		
		КодМинздраваДолжность_ = ФедеральныеВебСервисыПереопределяемый.ПолучитьКодМинздраваПоСсылке(Должность_, "1.2.643.5.1.13.13.11.1002").code;
		
		КодГруппы_ = ФедеральныеВебСервисыПереопределяемый.ПолучитьКодМинздраваРодителя(КодМинздраваДолжность_, "1.2.643.5.1.13.13.11.1002");
		
		Запрос_.УстановитьПараметр("КодМинздраваДолжность", КодМинздраваДолжность_);
		Запрос_.УстановитьПараметр("КодМинздраваГруппыДолжностей", КодГруппы_);

		Выборка_ = Запрос_.Выполнить().Выбрать();
		
		ПодразделениеСотрудника_ = ФедеральныеВебСервисыПереопределяемый.ПолучитьДанныеСотрудника(ЭтотОбъект.Сотрудник, "Подразделение").Подразделение;
		
		Пока Выборка_.Следующий() Цикл
			Если Не ТекущийПользовательУжеПодписал(Выборка_.ПодписываемыйОбъект) Тогда

				Попытка
					ПодразделениеДокумента_ = ФедеральныеВебСервисыПереопределяемый.ПолучитьСвойстваДокументаДляРЭМД(Выборка_.ДокументИнформационнойБазы, "Подразделение").Подразделение;
				Исключение
					Сообщить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					Продолжить;
				КонецПопытки;

				Если Не ЗначениеЗаполнено(ПодразделениеСотрудника_) Или Не ЗначениеЗаполнено(ПодразделениеДокумента_) Или ПодразделениеСотрудника_ = ПодразделениеДокумента_ Тогда
					НоваяСтрока_ = ЭтотОбъект.СписокОтложенного.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока_, Выборка_);
				Иначе
					ВсеВышестоящиеПодразделения_ = ПолучитьИерархиюПодразделений(ПодразделениеДокумента_);
					
					Если ВсеВышестоящиеПодразделения_.Найти(ПодразделениеСотрудника_) <> Неопределено Тогда
						// Сотрудник в одном из вышестоящих подразделений.
						НоваяСтрока_ = ЭтотОбъект.СписокОтложенного.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока_, Выборка_);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	Иначе 	
		Запрос_ = Новый Запрос;
		Запрос_.Текст =
			"ВЫБРАТЬ
			|	ОтложенноеПодписаниеЭП.Сотрудник,
			|	ОтложенноеПодписаниеЭП.РольПриПодписании,
			|	ОтложенноеПодписаниеЭП.ПодписываемыйОбъект КАК ПодписываемыйОбъект,
			|	ЭМД.ДокументИнформационнойБазы КАК ДокументИнформационнойБазы,
			|	ОтложенноеПодписаниеЭП.КоличествоПодписей КАК КоличествоПодписей,
			|	ЭМД.ДокументИнформационнойБазы.ПометкаУдаления КАК ПометкаУдаления
			|ИЗ
			|	РегистрСведений.ОтложенноеПодписаниеЭП КАК ОтложенноеПодписаниеЭП
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФедеральныеВебСервисыЭМД КАК ЭМД
			|		ПО ОтложенноеПодписаниеЭП.ПодписываемыйОбъект = ЭМД.Ссылка";
			
		Выборка_ = Запрос_.Выполнить().Выбрать();
		
		Пока Выборка_.Следующий() Цикл
			НоваяСтрока_ = ЭтотОбъект.СписокОтложенного.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока_, Выборка_);
		КонецЦикла;

	КонецЕсли;
	
	ЭтотОбъект.СписокОтложенного.Сортировать("ДокументИнформационнойБазы,Сотрудник,РольПриПодписании");
КонецПроцедуры

&НаСервере
Функция ПолучитьИерархиюПодразделений(Подразделение)
	Результат_ = Новый Массив;
	
	Результат_.Добавить(Подразделение);
	
	Родитель_ = Подразделение;
	Пока ЗначениеЗаполнено(Родитель_) Цикл
		ДанныеПодразделения_ = ФедеральныеВебСервисыПереопределяемый.ПолучитьДанныеПодразделения(Родитель_);
		Если ДанныеПодразделения_ <> Неопределено И ЗначениеЗаполнено(ДанныеПодразделения_.Родитель) Тогда
			Родитель_ = ДанныеПодразделения_.Родитель;
			Результат_.Добавить(Родитель_);
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат_;
КонецФункции

&НаСервере
Функция ТекущийПользовательУжеПодписал(ПодписанныйОбъект)

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ
		|	ЭлектронныеПодписи.Ссылка КАК ПодписанныйОбъект
		|ИЗ
		|	Справочник.ФедеральныеВебСервисыЭМД.ЭлектронныеПодписиЭМД КАК ЭлектронныеПодписи
		|ГДЕ
		|	ЭлектронныеПодписи.Ссылка = &ПодписанныйОбъект
		|	И ЭлектронныеПодписи.ЭтоПодписьМО = ЛОЖЬ
		|	И ЭлектронныеПодписи.Сотрудник = &УстановившийПодпись";
	Запрос_.УстановитьПараметр("ПодписанныйОбъект", ПодписанныйОбъект);
	Запрос_.УстановитьПараметр("УстановившийПодпись", ЭтотОбъект.Сотрудник);

	Выборка_ = Запрос_.Выполнить().Выбрать();
	
	Если Выборка_.Следующий() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

#КонецОбласти

#КонецОбласти
