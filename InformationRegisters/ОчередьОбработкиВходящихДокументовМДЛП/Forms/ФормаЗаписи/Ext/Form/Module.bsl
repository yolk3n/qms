
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		Запись.ИдентификаторСообщения = Строка(Новый УникальныйИдентификатор);
		Запись.ДатаРегистрации = ТекущаяДатаСеанса();
		Если Не Параметры.ЗначениеКопирования.Пустой() Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ Данные, Квитанция ИЗ РегистрСведений.ОчередьОбработкиВходящихДокументовМДЛП ГДЕ ИдентификаторСообщения = &Идентификатор");
			Запрос.УстановитьПараметр("Идентификатор", Параметры.ЗначениеКопирования.ИдентификаторСообщения);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				УстановитьТекстСообщения(ТекстСообщения, Выборка.Данные.Получить());
				УстановитьТекстСообщения(ТекстКвитанции, Выборка.Квитанция.Получить());
			КонецЕсли;
		КонецЕсли;
	Иначе
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УстановитьТекстСообщения(ТекстСообщения, ТекущийОбъект.Данные.Получить());
	УстановитьТекстСообщения(ТекстКвитанции, ТекущийОбъект.Квитанция.Получить());
	
	Если ПустаяСтрока(ТекстКвитанции.ПолучитьТекст()) Тогда
		Элементы.ТекстКвитанции.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Сообщение = ИнтеграцияМДЛП.ФорматироватьТекстСообщения(ТекстСообщения.ПолучитьТекст(), Ложь);
	ТекущийОбъект.Данные = Новый ХранилищеЗначения(Сообщение, Новый СжатиеДанных(9));
	
	Квитанция = ТекстКвитанции.ПолучитьТекст();
	Если Не ПустаяСтрока(Квитанция) Тогда
		ТекущийОбъект.Квитанция = Новый ХранилищеЗначения(Квитанция, Новый СжатиеДанных(9));
	КонецЕсли;
	
	ХешСумма = ИнтеграцияМДЛП.ХешСуммаСтрокой(Сообщение);
	ТекущийОбъект.ХешСумма = ХешСумма;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТекстСообщения(ТекстовыйДокумент, Сообщение)
	
	Если Сообщение <> Неопределено Тогда
		
		Текст = ИнтеграцияМДЛП.ФорматироватьТекстСообщения(Сообщение);
		ТекстовыйДокумент.УстановитьТекст(Текст);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
