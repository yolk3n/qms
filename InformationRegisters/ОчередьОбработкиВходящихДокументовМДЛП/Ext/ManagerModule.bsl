#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СтруктураДанныхСообщения() Экспорт
	
	Возврат ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(СоздатьМенеджерЗаписи(), ПустойКлюч().Метаданные());
	
КонецФункции

Процедура ДобавитьСообщениеВОчередь(ДанныеСообщения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ХешСумма = ИнтеграцияМДЛП.ХешСуммаСтрокой(ДанныеСообщения.Данные);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Очередь.Данные КАК Данные
	|ИЗ
	|	РегистрСведений.ОчередьОбработкиВходящихДокументовМДЛП КАК Очередь
	|ГДЕ
	|	Очередь.ИдентификаторЗапроса = &ИдентификаторЗапроса
	|	И Очередь.ХешСумма = &ХешСумма
	|");
	Запрос.УстановитьПараметр("ИдентификаторЗапроса", ДанныеСообщения.ИдентификаторЗапроса);
	Запрос.УстановитьПараметр("ХешСумма", ХешСумма);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Данные.Получить() = ДанныеСообщения.Данные Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	Запись = РегистрыСведений.ОчередьОбработкиВходящихДокументовМДЛП.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, ДанныеСообщения);
	Запись.ИдентификаторСообщения = Новый УникальныйИдентификатор;
	Запись.Данные = Новый ХранилищеЗначения(ДанныеСообщения.Данные, Новый СжатиеДанных(9));
	Если ТипЗнч(ДанныеСообщения.Квитанция) = Тип("Строка") И ЗначениеЗаполнено(ДанныеСообщения.Квитанция) Тогда
		Запись.Квитанция = Новый ХранилищеЗначения(ДанныеСообщения.Квитанция, Новый СжатиеДанных(9));
	КонецЕсли;
	Запись.ДатаРегистрации = ТекущаяДатаСеанса();
	Запись.ХешСумма = ХешСумма;
	Запись.Записать();
	
КонецПроцедуры

Процедура УдалитьСообщениеИзОчереди(ИдентификаторСообщения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.ОчередьОбработкиВходящихДокументовМДЛП.СоздатьНаборЗаписей();
	Набор.Отбор.ИдентификаторСообщения.Установить(ИдентификаторСообщения);
	Набор.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли