
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Для Каждого КлючИЗначение Из Параметры.РезультатыПроверки Цикл
		
		ИдентификаторЭлемента     = КлючИЗначение.Ключ;
		РезультатЭлементаПроверки = КлючИЗначение.Значение;
		
		Запись = РезультатыПроверки.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, РезультатЭлементаПроверки);
		Запись.ИдентификаторЭлемента = ИдентификаторЭлемента;
		Запись.КонтрольПройденПользовательский = РезультатЭлементаПроверки.КонтрольПройден;
		
		Представление = КонтрольКодовМаркировкиМДЛПКлиентСервер.ПредставлениеРезультатаПроверкиКМ(РезультатЭлементаПроверки.КлючГруппыНастроекКонтроляКМ, Новый Структура("РезультатПроверкиКМ", РезультатЭлементаПроверки));
		Если ЗначениеЗаполнено(Представление) Тогда
			ЗаполнитьЗначенияСвойств(Запись, Представление);
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьРежимРаботыФормы(Отказ);
	
	СброситьРазмерыИПоложениеОкна();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПодтвердитьРезультатыПроверки(Команда)
	
	РежимВопросИспользованияКМ = Элементы.СтраницыРежим.ТекущаяСтраница = Элементы.СтраницаРежимВопросИспользованияКМ;
	
	КэшСвойствРезультатаСтрокой = Новый Соответствие;
	
	Результаты = Новый Соответствие;
	Для Каждого Запись Из РезультатыПроверки Цикл
		
		ВсеСвойстваРезультатаСтрокой = КэшСвойствРезультатаСтрокой.Получить(Запись.КлючГруппыНастроекКонтроляКМ);
		Если ВсеСвойстваРезультатаСтрокой = Неопределено Тогда
			ВсеСвойстваРезультатаСтрокой = СтрСоединить(СвойстваРезультатаПроверки(Запись.КлючГруппыНастроекКонтроляКМ), ",");
			КэшСвойствРезультатаСтрокой.Вставить(Запись.КлючГруппыНастроекКонтроляКМ, ВсеСвойстваРезультатаСтрокой);
		КонецЕсли;
		
		Результат = Новый Структура(ВсеСвойстваРезультатаСтрокой);
		ЗаполнитьЗначенияСвойств(Результат, Запись);
		Результат.КонтрольПройден = Запись.КонтрольПройденПользовательский;
		
		Если Запись.ТребуетсяПолныйКодМаркировки Тогда
			Результат.КонтрольПройден = Ложь;
		ИначеЕсли РежимВопросИспользованияКМ Тогда
			Результат.КонтрольПройден = Истина;
		КонецЕсли;
		
		Результаты.Вставить(Запись.ИдентификаторЭлемента, Результат);
		
	КонецЦикла;
	
	Закрыть(Результаты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.РезультатыПроверки.Имя);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Элемент.Отбор, "РезультатыПроверки.ТребуетсяПолныйКодМаркировки", Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРежимРаботыФормы(Отказ)
	
	КоличествоЭлементовВРезультатеПроверки = РезультатыПроверки.Количество();
	Если КоличествоЭлементовВРезультатеПроверки > 1 Тогда
		
		Элементы.СтраницыРежим.ТекущаяСтраница = Элементы.СтраницаРежимМножествоКМ;
		
	ИначеЕсли КоличествоЭлементовВРезультатеПроверки = 1 Тогда
		
		Элементы.РезультатыПроверки.Видимость = Ложь;
		
		ТекущиеДанные = РезультатыПроверки[0];
		
		Текст = НСтр("ru = 'Код маркировки ""%1"" не прошел проверку по причине:'");
		
		Если ТолькоПросмотр Или Не ТекущиеДанные.ЕстьОшибки Или ТекущиеДанные.ТребуетсяПолныйКодМаркировки Тогда
			
			Элементы.СтраницыРежим.ТекущаяСтраница = Элементы.СтраницаРежимПросмотрРезультатаПроверкиКМ;
			Элементы.Закрыть.КнопкаПоУмолчанию = Истина;
			
			Элементы.ДекорацияКМНеПрошелПроверку1.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ТекущиеДанные.НомерУпаковки);
			Элементы.ДекорацияПояснениеСтатусаПроверкиКМ1.Заголовок = ТекущиеДанные.ПояснениеСтатусаПроверкиКМ;
			
		Иначе
			
			Элементы.СтраницыРежим.ТекущаяСтраница = Элементы.СтраницаРежимВопросИспользованияКМ;
			Элементы.КомандаПодтвердитьРезультатПроверки.КнопкаПоУмолчанию = Истина;
			
			Элементы.ДекорацияКМНеПрошелПроверку.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ТекущиеДанные.НомерУпаковки);
			Элементы.ДекорацияПояснениеСтатусаПроверкиКМ.Заголовок = ТекущиеДанные.ПояснениеСтатусаПроверкиКМ;
			
		КонецЕсли;
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Нет данных для отображения результата проверки КМ.'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СвойстваРезультатаПроверки(КлючГруппыНастроекКонтроляКМ)
	
	Возврат РегистрыСведений.РезультатыПроверкиКММДЛП.ВсеСвойстваРезультатаПроверкиКМ(КлючГруппыНастроекКонтроляКМ);
	
КонецФункции

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	УстановитьПривилегированныйРежим(Истина);
	ХранилищеСистемныхНастроек.Удалить(ИмяФормы, "", ИмяПользователя);
	УстановитьПривилегированныйРежим(Ложь);
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти
