#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Заполняет коэффициенты единиц измерений, указанных элементов справочника Номенклатура.
//
// Параметры:
//  Номенклатура - список элементов СправочникСсылка.Номенклатура - номенклатура, для которой заполняются единицы измерения.
//
Процедура ЗаполнитьЕдиницыИзмерения(Номенклатура = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Ссылка             КАК Номенклатура,
	|	ЕдиницаИзмерения   КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА Упаковка.Номенклатура = Ссылка
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|		ИНАЧЕ Упаковка
	|	КОНЕЦ              КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ЕдиницаИзмерения.ТипЕдиницы В (&МерныеЕдиницы)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ              КАК МернаяЕдиница,
	|	ВЫБОР
	|		КОГДА ВесИспользовать И ВесМожноУказыватьВДокументах
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ              КАК ИспользоватьВес,
	|	ВЫБОР
	|		КОГДА ОбъемИспользовать И ОбъемМожноУказыватьВДокументах
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ              КАК ИспользоватьОбъем,
	|	ВЫБОР
	|		КОГДА ДлинаИспользовать И ДлинаМожноУказыватьВДокументах
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ              КАК ИспользоватьДлину,
	|	ВЫБОР
	|		КОГДА ПлощадьИспользовать И ПлощадьМожноУказыватьВДокументах
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ              КАК ИспользоватьПлощадь
	|ПОМЕСТИТЬ ДанныеНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ ЭтоГруппа
	|	И &Отбор
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	// Упаковки номенклатуры
	|ВЫБРАТЬ
	|	60 КАК Порядок,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения,
	|	ЕдиницыИзмерения.Коэффициент КАК Коэффициент
	|ПОМЕСТИТЬ НовыеЕдиницыИзмеренияНоменклатуры
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|		ПО
	|			ДанныеНоменклатуры.Номенклатура = ЕдиницыИзмерения.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Упаковки препарата
	|ВЫБРАТЬ
	|	56 КАК Порядок,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения,
	|	ЕдиницыИзмерения.Коэффициент КАК Коэффициент
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|		ПО
	|			ДанныеНоменклатуры.Упаковка = ЕдиницыИзмерения.Ссылка
	|			И ЕдиницыИзмерения.ТипЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыЕдиницИзмерения.Упаковка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	54 КАК Порядок,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения,
	|	ЕдиницыИзмерения.Коэффициент КАК Коэффициент
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|		ПО
	|			ДанныеНоменклатуры.Упаковка.Родитель = ЕдиницыИзмерения.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	52 КАК Порядок,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения,
	|	ЕдиницыИзмерения.Коэффициент КАК Коэффициент
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|		ПО
	|			ДанныеНоменклатуры.Упаковка.Родитель.Родитель = ЕдиницыИзмерения.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Штучные единицы измерения
	|ВЫБРАТЬ
	|	40 КАК Порядок,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения,
	|	ЕдиницыИзмерения.Коэффициент КАК Коэффициент
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|		ПО
	|			ДанныеНоменклатуры.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|			И НЕ ДанныеНоменклатуры.МернаяЕдиница
	|			И ЕдиницыИзмерения.ТипЕдиницы <> ЗНАЧЕНИЕ(Перечисление.ТипыЕдиницИзмерения.Упаковка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Единицы измерения массы
	|ВЫБРАТЬ
	|	30 КАК Порядок,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения,
	|	ЕдиницыИзмерения.Коэффициент КАК Коэффициент
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|		ПО
	|			ЕдиницыИзмерения.ТипЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыЕдиницИзмерения.Вес)
	|			И ДанныеНоменклатуры.ИспользоватьВес
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Единицы измерения объема
	|ВЫБРАТЬ
	|	20 КАК Порядок,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения,
	|	ЕдиницыИзмерения.Коэффициент КАК Коэффициент
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|		ПО
	|			ЕдиницыИзмерения.ТипЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыЕдиницИзмерения.Объем)
	|			И ДанныеНоменклатуры.ИспользоватьОбъем
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Единицы измерения длины
	|ВЫБРАТЬ
	|	10 КАК Порядок,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения,
	|	ЕдиницыИзмерения.Коэффициент КАК Коэффициент
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|		ПО
	|			ЕдиницыИзмерения.ТипЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыЕдиницИзмерения.Длина)
	|			И ДанныеНоменклатуры.ИспользоватьДлину
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Единицы измерения площади
	|ВЫБРАТЬ
	|	5 КАК Порядок,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения,
	|	ЕдиницыИзмерения.Коэффициент КАК Коэффициент
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|		ПО
	|			ЕдиницыИзмерения.ТипЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыЕдиницИзмерения.Площадь)
	|			И ДанныеНоменклатуры.ИспользоватьПлощадь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Существующие единицы измерения номенклатуры
	|ВЫБРАТЬ
	|	70 КАК Порядок,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	СтарыеЕдиницыИзмеренияНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СтарыеЕдиницыИзмеренияНоменклатуры.Коэффициент КАК Коэффициент
	|ПОМЕСТИТЬ СтарыеЕдиницыИзмеренияНоменклатуры
	|ИЗ
	|	РегистрСведений.ЕдиницыИзмеренияНоменклатуры КАК СтарыеЕдиницыИзмеренияНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|		ПО
	|			ДанныеНоменклатуры.Номенклатура = СтарыеЕдиницыИзмеренияНоменклатуры.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Новые и существующие единицы измерения номенклатуры
	|ВЫБРАТЬ
	|	НовыеЕдиницыИзмеренияНоменклатуры.Порядок КАК Порядок,
	|	НовыеЕдиницыИзмеренияНоменклатуры.Номенклатура КАК Номенклатура,
	|	НовыеЕдиницыИзмеренияНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	НовыеЕдиницыИзмеренияНоменклатуры.Коэффициент КАК Коэффициент
	|ИЗ
	|	НовыеЕдиницыИзмеренияНоменклатуры КАК НовыеЕдиницыИзмеренияНоменклатуры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтарыеЕдиницыИзмеренияНоменклатуры.Порядок КАК Порядок,
	|	СтарыеЕдиницыИзмеренияНоменклатуры.Номенклатура КАК Номенклатура,
	|	СтарыеЕдиницыИзмеренияНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СтарыеЕдиницыИзмеренияНоменклатуры.Коэффициент КАК Коэффициент
	|ИЗ
	|	СтарыеЕдиницыИзмеренияНоменклатуры КАК СтарыеЕдиницыИзмеренияНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			НовыеЕдиницыИзмеренияНоменклатуры КАК НовыеЕдиницыИзмеренияНоменклатуры
	|		ПО
	|			НовыеЕдиницыИзмеренияНоменклатуры.Номенклатура = СтарыеЕдиницыИзмеренияНоменклатуры.Номенклатура
	|			И НовыеЕдиницыИзмеренияНоменклатуры.ЕдиницаИзмерения = СтарыеЕдиницыИзмеренияНоменклатуры.ЕдиницаИзмерения
	|ГДЕ
	|	НовыеЕдиницыИзмеренияНоменклатуры.ЕдиницаИзмерения ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок УБЫВ,
	|	Коэффициент
	|
	|ИТОГИ ПО
	|	Номенклатура
	|");
	
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Отбор", ?(ЗначениеЗаполнено(Номенклатура), "Ссылка В (&Номенклатура)", "ИСТИНА"));
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("МерныеЕдиницы", Справочники.ЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
	
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Номенклатура");
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Набор = РегистрыСведений.ЕдиницыИзмеренияНоменклатуры.СоздатьНаборЗаписей();
		Набор.Отбор.Номенклатура.Установить(ВыборкаНоменклатура.Номенклатура);
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() Цикл
			Запись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Запись.Коэффициент = НоменклатураСервер.РассчитатьКоэффициентЕдиницыИзмерения(Выборка.Номенклатура, Выборка.ЕдиницаИзмерения, 0);
		КонецЦикла;
		
		Набор.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет перерасчет коэффициентов указанной единицы измерения.
//
// Параметры:
//  ЕдиницаИзмерения - СправочникСсылка.ЕдиницыИзмерения - единица, по которой выполняется перерасчет.
//  ТипЕдиницы       - ПеречислениеСсылка.ТипыЕдиницИзмерения - тип единицы измерения.
//
Процедура ПересчитатьКоэффициентЕдиницыИзмеренияНоменклатуры(ЕдиницаИзмерения, ТипЕдиницы) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Номенклатура
	|ИЗ
	|	РегистрСведений.ЕдиницыИзмеренияНоменклатуры
	|ГДЕ
	|	ЕдиницаИзмерения = &ЕдиницаИзмерения
	|");
	
	Запрос.УстановитьПараметр("ЕдиницаИзмерения", ЕдиницаИзмерения);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Набор = РегистрыСведений.ЕдиницыИзмеренияНоменклатуры.СоздатьНаборЗаписей();
		Набор.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
		Если Справочники.ЕдиницыИзмерения.МерныеТипыЕдиницИзмерений().Найти(ТипЕдиницы) = Неопределено Тогда
			Набор.Отбор.ЕдиницаИзмерения.Установить(ЕдиницаИзмерения);
		КонецЕсли;
		
		Набор.Прочитать();
		Для Каждого Запись Из Набор Цикл
			Запись.Коэффициент = НоменклатураСервер.РассчитатьКоэффициентЕдиницыИзмерения(Запись.Номенклатура, Запись.ЕдиницаИзмерения, 0);
		КонецЦикла;
		
		Набор.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет перерасчет коэффициентов указанной номенклатуры.
//
// Параметры:
//  Номенклатура - список элементов СправочникСсылка.Номенклатура - элементы,
//                 по которым выполняется перерасчет коэффициентов.
//
Процедура ПересчитатьКоэффициентыНоменклатуры(Номенклатура) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Номенклатура КАК Номенклатура,
	|	ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Порядок КАК Порядок
	|ИЗ
	|	РегистрСведений.ЕдиницыИзмеренияНоменклатуры
	|ГДЕ
	|	Номенклатура В (&Номенклатура)
	|ИТОГИ ПО
	|	Номенклатура
	|");
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Набор = РегистрыСведений.ЕдиницыИзмеренияНоменклатуры.СоздатьНаборЗаписей();
		Набор.Отбор.Номенклатура.Установить(ВыборкаНоменклатура.Номенклатура);
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() Цикл
			Запись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Запись.Коэффициент = НоменклатураСервер.РассчитатьКоэффициентЕдиницыИзмерения(Запись.Номенклатура, Запись.ЕдиницаИзмерения, 0);
		КонецЦикла;
		
		Набор.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Создает коэффициент указанной единицы измерения для указанной номенклатуры.
//
// Параметры:
//  ЕдиницаИзмерения - СправочникСсылка.ЕдиницыИзмерения - элемент, по которому создается коэффициент.
//  ТипЕдиницы       - ПеречислениеСсылка.ТипыЕдиницИзмерения - тип единицы измерения.
//  Номенклатура     - СправочникСсылка.Номенклатура - элемент, для которого формируется коэффициент единицы измерения.
//
Процедура ДобавитьЕдиницуИзмеренияНоменклатуры(ЕдиницаИзмерения, ТипЕдиницы, Номенклатура) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Упаковка И ЗначениеЗаполнено(Номенклатура) Тогда
		Запись = РегистрыСведений.ЕдиницыИзмеренияНоменклатуры.СоздатьМенеджерЗаписи();
		Запись.Номенклатура = Номенклатура;
		Запись.ЕдиницаИзмерения = ЕдиницаИзмерения;
		Запись.Коэффициент = НоменклатураСервер.РассчитатьКоэффициентЕдиницыИзмерения(Запись.Номенклатура, Запись.ЕдиницаИзмерения, 0);
		Запись.Порядок = "60";
		Запись.Записать();
	ИначеЕсли Справочники.ЕдиницыИзмерения.МерныеТипыЕдиницИзмерений().Найти(ТипЕдиницы) <> Неопределено Тогда
		
		Мера = ОбщегоНазначения.ИмяЗначенияПеречисления(ТипЕдиницы);
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Ссылка КАК Номенклатура
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	НЕ ЭтоГруппа
		|	И ВЫБОР
		|		КОГДА " + Мера + "Использовать И " + Мера + "МожноУказыватьВДокументах
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ = ИСТИНА
		|";
		
		Набор = РегистрыСведений.ЕдиницыИзмеренияНоменклатуры.СоздатьНаборЗаписей();
		Набор.Отбор.ЕдиницаИзмерения.Установить(ЕдиницаИзмерения);
		
		Запрос = Новый Запрос(ТекстЗапроса);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Запись = Набор.Добавить();
			Запись.Номенклатура = Выборка.Номенклатура;
			Запись.ЕдиницаИзмерения = ЕдиницаИзмерения;
			Запись.Коэффициент = НоменклатураСервер.РассчитатьКоэффициентЕдиницыИзмерения(Запись.Номенклатура, Запись.ЕдиницаИзмерения, 0);
			Запись.Порядок =
				?(ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Вес, "30",
				?(ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Объем, "20",
				?(ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Длина, "10", "5")));
		КонецЦикла;
		
		Если Набор.Количество() > 0 Тогда
			Набор.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#КонецЕсли