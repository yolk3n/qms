#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Очищает всю информацию ABC-классификации номенклатуры.
//
Процедура ОчиститьABCКлассификацию() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ABCКлассификацияНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет расчет ABC класса номенклатуры и записывает его в регистр
//
//	Параметры:
//		ДатаКлассификации - Дата - дата окончания периода, за который нужно выбрать данные для
//			классификации, если Неопределенно - используется текущая дата
//
Процедура ВыполнитьABCКлассификацию(ДатаКлассификации = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИспользоватьABCКлассификациюПоСуммеЗакупок = Константы.ИспользоватьABCКлассификациюНоменклатурыПоСуммеЗакупок.Получить();
	ИспользоватьABCКлассификациюПоКоличествуЗакупок = Константы.ИспользоватьABCКлассификациюНоменклатурыПоКоличествуЗакупок.Получить();
	
	Если Не ИспользоватьABCКлассификациюПоСуммеЗакупок И Не ИспользоватьABCКлассификациюПоКоличествуЗакупок Тогда
		Возврат;
	КонецЕсли;
	
	ПериодКлассификации = Константы.ПериодABCКлассификацииНоменклатуры.Получить();
	КоличествоПериодовКлассификации = Константы.КоличествоПериодовABCКлассификацииНоменклатуры.Получить();
	
	Если ПериодКлассификации.Пустая() Или КоличествоПериодовКлассификации = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Закупки.Организация КАК Организация,
	|	Закупки.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	АналитикаНоменклатуры.Номенклатура КАК Номенклатура,
	|	АналитикаНоменклатуры.Номенклатура.ЭтоЛекарственноеСредство КАК ЭтоЛекарственноеСредство,
	|	ВЫБОР
	|		КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатурыРасширенный = ЗНАЧЕНИЕ(Справочник.ТипыНоменклатурыРасширенные.ЛекарственноеСредство)
	|			ТОГДА АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ТорговоеНаименование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|	КОНЕЦ КАК ТорговоеНаименование,
	|	ВЫБОР
	|		КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатурыРасширенный = ЗНАЧЕНИЕ(Справочник.ТипыНоменклатурыРасширенные.ЛекарственноеСредство)
	|			ТОГДА АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ДействующиеВеществаМНН
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|	КОНЕЦ КАК ДействующиеВеществаМНН,
	|	ЕСТЬNULL(АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ФормаВыпуска, ЗНАЧЕНИЕ(Справочник.ФормыВыпуска.ПустаяСсылка)) КАК ФормаВыпуска,
	|	ЕСТЬNULL(АналитикиОписанийНоменклатура.КлючАналитики, НЕОПРЕДЕЛЕНО) КАК АналитикаОписанияНоменклатуры,
	|	ЕСТЬNULL(АналитикиОписанийТорговоеНаименование.КлючАналитики, НЕОПРЕДЕЛЕНО) КАК АналитикаОписанияТорговогоНаименования,
	|	ЕСТЬNULL(АналитикиОписанийДействующиеВеществаМНН.КлючАналитики, НЕОПРЕДЕЛЕНО) КАК АналитикаОписанияДействующихВеществМНН,
	|	Закупки.СуммаОборот КАК Сумма,
	|	Закупки.КоличествоОборот КАК Количество
	|ПОМЕСТИТЬ ИсточникДанных
	|ИЗ
	|	РегистрНакопления.Закупки.Обороты(&НачалоПериода, &ОкончаниеПериода, , ) КАК Закупки
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|			ПО
	|				АналитикаНоменклатуры.КлючАналитики = Закупки.АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаОписанийОтборовНоменклатуры КАК АналитикиОписанийНоменклатура
	|			ПО
	|				АналитикаНоменклатуры.Номенклатура = АналитикиОписанийНоменклатура.Номенклатура
	|				И ((НЕ АналитикаНоменклатуры.Номенклатура.ЭтоЛекарственноеСредство)
	|				ИЛИ (ВЫБОР
	|						КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатурыРасширенный = ЗНАЧЕНИЕ(Справочник.ТипыНоменклатурыРасширенные.ЛекарственноеСредство)
	|							ТОГДА АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ТорговоеНаименование
	|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|					КОНЕЦ = АналитикиОписанийНоменклатура.ТорговоеНаименование
	|					И ВЫБОР
	|						КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатурыРасширенный = ЗНАЧЕНИЕ(Справочник.ТипыНоменклатурыРасширенные.ЛекарственноеСредство)
	|							ТОГДА АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ДействующиеВеществаМНН
	|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|					КОНЕЦ = АналитикиОписанийНоменклатура.ДействующиеВеществаМНН
	|					И АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ФормаВыпуска = АналитикиОписанийНоменклатура.ФормаВыпуска)
	|				)
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаОписанийОтборовНоменклатуры КАК АналитикиОписанийТорговоеНаименование
	|			ПО
	|					(НЕ АналитикаНоменклатуры.Номенклатура.ЭтоЛекарственноеСредство)
	|					И АналитикаНоменклатуры.Номенклатура = АналитикиОписанийТорговоеНаименование.Номенклатура
	|				ИЛИ
	|					(АналитикаНоменклатуры.Номенклатура.ЭтоЛекарственноеСредство)
	|					И АналитикиОписанийТорговоеНаименование.Номенклатура =
	|						ВЫБОР
	|							КОГДА АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|								ТОГДА АналитикаНоменклатуры.Номенклатура
	|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|						КОНЕЦ
	|					И АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ТорговоеНаименование = АналитикиОписанийТорговоеНаименование.ТорговоеНаименование
	|					И ВЫБОР
	|						КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатурыРасширенный = ЗНАЧЕНИЕ(Справочник.ТипыНоменклатурыРасширенные.ЛекарственноеСредство)
	|							ТОГДА АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ДействующиеВеществаМНН
	|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|					КОНЕЦ = АналитикиОписанийТорговоеНаименование.ДействующиеВеществаМНН
	|					И АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ФормаВыпуска = АналитикиОписанийТорговоеНаименование.ФормаВыпуска
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаОписанийОтборовНоменклатуры КАК АналитикиОписанийДействующиеВеществаМНН
	|			ПО
	|					(НЕ АналитикаНоменклатуры.Номенклатура.ЭтоЛекарственноеСредство)
	|					И АналитикаНоменклатуры.Номенклатура = АналитикиОписанийДействующиеВеществаМНН.Номенклатура
	|				ИЛИ
	|					(АналитикаНоменклатуры.Номенклатура.ЭтоЛекарственноеСредство)
	|					И АналитикиОписанийДействующиеВеществаМНН.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|					И ВЫБОР
	|						КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатурыРасширенный = ЗНАЧЕНИЕ(Справочник.ТипыНоменклатурыРасширенные.ЛекарственноеСредство)
	|							ТОГДА АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ТорговоеНаименование
	|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|					КОНЕЦ = АналитикиОписанийДействующиеВеществаМНН.ТорговоеНаименование
	|					И ВЫБОР
	|						КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатурыРасширенный = ЗНАЧЕНИЕ(Справочник.ТипыНоменклатурыРасширенные.ЛекарственноеСредство)
	|							ТОГДА АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ДействующиеВеществаМНН
	|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|					КОНЕЦ = АналитикиОписанийДействующиеВеществаМНН.ДействующиеВеществаМНН
	|					И АналитикаНоменклатуры.Номенклатура.ЭлементКАТ.ФормаВыпуска = АналитикиОписанийДействующиеВеществаМНН.ФормаВыпуска
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 1 Классификация Номенклатуры
	// 1.1 Данные для классификация в целом по предприятию
	|ВЫБРАТЬ
	|	0 КАК РазделительКлассификации,
	|	НЕОПРЕДЕЛЕНО КАК РазделКлассификации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.Номенклатура) КАК ТипОбъектаКлассификации,
	|	ИсточникДанных.Номенклатура КАК Номенклатура,
	|	ИсточникДанных.ТорговоеНаименование КАК ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН КАК ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска КАК ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияНоменклатуры КАК ОбъектКлассификации,
	|	СУММА(ИсточникДанных.Сумма) КАК Сумма,
	|	СУММА(Количество) КАК Количество
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.Номенклатура,
	|	ИсточникДанных.ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияНоменклатуры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 1.2 Данные для классификация в разрезе организаций
	|ВЫБРАТЬ
	|	1,
	|	ИсточникДанных.Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.Номенклатура),
	|	ИсточникДанных.Номенклатура,
	|	ИсточникДанных.ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияНоменклатуры,
	|	СУММА(ИсточникДанных.Сумма),
	|	СУММА(Количество)
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.Организация,
	|	ИсточникДанных.Номенклатура,
	|	ИсточникДанных.ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияНоменклатуры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 1.3 Данные для классификация в разрезе подразделений организаций
	|ВЫБРАТЬ
	|	2,
	|	ИсточникДанных.ПодразделениеОрганизации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.Номенклатура),
	|	ИсточникДанных.Номенклатура,
	|	ИсточникДанных.ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияНоменклатуры,
	|	СУММА(ИсточникДанных.Сумма),
	|	СУММА(Количество)
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.ПодразделениеОрганизации,
	|	ИсточникДанных.Номенклатура,
	|	ИсточникДанных.ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 2 Классификация Торговых наименований + формы выпуска
	// 2.1 Данные для классификация в целом по предприятию
	|ВЫБРАТЬ
	|	0 КАК РазделительКлассификации,
	|	НЕОПРЕДЕЛЕНО КАК РазделКлассификации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ТорговоеНаименование_ФормаВыпуска) КАК ТипОбъектаКлассификации,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ КАК Номенклатура,
	|	ИсточникДанных.ТорговоеНаименование КАК ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН КАК ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска КАК ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияТорговогоНаименования КАК ОбъектКлассификации,
	|	СУММА(ИсточникДанных.Сумма) КАК Сумма,
	|	СУММА(Количество) КАК Количество
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ИсточникДанных.ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияТорговогоНаименования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 2.2 Данные для классификация в разрезе организаций
	|ВЫБРАТЬ
	|	1,
	|	ИсточникДанных.Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ТорговоеНаименование_ФормаВыпуска),
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ИсточникДанных.ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияТорговогоНаименования,
	|	СУММА(ИсточникДанных.Сумма),
	|	СУММА(Количество)
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.Организация,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ИсточникДанных.ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияТорговогоНаименования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 2.3 Данные для классификация в разрезе подразделений организаций
	|ВЫБРАТЬ
	|	2,
	|	ИсточникДанных.ПодразделениеОрганизации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ТорговоеНаименование_ФормаВыпуска),
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ИсточникДанных.ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияТорговогоНаименования,
	|	СУММА(ИсточникДанных.Сумма),
	|	СУММА(Количество)
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.ПодразделениеОрганизации,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ИсточникДанных.ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияТорговогоНаименования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 3 Классификация Торговых наименований
	// 3.1 Данные для классификация в целом по предприятию
	|ВЫБРАТЬ
	|	0 КАК РазделительКлассификации,
	|	НЕОПРЕДЕЛЕНО КАК РазделКлассификации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ТорговоеНаименование) КАК ТипОбъектаКлассификации,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ИсточникДанных.ТорговоеНаименование
	|	КОНЕЦ КАК ОбъектКлассификации,
	|	СУММА(ИсточникДанных.Сумма) КАК Сумма,
	|	СУММА(Количество) КАК Количество
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ИсточникДанных.ТорговоеНаименование
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 3.2 Данные для классификация в разрезе организаций
	|ВЫБРАТЬ
	|	1,
	|	ИсточникДанных.Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ТорговоеНаименование),
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ИсточникДанных.ТорговоеНаименование
	|	КОНЕЦ КАК ОбъектКлассификации,
	|	СУММА(ИсточникДанных.Сумма),
	|	СУММА(Количество)
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.Организация,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ИсточникДанных.ТорговоеНаименование
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 3.3 Данные для классификация в разрезе подразделений организаций
	|ВЫБРАТЬ
	|	2,
	|	ИсточникДанных.ПодразделениеОрганизации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ТорговоеНаименование),
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ИсточникДанных.ТорговоеНаименование
	|	КОНЕЦ КАК ОбъектКлассификации,
	|	СУММА(ИсточникДанных.Сумма),
	|	СУММА(Количество)
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.ПодразделениеОрганизации,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ИсточникДанных.ТорговоеНаименование
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 4 Классификация Действующих веществ(МНН) + формы выпуска
	// 4.1 Данные для классификация в целом по предприятию
	|ВЫБРАТЬ
	|	0 КАК РазделительКлассификации,
	|	НЕОПРЕДЕЛЕНО КАК РазделКлассификации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ДействующиеВеществаМНН_ФормаВыпуска) КАК ТипОбъектаКлассификации,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|	КОНЕЦ КАК ТорговоеНаименование,
	|	ИсточникДанных.ДействующиеВеществаМНН КАК ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска КАК ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияДействующихВеществМНН КАК ОбъектКлассификации,
	|	СУММА(ИсточникДанных.Сумма) КАК Сумма,
	|	СУММА(Количество) КАК Количество
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|	КОНЕЦ,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияДействующихВеществМНН
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 4.2 Данные для классификация в разрезе организаций
	|ВЫБРАТЬ
	|	1,
	|	ИсточникДанных.Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ДействующиеВеществаМНН_ФормаВыпуска),
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|	КОНЕЦ,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияДействующихВеществМНН,
	|	СУММА(ИсточникДанных.Сумма),
	|	СУММА(Количество)
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.Организация,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|	КОНЕЦ,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияДействующихВеществМНН
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 4.3 Данные для классификация в разрезе подразделений организаций
	|ВЫБРАТЬ
	|	2,
	|	ИсточникДанных.ПодразделениеОрганизации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ДействующиеВеществаМНН_ФормаВыпуска),
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|	КОНЕЦ,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияДействующихВеществМНН,
	|	СУММА(ИсточникДанных.Сумма),
	|	СУММА(Количество)
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.ПодразделениеОрганизации,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|	КОНЕЦ,
	|	ИсточникДанных.ДействующиеВеществаМНН,
	|	ИсточникДанных.ФормаВыпуска,
	|	ИсточникДанных.АналитикаОписанияДействующихВеществМНН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 5 Классификация Действующих веществ(МНН)
	// 5.1 Данные для классификация в целом по предприятию
	|ВЫБРАТЬ
	|	0 КАК РазделительКлассификации,
	|	НЕОПРЕДЕЛЕНО КАК РазделКлассификации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ДействующиеВеществаМНН) КАК ТипОбъектаКлассификации,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ИсточникДанных.ДействующиеВеществаМНН
	|	КОНЕЦ КАК ОбъектКлассификации,
	|	СУММА(ИсточникДанных.Сумма) КАК Сумма,
	|	СУММА(Количество) КАК Количество
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ИсточникДанных.ДействующиеВеществаМНН
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 5.2 Данные для классификация в разрезе организаций
	|ВЫБРАТЬ
	|	1,
	|	ИсточникДанных.Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ДействующиеВеществаМНН),
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ИсточникДанных.ДействующиеВеществаМНН
	|	КОНЕЦ,
	|	СУММА(ИсточникДанных.Сумма),
	|	СУММА(Количество)
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.Организация,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ИсточникДанных.ДействующиеВеществаМНН
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 5.3 Данные для классификация в разрезе подразделений организаций
	|ВЫБРАТЬ
	|	2,
	|	ИсточникДанных.ПодразделениеОрганизации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКлассификации.ДействующиеВеществаМНН),
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ИсточникДанных.ДействующиеВеществаМНН
	|	КОНЕЦ,
	|	СУММА(ИсточникДанных.Сумма),
	|	СУММА(Количество)
	|ИЗ
	|	ИсточникДанных КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.ПодразделениеОрганизации,
	|	ВЫБОР 
	|		КОГДА ИсточникДанных.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
	|				И ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.Номенклатура
	|		КОГДА ИсточникДанных.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
	|			ТОГДА ИсточникДанных.ТорговоеНаименование
	|		ИНАЧЕ ИсточникДанных.ДействующиеВеществаМНН
	|	КОНЕЦ
	|";
	
	Период = КлассификацияБольничнаяАптека.РасширенныйПериод(?(ДатаКлассификации = Неопределено, ТекущаяДатаСеанса(), ДатаКлассификации), ПериодКлассификации, -КоличествоПериодовКлассификации);
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ОкончаниеПериода", Период.ДатаОкончания);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	РезультатыКлассификации = Запрос.ВыполнитьПакет();
	
	РезультатКлассификацииНоменклатура = РезультатыКлассификации[1].Выгрузить();
	РезультатКлассификацииТорговоеНаименованиеФормаВыпуска = РезультатыКлассификации[2].Выгрузить();
	РезультатКлассификацииТорговоеНаименование = РезультатыКлассификации[3].Выгрузить();
	РезультатКлассификацииДействующиеВеществаМННФормаВыпуска = РезультатыКлассификации[4].Выгрузить();
	РезультатКлассификацииДействующиеВеществаМНН = РезультатыКлассификации[5].Выгрузить();
	
	Если ИспользоватьABCКлассификациюПоСуммеЗакупок Тогда
		
		КлассификацияБольничнаяАптека.ВыполнитьABCКлассификацию(РезультатКлассификацииНоменклатура, "Сумма");
		КлассификацияБольничнаяАптека.ВыполнитьABCКлассификацию(РезультатКлассификацииТорговоеНаименованиеФормаВыпуска, "Сумма");
		КлассификацияБольничнаяАптека.ВыполнитьABCКлассификацию(РезультатКлассификацииТорговоеНаименование, "Сумма");
		КлассификацияБольничнаяАптека.ВыполнитьABCКлассификацию(РезультатКлассификацииДействующиеВеществаМННФормаВыпуска, "Сумма");
		КлассификацияБольничнаяАптека.ВыполнитьABCКлассификацию(РезультатКлассификацииДействующиеВеществаМНН, "Сумма");
		
		ОбновитьДанныеКлассификации(
			РезультатКлассификацииНоменклатура,
			Перечисления.ТипыОбъектовКлассификации.Номенклатура,
			Перечисления.ТипыПараметровКлассификации.ЗакупкиСумма,
			ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ДобавитьКДате(Период.ДатаОкончания, "СЕКУНДА", 1),
			Перечисления.ABCКлассификация.НеКлассифицирован);
		ОбновитьДанныеКлассификации(
			РезультатКлассификацииТорговоеНаименованиеФормаВыпуска,
			Перечисления.ТипыОбъектовКлассификации.ТорговоеНаименование_ФормаВыпуска,
			Перечисления.ТипыПараметровКлассификации.ЗакупкиСумма,
			ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ДобавитьКДате(Период.ДатаОкончания, "СЕКУНДА", 1),
			Перечисления.ABCКлассификация.НеКлассифицирован);
		ОбновитьДанныеКлассификации(
			РезультатКлассификацииТорговоеНаименование,
			Перечисления.ТипыОбъектовКлассификации.ТорговоеНаименование,
			Перечисления.ТипыПараметровКлассификации.ЗакупкиСумма,
			ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ДобавитьКДате(Период.ДатаОкончания, "СЕКУНДА", 1),
			Перечисления.ABCКлассификация.НеКлассифицирован);
		ОбновитьДанныеКлассификации(
			РезультатКлассификацииДействующиеВеществаМННФормаВыпуска,
			Перечисления.ТипыОбъектовКлассификации.ДействующиеВеществаМНН_ФормаВыпуска,
			Перечисления.ТипыПараметровКлассификации.ЗакупкиСумма,
			ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ДобавитьКДате(Период.ДатаОкончания, "СЕКУНДА", 1),
			Перечисления.ABCКлассификация.НеКлассифицирован);
		ОбновитьДанныеКлассификации(
			РезультатКлассификацииДействующиеВеществаМНН,
			Перечисления.ТипыОбъектовКлассификации.ДействующиеВеществаМНН,
			Перечисления.ТипыПараметровКлассификации.ЗакупкиСумма,
			ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ДобавитьКДате(Период.ДатаОкончания, "СЕКУНДА", 1),
			Перечисления.ABCКлассификация.НеКлассифицирован);
		
	КонецЕсли;
	
	Если ИспользоватьABCКлассификациюПоКоличествуЗакупок Тогда
		
		КлассификацияБольничнаяАптека.ВыполнитьABCКлассификацию(РезультатКлассификацииНоменклатура, "Количество");
		КлассификацияБольничнаяАптека.ВыполнитьABCКлассификацию(РезультатКлассификацииТорговоеНаименованиеФормаВыпуска, "Количество");
		КлассификацияБольничнаяАптека.ВыполнитьABCКлассификацию(РезультатКлассификацииТорговоеНаименование, "Количество");
		КлассификацияБольничнаяАптека.ВыполнитьABCКлассификацию(РезультатКлассификацииДействующиеВеществаМННФормаВыпуска, "Количество");
		КлассификацияБольничнаяАптека.ВыполнитьABCКлассификацию(РезультатКлассификацииДействующиеВеществаМНН, "Количество");
		
		ОбновитьДанныеКлассификации(
			РезультатКлассификацииНоменклатура,
			Перечисления.ТипыОбъектовКлассификации.Номенклатура,
			Перечисления.ТипыПараметровКлассификации.ЗакупкиКоличество,
			ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ДобавитьКДате(Период.ДатаОкончания, "СЕКУНДА", 1),
			Перечисления.ABCКлассификация.НеКлассифицирован);
		ОбновитьДанныеКлассификации(
			РезультатКлассификацииТорговоеНаименованиеФормаВыпуска,
			Перечисления.ТипыОбъектовКлассификации.ТорговоеНаименование_ФормаВыпуска,
			Перечисления.ТипыПараметровКлассификации.ЗакупкиКоличество,
			ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ДобавитьКДате(Период.ДатаОкончания, "СЕКУНДА", 1),
			Перечисления.ABCКлассификация.НеКлассифицирован);
		ОбновитьДанныеКлассификации(
			РезультатКлассификацииТорговоеНаименование,
			Перечисления.ТипыОбъектовКлассификации.ТорговоеНаименование,
			Перечисления.ТипыПараметровКлассификации.ЗакупкиКоличество,
			ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ДобавитьКДате(Период.ДатаОкончания, "СЕКУНДА", 1),
			Перечисления.ABCКлассификация.НеКлассифицирован);
		ОбновитьДанныеКлассификации(
			РезультатКлассификацииДействующиеВеществаМННФормаВыпуска,
			Перечисления.ТипыОбъектовКлассификации.ДействующиеВеществаМНН_ФормаВыпуска,
			Перечисления.ТипыПараметровКлассификации.ЗакупкиКоличество,
			ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ДобавитьКДате(Период.ДатаОкончания, "СЕКУНДА", 1),
			Перечисления.ABCКлассификация.НеКлассифицирован);
		ОбновитьДанныеКлассификации(
			РезультатКлассификацииДействующиеВеществаМНН,
			Перечисления.ТипыОбъектовКлассификации.ДействующиеВеществаМНН,
			Перечисления.ТипыПараметровКлассификации.ЗакупкиКоличество,
			ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ДобавитьКДате(Период.ДатаОкончания, "СЕКУНДА", 1),
			Перечисления.ABCКлассификация.НеКлассифицирован);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьДанныеКлассификации(РезультатКлассификации, ТипОбъектаКлассификации, ТипПараметраКлассификации, ПериодКлассификации, НеКлассифицирован)
	
	Если РезультатКлассификации.Колонки.Найти("Период") = Неопределено Тогда
		РезультатКлассификации.Колонки.Добавить("Период");
	КонецЕсли;
	
	Если РезультатКлассификации.Колонки.Найти("ТипПараметраКлассификации") = Неопределено Тогда
		РезультатКлассификации.Колонки.Добавить("ТипПараметраКлассификации");
	КонецЕсли;
	
	Если РезультатКлассификации.Колонки.Найти("ТипОбъектаКлассификации") = Неопределено Тогда
		РезультатКлассификации.Колонки.Добавить("ТипОбъектаКлассификации");
	КонецЕсли;
	
	Если РезультатКлассификации.Колонки.Найти("ОбъектКлассификации") = Неопределено Тогда
		РезультатКлассификации.Колонки.Добавить("ОбъектКлассификации");
	КонецЕсли;
	
	РезультатКлассификации.ЗаполнитьЗначения(ПериодКлассификации, "Период");
	РезультатКлассификации.ЗаполнитьЗначения(ТипПараметраКлассификации, "ТипПараметраКлассификации");
	РезультатКлассификации.ЗаполнитьЗначения(ТипОбъектаКлассификации, "ТипОбъектаКлассификации");

	Для Каждого ДанныеКлассификации Из РезультатКлассификации Цикл
		Если ДанныеКлассификации.ОбъектКлассификации = Неопределено Тогда
			
			ДанныеКлассификации.ОбъектКлассификации 
				= Справочники.КлючиАналитикиОписанийОтборовНоменклатуры.ЗначениеКлючаАналитики(ДанныеКлассификации);
			
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.ABCКлассификацияНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ПериодКлассификации);
	НаборЗаписей.Отбор.ТипОбъектаКлассификации.Установить(ТипОбъектаКлассификации);
	НаборЗаписей.Отбор.ТипПараметраКлассификации.Установить(ТипПараметраКлассификации);

	НаборЗаписей.Загрузить(РезультатКлассификации);
	НаборЗаписей.Записать(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ABCКлассификацияНоменклатуры.ОбъектКлассификации КАК ОбъектКлассификации,
	|	ABCКлассификацияНоменклатуры.РазделКлассификации КАК РазделКлассификации
	|ПОМЕСТИТЬ РезультатКлассификации
	|ИЗ
	|	РегистрСведений.ABCКлассификацияНоменклатуры КАК ABCКлассификацияНоменклатуры
	|ГДЕ
	|	ABCКлассификацияНоменклатуры.Период = &ПериодКлассификации
	|	И ABCКлассификацияНоменклатуры.ТипПараметраКлассификации = &ТипПараметраКлассификации
	|	И ABCКлассификацияНоменклатуры.ТипОбъектаКлассификации = &ТипОбъектаКлассификации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектКлассификации,
	|	РазделКлассификации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ПериодКлассификации КАК Период,
	|	ABCКлассификацияНоменклатурыСрезПоследних.ОбъектКлассификации КАК ОбъектКлассификации,
	|	ABCКлассификацияНоменклатурыСрезПоследних.РазделКлассификации КАК РазделКлассификации,
	|	ABCКлассификацияНоменклатурыСрезПоследних.ТипПараметраКлассификации КАК ТипПараметраКлассификации,
	|	ABCКлассификацияНоменклатурыСрезПоследних.ТипОбъектаКлассификации КАК ТипОбъектаКлассификации,
	|	ABCКлассификацияНоменклатурыСрезПоследних.Класс КАК СтарыйКласс,
	|	&НеКлассифицирован КАК Класс,
	|	0 КАК ЗначениеПараметраКлассификации
	|ИЗ
	|	РегистрСведений.ABCКлассификацияНоменклатуры.СрезПоследних(
	|			ДОБАВИТЬКДАТЕ(&ПериодКлассификации, СЕКУНДА, -1),
	|			(НЕ (ОбъектКлассификации, РазделКлассификации) В
	|						(ВЫБРАТЬ
	|							РезультатКлассификации.ОбъектКлассификации КАК ОбъектКлассификации,
	|							РезультатКлассификации.РазделКлассификации КАК РазделКлассификации
	|						ИЗ
	|							РезультатКлассификации КАК РезультатКлассификации))
	|				И ТипПараметраКлассификации = &ТипПараметраКлассификации
	|				И ТипОбъектаКлассификации = &ТипОбъектаКлассификации) КАК ABCКлассификацияНоменклатурыСрезПоследних
	|ГДЕ
	|	НЕ ABCКлассификацияНоменклатурыСрезПоследних.Класс = &НеКлассифицирован");
	
	Запрос.УстановитьПараметр("ПериодКлассификации", ПериодКлассификации);
	Запрос.УстановитьПараметр("ТипОбъектаКлассификации", ТипОбъектаКлассификации);
	Запрос.УстановитьПараметр("ТипПараметраКлассификации", ТипПараметраКлассификации);
	Запрос.УстановитьПараметр("НеКлассифицирован", НеКлассифицирован);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		НаборЗаписей.Загрузить(РезультатЗапроса.Выгрузить());
		НаборЗаписей.Записать(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли