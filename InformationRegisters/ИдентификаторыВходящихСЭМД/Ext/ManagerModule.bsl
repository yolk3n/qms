#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

Процедура ЗаписатьИдентификаторВходящегоСЭМД(ДокументСсылка, ТипСЭМД, ИдентификаторСообщенияСЭМД) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Документ.Установить(ДокументСсылка);
	Набор.Отбор.Документ.Установить(ТипСЭМД);
	
	Запись = Набор.Добавить();
	Запись.Документ = ДокументСсылка;
	Запись.ТипСЭМД = ТипСЭМД;
	Запись.ИдентификаторСообщенияСЭМД = ИдентификаторСообщенияСЭМД;
	
	Набор.Записать();
	
КонецПроцедуры

Функция ПолучитьТекстВходящихСЭМД(ДокументСсылка, ТипСЭМД = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИдентификаторыВходящихСЭМД.ТипСЭМД             КАК ТипСЭМД,
	|	ФедеральныеВебСервисыСообщенияРЭМД.ПутьКФайлу  КАК ПутьКФайлу
	|ИЗ
	|	РегистрСведений.ИдентификаторыВходящихСЭМД КАК ИдентификаторыВходящихСЭМД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ФедеральныеВебСервисыСообщенияРЭМД КАК ФедеральныеВебСервисыСообщенияРЭМД
	|		ПО
	|			ФедеральныеВебСервисыСообщенияРЭМД.ИдентификаторСообщения = ИдентификаторыВходящихСЭМД.ИдентификаторСообщенияСЭМД
	|ГДЕ
	|	ИдентификаторыВходящихСЭМД.Документ = &Документ
	|");
	
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	
	Если ЗначениеЗаполнено(ТипСЭМД) Тогда
		
		// Добавление отбора по ТипСЭМД.
		
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		
		Отбор = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор;
		Отбор.Добавить("ИдентификаторыВходящихСЭМД.ТипСЭМД = &ТипСЭМД");
		
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		
		Запрос.УстановитьПараметр("ТипСЭМД", ТипСЭМД);
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.ТипСЭМД, ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(Выборка.ПутьКФайлу)));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
