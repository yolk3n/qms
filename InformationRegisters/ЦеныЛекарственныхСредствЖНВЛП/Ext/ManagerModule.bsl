#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Возвращает зарегистрированную цену ЖНВЛП производителя на препарат
//
// Параметры
//  Препарат - СправочникСсылка.КЛП, СправочникСсылка.РегистрЛекарственныхСредств, СправочникСсылка.Номенклатура
//  ДатаЦены - Дата
//  Валюта - СправочникСсылка.Валюты
//
// Возвращаемое значение
//  Число
//
Функция ЗарегистрированнаяЦенаПроизводителя(Препарат, ДатаЦены, Валюта) Экспорт
	
	Если Не ЗначениеЗаполнено(Препарат) Тогда
		Возврат 0;
	КонецЕсли;
	
	ЭлементКАТ = Неопределено;
	Если ТипЗнч(Препарат) = Тип("СправочникСсылка.КЛП") Или ТипЗнч(Препарат) = Тип("СправочникСсылка.РегистрЛекарственныхСредств") Тогда
		ЭлементКАТ = Препарат;
	ИначеЕсли ТипЗнч(Препарат) = Тип("СправочникСсылка.Номенклатура") Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Препарат, "ЭтоЛекарственноеСредство, ЭлементКАТ");
		Если Реквизиты.ЭтоЛекарственноеСредство Тогда
			ЭлементКАТ = Реквизиты.ЭлементКАТ;
		КонецЕсли;
	КонецЕсли;
	
	Цена = 0;
	Если ЗначениеЗаполнено(ЭлементКАТ) Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Цены.ЦенаРуб * КурсыВалюты.Кратность / КурсыВалюты.Курс КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныЛекарственныхСредствЖНВЛП.СрезПоследних(&Дата, КАТ = &Препарат) КАК Цены
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта = &Валюта) КАК КурсыВалюты
		|		ПО ИСТИНА
		|ГДЕ
		|	Цены.ДатаИсключения = ДАТАВРЕМЯ(1,1,1) ИЛИ Цены.ДатаИсключения >= &Дата
		|");
		Запрос.УстановитьПараметр("Дата", ДатаЦены);
		Запрос.УстановитьПараметр("Препарат", ЭлементКАТ);
		Запрос.УстановитьПараметр("Валюта", Валюта);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			Цена = Выборка.Цена;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Цена;
	
КонецФункции

Процедура ОбновитьЦеныЖНВЛП(ЦеныЖНВЛП) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ЦеныЛекарственныхСредствЖНВЛП;
	
	Для Каждого ЦенаЖНВЛП Из ЦеныЖНВЛП Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Запись = СоздатьМенеджерЗаписи();
			Запись.Период = ЦенаЖНВЛП.Период;
			Запись.КАТ    = ЦенаЖНВЛП.КАТ;
			
			Запись.Прочитать();
			ЗаписьСуществует = Запись.Выбран();
			
			Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
				Если ЗаписьСуществует И Запись[Ресурс.Имя] = ЦенаЖНВЛП[Ресурс.Имя] Тогда
					Продолжить;
				КонецЕсли;
				Запись[Ресурс.Имя] = ЦенаЖНВЛП[Ресурс.Имя];
			КонецЦикла;
			
			Если Запись.Модифицированность() Тогда
				Запись.Период = ЦенаЖНВЛП.Период;
				Запись.КАТ    = ЦенаЖНВЛП.КАТ;
				Запись.Записать();
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ИмяСобытия = НСтр("ru = 'Обновление цен ЖНВЛП'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			
			Текст = НСтр("ru = 'Не удалось обновить данные цен ЖНВЛП по элементу КАТ ""%1"" по причине:'");
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ЦенаЖНВЛП.КАТ);
			Текст = Текст + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, МетаданныеРегистра,, Текст);
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#КонецЕсли