////////////////////////////////////////////////////////////////////////////////
// Модуль формы записи РегистрСведений.НоменклатураКонтрагентовБЭД
//
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьСвойстваПереопределяемыхЭлементовФормы();
	
	Если ЗначениеЗаполнено(Параметры.Ключ) Тогда 
		УстановитьРазрешениеРедактирования(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Запись.Номенклатура) Тогда
		Запись.Номенклатура = Неопределено;
		Если ТипЗнч(Запись.Упаковка) <> Тип("Строка") Тогда
			Запись.Упаковка = Неопределено;
		КонецЕсли;
		Если ТипЗнч(Запись.Характеристика) <> Тип("Строка") Тогда
			Запись.Характеристика = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Запись.Упаковка)
		И ТипЗнч(Запись.Упаковка) <> Тип("Строка") Тогда
			Запись.Упаковка = Неопределено;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РазрешитьРедактирование(Команда)
	
	ПоказатьВопросОРазрешенииРедактирования();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСвойстваПереопределяемыхЭлементовФормы()
	
	МетаданныеСопоставления = СопоставлениеНоменклатурыКонтрагентовСлужебный.МетаданныеСопоставленияНоменклатуры();
	
	Заголовок = МетаданныеСопоставления.НоменклатураКонтрагентаПредставлениеОбъекта;
	
	Элементы.Владелец.Заголовок = МетаданныеСопоставления.ВладелецНоменклатурыПредставлениеОбъекта;
	Элементы.Номенклатура.Заголовок = МетаданныеСопоставления.НоменклатураПредставлениеОбъекта;
	Элементы.Характеристика.Заголовок = МетаданныеСопоставления.ХарактеристикаПредставлениеОбъекта;
	Элементы.Упаковка.Заголовок = МетаданныеСопоставления.УпаковкаПредставлениеОбъекта;
	
	Если ЗначениеЗаполнено(МетаданныеСопоставления.ИмяПараметраСвязиХарактеристики) Тогда
		
		НоваяСвязь = Новый СвязьПараметраВыбора(МетаданныеСопоставления.ИмяПараметраСвязиХарактеристики, 
			"Запись.Номенклатура");
		ВсеСвязи = Новый Массив();
		ВсеСвязи.Добавить(НоваяСвязь);
		Элементы.Характеристика.СвязиПараметровВыбора = Новый ФиксированныйМассив(ВсеСвязи);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МетаданныеСопоставления.ИмяПараметраСвязиУпаковки) Тогда
		
		НоваяСвязь = Новый СвязьПараметраВыбора(МетаданныеСопоставления.ИмяПараметраСвязиУпаковки, 
			"Запись.Номенклатура");
		ВсеСвязи = Новый Массив();
		ВсеСвязи.Добавить(НоваяСвязь);
		Элементы.Упаковка.СвязиПараметровВыбора = Новый ФиксированныйМассив(ВсеСвязи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРазрешениеРедактирования(Знач Разрешить) 
	
	Элементы.Владелец.ТолькоПросмотр                           = НЕ Разрешить;
	Элементы.Наименование.ТолькоПросмотр                       = НЕ Разрешить;
	Элементы.НаименованиеХарактеристики.ТолькоПросмотр         = НЕ Разрешить;
	Элементы.ЕдиницаИзмерения.ТолькоПросмотр                   = НЕ Разрешить;
	Элементы.ЕдиницаИзмеренияКод.ТолькоПросмотр                = НЕ Разрешить;
	Элементы.Артикул.ТолькоПросмотр                            = НЕ Разрешить;
	Элементы.СтавкаНДС.ТолькоПросмотр                          = НЕ Разрешить;
	Элементы.Штрихкод.ТолькоПросмотр                           = НЕ Разрешить;
	Элементы.ШтрихкодКомбинации.ТолькоПросмотр                 = НЕ Разрешить;
	Элементы.ШтрихкодыНоменклатуры.ТолькоПросмотр              = НЕ Разрешить;
	Элементы.Идентификатор.ТолькоПросмотр                      = НЕ Разрешить;
	Элементы.ИдентификаторНоменклатурыСервиса.ТолькоПросмотр   = НЕ Разрешить;
	Элементы.ИдентификаторХарактеристикиСервиса.ТолькоПросмотр = НЕ Разрешить;
	Элементы.КомандаРазрешитьРедактирование.Доступность        = НЕ Разрешить;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросОРазрешенииРедактирования() 
	
	ТекстВопроса = НСтр("ru = 'Разрешить редактирование данных контрагента?'");
	ОбработкаОтвета = Новый ОписаниеОповещения("ОбработатьВопросОРазрешенииРедактирования", ЭтотОбъект);
	ПоказатьВопрос(ОбработкаОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВопросОРазрешенииРедактирования(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УстановитьРазрешениеРедактирования(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПриИзменении(Элемент)
	
	СопоставлениеНоменклатурыКонтрагентовКлиентСервер.РазделитьИдентификаторНаЧасти(Запись.Идентификатор, Запись);

КонецПроцедуры

#КонецОбласти