////////////////////////////////////////////////////////////////////////////////
// Модуль формы РегистрСведений.НоменклатураКонтрагентовБЭД.ФормаВыбора
//
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ВладелецНоменклатуры") Тогда
		
		УстановитьОтборПоВладельцуНоменклатуры(Параметры.ВладелецНоменклатуры);
		
	КонецЕсли;
	
	УстановитьСвойстваПереопределяемыхЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Данные = ДанныеРегистраПоКлючуЗаписи(ВыбраннаяСтрока);
	
	ОповеститьОВыборе(Данные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	КлючЗаписи = Элементы.Список.ТекущаяСтрока;
	Если КлючЗаписи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Данные = ДанныеРегистраПоКлючуЗаписи(КлючЗаписи);
	
	ОповеститьОВыборе(Данные);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПоВладельцуНоменклатуры(Знач Владелец)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
		"Владелец", Владелец, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеРегистраПоКлючуЗаписи(Знач КлючЗаписи)
	
	Данные = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НоменклатураКонтрагентовБЭД.Владелец КАК Владелец,
	|	НоменклатураКонтрагентовБЭД.Идентификатор КАК Идентификатор,
	|	НоменклатураКонтрагентовБЭД.Наименование КАК Наименование,
	|	НоменклатураКонтрагентовБЭД.НаименованиеХарактеристики КАК НаименованиеХарактеристики,
	|	НоменклатураКонтрагентовБЭД.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	НоменклатураКонтрагентовБЭД.Артикул КАК Артикул,
	|	НоменклатураКонтрагентовБЭД.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
	|	НоменклатураКонтрагентовБЭД.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры,
	|	НоменклатураКонтрагентовБЭД.ИдентификаторНоменклатурыСервиса КАК ИдентификаторНоменклатурыСервиса,
	|	НоменклатураКонтрагентовБЭД.ИдентификаторХарактеристикиСервиса КАК ИдентификаторХарактеристикиСервиса,
	|	НоменклатураКонтрагентовБЭД.Номенклатура КАК Номенклатура,
	|	НоменклатураКонтрагентовБЭД.Характеристика КАК Характеристика,
	|	НоменклатураКонтрагентовБЭД.Упаковка КАК Упаковка,
	|	НоменклатураКонтрагентовБЭД.ИдентификаторНоменклатуры КАК ИдентификаторНоменклатуры,
	|	НоменклатураКонтрагентовБЭД.ИдентификаторХарактеристики КАК ИдентификаторХарактеристики,
	|	НоменклатураКонтрагентовБЭД.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	НоменклатураКонтрагентовБЭД.ИспользоватьХарактеристики КАК ИспользоватьХарактеристики,
	|	НоменклатураКонтрагентовБЭД.НаименованиеУпаковки
	|ИЗ
	|	РегистрСведений.НоменклатураКонтрагентовБЭД КАК НоменклатураКонтрагентовБЭД
	|ГДЕ
	|	НоменклатураКонтрагентовБЭД.Владелец = &Владелец
	|	И НоменклатураКонтрагентовБЭД.Идентификатор = &Идентификатор";
	Запрос.УстановитьПараметр("Владелец", КлючЗаписи.Владелец);
	Запрос.УстановитьПараметр("Идентификатор", КлючЗаписи.Идентификатор);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Следующий() Тогда
		
		Данные = Новый Структура;
		НоменклатураКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента();
		ЗаполнитьЗначенияСвойств(НоменклатураКонтрагента, Выборка);
		НоменклатураКонтрагента.Характеристика = Выборка.НаименованиеХарактеристики;
		
		НоменклатураИБ = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы(
			Выборка.Номенклатура, Выборка.Характеристика, Выборка.Упаковка);
			
		Данные.Вставить("НоменклатураКонтрагента", НоменклатураКонтрагента);
		Данные.Вставить("НоменклатураИБ", НоменклатураИБ);
		
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

&НаСервере
Процедура УстановитьСвойстваПереопределяемыхЭлементовФормы()
	
	МетаданныеСопоставления = СопоставлениеНоменклатурыКонтрагентовСлужебный.МетаданныеСопоставленияНоменклатуры();
	
	Заголовок = МетаданныеСопоставления.НоменклатураКонтрагентаПредставлениеСписка;
	Элементы.Владелец.Заголовок = МетаданныеСопоставления.ВладелецНоменклатурыПредставлениеОбъекта;
	Элементы.Номенклатура.Заголовок = МетаданныеСопоставления.НоменклатураПредставлениеОбъекта;
	Элементы.Характеристика.Заголовок = МетаданныеСопоставления.ХарактеристикаПредставлениеОбъекта;
	Элементы.Упаковка.Заголовок = МетаданныеСопоставления.УпаковкаПредставлениеОбъекта;
	
КонецПроцедуры

#КонецОбласти