
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Владелец.Видимость = ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияМДЛП.Производство");
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		ПриСозданииНовогоПриЧтенииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииНовогоПриЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Запись.ГрупповаяУпаковка Тогда
		Если Не ИнтеграцияМДЛП.ЗначениеСоответствуетТипуXDTO(Запись.НомерУпаковки, "sscc_type") Тогда
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", НСтр("ru = 'Номер упаковки'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "мНомерУпаковки",, Отказ);
		КонецЕсли;
	Иначе
		Если Не ИнтеграцияМДЛП.ЗначениеСоответствуетТипуXDTO(Запись.НомерУпаковки, "sign_sgtin_type") Тогда
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", НСтр("ru = 'Номер упаковки'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Запись.НомерУпаковки",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ВРезерве =
		Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.ВРезерве
		Или Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.КПоступлению
		Или Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.ОтгруженВРФ
		Или Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.ОжидаетПодтвержденияИмпортером
		Или Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.ОжидаетПодтвержденияСменыСобственника;
	
	ВУпаковке = Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.ВУпаковке;
	
	Если Не ВУпаковке Тогда
		ТекущийОбъект.НомерГрупповойУпаковки = "";
	КонецЕсли;
	Если Не ВРезерве И Не ВУпаковке Тогда
		ТекущийОбъект.ДокументРезерва = Неопределено;
	КонецЕсли;
	Если Не ВРезерве Тогда
		ТекущийОбъект.ИсходныйСтатус = Перечисления.СтатусыУпаковокМДЛП.ПустаяСсылка();
	КонецЕсли;
	
	ТекущийОбъект.КлючУпаковки = ИнтеграцияМДЛПКлиентСервер.ПолучитьКлючУпаковки(ТекущийОбъект.НомерУпаковки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипУпаковкиПриИзменении(Элемент)
	
	Запись.ГрупповаяУпаковка = ТипУпаковки > 0;
	НастроитьФормуПоТипуУпаковки();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	НастроитьФормуПоСтатусу();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНовогоПриЧтенииНаСервере()
	
	ТипУпаковки = Запись.ГрупповаяУпаковка;
	НастроитьФормуПоСтатусу();
	НастроитьФормуПоТипуУпаковки();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоТипуУпаковки()
	
	Элементы.НомерУпаковки.Видимость = Запись.ГрупповаяУпаковка;
	Элементы.ВложеныПотребительскиеУпаковки.Видимость = Запись.ГрупповаяУпаковка;
	Элементы.НомерПотребительскойУпаковки.Видимость = Не Запись.ГрупповаяУпаковка;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоСтатусу()
	
	ВРезерве =
		Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.ВРезерве
		Или Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.КПоступлению
		Или Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.ОтгруженВРФ
		Или Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.ОжидаетПодтвержденияИмпортером
		Или Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.ОжидаетПодтвержденияСменыСобственника;
	
	КПоступлению =
		Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.КПоступлению
		Или Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.ОтгруженВРФ;
	
	ВУпаковке = Запись.Статус = Перечисления.СтатусыУпаковокМДЛП.ВУпаковке;
	
	Элементы.ДокументРезерва.Видимость = ВРезерве;
	Элементы.ИсходныйСтатус.Видимость = ВРезерве И Не КПоступлению;
	
	Элементы.НомерГрупповойУпаковки.Видимость = ВУпаковке;
	Элементы.НомерВерхнеурвневойУпаковки.Видимость = ВУпаковке;
	
	Если ВРезерве Тогда
		Запись.ДокументРезерва = Неопределено;
	ИначеЕсли ВУпаковке Тогда
		Запись.ДокументРезерва = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
