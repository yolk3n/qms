#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВойтиВОбласть(Команда)

	ДанныеСтроки = Элементы.Список.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		ПоказатьПредупреждение(, "Не выбрана область для входа!", 180);
		Возврат;
	КонецЕсли;
	
	ВыполнитьВыходИзОбластиДанных();
	ВойтиВОбластьНаСервере(ДанныеСтроки.ОбластьДанныхВспомогательныеДанные);
	ОбработкаЗавершения = Новый ОписаниеОповещения(
		"ПродолжениеВходаВОбластьДанныхПослеДействийПередНачаломРаботыСистемы", ЭтотОбъект);
	
	СтандартныеПодсистемыКлиент.ПередНачаломРаботыСистемы(ОбработкаЗавершения);
	Оповестить("ВыполненВходВОбластьДанных");

КонецПроцедуры

&НаКлиенте
Процедура ВыйтиИзОбласти(Команда)

	ВыполнитьВыходИзОбластиДанных();
	Оповестить("ВыполненВыходИзОбластиДанных");

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВходаВОбласть(Команда)

	ДанныеСтроки = Элементы.Список.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		ПоказатьПредупреждение(, "Не выбрана область для входа!", 180);
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ВходВОбластьДанных",
		Новый Структура("ОбластьДанных", ДанныеСтроки.ОбластьДанныхВспомогательныеДанные),
		ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьВыходИзОбластиДанных()

	ВыйтиИзОбластиДанныхНаСервере();
	ОбновитьИнтерфейс();
	СтандартныеПодсистемыКлиент.УстановитьРасширенныйЗаголовокПриложения(Истина);

КонецПроцедуры

&НаСервере
Процедура ВойтиВОбластьНаСервере(ОбластьДанных)

	УстановитьПривилегированныйРежим(Истина);
	РаботаВМоделиСервиса.ВойтиВОбластьДанных(ОбластьДанных);

КонецПроцедуры

&НаСервере
Процедура ВыйтиИзОбластиДанныхНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не РаботаВМоделиСервиса.ИспользованиеРазделителяСеанса() Тогда
		Возврат;
	КонецЕсли;
	
	// Восстановление форм разделенного рабочего стола.
	СтандартныеПодсистемыВызовСервера.СкрытьРабочийСтолПриНачалеРаботыСистемы(Ложь);
	СтандартныеПодсистемыСервер.УстановитьПустуюФормуНаПустойРабочийСтол();
	РаботаВМоделиСервиса.ВыйтиИзОбластиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеВходаВОбластьДанныхПослеДействийПередНачаломРаботыСистемы(Результат, Контекст) Экспорт
	
	Если Результат.Отказ Тогда
		ВыполнитьВыходИзОбластиДанных();
		Активизировать();
	Иначе
		ОбработкаЗавершения = Новый ОписаниеОповещения(
			"ПродолжениеВходаВОбластьДанныхПослеДействийПриНачалеРаботыСистемы", ЭтотОбъект);
		
		СтандартныеПодсистемыКлиент.ПриНачалеРаботыСистемы(ОбработкаЗавершения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеВходаВОбластьДанныхПослеДействийПриНачалеРаботыСистемы(Результат, Контекст) Экспорт
	
	Если Результат.Отказ Тогда
		ВыполнитьВыходИзОбластиДанных();
	КонецЕсли;
	
	ОбновитьИнтерфейс();
	Активизировать();
	
	Оповестить("ВыполненВходВОбластьДанных");
	
КонецПроцедуры

#КонецОбласти