#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Реквизиты = ОборудованиеЧекопечатающиеУстройстваВызовСервера.ДанныеЧекаВОчереди(Запись.ИдентификаторЗаписи);
	Если Реквизиты <> Неопределено Тогда
		Текст = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.СформироватьТекстНефискальногоДокумента(0, Реквизиты.ДанныеЧека, 42);
		Если Реквизиты.СтатусЧека = Перечисления.СтатусЧекаККТВОчереди.Фискализирован Тогда
			ФискальнаяОперации = ОборудованиеЧекопечатающиеУстройстваВызовСервера.ДанныеФискальнойОперации(Реквизиты.ДокументОснование, Реквизиты.ИдентификаторЗаписи);
			Если ФискальнаяОперации <> Неопределено Тогда
				ПараметрыQRКода = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыQRКодаЧекаККТ();
				ПараметрыQRКода.ДатаВремяРасчета = ФискальнаяОперации.Дата;
				ПараметрыQRКода.СуммаРасчета = ФискальнаяОперации.Сумма;
				ПараметрыQRКода.НомерФискальногоНакопителя = ФискальнаяОперации.ЗаводскойНомерФН;
				ПараметрыQRКода.НомерФискальногоДокумента = ФискальнаяОперации.НомерЧекаККМ;
				ПараметрыQRКода.ФискальныйПризнак = ФискальнаяОперации.ФискальныйПризнак;
				ПараметрыQRКода.ПризнакРасчета = ФискальнаяОперации.ТипРасчета;
				QRКодЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.СформироватьQRКодЧекаККТ(ПараметрыQRКода);
			КонецЕсли;
		КонецЕсли;
		СформироватьНаСервере(Текст, QRКодЧека);
	КонецЕсли;
	
	Элементы.ТекстОшибки.Видимость = Запись.СтатусЧека = Перечисления.СтатусЧекаККТВОчереди.Ошибка;
	
КонецПроцедуры

#КонецОбласти 

&НаСервере
Функция ПолучитьШтрихкод(ШиринаШтрихкода, ВысотаШтрихкода, Штрихкод)
	
	ПараметрыШтрихкода = ГенерацияШтрихкода.ПараметрыГенерацииШтрихкода();
	ПараметрыШтрихкода.Ширина  = ШиринаШтрихкода;
	ПараметрыШтрихкода.Высота  = ВысотаШтрихкода;
	ПараметрыШтрихкода.ТипКода = 16;
	ПараметрыШтрихкода.ОтображатьТекст = Истина;
	ПараметрыШтрихкода.РазмерШрифта   = 12;
	ПараметрыШтрихкода.УголПоворота   = 0;
	ПараметрыШтрихкода.Штрихкод       = Штрихкод;
	ПараметрыШтрихкода.ПрозрачныйФон  = Ложь;
	ПараметрыШтрихкода.Масштабировать = Ложь;
	
	РезультатОперации = ГенерацияШтрихкода.ИзображениеШтрихкода(ПараметрыШтрихкода);
	Возврат РезультатОперации.Картинка;
	
КонецФункции

&НаСервере
Процедура СформироватьНаСервере(Текст, QRКодЧека)
	
	QRКод.Очистить();
	
	Макет = РегистрыСведений.ОчередьЧековККТ.ПолучитьМакет("Макет");
	Область = Макет.ПолучитьОбласть("Строка|Колонка");
	Рисунок = Область.Рисунки.ШтрихКод;
	
	ОбластьТекст = Макет.ПолучитьОбласть("Текст");
	ОбластьТекст.Область("Текст").Текст = Текст;
	QRКод.Вывести(ОбластьТекст);
	
	Если QRКодЧека <> Неопределено Тогда
		Эталон = Справочники.ПодключаемоеОборудование.ПолучитьМакет("МакетДляОпределенияКоэффициентовЕдиницИзмерения");
		КоличествоМиллиметровВПикселеВысота = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		КоличествоМиллиметровВПикселеШирина = Эталон.Рисунки.Квадрат100Пикселей.Ширина / 100;
		ШиринаШтрихкода = Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселеШирина);
		ВысотаШтрихкода = Окр(Рисунок.Высота / КоличествоМиллиметровВПикселеВысота);
		Картинка = ПолучитьШтрихкод(ШиринаШтрихкода, ВысотаШтрихкода, QRКодЧека);
		Рисунок.Картинка = Картинка;
		QRКод.Вывести(Область);
	КонецЕсли;
	
КонецПроцедуры