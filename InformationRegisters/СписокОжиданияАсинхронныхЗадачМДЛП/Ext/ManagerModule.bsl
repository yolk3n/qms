#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьЗадачи(Отбор = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрСведений.СписокОжиданияАсинхронныхЗадачМДЛП КАК Очередь
	|");
	
	Если Отбор <> Неопределено Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		Оператор = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
		Для Каждого КлючИЗначение Из Отбор Цикл
			Если ТипЗнч(КлючИЗначение.Значение) = Тип("Массив") Тогда
				Выражение = СтрШаблон("%1 В (&%1)", КлючИЗначение.Ключ);
			Иначе
				Выражение = СтрШаблон("%1 = &%1", КлючИЗначение.Ключ);
			КонецЕсли;
			Оператор.Отбор.Добавить(Выражение);
			Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ДобавитьЗадачу(Данные) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.СписокОжиданияАсинхронныхЗадачМДЛП.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, Данные);
	Запись.ДатаРегистрации = ТекущаяДатаСеанса();
	Запись.Записать();
	
КонецПроцедуры

Процедура УдалитьЗадачи(Отбор = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрСведений.СписокОжиданияАсинхронныхЗадачМДЛП КАК Очередь
	|");
	
	Измерения = Новый Массив;
	Для Каждого Измерение Из Метаданные.РегистрыСведений.СписокОжиданияАсинхронныхЗадачМДЛП.Измерения Цикл
		Измерения.Добавить(Измерение.Имя);
	КонецЦикла;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "*", СтрСоединить(Измерения, ","));
	
	Если Отбор <> Неопределено Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		Оператор = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
		Для Каждого КлючИЗначение Из Отбор Цикл
			Если ТипЗнч(КлючИЗначение.Значение) = Тип("Массив") Тогда
				Выражение = СтрШаблон("%1 В (&%1)", КлючИЗначение.Ключ);
			Иначе
				Выражение = СтрШаблон("%1 = &%1", КлючИЗначение.Ключ);
			КонецЕсли;
			Оператор.Отбор.Добавить(Выражение);
			Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Запись = СоздатьМенеджерЗаписи();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.Удалить();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли