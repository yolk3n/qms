#Область ПрограммныйИнтерфейс

#Область ЧтениеЖурналаРегистрации
&НаКлиенте
Процедура ПоказатьВсеСобытияСеансаЗапроса(Команда)
	
	ТекущиеДанные_ = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные_ <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные_.ДатаСообщения)  И ЗначениеЗаполнено(ТекущиеДанные_.ИдентификаторСообщения) Тогда
		ЗагрузитьДанныеИзЖР(ТекущиеДанные_);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзЖР(ТекущиеДанные)

	
	РезультатВыполнения = ФедеральныеВебСервисыРЭМД.ПрочитатьЖурналРегистрацииСОтборомПоКомментарию(
		ТекущиеДанные.ИдентификаторСообщения,
		ТекущиеДанные.ДатаСообщения - МагическиеКонстанты.ОднаМинута(),
		ТекущиеДанные.ДатаСообщения + МагическиеКонстанты.ОднаМинута() * 15,
		ЭтотОбъект.УникальныйИдентификатор
	);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьДанныеИзЖРЗавершение", ЭтотОбъект, ТекущиеДанные);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры


&НаКлиенте
Процедура ЗагрузитьДанныеИзЖРЗавершение(Результат, ТекущиеДанные) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		Сеансы_ = ПолучитьСписокСеансовИзРезультата(Результат.АдресРезультата);
		
		Если Сеансы_.Количество() > 0 Тогда
			П_ = Новый Структура("ДатаНачала, ДатаОкончания, Сеанс",
				ТекущиеДанные.ДатаСообщения - МагическиеКонстанты.ОднаМинута(), ТекущиеДанные.ДатаСообщения + МагическиеКонстанты.ОднаМинута() * 15, Сеансы_);
			ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", П_,, Истина);
		Иначе
			ВызватьИсключение "Не найдены события журнала регистрации с отбором по идентификатору сообщения: " + ТекущиеДанные.ИдентификаторСообщения;
		КонецЕсли;

	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокСеансовИзРезультата(АдресРезультата)
	
	Результат      = ПолучитьИзВременногоХранилища(АдресРезультата);
	СобытияЖурнала = Результат.СобытияЖурнала;
	
	ПоместитьВоВременноеХранилище(Неопределено, АдресРезультата);// удалим
	
	Сеансы_ = Новый СписокЗначений;
	
	Для Каждого строкаТЗ_ Из СобытияЖурнала Цикл
		Если Сеансы_.НайтиПоЗначению(строкаТЗ_.Сеанс) = Неопределено Тогда
			Сеансы_.Добавить(строкаТЗ_.Сеанс);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Сеансы_;
КонецФункции
#КонецОбласти


#КонецОбласти