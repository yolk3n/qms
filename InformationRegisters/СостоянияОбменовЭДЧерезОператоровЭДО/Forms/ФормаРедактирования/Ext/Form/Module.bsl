
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("УчетнаяЗаписьЭДО") Тогда
		СостояниеОбмена = СинхронизацияЭДОСлужебный.СостояниеОбменаЭлектроннымиДокументами(Параметры.УчетнаяЗаписьЭДО);
		ДатаПолученияЭД = СостояниеОбмена.ДатаПоследнегоПолученияДокументов;
		ДатаПолученияЭПД = СостояниеОбмена.ДатаПоследнегоПолученияЭПД;
		ДатаПоследнегоПолученияПриглашений = СостояниеОбмена.ДатаПоследнегоПолученияПриглашений;
		ДатаПоследнейАктивности = СостояниеОбмена.ДатаПоследнейАктивности;
		ИдентификаторОрганизации = Параметры.УчетнаяЗаписьЭДО;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И Не ЗавершениеРаботы Тогда
		Отказ = Истина;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОтветаНаВопрос", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНаСервере();
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеОтветаНаВопрос(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписатьИЗакрыть(Неопределено);
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	СинхронизацияЭДОСлужебный.ИзменитьСостояниеОбменаЭлектроннымиДокументами(ИдентификаторОрганизации,
		ЭтотОбъект.ДатаПолученияЭД, ЭтотОбъект.ДатаПолученияЭПД,
		ЭтотОбъект.ДатаПоследнегоПолученияПриглашений, ЭтотОбъект.ДатаПоследнейАктивности);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти
