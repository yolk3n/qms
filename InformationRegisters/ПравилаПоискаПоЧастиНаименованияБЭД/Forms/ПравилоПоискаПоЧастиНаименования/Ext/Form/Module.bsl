
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Ключ") И Не Параметры.Ключ.Пустой() Тогда
		ПрочитатьПравило(Параметры.Ключ);
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ПрочитатьПравило(Параметры.ЗначениеКопирования, Истина);
	Иначе
		Запись.Владелец = Параметры.Владелец;
		Запись.Номенклатура = Параметры.Номенклатура;
		Запись.Характеристика = Параметры.Характеристика;
		Запись.Упаковка = Параметры.Упаковка;
		Запись.ПоисковаяСтрока = Параметры.ПоисковаяСтрока;
	КонецЕсли;
	
	Если Параметры.Свойство("ОбязательноеЗаполнениеХарактеристики") Тогда
		Элементы.Характеристика.АвтоОтметкаНезаполненного = Параметры.ОбязательноеЗаполнениеХарактеристики;
	ИначеЕсли ЗначениеЗаполнено(Запись.Номенклатура) Тогда
		Элементы.Характеристика.АвтоОтметкаНезаполненного = ОбязательноеЗаполнениеХарактеристики(Запись.Номенклатура);
	КонецЕсли;
	
	УстановитьСвойстваПереопределяемыхЭлементовФормы();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаИзменитьФорму", 
		"Видимость",
		Пользователи.ЭтоПолноправныйПользователь());
	
	НовыеДанныеПравилаПоиска = СопоставлениеНоменклатурыКонтрагентовСлужебный.НовоеПравилоПоискаПоЧастиНаименования();
	НовыеДанныеПравилаПоиска.Вставить("ИсходныйКлючЗаписи");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоисковаяСтрокаПриИзменении(Элемент)
	Если ПустаяСтрока(Запись.ПоисковаяСтрока) Тогда
		Запись.ХешПоисковойСтроки = "";
	Иначе
		Запись.ХешПоисковойСтроки = КонтрольнаяСуммаСтрокой(Запись.ПоисковаяСтрока);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	Если Элементы.Характеристика.Видимость И ЗначениеЗаполнено(Запись.Номенклатура) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Характеристика", 
			"АвтоОтметкаНезаполненного", ОбязательноеЗаполнениеХарактеристики(Запись.Номенклатура));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьПравило();
	ЗаполнитьЗначенияСвойств(НовыеДанныеПравилаПоиска, Запись);
	ОповеститьОВыборе(НовыеДанныеПравилаПоиска);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЧтениеИЗапись

&НаСервере
Процедура ПрочитатьПравило(КлючЗаписи, ЭтоКопирование = Ложь)
	
	МенеджерЗаписи = РеквизитФормыВЗначение("Запись");
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, КлючЗаписи);
	МенеджерЗаписи.Прочитать();
	Если ЭтоКопирование Тогда
		ЗаполнитьЗначенияСвойств(Запись, МенеджерЗаписи);
	Иначе
		ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПравило()
	
	Запись.ХешПоисковойСтроки = КонтрольнаяСуммаСтрокой(Запись.ПоисковаяСтрока);
	МенеджерЗаписи = РеквизитФормыВЗначение("Запись");
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьСвойстваПереопределяемыхЭлементовФормы()
	
	МетаданныеСопоставления = СопоставлениеНоменклатурыКонтрагентовСлужебный.МетаданныеСопоставленияНоменклатуры();
	
	Элементы.Владелец.Заголовок       = МетаданныеСопоставления.ВладелецНоменклатурыПредставлениеОбъекта;
	Элементы.Номенклатура.Заголовок   = МетаданныеСопоставления.НоменклатураПредставлениеОбъекта;
	Элементы.Характеристика.Заголовок = МетаданныеСопоставления.ХарактеристикаПредставлениеОбъекта;
	Элементы.Упаковка.Заголовок       = МетаданныеСопоставления.УпаковкаПредставлениеОбъекта;
	
	Если ЗначениеЗаполнено(МетаданныеСопоставления.ИмяПараметраСвязиХарактеристики) Тогда
		
		НоваяСвязь = Новый СвязьПараметраВыбора(МетаданныеСопоставления.ИмяПараметраСвязиХарактеристики, 
			"Запись.Номенклатура");
		ВсеСвязи = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НоваяСвязь);
		Элементы.Характеристика.СвязиПараметровВыбора = Новый ФиксированныйМассив(ВсеСвязи);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МетаданныеСопоставления.ИмяПараметраСвязиУпаковки) Тогда
		
		НоваяСвязь = Новый СвязьПараметраВыбора(МетаданныеСопоставления.ИмяПараметраСвязиУпаковки, 
			"Запись.Номенклатура");
		ВсеСвязи = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НоваяСвязь);
		Элементы.Упаковка.СвязиПараметровВыбора = Новый ФиксированныйМассив(ВсеСвязи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КонтрольнаяСуммаСтрокой(Знач Данные)
	Возврат ОбщегоНазначения.КонтрольнаяСуммаСтрокой(Данные);
КонецФункции

&НаСервереБезКонтекста
Функция ОбязательноеЗаполнениеХарактеристики(Знач Номенклатура)
	
	Результат   = Ложь;
	Свойства    = Неопределено;
	ВсеСвойства = СопоставлениеНоменклатурыКонтрагентовСлужебный.СвойстваНоменклатурыИнформационнойБазы(Номенклатура);
	
	Если ВсеСвойства <> Неопределено Тогда
		Свойства = ВсеСвойства.Получить(Номенклатура);
	КонецЕсли;
	
	Если Свойства <> Неопределено Тогда
		Результат = Свойства.ИспользоватьХарактеристики И Свойства.ОбязательноеЗаполнениеХарактеристики;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти