
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Запись, ЭтотОбъект);
	
	ПланОбмена = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Запись.УзелОбмена.Метаданные());
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПериодПредставление = ПредставлениеМесяца(Запись.Период);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПериодПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыВыбораПериода = Новый Структура;
	ПараметрыВыбораПериода.Вставить("Значение", Запись.Период);
	ПараметрыВыбораПериода.Вставить("РежимВыбораПериода", "Месяц");
	ПараметрыВыбораПериода.Вставить("ЗапрашиватьРежимВыбораПериодаУВладельца", Ложь);
	
	Оповещение = Новый ОписаниеОповещения("ПериодПредставлениеНачалоВыбораЗавершение", ЭтотОбъект);
	
	ОткрытьФорму(
		"ОбщаяФорма.ВыборПериода",
		ПараметрыВыбораПериода,
		ЭтотОбъект,
		,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПредставлениеПриИзменении(Элемент)
	
	Если ПустаяСтрока(ПериодПредставление) Тогда
		Запись.Период = Дата(1, 1, 1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПредставлениеРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	Запись.Период = ДобавитьМесяц(Запись.Период, Направление);
	ПериодПредставление = ПредставлениеМесяца(Запись.Период);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПериодПредставлениеНачалоВыбораЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запись.Период = ВыбранноеЗначение;
	ПериодПредставление = ПредставлениеМесяца(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеМесяца(Значение)
	
	Возврат Формат(Значение, "ДФ='ММММ гггг'");
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
