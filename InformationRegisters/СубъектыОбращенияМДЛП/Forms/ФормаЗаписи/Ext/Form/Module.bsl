
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ТипОрганизация, ТипОбъектМестаДеятельностиОрганизации, ТипКонтрагент, ТипОбъектМестаДеятельностиКонтрагента;
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИнтеграцияМДЛП.ОписаниеТиповОрганизацииКонтрагентаОбъектаМестаДеятельности(
		ТипОрганизация, ТипОбъектМестаДеятельностиОрганизации, ТипКонтрагент, ТипОбъектМестаДеятельностиКонтрагента);
	
	Элементы.Организация.ОграничениеТипа = ТипОрганизация;
	ОрганизацияЗначениеПоУмолчанию = ТипОрганизация.ПривестиЗначение();
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ОрганизацияЗначениеПоУмолчанию)) Тогда
		Элементы.Организация.Заголовок = ПредставлениеОбъекта(ОрганизацияЗначениеПоУмолчанию);
	КонецЕсли;
	
	ИспользуютсяМестаДеятельности = ИнтеграцияМДЛП.ИспользоватьМестаДеятельности();
	Если ИспользуютсяМестаДеятельности Тогда
		Элементы.МестоДеятельности.ОграничениеТипа = ТипОбъектМестаДеятельностиОрганизации;
		МестоДеятельностиЗначениеПоУмолчанию = ТипОбъектМестаДеятельностиОрганизации.ПривестиЗначение();
		Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(МестоДеятельностиЗначениеПоУмолчанию)) Тогда
			Элементы.МестоДеятельности.Заголовок = ПредставлениеОбъекта(МестоДеятельностиЗначениеПоУмолчанию);
		КонецЕсли;
	Иначе
		Элементы.МестоДеятельности.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.Контрагент.ОграничениеТипа = ТипКонтрагент;
	КонтрагентЗначениеПоУмолчанию = ТипКонтрагент.ПривестиЗначение();
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(КонтрагентЗначениеПоУмолчанию)) Тогда
		Элементы.Контрагент.Заголовок = ПредставлениеОбъекта(КонтрагентЗначениеПоУмолчанию);
	КонецЕсли;
	
	ИспользуютсяМестаДеятельностиКонтрагента = ИнтеграцияМДЛП.ИспользоватьМестаДеятельностиКонтрагентов();
	Если ИспользуютсяМестаДеятельностиКонтрагента Тогда
		Элементы.МестоДеятельностиКонтрагента.ОграничениеТипа = ТипОбъектМестаДеятельностиКонтрагента;
		КонтрагентМестоДеятельностиЗначениеПоУмолчанию = ТипОбъектМестаДеятельностиКонтрагента.ПривестиЗначение();
		Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(КонтрагентМестоДеятельностиЗначениеПоУмолчанию)) Тогда
			Элементы.МестоДеятельностиКонтрагента.Заголовок = ПредставлениеОбъекта(КонтрагентМестоДеятельностиЗначениеПоУмолчанию);
		КонецЕсли;
	Иначе
		Элементы.МестоДеятельностиКонтрагента.Видимость = Ложь;
	КонецЕсли;
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		ПриСозданииЧтенииНаСервере();
	КонецЕсли;
	
	СобытияФормМДЛППереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииЧтенииНаСервере();
	
	СобытияФормМДЛППереопределяемый.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СобытияФормМДЛПКлиентПереопределяемый.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ИспользуютсяМестаДеятельности И Запись.СобственнаяОрганизация Тогда
		Если Не ЗначениеЗаполнено(Запись.ОбъектМестаДеятельности) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, Элементы.МестоДеятельности.Заголовок);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ОбъектМестаДеятельности", "Запись", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ИспользуютсяМестаДеятельностиКонтрагента И Не Запись.СобственнаяОрганизация Тогда
		Если Не ЗначениеЗаполнено(Запись.ОбъектМестаДеятельности) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, Элементы.МестоДеятельностиКонтрагента.Заголовок);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ОбъектМестаДеятельности", "Запись", Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	СобытияФормМДЛПКлиентПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормыШапки

&НаКлиенте
Процедура СоответствуетОрганизацииПриИзменении(Элемент)
	
	Запись.СобственнаяОрганизация = СоответствуетОрганизации;
	
	Если Запись.СобственнаяОрганизация Тогда
		Запись.Организация = ОрганизацияЗначениеПоУмолчанию;
		Запись.МестоДеятельности = МестоДеятельностиЗначениеПоУмолчанию;
	Иначе
		Запись.Организация = КонтрагентЗначениеПоУмолчанию;
		Запись.МестоДеятельности = КонтрагентМестоДеятельностиЗначениеПоУмолчанию;
	КонецЕсли;
	
	Элементы.ГруппаОрганизация.Видимость = Запись.СобственнаяОрганизация;
	Элементы.ГруппаКонтрагент.Видимость  = Не Запись.СобственнаяОрганизация;
	Элементы.ДоступноОбратноеАкцептование.Видимость = Не Запись.СобственнаяОрганизация;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормМДЛПКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	СоответствуетОрганизации = Запись.СобственнаяОрганизация;
	
	Элементы.ГруппаОрганизация.Видимость = Запись.СобственнаяОрганизация;
	Элементы.ГруппаКонтрагент.Видимость  = Не Запись.СобственнаяОрганизация;
	Элементы.ДоступноОбратноеАкцептование.Видимость = Не Запись.СобственнаяОрганизация;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеОбъекта(Ссылка)
	
	ПредставлениеОбъекта = Ссылка.Метаданные().ПредставлениеОбъекта;
	Если ПустаяСтрока(ПредставлениеОбъекта) Тогда
		ПредставлениеОбъекта = Ссылка.Метаданные().Представление();
	КонецЕсли;
	
	Возврат ПредставлениеОбъекта;
	
КонецФункции

#КонецОбласти
