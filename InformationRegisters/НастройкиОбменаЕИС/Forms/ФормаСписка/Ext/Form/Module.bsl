#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	КонецЕсли;
	
	ЭлектронноеАктированиеЕИСВызовСервера.
		СкрытьЭлементыФормыПриИспользованииОднойОрганизации(ЭтотОбъект, "СписокОрганизация");
		
	Версии = ЭлектронноеАктированиеЕИС.ПоддерживаемыеВерсииСхем();
	Элементы.ВерсияСхемЕИС.Видимость = Версии.Количество() > 1;
	Элементы.ВерсияСхемЕИС.Доступность = ПравоДоступа("Изменение",
		Метаданные.Константы.ВерсияСхемЭлектронногоАктированияЕИС);
	
	Элементы.ВерсияСхемЕИС.СписокВыбора.Добавить("", "По умолчанию");
	Для Каждого Версия Из Версии Цикл
		Элементы.ВерсияСхемЕИС.СписокВыбора.Добавить(Версия);
	КонецЦикла;

	ВерсияСхемЕИС = ЭлектронноеАктированиеЕИС.
		ПользовательскаяВерсияСхемЭлектронногоАктированияЕИС();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ЭлектронноеАктированиеЕИСКлиент.ОткрытьМастерНастроек(
			Организация,
			УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если НЕ Копирование Тогда
		Отказ = Истина;
		ЭлектронноеАктированиеЕИСКлиент.ОткрытьМастерНастроек(Неопределено, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВерсияСхемЕИСПриИзменении(Элемент)
	
	ВерсияСхемЕИСПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ЭлектронноеАктированиеЕИСКлиент.ИмяСобытияСохраненияНастроекПодключения() Тогда
		Если ЗначениеЗаполнено(Организация) Тогда
			Закрыть();
		КонецЕсли;
		ОбновитьОтображениеДанных();
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура МастерНастроек(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ЭлектронноеАктированиеЕИСКлиент.ОткрытьМастерНастроек(
			ТекущиеДанные.Организация,
			УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВерсияСхемЕИСПриИзмененииНаСервере()
	
	ЭлектронноеАктированиеЕИС.
		УстановитьПользовательскуюВерсиюСхемЭлектронногоАктированияЕИС(ВерсияСхемЕИС);
	
КонецПроцедуры

#КонецОбласти