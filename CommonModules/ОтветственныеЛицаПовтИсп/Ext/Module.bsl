
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Возвращает структуру реквизитов документа, предназначенных для хранения информации об ответственных лицах.
// Реквизит ответственного лица документа должен
//  - иметь тип СправочникСсылка.ОтветственныеЛицаОрганизаций
//  - содержать параметр выбора с типом ПеречислениеСсылка.ОтветственныеЛицаОрганизаций ("вид" ответственного).
//
// Параметры:
//  ИмяДокумента - Строка - имя документа в метаданных конфигурации.
//
// Возвращаемое значение:
//  Структура - структура ответственных лиц документа
//    Ключ     - имя реквизита документа
//    Значение - "вид" ответственного лица документа
//
Функция РеквизитыОтветственныхЛицДокумента(ИмяДокумента) Экспорт
	
	МетаданныеДокумента    = Метаданные.Документы[ИмяДокумента];
	СтруктураОтветственных = Новый Структура;
	
	ТипРеквизита 	  = Тип("СправочникСсылка.ОтветственныеЛицаОрганизаций");
	ТипЗначенияВыбора = Тип("ПеречислениеСсылка.ОтветственныеЛицаОрганизаций");
	
	Для Каждого Реквизит Из МетаданныеДокумента.Реквизиты Цикл
		
		Если НЕ Реквизит.Тип.СодержитТип(ТипРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		
		// Нашли в документе реквизит ответственного лица.
		Для Индекс = 0 По Реквизит.ПараметрыВыбора.Количество() - 1 Цикл
			
			ЗначениеВыбора = Реквизит.ПараметрыВыбора.Получить(Индекс).Значение;
			
			Если ТипЗнч(ЗначениеВыбора) = ТипЗначенияВыбора Тогда
				// Нашли "вид" реквизита ответственного лица.
				СтруктураОтветственных.Вставить(Реквизит.Имя, ЗначениеВыбора);
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат СтруктураОтветственных;
	
КонецФункции

// Проверяет наличие реквизитов, предназначенных для хранения информации об ответственных лицах, в метаданных документа.
//
// Параметры:
//  ИмяДокумента - Строка - имя документа в метаданных конфигурации.
//
// Возвращаемое значение:
//  Булево - признак наличия реквизитов в метаданных документа.
//
Функция ЕстьРеквизитыОтветственныхЛицДокумента(ИмяДокумента) Экспорт
	
	Возврат РеквизитыОтветственныхЛицДокумента(ИмяДокумента).Количество() > 0;
	
КонецФункции

#КонецОбласти
