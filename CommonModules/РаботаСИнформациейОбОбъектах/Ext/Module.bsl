
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Устанавливает значение свойства Забракован передаваемого Объекта.
//
// Параметры:
//  Объект     - ЛюбаяСсылка - объект, по которому сохраняется информация.
//  Забракован - Булево - устанавливаемое значение.
//
// Возвращаемое значение:
//  Булево - признак того, что свойство установлено.
//
Функция УстановитьСвойствоЗабракован(Объект, Забракован = Истина) Экспорт
	
	Возврат УстановитьСвойство(Объект, "Забракован", Забракован);
	
КонецФункции

// Проверяет забракованность передаваемого объекта.
//
// Параметры:
//  Объект - ЛюбаяСсылка - объект, свойство которого проверяется.
//
// Возвращаемое значение:
//  Булево - признак того, забракован объект или нет.
//
Функция ОбъектЗабракован(Объект) Экспорт
	
	Возврат РегистрыСведений.КэшИнформацииОбОбъектах.ПолучитьИнформациюОбОбъекте(Объект, "Забракован");
	
КонецФункции

// Устанавливает значение свойства Обработан передаваемого Объекта.
//
// Параметры:
//  Объект    - ЛюбаяСсылка - объект, по которому сохраняется информация.
//  Обработан - Булево - устанавливаемое значение.
//
// Возвращаемое значение:
//  Булево - признак того, что свойство установлено.
//
Функция УстановитьСвойствоОбработан(Объект, Обработан = Истина) Экспорт
	
	Возврат УстановитьСвойство(Объект, "Обработан", Обработан);
	
КонецФункции

// Проверяет, является ли передаваемый объект обработанным.
//
// Параметры:
//  Объект - ЛюбаяСсылка - объект, свойство которого проверяется.
//
// Возвращаемое значение:
//  Булево - признак того, обработан объект или нет.
//
Функция ОбъектОбработан(Объект) Экспорт
	
	Возврат РегистрыСведений.КэшИнформацииОбОбъектах.ПолучитьИнформациюОбОбъекте(Объект, "Обработан");
	
КонецФункции

// Устанавливает значение свойства ЕстьЗадачи передаваемого Объекта.
//
// Параметры:
//  Объект     - ЛюбаяСсылка - объект, по которому сохраняется информация.
//  ЕстьЗадачи - Булево - устанавливаемое значение.
//
// Возвращаемое значение:
//  Булево - признак того, что свойство установлено.
//
Функция УстановитьСвойствоЕстьЗадачи(Объект, ЕстьЗадачи = Истина) Экспорт
	
	Возврат УстановитьСвойство(Объект, "ЕстьЗадачи", ЕстьЗадачи);
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Функция УстановитьСвойство(Объект, ИмяСвойства, Значение)
	
	Если Не ЗначениеЗаполнено(Объект) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Объект)) Тогда
		СвойствоУстановлено = УстановитьСвойствоОбъект(Объект, ИмяСвойства, Значение);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("Массив") Тогда
		СвойствоУстановлено = УстановитьСвойствоМассив(Объект, ИмяСвойства, Значение);
		
	Иначе
		СвойствоУстановлено = Ложь;
	КонецЕсли;
	
	Возврат СвойствоУстановлено;
	
КонецФункции

Функция УстановитьСвойствоОбъект(Объект, ИмяСвойства, Значение)
	
	Если ЗначениеЗаполнено(Объект) Тогда
		
		ЗначениеВРегистре = РегистрыСведений.КэшИнформацииОбОбъектах.ПолучитьИнформациюОбОбъекте(Объект, ИмяСвойства);
		Если Значение <> ЗначениеВРегистре Тогда
			
			РегистрыСведений.КэшИнформацииОбОбъектах.УстановитьИнформациюОбОбъекте(Объект, ИмяСвойства, Значение);
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция УстановитьСвойствоМассив(Массив, ИмяСвойства, Значение)
	
	СвойствоУстановлено = Ложь;
	
	Для каждого Объект Из Массив Цикл
		СвойствоУстановленоДляОбъекта = УстановитьСвойствоОбъект(Объект, ИмяСвойства, Значение);
		СвойствоУстановлено = СвойствоУстановлено Или СвойствоУстановленоДляОбъекта;
	КонецЦикла;
	
	Возврат СвойствоУстановлено;
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
