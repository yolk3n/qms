
#Область ПрограммныйИнтерфейс

/// Обработчик команды помещения в архив или извлечения из архива
//
// Параметры:
//   Форма - Управляемая форма
//     Форма, в которой находится комнада.
//   МассивОбъектов - Массив
//     Массив объектов, у которых надо поменять архивность.
//   ПоместитьВАрхив - Булево
//     Если Истина, элемент помещается в архив,
//     если Ложь, извлекается из архива.
///
Процедура КомандаИзменитьАрхивность(Форма, МассивОбъектов, Знач ПоместитьВАрхив) Экспорт
	
	Если МассивОбъектов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЭлемент = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(";%1 элемент;;%1 элемента;%1 элементов;%1 элемента ", МассивОбъектов.Количество());
	
	Если ПоместитьВАрхив Тогда
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ФункциональныеПодсистемы.СообщенияПользователю") Тогда
			МодульСообщенияПользователю = ОбщегоНазначенияКлиент.ОбщийМодуль("СообщенияПользователю");
			ТекстВопроса = МодульСообщенияПользователю.Получить("Архивирование_ТекстВопросаПоместить", Новый Структура("Элемент", ТекстЭлемент));
		Иначе
			ТекстВопроса = стрШаблон("Поместить %1 в архив?", ТекстЭлемент);
		КонецЕсли;
	Иначе
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ФункциональныеПодсистемы.СообщенияПользователю") Тогда
			МодульСообщенияПользователю = ОбщегоНазначенияКлиент.ОбщийМодуль("СообщенияПользователю");
			ТекстВопроса = МодульСообщенияПользователю.Получить("Архивирование_ТекстВопросаИзвлечь", Новый Структура("Элемент", ТекстЭлемент));
		Иначе
			ТекстВопроса = стрШаблон("Извлечь %1 из архива?", ТекстЭлемент);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыВопроса = Новый Структура("Форма, МассивОбъектов, ПоместитьВАрхив", Форма, МассивОбъектов, ПоместитьВАрхив);
	ОповещениеОтвета = Новый ОписаниеОповещения("ОбработкаОтветаУстановки", ЭтотОбъект, ПараметрыВопроса);
	
	ПоказатьВопрос(ОповещениеОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
	// Синтаксический контроль.
	Если 0 Тогда
		ОбработкаОтветаУстановки(0, 0);
		АрхивированиеКлиент.ОбработкаОтветаУстановки(0, 0);
	КонецЕсли;
	
КонецПроцедуры

////
 // Процедура: ОбработкаОтветаУстановки
 //   обработчик ответа на вопрос.
 //
 // Параметры:
 //   Ответ - PКодВозвратаДиалога}
 //     ответ на вопрос диалога.
 //   ДополнительныеПараметры - структура
 //     Дополнительные параметры вопроса
  ///
Процедура ОбработкаОтветаУстановки(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		МассивИзмененных = АрхивированиеСервер.УстановитьАрхивность(ДополнительныеПараметры.МассивОбъектов, ДополнительныеПараметры.ПоместитьВАрхив);
		Если МассивИзмененных.Количество() > 0 Тогда
			ОповеститьОбИзменении(ТипЗнч(МассивИзмененных.Получить(0)));
		КонецЕсли;
		Если МассивИзмененных.Количество() > 0  Тогда
			// для формы элемента перечитаем данные
			Если ДополнительныеПараметры.Форма.Параметры.Свойство("РежимВыбора") = Ложь Тогда
				Попытка
					ДополнительныеПараметры.Форма.Прочитать();
				Исключение
					// видимо произвольная форма
				КонецПопытки;
			КонецЕсли; 
			// Оповещаем об изменении объектов, отлавливать, если необходимо управлять видимостью команд.
			Оповестить(Архивирование.ИмяОповещения(), МассивИзмененных, ДополнительныеПараметры.Форма);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти