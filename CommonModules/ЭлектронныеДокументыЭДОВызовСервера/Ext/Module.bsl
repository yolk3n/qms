//@strict-types

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаДействийПоЭДО

// Возвращает описание длительной операции выполнения действий по ЭДО.
// 
// Параметры:
//  ПараметрыВыполненияДействий - См. ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ВыполнитьДействияПоЭДОВФоне
Функция ВыполнитьДействияПоЭДОВФоне(Знач ПараметрыВыполненияДействий, Знач КонтекстДиагностики) Экспорт
	Возврат ЭлектронныеДокументыЭДО.ВыполнитьДействияПоЭДОВФоне(ПараметрыВыполненияДействий, КонтекстДиагностики);
КонецФункции

// Возвращает результат заполнения доверенностей и подписантов по сертификату.
//
// Параметры:
//  ДанныеДляЗаполнения - Соответствие из КлючИЗначение:
//    * Ключ - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
//    * Значение - Структура:
//  	** ТребуетсяЗаполнитьПодписанта   - Булево
//  	** ТребуетсяЗаполнитьДоверенность - Булево
//  	** НаборМЧД						  - Массив из СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  	** Данные						  - Неопределено, Строка, ДвоичныеДанные - данные файлов.
//  Сертификат          - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  РезультатУточненияМЧД - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ЗаполнитьПодписантовИДоверенностиПоСертификату
//
Функция ЗаполнитьПодписантовИДоверенностиПоСертификату(Знач ДанныеДляЗаполнения,
	Знач Сертификат, КонтекстДиагностики, Знач РезультатУточненияМЧД) Экспорт
	Возврат ЭлектронныеДокументыЭДО.ЗаполнитьПодписантовИДоверенностиПоСертификату(
		ДанныеДляЗаполнения, Сертификат, КонтекстДиагностики, РезультатУточненияМЧД);
КонецФункции

// Возвращает описание длительной операции выполнения действий по ЭДО после интерактивного подписания.
// 
// Параметры:
//  ПодписанныеСообщенияКлиент - Массив из См. ЭлектронныеДокументыЭДОКлиент.НовоеОписаниеПодписанногоСообщения
//  АдресКонтекста - Строка
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
//
Функция ВыполнитьДействияПоЭДОПослеПодписания(Знач ПодписанныеСообщенияКлиент, Знач АдресКонтекста, Знач КонтекстДиагностики) Экспорт
	
	КонтекстНаСервере = ЭлектронныеДокументыЭДО.КонтекстИнтерактивногоПодписанияНаСервереПоАдресу(АдресКонтекста);
	УдалитьИзВременногоХранилища(АдресКонтекста);
	
	Если ЗначениеЗаполнено(КонтекстНаСервере.АдресаДанныхДляОчистки) Тогда
		Для Каждого АдресДанных Из КонтекстНаСервере.АдресаДанныхДляОчистки Цикл
			УдалитьИзВременногоХранилища(АдресДанных);
		КонецЦикла;
	КонецЕсли;
	
	ПодписанныеСообщения = Новый Массив;
	
	Для Каждого ОписаниеДанныхКлиент Из ПодписанныеСообщенияКлиент Цикл
		
		ОписаниеДанных = ЭлектронныеДокументыЭДО.НовоеОписаниеПодписанногоСообщения();
		ОписаниеДанных.Ссылка = ОписаниеДанныхКлиент.Ссылка;
		ОписаниеДанных.ПрисоединенныйФайл = ОписаниеДанныхКлиент.ПрисоединенныйФайл;
		ОписаниеДанных.ВыбранныйСертификат = ОписаниеДанныхКлиент.ВыбранныйСертификат;
		ОписаниеДанных.ВыбраннаяМЧД = ОписаниеДанныхКлиент.ВыбраннаяМЧД;
		ОписаниеДанных.ТребуетсяМЧД = ОписаниеДанныхКлиент.ТребуетсяМЧД;
		
		Если ТипЗнч(ОписаниеДанныхКлиент.СвойстваПодписи) = Тип("Строка") Тогда
			СвойстваПодписи = ПолучитьИзВременногоХранилища(ОписаниеДанныхКлиент.СвойстваПодписи); // См. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
			УдалитьИзВременногоХранилища(ОписаниеДанныхКлиент.СвойстваПодписи);
			ОписаниеДанных.СвойстваПодписи = СвойстваПодписи;
		Иначе
			ОписаниеДанных.СвойстваПодписи = ОписаниеДанныхКлиент.СвойстваПодписи;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеДанныхКлиент.АдресДанныхДляОбновления) Тогда
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(ОписаниеДанныхКлиент.АдресДанныхДляОбновления);
			УдалитьИзВременногоХранилища(ОписаниеДанныхКлиент.АдресДанныхДляОбновления);
			ОписаниеДанных.Данные = ДвоичныеДанные;
			ОписаниеДанных.ОбновитьДанные = Истина;
		Иначе 
			ОписаниеДанных.Данные = ОписаниеДанныхКлиент.Данные;
		КонецЕсли;
		
		ПодписанныеСообщения.Добавить(ОписаниеДанных);
		
	КонецЦикла;
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"ЭлектронныеДокументыЭДО.ВыполнитьДействияПоЭДОПослеПодписания",
		ПодписанныеСообщения, КонтекстНаСервере.ПараметрыВыполнения, КонтекстДиагностики,
		КонтекстНаСервере.ИтогДействийПоЭДО);
	
КонецФункции

// Возвращает преобразованный результат действий по ЭДО.
// 
// Параметры:
//  АдресРезультата - Строка
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ОбработатьРезультатДействийПоЭДОФоне
//
Функция РезультатДействийПоЭДО(Знач АдресРезультата) Экспорт
	
	РезультатДействийПоЭДОФоне = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	РезультатДействийПоЭДО = ЭлектронныеДокументыЭДО.ОбработатьРезультатДействийПоЭДОФоне(РезультатДействийПоЭДОФоне);
	
	Возврат РезультатДействийПоЭДО;
	
КонецФункции

#КонецОбласти

#Область ЗагрузкаДокументов

// Возвращает преобразованный результат загрузки данных из транспортных контейнеров.
// 
// Параметры:
//  АдресРезультата - Строка
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ОбработатьРезультатЗагрузкиВФоне
//
Функция РезультатЗагрузкиДанныхОбъектовКонтейнеров(Знач АдресРезультата) Экспорт
	
	РезультатЗагрузкиВФоне = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	РезультатЗагрузки = ЭлектронныеДокументыЭДО.ОбработатьРезультатЗагрузкиВФоне(РезультатЗагрузкиВФоне);
	
	Возврат РезультатЗагрузки;
	
КонецФункции

// Возвращает описание длительной операции по загрузке данных из транспортных контейнеров после проверки подписей на клиенте.
// 
// Параметры:
//  ПроверенныеПодписи - Соответствие из КлючИЗначение:
//  * Ключ - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
//  * Значение - Массив из См. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
//  АдресКонтекста - Строка
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
//
Функция ЗагрузитьДанныеОбъектовКонтейнеровПослеПроверкиПодписей(Знач ПроверенныеПодписи, Знач АдресКонтекста, Знач КонтекстДиагностики) Экспорт
	
	Контекст = ПолучитьИзВременногоХранилища(АдресКонтекста); // См. ЭлектронныеДокументыЭДО.НовыйКонтекстПроверкиПодписей
	УдалитьИзВременногоХранилища(АдресКонтекста);
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"ЭлектронныеДокументыЭДО.ЗагрузитьДанныеОбъектовКонтейнеровПослеПроверкиПодписей",
		Контекст.ДанныеДокументов, ПроверенныеПодписи, КонтекстДиагностики,
		Контекст.ОтпечаткиСертификатов, Контекст.ПаролиСертификатов);
	
КонецФункции

#КонецОбласти

#Область ВидыДокументов

// См. ОбщегоНазначения.ЭтоМобильныйКлиент
Функция ИспользоватьКраткоеПредставлениеВидовДокументов() Экспорт
	Возврат ОбщегоНазначения.ЭтоМобильныйКлиент();
КонецФункции

#КонецОбласти

#Область Подписи

// Возвращает результат проверки подписей электронного документа.
// 
// Параметры:
//  ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//                      - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ПроверитьПодписиДокумента
//
Функция ПроверитьПодписиДокумента(Знач ЭлектронныйДокумент, КонтекстДиагностики) Экспорт
	Возврат ЭлектронныеДокументыЭДО.ПроверитьПодписиДокумента(ЭлектронныйДокумент, КонтекстДиагностики);
КонецФункции

// Возвращает результат проверки подписей сообщения.
// 
// Параметры:
//  СообщениеЭДО - ДокументСсылка.СообщениеЭДО
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ПроверитьПодписиСообщения
//
Функция ПроверитьПодписиСообщения(Знач СообщениеЭДО, КонтекстДиагностики) Экспорт
	Возврат ЭлектронныеДокументыЭДО.ПроверитьПодписиСообщения(СообщениеЭДО, КонтекстДиагностики);
КонецФункции

// См. ЭлектронныеДокументыЭДО.ЗаписатьРезультатПроверкиПодписейДокумента
Функция ЗаписатьРезультатПроверкиПодписейДокумента(Знач ПроверенныеПодписи) Экспорт
	Возврат ЭлектронныеДокументыЭДО.ЗаписатьРезультатПроверкиПодписейДокумента(ПроверенныеПодписи);
КонецФункции

#КонецОбласти

#Область МашиночитаемыеДоверенности

// Возвращает результаты проверок подписей по МЧД при регистрации сертификатов.
// 
// Параметры:
//  НаборПараметровПроверок - Массив из См. ЭлектронныеДокументыЭДОКлиент.НовыеПараметрыПроверкиПодписиПоМЧДПриРегистрацииСертификата
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ПроверитьПодписиПоМЧДПриРегистрацииСертификатов
Функция ПроверитьПодписиПоМЧДПриРегистрацииСертификатов(Знач НаборПараметровПроверок, КонтекстДиагностики) Экспорт
	Возврат ЭлектронныеДокументыЭДО.ПроверитьПодписиПоМЧДПриРегистрацииСертификатов(
		НаборПараметровПроверок, КонтекстДиагностики)
КонецФункции

// Продолжает проверку подписей по МЧД при регистрации сертификатов
//
// Параметры:
//  РезультатыПроверокПодписейПоМЧД - См. ЭлектронныеДокументыЭДО.ПроверитьПодписиПоМЧДПриРегистрацииСертификатов
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ПроверитьПодписиПоМЧДПриРегистрацииСертификатовПродолжение
Функция ПроверитьПодписиПоМЧДПриРегистрацииСертификатовПродолжение(Знач РезультатыПроверокПодписейПоМЧД, 
	КонтекстДиагностики) Экспорт
	Возврат ЭлектронныеДокументыЭДО.ПроверитьПодписиПоМЧДПриРегистрацииСертификатовПродолжение(
		РезультатыПроверокПодписейПоМЧД, КонтекстДиагностики);
КонецФункции

#КонецОбласти

#КонецОбласти