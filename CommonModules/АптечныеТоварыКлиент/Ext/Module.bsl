
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Открывает форму выбора вида номенклатуры, для заполнения реквизита нового элемента справочника Номенклатура.
//
// Параметры:
//  Ответ                 - КодВозвратаДиалога
//  ЭлементКлассификатора - СправочникОбъект.РегистрЛекарственныхСредств - значения заполнения нового элемента номенклатуры
//
Процедура ОткрытьФормуНовогоЭлементаНоменклатурыПоКлассификатору(Ответ, ЭлементКлассификатора) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить = Новый ОписаниеОповещения("ОткрытьФормуНовогоЭлементаНоменклатурыПоВидуНоменклатурыИКлассификатору", АптечныеТоварыКлиент, ЭлементКлассификатора);
	
	Тип = ПредопределенноеЗначение("Справочник.ТипыНоменклатурыРасширенные.ЛекарственноеСредство");
	ВидНоменклатуры = НоменклатураВызовСервера.ВидНоменклатурыПоУмолчанию(Тип);
	
	Если ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		ВыполнитьОбработкуОповещения(Оповестить, ВидНоменклатуры);
	Иначе
		Отбор = Новый Структура;
		Отбор.Вставить("ТипНоменклатурыРасширенный", Тип);
		
		ОткрытьФорму("Справочник.ВидыНоменклатуры.ФормаВыбора", Новый Структура("Отбор", Отбор),,,,, Оповестить);
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму нового элемента справочника номенклатуры,
// с параметром ЗначенияЗаполнения.
//
// Параметры:
//  ВидНоменклатуры       - СправочникСсылка.ВидыНоменклатуры            - значения заполнения нового элемента номенклатуры
//  ЭлементКлассификатора - СправочникОбъект.РегистрЛекарственныхСредств - значения заполнения нового элемента номенклатуры
//
Процедура ОткрытьФормуНовогоЭлементаНоменклатурыПоВидуНоменклатурыИКлассификатору(ВидНоменклатуры, ЭлементКлассификатора) Экспорт
	
	Если ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		
		ЗначенияЗаполнения = Новый Структура("ВидНоменклатуры, ЭлементКАТ", ВидНоменклатуры, ЭлементКлассификатора.Ссылка);
		ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения));
		
	КонецЕсли;
	
КонецПроцедуры

// Создает элемент справочника номенклатуры на основании элементов КАТ
//
// Параметры:
//  ВидНоменклатуры       - СправочникСсылка.ВидыНоменклатуры - данные для заполнения создаваемого объекта
//  СписокАптечныхТоваров - Массив ссылок элементов КАТ
//
Процедура СоздатьНоменклатуруПоКлассификатору(ВидНоменклатуры, СписокАптечныхТоваров) Экспорт
	
	Если Не ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоСозданных = АптечныеТоварыВызовСервера.СоздатьНоменклатуруПоКлассификатору(ВидНоменклатуры, СписокАптечныхТоваров);
	
	Текст = НСтр("ru = 'Элементы номенклатуры созданы.'");
	
	ПояснениеШаблон = НСтр("ru = 'Для выделенных в списке позиций созданы элементы справочника ""Номенклатура"". Успешно обработано: %1 из %2'");
	Пояснение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПояснениеШаблон, КоличествоСозданных, СписокАптечныхТоваров.Количество());
	
	ПоказатьОповещениеПользователя(Текст,, Пояснение, БиблиотекаКартинок.Информация32);
	
	Если КоличествоСозданных > 0 Тогда
		ОповеститьОбИзменении(Тип("СправочникСсылка.Номенклатура"));
		Оповестить("Запись_НоменклатураПоКлассификатору");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс
