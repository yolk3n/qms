
#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область ОбновлениеКонфигурации

// См. ЭлектронноеВзаимодействие.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
#Область Версия_1_9_1

#Область РегистрыСведений_ПодписываемыеВидыЭД_ОбработатьДанныеДляПереходаНаНовуюВерсию

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ПодписываемыеВидыЭД.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "1.9.3.49";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.ЗапускатьИВПодчиненномУзлеРИБСФильтрами = Истина;
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("7076d4bf-e77e-4888-9b76-aab4ec3cc9da");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ПодписываемыеВидыЭД.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = '1С:БЭД: обновление подписываемых видов электронных документов'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.ПодписываемыеВидыЭД.ПолноеИмя());
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами") Тогда
		Читаемые.Добавить("Справочник.ВидыДокументовЭДО");
	КонецЕсли;
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ПодписываемыеВидыЭД.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ПодписываемыеВидыЭД.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами") Тогда
		НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
		НоваяСтрока.Процедура = "Справочники.ВидыДокументовЭДО.ОбработатьДанныеДляПереходаНаНовуюВерсию";
		НоваяСтрока.Порядок = "После";
		
		НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
		НоваяСтрока.Процедура = "Справочники.СообщениеЭДОПрисоединенныеФайлы.ОбработатьДанныеДляПереходаНаНовуюВерсию";
		НоваяСтрока.Порядок = "Любой";
	КонецЕсли;

#КонецОбласти

#КонецОбласти
	 
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область КриптографическиеОперации

// Получает серверные и облачные отпечатки сертификатов, при необходимости
// дополняя их клиентскими отпечатками (в качестве параметра КлиентскиеОтпечатки необходимо передать
// результат выполнения метода см. КриптографияБЭДКлиент.ПолучитьОтпечаткиСертификатов) .
// 
// Параметры:
// 	ВидОперации - Строка
// 	КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 	                    - Неопределено - ошибка получения отпечатков не будет добавлена в контекст диагностики,
// 	                      ее необходимо будет добавить на вызывающей стороне.
// 	РезультатыПолученияОтпечатков - см. КриптографияБЭДКлиентСервер.НовыеРезультатыПолученияОтпечатков
// Возвращаемое значение:
// 	См. КриптографияБЭДКлиентСервер.НовыеРезультатыПолученияОтпечатков
Функция ПолучитьОтпечаткиСертификатов(ВидОперации, КонтекстДиагностики, Знач РезультатыПолученияОтпечатков = Неопределено) Экспорт
	
	Если РезультатыПолученияОтпечатков = Неопределено Тогда
		РезультатыПолученияОтпечатков = КриптографияБЭДКлиентСервер.НовыеРезультатыПолученияОтпечатков();
	КонецЕсли;
	
	Если РезультатыПолученияОтпечатков.Клиент = Неопределено Тогда
		РезультатыПолученияОтпечатков.Клиент = КриптографияБЭДКлиентСервер.НовыйРезультатПолученияОтпечатков();
	КонецЕсли;
	Если РезультатыПолученияОтпечатков.Сервер = Неопределено Тогда
		РезультатыПолученияОтпечатков.Сервер = КриптографияБЭДСлужебный.ПолучитьОтпечаткиСертификатовНаСервере();
	КонецЕсли;
	Если РезультатыПолученияОтпечатков.Облако = Неопределено Тогда
		РезультатыПолученияОтпечатков.Облако = КриптографияБЭДСлужебный.ПолучитьОтпечаткиСертификатовВСервисе();
	КонецЕсли;
	
	Если КонтекстДиагностики <> Неопределено Тогда
		ДобавитьОшибкуПолученияОтпечатковСертификатов(КонтекстДиагностики, ВидОперации, РезультатыПолученияОтпечатков);
	КонецЕсли;
	
	Возврат РезультатыПолученияОтпечатков;
	
КонецФункции

// Возвращает отпечатки сертификатов криптографии на сервере.
//
// Параметры:
//   ПоказыватьОшибку  - Булево - если Ложь, то ошибка создания менеджера криптографии не отображается.
//
// Возвращаемое значение:
//   См. КриптографияБЭДКлиентСервер.НовыйРезультатПолученияОтпечатков
Функция ПолучитьОтпечаткиСертификатовНаСервере(ПоказыватьОшибку = Ложь) Экспорт
	
	Возврат КриптографияБЭДСлужебный.ПолучитьОтпечаткиСертификатовНаСервере(ПоказыватьОшибку);
	
КонецФункции

// Возвращает отпечатки сертификатов всех контекстов.
// 
// Параметры:
// 	РезультатыПолученияОтпечатков - см. КриптографияБЭДКлиентСервер.НовыеРезультатыПолученияОтпечатков
// Возвращаемое значение:
// 	Массив из Строка 
Функция ПолучитьВсеОтпечаткиСертификатов(РезультатыПолученияОтпечатков) Экспорт
	
	ВсеОтпечатки = Новый Массив;
	
	Если РезультатыПолученияОтпечатков.Клиент <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеОтпечатки, РезультатыПолученияОтпечатков.Клиент.Отпечатки, Истина);
	КонецЕсли;
	Если РезультатыПолученияОтпечатков.Сервер <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеОтпечатки, РезультатыПолученияОтпечатков.Сервер.Отпечатки, Истина);
	КонецЕсли;
	Если РезультатыПолученияОтпечатков.Облако <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеОтпечатки, РезультатыПолученияОтпечатков.Облако.Отпечатки, Истина);
	КонецЕсли;
	
	Возврат ВсеОтпечатки;
	
КонецФункции

// Расшифровывает данные.
// 
// Параметры:
// 	Данные - ДвоичныеДанные - расшифровываемые данные
// 	Пароль - Строка - пароль доступа к закрытому ключу
// 	КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 	Программа - СправочникСсылка.ПрограммыЭлектроннойПодписиИШифрования - программа электронной подписи и шифрования
// Возвращаемое значение:
// 	- ДвоичныеДанные - расшифрованный маркер
// 	- Неопределено - если маркер расшифровать не удалось
//
Функция Расшифровать(Данные, Пароль, КонтекстДиагностики, Программа = Неопределено) Экспорт
	
	ВидОперации = НСтр("ru = 'Расшифровка маркера'");
	ПодробноеПредставлениеОшибки = "";
	МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("Расшифровка", Истина, ПодробноеПредставлениеОшибки, Программа);
	Если МенеджерКриптографии = Неопределено Тогда
		КраткоеПредставлениеОшибки = ОбработкаНеисправностейБЭД.ПолучитьСообщениеОбОшибкеДругойСистемы("110");
		ОбработатьОшибкиКриптографическойОперации(ВидОперации, КонтекстДиагностики,
			ПодробноеПредставлениеОшибки, КраткоеПредставлениеОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Пароль) Тогда
		МенеджерКриптографии.ПарольДоступаКЗакрытомуКлючу = Пароль;
	КонецЕсли;
	
	Попытка
		РасшифрованныеДвоичныеДанные = МенеджерКриптографии.Расшифровать(Данные);
	Исключение
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КраткоеПредставлениеОшибки = ОбработкаНеисправностейБЭД.ПолучитьСообщениеОбОшибкеДругойСистемы("113");
		ОбработатьОшибкиКриптографическойОперации(ВидОперации, КонтекстДиагностики,
			ПодробноеПредставлениеОшибки, КраткоеПредставлениеОшибки);
		Возврат Неопределено;
	КонецПопытки;
		
	Возврат РасшифрованныеДвоичныеДанные;
	
КонецФункции

// Подписывает данные.
// 
// Параметры:
// 	ОписаниеДанных - см. НовоеОписаниеПодписываемыхДанных
// 	КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 	ПаролиСертификатов - см. НовыеПаролиСертификатов
// Возвращаемое значение:
// 	См. НовоеОписаниеПодписываемыхДанных - при успешном подписании дополняется свойствами:
// 	 * ПаролиСертификатов - см. НовыеПаролиСертификатов
// 	 * СвойстваПодписи - см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи - дополняет свойства элементов
//                       массива НаборДанных.
// 	 * ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - дополняет свойства элементов
//                       массива НаборДанных.
Функция Подписать(ОписаниеДанных, КонтекстДиагностики, ПаролиСертификатов = Неопределено, МенеджерКриптографии = Неопределено) Экспорт
	
	Если ПаролиСертификатов = Неопределено Тогда
		ПаролиСертификатов = НовыеПаролиСертификатов();
	КонецЕсли;
	
	ВидОперации = НСтр("ru = 'Установка подписи на сервере'");
	ПодробноеПредставлениеОшибки = "";
	
	Если МенеджерКриптографии = Неопределено Тогда
		МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("Подписание", Ложь, ПодробноеПредставлениеОшибки,
			ОписаниеДанных.Программа);
	КонецЕсли;
	
	Если МенеджерКриптографии = Неопределено Тогда
		КраткоеПредставлениеОшибки = ОбработкаНеисправностейБЭД.ПолучитьСообщениеОбОшибкеДругойСистемы("110");
		ОбработатьОшибкиКриптографическойОперации(ВидОперации, КонтекстДиагностики,
			ПодробноеПредставлениеОшибки, КраткоеПредставлениеОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	СертификатКриптографии = СертификатПоОтпечатку(МенеджерКриптографии, ОписаниеДанных.Отпечаток);
	Если СертификатКриптографии = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	РезультатПроверки = ЭлектроннаяПодпись.ПроверитьСертификат(МенеджерКриптографии, СертификатКриптографии,
		ПодробноеПредставлениеОшибки, ТекущаяДатаСеанса());
	Если Не РезультатПроверки Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Пароль = ОписаниеДанных.Пароль;
	Если ЗначениеЗаполнено(Пароль) Тогда
		ПаролиСертификатов.Вставить(ОписаниеДанных.Сертификат, Пароль);
	Иначе
		ПарольИзКэша = ПаролиСертификатов.Получить(ОписаниеДанных.Сертификат);
		Если ЗначениеЗаполнено(ПарольИзКэша) Тогда
			Пароль = ПарольИзКэша;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеДанных.Вставить("ПаролиСертификатов", ПаролиСертификатов);
	
	МенеджерКриптографии.ПарольДоступаКЗакрытомуКлючу = Пароль;
	
	Для Каждого ДанныеНабора Из ОписаниеДанных.НаборДанных Цикл
		
		ДвоичныеДанныеПодписи = МенеджерКриптографии.Подписать(ДанныеНабора.Данные, СертификатКриптографии);
	
		СвойстваСертификата = ЭлектроннаяПодпись.СвойстваСертификата(СертификатКриптографии);
		СвойстваСертификата.Вставить("ДвоичныеДанные", СертификатКриптографии.Выгрузить());
		
		СвойстваПодписи = КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи();
		СвойстваПодписи.Подпись = ДвоичныеДанныеПодписи;
		СвойстваПодписи.УстановившийПодпись = Пользователи.АвторизованныйПользователь();
		СвойстваПодписи.ДатаПодписи = ЭлектроннаяПодпись.ДатаПодписания(ДвоичныеДанныеПодписи);;
		СвойстваПодписи.Сертификат = СвойстваСертификата.ДвоичныеДанные;
		СвойстваПодписи.Отпечаток = СвойстваСертификата.Отпечаток;
		СвойстваПодписи.КомуВыданСертификат = СвойстваСертификата.КомуВыдан;
		
		ДанныеНабора.Вставить("СвойстваПодписи", СвойстваПодписи);
		ДанныеНабора.Вставить("ВыбранныйСертификат", ОписаниеДанных.Сертификат);
		
	КонецЦикла;
	
	Возврат ОписаниеДанных;
	
КонецФункции

// Проверяет корректность подписи и сертификата.
// 
// Параметры:
//  МенеджерКриптографии - Неопределено - получить менеджер криптографии для проверки
//                          электронных подписей, согласно настройкам.
//                        - МенеджерКриптографии - использовать указанный менеджер криптографии.
//  ИсходныеДанные - ДвоичныеДанные - исходные данные для проверки 
//  Подпись - ДвоичныеДанные - подпись для проверки 
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// Возвращаемое значение:
// 	см. КриптографияБЭДКлиентСервер.НовыйРезультатПроверкиПодписи
Функция ПроверитьПодпись(МенеджерКриптографии, ИсходныеДанные, Подпись, КонтекстДиагностики) Экспорт
	
	ВидОперации = НСтр("ru = 'Проверка подписи'");
	РезультатПроверки = КриптографияБЭДКлиентСервер.НовыйРезультатПроверкиПодписи();
	
	ТекстОшибки = "";
	ПодписьВерна = ЭлектроннаяПодпись.ПроверитьПодпись(
		Неопределено, ИсходныеДанные, Подпись, ТекстОшибки);
	РезультатПроверки.СвойстваПодписи.ДатаПроверкиПодписи = ТекущаяДатаСеанса();
	РезультатПроверки.СвойстваПодписи.ПодписьВерна = ПодписьВерна;
	РезультатПроверки.СвойстваПодписи.Комментарий = ТекстОшибки;
	РезультатПроверки.ОписаниеОшибки = ТекстОшибки;
	
	Попытка
		Сертификаты = ПолучитьСертификатыИзПодписи(Подпись);
	Исключение
		Сертификаты = Неопределено;
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибкиКриптографическойОперации(ВидОперации, КонтекстДиагностики,
			ПодробноеПредставлениеОшибки, КраткоеПредставлениеОшибки);
	КонецПопытки;
	
	Если ЗначениеЗаполнено(Сертификаты) Тогда
		Сертификат = КриптографияБЭДСлужебныйКлиентСервер.НайтиСертификатПодписавшейСтороныРекурсивно(Сертификаты);
		ДанныеСертификата = Сертификат.Выгрузить();
		РезультатПроверки.СвойстваПодписи.Отпечаток = Base64Строка(Сертификат.Отпечаток);
		РезультатПроверки.СвойстваПодписи.КомуВыданСертификат = ЭлектроннаяПодпись.ПредставлениеСубъекта(Сертификат);
		РезультатПроверки.СвойстваПодписи.Сертификат = ДанныеСертификата;
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

// Возвращает параметры подписания, см. Подписать.
// 
// Возвращаемое значение:
// 	Структура: 
//    * НаборДанных - Массив из Структура:
//        * Данные - ДвоичныеДанные - (обязательный параметр), подписываемые данные
//    * Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - (обязательный параметр),
//                   сертификат, которым будут подписаны данные.
//    * Отпечаток - Строка - (обязательный параметр), отпечаток сертификата
//    * Пароль - Строка - (обязательный параметр)
//    * Программа - СправочникСсылка.ПрограммыЭлектроннойПодписиИШифрования - (необязательный параметр) - используется
//                                для создания менеджера криптографии.
Функция НовоеОписаниеПодписываемыхДанных() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("НаборДанных", Новый Массив);
	Параметры.Вставить("Сертификат", Справочники.СертификатыКлючейЭлектроннойПодписиИШифрования.ПустаяСсылка());
	Параметры.Вставить("Отпечаток", "");
	Параметры.Вставить("Программа", Справочники.ПрограммыЭлектроннойПодписиИШифрования.ПустаяСсылка());
	Параметры.Вставить("Пароль", "");
	
	Возврат Параметры;
	
КонецФункции

// Возвращает менеджер криптографии (на сервере) для указанной программы.
//
// Параметры:
//  ПоказатьОшибку - Булево - если Истина, тогда будет вызвано исключение, содержащее описание ошибки.
//  ОписаниеОшибки - Строка - возвращаемое описание ошибки, когда функция возвратила значение Неопределено.
//  Программа - Неопределено - возвращает менеджер криптографии первой
//              программы из справочника для которой удалось его создать.
//            - СправочникСсылка.ПрограммыЭлектроннойПодписиИШифрования - программа
//              для которой нужно создать и вернуть менеджер криптографии.
//
// Возвращаемое значение:
//  - МенеджерКриптографии  - менеджер криптографии.
//  - Неопределено - произошла ошибка, описание которой в параметре ОписаниеОшибки.
Функция МенеджерКриптографии(ПоказатьОшибку = Истина, ОписаниеОшибки = "", Программа = Неопределено) Экспорт
	
	МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("", ПоказатьОшибку, ОписаниеОшибки, Программа);
	
	Возврат МенеджерКриптографии;
	
КонецФункции

// Возвращает сертификаты из подписи.
// Если на сервере недоступно получение сертификатов, то возвращает пустой массив.
// При возникновении ошибки вызывается исключение.
//
// Параметры:
//  Подпись - ДвоичныеДанные - Подпись, из которой нужно получить сертификаты.
//  Выполнено - Булево - Устанавливается в Истина, если было получение сертификатов.
//
// Возвращаемое значение:
//  Массив из СертификатКриптографии - Сертификаты, полученные из подписи.
//
Функция ПолучитьСертификатыИзПодписи(Подпись, Выполнено = Ложь) Экспорт
	
	Сертификаты = Новый Массив;
	
	Если ИспользоватьЭлектроннуюПодписьВМоделиСервиса() Тогда
		
		СертификатыВДвоичномВиде = СервисКриптографии.ПолучитьСертификатыИзПодписи(Подпись);
		Если СертификатыВДвоичномВиде.Количество() Тогда
			Для Каждого СертификатВДвоичномВиде Из СертификатыВДвоичномВиде Цикл
				Сертификаты.Добавить(Новый СертификатКриптографии(СертификатВДвоичномВиде));
			КонецЦикла;
		КонецЕсли;
		Выполнено = Истина;
		
	ИначеЕсли ЭлектроннаяПодпись.ПроверятьЭлектронныеПодписиНаСервере() Тогда
		
		МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("ПолучениеСертификатов");
		Сертификаты = МенеджерКриптографии.ПолучитьСертификатыИзПодписи(Подпись);
		Выполнено = Истина;
		
	КонецЕсли;
	
	Возврат Сертификаты;
	
КонецФункции

#КонецОбласти

#Область ПодписываемыеВидыДокументов

// Возвращает описание запроса, в результате которого будут содержаться подписываемые виды документов.
// Запрос содержит следующие поля:
//   * Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//   * ВидДокумента - ОпределяемыйТип.ВидыЭлектронныхДокументовБЭД
//   * Использовать - Булево - вид документа можно подписать данным сертификатом.
// 
// Параметры:
// 	ИмяВременнойТаблицы - Строка - таблица, в которую будет помещен результат запроса
// 	Отбор - см. НовыйОтборПодписываемыхВидовДокументов
// Возвращаемое значение:
// 	см. ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса
Функция ЗапросПодписываемыхВидовДокументов(ИмяВременнойТаблицы, Отбор) Экспорт
	
	Если Отбор = Неопределено Тогда
		Отбор = НовыйОтборПодписываемыхВидовДокументов();
	КонецЕсли;
	
	ПоляУсловия = Новый Массив;
	Если Отбор.Сертификаты <> "" Тогда
		ПоляУсловия.Добавить(СтрШаблон("ПодписываемыеВидыЭД.СертификатЭП В(%1)", Отбор.Сертификаты));
	КонецЕсли;
	Если Отбор.ВидыДокументов <> "" Тогда
		ПоляУсловия.Добавить(СтрШаблон("ПодписываемыеВидыЭД.ВидЭД В(%1)", Отбор.ВидыДокументов));
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПодписываемыеВидыЭД.СертификатЭП КАК Сертификат,
		|	ПодписываемыеВидыЭД.ВидЭД КАК ВидДокумента,
		|	ПодписываемыеВидыЭД.Использовать
		|ПОМЕСТИТЬ ИмяВременнойТаблицы
		|ИЗ
		|	РегистрСведений.ПодписываемыеВидыЭД КАК ПодписываемыеВидыЭД
		|ГДЕ &ПоляУсловия";
	
	ТекстЗапроса = ОбщегоНазначенияБЭД.ТекстЗапросаИзШаблона(ТекстЗапроса, ИмяВременнойТаблицы, "", ПоляУсловия);
	
	ОписаниеЗапроса = ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса();
	ОписаниеЗапроса.Текст = ТекстЗапроса;
	
	Возврат ОписаниеЗапроса;
	
КонецФункции

// Добавляет подписываемый вид документа для всех сертификатов. Если для сертификата все флаги "Использовать"
// установлены, новый вид документа будет добавлен с установленным флагом "Использовать", в ином случае флаг
// установлен не будет.
// 
// Параметры:
// 	ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
Процедура ДобавитьПодписываемыйВидДокумента(ВидДокумента) Экспорт
	
	ОбщегоНазначенияБЭД.УстановитьУправляемуюБлокировку("РегистрСведений.ПодписываемыеВидыЭД");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодписываемыеВидыЭД.СертификатЭП КАК СертификатЭП,
		|	МИНИМУМ(ПодписываемыеВидыЭД.Использовать) КАК ВсеФлагиУстановлены
		|ИЗ
		|	РегистрСведений.ПодписываемыеВидыЭД КАК ПодписываемыеВидыЭД
		|ГДЕ
		|	ПодписываемыеВидыЭД.СертификатЭП <> ЗНАЧЕНИЕ(Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.ПустаяСсылка)
		|СГРУППИРОВАТЬ ПО
		|	ПодписываемыеВидыЭД.СертификатЭП";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.ПодписываемыеВидыЭД.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.СертификатЭП = ВыборкаДетальныеЗаписи.СертификатЭП;
		МенеджерЗаписи.ВидЭД = ВидДокумента;
		МенеджерЗаписи.Использовать = ВыборкаДетальныеЗаписи.ВсеФлагиУстановлены;
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Инициализирует структуру для отбора подписываемых видов документов.
// 
// Возвращаемое значение:
// 	Структура:
// * Сертификаты - Строка - параметр или выражение для отбора по сертификатам
// * ВидыДокументов - Строка - параметр или выражение для отбора по видам документов
Функция НовыйОтборПодписываемыхВидовДокументов() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Сертификаты", "");
	Отбор.Вставить("ВидыДокументов", "");
	
	Возврат Отбор;
	
КонецФункции

// Возвращает виды подписываемых электронных документов.
//
// Возвращаемое значение:
//  - Массив из СправочникСсылка.ВидыДокументовЭДО, ПеречислениеСсылка.ВидыЭДОбменСБанками - виды актуальных ЭД.
Функция ПодписываемыеВидыДокументов() Экспорт
	
	ВидыДокументов = Новый Массив;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами.ЭлектронныеДокументы") Тогда
		Запросы = Новый Массив;
		МодульЭлектронныеДокументыЭДО = ОбщегоНазначения.ОбщийМодуль("ЭлектронныеДокументыЭДО");
		ЗапросВидовДокументов = МодульЭлектронныеДокументыЭДО.ЗапросВидовДокументов("ВидыДокументов");
		Запросы.Добавить(ЗапросВидовДокументов);
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|ВидыДокументов.Ссылка КАК Ссылка
		|ИЗ
		|ВидыДокументов КАК ВидыДокументов";
		
		ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
		
		Результат = ИтоговыйЗапрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ВидыДокументов.Добавить(Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСБанками") Тогда
		МодульОбменСБанкамиСлужебныйПовтИсп = ОбщегоНазначения.ОбщийМодуль("ОбменСБанкамиСлужебныйПовтИсп");
		МассивЭДОбменСБанками = МодульОбменСБанкамиСлужебныйПовтИсп.АктуальныеВидыЭД();
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыДокументов, МассивЭДОбменСБанками);
	КонецЕсли;

	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(ВидыДокументов);
	
КонецФункции


#КонецОбласти

#Область Сертификаты

// Возвращает описание запроса, в результате которого будут содержаться действующие сертификаты.
// Запрос содержит следующие поля:
//   * Ссылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат
//   * Организация - ОпределяемыйТип.Организация
//   * Отпечаток - Строка - отпечаток сертификата в кодировке Base64
//   * Отозван - Булево
//   * ДанныеСертификата - ДвоичныеДанные
//   * ДействителенДо - Дата - Дата окончания действия сертификата
//   * Программа - СправочникСсылка.ПрограммыЭлектроннойПодписиИШифрования - программа, требуемая для подписания
//                 и расшифровки документов
//   * СертификатДоступенТекущемуПользователю - Булево - текущий пользователь может работать с данным сертификатом.
//   * Пользователь - СправочникСсылка.Пользователи - пользователь сертификата.
//   * ВыпущенЧерезСервис1СПодпись - Булево - сертификат выпущен через сервис 1С-Подпись.
// 
// Параметры:
// 	ИмяВременнойТаблицы - Строка - таблица, в которую будет помещен результат запроса
// 	Отбор - см. НовыйОтборСертификатов
// Возвращаемое значение:
// 	см. ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса
Функция ЗапросДействующихСертификатов(ИмяВременнойТаблицы, Отбор = Неопределено) Экспорт
	
	Если Отбор = Неопределено Тогда
		Отбор = НовыйОтборСертификатов();
	КонецЕсли;
	
	ПоляУсловия = Новый Массив;
	ДобавлятьУсловиеДействительныхСертификатов = Отбор.ТолькоДействительные
		И Не Отбор.ВключатьЗаявленияНаВыпускСертификатов;
	Если ДобавлятьУсловиеДействительныхСертификатов Тогда
		ПоляУсловия.Добавить(ТекстПоляСертификатДействителен());
	КонецЕсли;
	Если Отбор.Отпечатки <> "" Тогда
		Если Отбор.ВключатьЗаявленияНаВыпускСертификатов Тогда
			УсловиеВключатьЗаявленияНаВыпускСертификатов = "
				| ИЛИ ЕСТЬNULL(НЕ ЗаявленияНаВыпускСертификата.СостояниеЗаявления В (&КонечныеСостоянияЗаявления), ЛОЖЬ)";
		Иначе
			УсловиеВключатьЗаявленияНаВыпускСертификатов = "";
		КонецЕсли;
		ПолеУсловия = СтрШаблон("(Сертификаты.Отпечаток В(%1) %2)", Отбор.Отпечатки, УсловиеВключатьЗаявленияНаВыпускСертификатов);
		ПоляУсловия.Добавить(ПолеУсловия);
	КонецЕсли;
	ИННОрганизации = "";
	Если ЗначениеЗаполнено(Отбор.Организация) Тогда
		ИмяРеквизитаИННОрганизации = ЭлектронноеВзаимодействие.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении(
			"ИННОрганизации");
		Если Отбор.ВключатьСертификатыСПустойОрганизацией Тогда
			УсловиеПустаяОрганизация = "ИЛИ Организация = &ПустаяОрганизация";
		Иначе 
			УсловиеПустаяОрганизация = "";
		КонецЕсли; 
		УсловиеОрганизация = СтрШаблон("(Организация.%1 = &ТекущаяОрганизацияИНН
			|%2)", ИмяРеквизитаИННОрганизации, УсловиеПустаяОрганизация);
		ПоляУсловия.Добавить(УсловиеОрганизация);
		ИННОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отбор.Организация, ИмяРеквизитаИННОрганизации);
	КонецЕсли;
	Если Отбор.Облачные <> Неопределено Тогда
		Условие = ?(Отбор.Облачные, "Программа ССЫЛКА Справочник.УчетныеЗаписиDSS",
			"Программа ССЫЛКА Справочник.ПрограммыЭлектроннойПодписиИШифрования");
		ПоляУсловия.Добавить(Условие);
	КонецЕсли;
	ПользователиДляОтбора = ПользователиДляОтбораСертификатов(Отбор.ДоступныеТекущемуПользователю <> Неопределено);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПользователиДляОтбора, Отбор.ДополнительныеПользователиДляОтбора);
	
	Поля = "Ссылка, Организация, Отпечаток, Отозван, ДанныеСертификата,
	|ДействителенДо, Программа, Пользователь В (&ДоступенПользователям) КАК СертификатДоступенТекущемуПользователю,
	|Пользователь,
	|ВЫБОР КОГДА ЗаявленияНаВыпускСертификата.СостояниеЗаявления ЕСТЬ NULL ТОГДА ЛОЖЬ
	|ИНАЧЕ НЕ ЗаявленияНаВыпускСертификата.СостояниеЗаявления В (&КонечныеСостоянияЗаявления)
	|КОНЕЦ КАК ОжидаетсяВыпускСертификата,
	|ВЫБОР КОГДА ЗаявленияНаВыпускСертификата.СостояниеЗаявления ЕСТЬ NULL ТОГДА ЛОЖЬ
	|ИНАЧЕ НЕ ЗаявленияНаВыпускСертификата.СостояниеЗаявления В (&СостоянияЗаявленияВыпущенЧерезСервис1СПодпись)
	|КОНЕЦ КАК ВыпущенЧерезСервис1СПодпись,
	|ДействителенДо > &ТекущаяДата КАК Действует";
	
	КонечныеСостоянияЗаявления = Новый Массив;
	КонечныеСостоянияЗаявления.Добавить(Перечисления.СостоянияЗаявленияНаВыпускСертификата.Исполнено);
	КонечныеСостоянияЗаявления.Добавить(Перечисления.СостоянияЗаявленияНаВыпускСертификата.Отклонено);
	КонечныеСостоянияЗаявления.Добавить(Перечисления.СостоянияЗаявленияНаВыпускСертификата.ПустаяСсылка());
	
	СостоянияЗаявленияВыпущенЧерезСервис1СПодпись = Новый Массив;
	СостоянияЗаявленияВыпущенЧерезСервис1СПодпись.Добавить(Перечисления.СостоянияЗаявленияНаВыпускСертификата.Отклонено);
	СостоянияЗаявленияВыпущенЧерезСервис1СПодпись.Добавить(Перечисления.СостоянияЗаявленияНаВыпускСертификата.ПустаяСсылка());
	
	ОтборПоПользователям = Новый Структура;
	ОтборПоПользователям.Вставить("ДоступныеТекущемуПользователю", Отбор.ДоступныеТекущемуПользователю);
	ОтборПоПользователям.Вставить("Пользователи", ПользователиДляОтбора);
	ОтборПоПользователям.Вставить("ДополнительныеПользователиДляОтбора", Отбор.ДополнительныеПользователиДляОтбора);
	ОписаниеЗапроса = ЗапросСертификатов(ИмяВременнойТаблицы, Поля, ПоляУсловия, Отбор.ТолькоНепомеченныеНаУдаление,
		Отбор.ТолькоДействительные, ОтборПоПользователям);
	ОписаниеЗапроса.СлужебныеПараметры.Вставить("ТекущаяОрганизацияИНН", ИННОрганизации);
	ОписаниеЗапроса.СлужебныеПараметры.Вставить("ПустаяОрганизация",
		Метаданные.ОпределяемыеТипы.Организация.Тип.ПривестиЗначение());
	ОписаниеЗапроса.СлужебныеПараметры.Вставить("КонечныеСостоянияЗаявления", КонечныеСостоянияЗаявления);
	ОписаниеЗапроса.СлужебныеПараметры.Вставить("СостоянияЗаявленияВыпущенЧерезСервис1СПодпись",
		СостоянияЗаявленияВыпущенЧерезСервис1СПодпись);
	
	Возврат ОписаниеЗапроса;
	
КонецФункции

// Возвращает описание запроса, в результате которого будут содержаться недействующие сертификаты,
// просроченные сертификаты.
// Запрос содержит следующие поля:
//   * Ссылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат.
// 
// Параметры:
// 	ИмяВременнойТаблицы - Строка - таблица, в которую будет помещен результат запроса
// 	Отбор - см. НовыйОтборНедействующихИлиНедоступныхСертификатов
// Возвращаемое значение:
// 	см. ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса
Функция ЗапросНедействующихИлиНедоступныхСертификатов(ИмяВременнойТаблицы, Отбор) Экспорт
	
	ПоляУсловия = Новый Массив;
	ПоляУсловия.Добавить(СтрШаблон("(НЕ Отпечаток В(&%1) ИЛИ НЕ %2)",
		Отбор.ОпечаткиДоступныхСертификатов, ТекстПоляСертификатДействителен()));
		
	Поля = "Ссылка";
	Возврат ЗапросСертификатов(ИмяВременнойТаблицы, Поля, ПоляУсловия);
	
КонецФункции

// Возвращает описание запроса, в результате которого будут содержаться заказанные, но еще не полученные сертификаты.
// Запрос содержит следующие поля:
//   * Ссылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат.
//   * Организация - ОпределяемыйТип.Организация - организация.
// 
// Параметры:
// 	ИмяВременнойТаблицы - Строка - таблица, в которую будет помещен результат запроса
// Возвращаемое значение:
// 	см. ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса
Функция ЗапросНеполученныхСертификатов(ИмяВременнойТаблицы) Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Сертификаты.Ссылка,
	|	Сертификаты.Организация
	|ПОМЕСТИТЬ ИмяВременнойТаблицы
	|ИЗ
	|	Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК Сертификаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаявленияНаВыпускСертификата КАК ЗаявленияНаВыпускСертификата
	|		ПО Сертификаты.Ссылка = ЗаявленияНаВыпускСертификата.Сертификат
	|ГДЕ
	|	ЗаявленияНаВыпускСертификата.СостояниеЗаявления = ЗНАЧЕНИЕ(Перечисление.СостоянияЗаявленияНаВыпускСертификата.Отправлено)";
	
	ОписаниеЗапроса = ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса();
	ОписаниеЗапроса.Текст = ОбщегоНазначенияБЭД.ТекстЗапросаИзШаблона(ТекстЗапроса, ИмяВременнойТаблицы, "", "");
	
	Возврат ОписаниеЗапроса;
	
КонецФункции

// Возвращает свойства сертификатов.
// 
// Параметры:
// 	Сертификаты - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// Возвращаемое значение:
// 	Соответствие из КлючИЗначение:
//    * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//    * Значение - Структура:
//        * Ссылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 		  * Отозван - Булево
//        * Отпечаток - Строка
//        * ДействителенДо - Дата - дата окончания действия сертификата
//        * КемВыдан - Строка - удостоверяющий центр, который выдал сертификат
//        * Наименование - Строка - пользовательское представление сертификата
//        * Программа - Строка - Программа, требуемая для подписания и расшифровки документов
//        * ДанныеСертификата - ДвоичныеДанные - данные файла сертификата
//        * ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - в ключ помещается 
//          ссылка на переданный сертификат
//        * ПарольПолучен - Булево - содержит Ложь
//        * ПарольПользователя - Неопределено
//        * Комментарий - Строка - содержит пустую строку
//        * СрокДействияВДнях - Число - количество дней, оставшееся до истечения срока действия сертификата
//        * Организация - ОпределяемыйТип.Организация
//        * Фамилия - Строка - фамилия владельца сертификата
//        * Имя - Строка - имя владельца сертификата
//        * Отчество - Строка - отчество владельца сертификата
//        * Должность - Строка - должность владельца сертификата
//		  * Пользователь
//        * КомуВыдан
//        * Фирма
Функция СвойстваСертификатов(Сертификаты) Экспорт
	
	СвойстваСертификатов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Сертификаты,
		"Ссылка, Отозван, Отпечаток, ДействителенДо, КемВыдан, Наименование, Программа, ДанныеСертификата, Фамилия, Имя, Отчество, Должность, Организация, Пользователь, КомуВыдан, Фирма");
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Для Каждого Свойство Из СвойстваСертификатов Цикл
		Если Свойство.Значение.ДействителенДо >= ТекущаяДата Тогда
			СрокДействияВДнях = Окр((Свойство.Значение.ДействителенДо - ТекущаяДата) / (60 * 60 * 24));
		Иначе
			СрокДействияВДнях = - 1;
		КонецЕсли;
		Свойство.Значение.Вставить("СрокДействияВДнях", СрокДействияВДнях);
		Свойство.Значение.ДанныеСертификата = Свойство.Значение.ДанныеСертификата.Получить();
		Свойство.Значение.Вставить("ВыбранныйСертификат", Свойство.Ключ);
		Свойство.Значение.Вставить("ПарольПолучен", Ложь);
		Свойство.Значение.Вставить("ПарольПользователя", Неопределено);
		
		// В БСП методах необходим параметр
		Свойство.Значение.Вставить("Комментарий", "");
	КонецЦикла;
	
	Возврат СвойстваСертификатов;
	
КонецФункции

// Получает свойства субъекта сертификата электронной подписи.
//
// Параметры:
//  СертификатКриптографии - СертификатКриптографии
//
// Возвращаемое значение:
//  См. ЭлектроннаяПодпись.СвойстваСубъектаСертификата
//
Функция СвойстваСубъектаСертификата(СертификатКриптографии) Экспорт
	
	Свойства = ЭлектроннаяПодпись.СвойстваСубъектаСертификата(СертификатКриптографии);
	
	КриптографияБЭДСлужебныйКлиентСервер.ЗаполнитьВСвойствахСертификатаИННПоИННЮЛ(Свойства);
	
	Возврат Свойства;
	
КонецФункции

// Получает свойства субъекта сертификата электронной подписи.
//
// Параметры:
//  Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//
// Возвращаемое значение:
//  См. ЭлектроннаяПодпись.СвойстваСубъектаСертификата
//
Функция СвойстваСубъектаСертификатаПоСсылке(Сертификат) Экспорт
	
	Сертификаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сертификат);
	ДанныеСертификата = СвойстваСертификатов(Сертификаты)[Сертификат].ДанныеСертификата;
	СертификатКриптографии = Новый СертификатКриптографии(ДанныеСертификата);
	
	Возврат СвойстваСубъектаСертификата(СертификатКриптографии);
	
КонецФункции

// Получает свойства субъекта сертификатов электронной подписи.
//
// Параметры:
//  Сертификаты - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//   * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//   * Значение - См. ЭлектроннаяПодпись.СвойстваСубъектаСертификата
//
Функция СвойстваСубъектаСертификатовПоСсылке(Сертификаты) Экспорт
	
	Результат = Новый Соответствие;
	Для Каждого КлючИЗначение Из СвойстваСертификатов(Сертификаты) Цикл
		СертификатКриптографии = Новый СертификатКриптографии(КлючИЗначение.Значение.ДанныеСертификата);
		СвойстваСубъекта = СвойстваСубъектаСертификата(СертификатКриптографии);
		Результат.Вставить(КлючИЗначение.Ключ, СвойстваСубъекта);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Получает свойства издателя сертификата электронной подписи.
//
// Параметры:
//  СертификатКриптографии - СертификатКриптографии
//
// Возвращаемое значение:
//  См. ЭлектроннаяПодпись.СвойстваИздателяСертификата
//
Функция СвойстваИздателяСертификата(СертификатКриптографии) Экспорт
	
	Свойства = ЭлектроннаяПодпись.СвойстваИздателяСертификата(СертификатКриптографии);
	
	КриптографияБЭДСлужебныйКлиентСервер.ЗаполнитьВСвойствахСертификатаИННПоИННЮЛ(Свойства);
	
	Возврат Свойства;
	
КонецФункции

// Получает свойства издателя сертификата электронной подписи.
//
// Параметры:
//  Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//
// Возвращаемое значение:
//  См. ЭлектроннаяПодпись.СвойстваИздателяСертификата
//
Функция СвойстваИздателяСертификатаПоСсылке(Сертификат) Экспорт
	
	Сертификаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сертификат);
	ДанныеСертификата = СвойстваСертификатов(Сертификаты)[Сертификат].ДанныеСертификата;
	СертификатКриптографии = Новый СертификатКриптографии(ДанныеСертификата);
	
	Возврат СвойстваИздателяСертификата(СертификатКриптографии);
	
КонецФункции

// Получает свойства издателя сертификатов электронной подписи.
//
// Параметры:
//  Сертификаты - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//   * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//   * Значение - См. ЭлектроннаяПодпись.СвойстваИздателяСертификата
//
Функция СвойстваИздателяСертификатовПоСсылке(Сертификаты) Экспорт
	
	Результат = Новый Соответствие;
	Для Каждого КлючИЗначение Из СвойстваСертификатов(Сертификаты) Цикл
		СертификатКриптографии = Новый СертификатКриптографии(КлючИЗначение.Значение.ДанныеСертификата);
		СвойстваСубъекта = СвойстваИздателяСертификата(СертификатКриптографии);
		Результат.Вставить(КлючИЗначение.Ключ, СвойстваСубъекта);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает ИНН субъекта сертификата без лидирующих нолей.
//
// Параметры:
//  Сертификат - СертификатКриптографии
//
// Возвращаемое значение:
//  Строка - ИНН субъекта сертификата без лидирующих нолей.
//
Функция ИННСубъектаСертификата(СертификатКриптографии) Экспорт
	
	СвойстваСубъекта = СвойстваСубъектаСертификата(СертификатКриптографии);
	Возврат СвойстваСубъекта.ИНН;
	
КонецФункции

// Получает сертификат в формате PEM из сертификата в формате CER.
// 
// Параметры:
// 	ДанныеСертификата - ДвоичныеДанные - сертификат в формате CER.
// Возвращаемое значение:
// 	Строка
Функция СертификатВСтрокуPEM(ДанныеСертификата) Экспорт

	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(КриптографияБЭДСлужебныйКлиентСервер.ТегНачалоСертификата());
	МассивСтрок.Добавить(Base64Строка(ДанныеСертификата));
	МассивСтрок.Добавить(КриптографияБЭДСлужебныйКлиентСервер.ТегКонецСертификата());
	
	Возврат СтрСоединить(МассивСтрок, Символы.ПС);

КонецФункции

// Возвращает сертификат организации, если он единственный или пустую ссылку на сертификат в ином случае.
// 
// Параметры:
// 	Организация - ОпределяемыйТип.Организация
// Возвращаемое значение:
// 	СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
Функция ЕдинственныйСертификатОрганизации(Организация) Экспорт
	
	СертификатКриптографии = Справочники.СертификатыКлючейЭлектроннойПодписиИШифрования.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат СертификатКриптографии;
	КонецЕсли;
	
	ОтборСертификатов = НовыйОтборСертификатов();
	ОтборСертификатов.Организация = Организация;
	ОтборСертификатов.ВключатьСертификатыСПустойОрганизацией = Ложь;
	ЗапросСертификатов = ЗапросДействующихСертификатов("СертификатыОрганизации", ОтборСертификатов);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СертификатыОрганизации.Ссылка КАК Ссылка
	|ИЗ
	|	СертификатыОрганизации КАК СертификатыОрганизации";
	
	Запросы = Новый Массив;
	Запросы.Добавить(ЗапросСертификатов);
	
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = ИтоговыйЗапрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Количество() = 1 И Выборка.Следующий() Тогда
		СертификатКриптографии = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат СертификатКриптографии;
	
КонецФункции

// Проверяет, возможность использования облачного сертификата.
// Возвращает Ложь, если передан облачный сертификат и его использование запрещено.
// 
// Параметры:
// 	Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 	СообщениеОбОшибке - Строка - содержит текст ошибки, если функция вернула Ложь.
// Возвращаемое значение:
// 	Булево
Функция ПроверитьПравомерностьИспользованияСертификата(Сертификат, СообщениеОбОшибке = "") Экспорт
	
	Результат = Истина;
	Если ИспользованиеЭлектроннойПодписиВМоделиСервисаВозможно()
		И Не КриптографияБЭДСлужебный.ИспользованиеСертификатовОблачногоСервисаВозможно()
		И ЭтоСертификатОблачногоСервиса(Сертификат) Тогда
		СообщениеОбОшибке = КриптографияБЭДСлужебный.ТекстОшибкиНеправомерногоИспользованияОблачногоСертификата();
	Результат = Ложь;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Устанавливает сертификату программу криптографии.
// 
// Параметры:
// 	Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 	Программа - СправочникСсылка.ПрограммыЭлектроннойПодписиИШифрования
Процедура УстановитьПрограммуСертификата(Сертификат, Программа) Экспорт
	
	НачатьТранзакцию();
	Попытка
		ОбщегоНазначенияБЭД.УстановитьУправляемуюБлокировкуПоСсылке(Сертификат);
		СертификатОбъект = Сертификат.ПолучитьОбъект();
		Если СертификатОбъект.Программа <> Программа Тогда
			СертификатОбъект.Заблокировать();
			СертификатОбъект.Программа = Программа;
			СертификатОбъект.Записать();
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ОбработкаНеисправностейБЭД.ОбработатьОшибку(НСтр("ru = 'Изменение программы криптографии сертификата'"),
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().Криптография, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Определяет, что пароль сертификата сохранен для всех пользователей.
// 
// Параметры:
// 	Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// Возвращаемое значение:
// 	Булево
Функция ПарольСертификатаСохраненДляВсех(Сертификат) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Данные = КриптографияБЭДСлужебный.ДанныеПароляСертификата(Сертификат);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ЗначениеЗаполнено(Данные[Справочники.Пользователи.ПустаяСсылка()]);
	
КонецФункции

// Возвращает пароль к сертификату, если доступен текущему пользователю.
// Необходимо вызывать в привилегированном режиме.
//
// Параметры:
//  Сертификаты - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//    * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//    * Пароль - Строка - если пароль сохранен
//             - Неопределено - если пароль не сохранен.
//  Если нет прав на чтение справочника СертификатыКлючейЭлектроннойПодписиИШифрования, возвращается пустое соответствие.
//
Функция ПаролиСертификатов(Сертификаты) Экспорт
	
	ПаролиСертификатов = Новый Соответствие;
	
	МетаданныеСправочникаСертификатов = Метаданные.НайтиПоПолномуИмени("Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования");
	Если Не ПравоДоступа("Чтение", МетаданныеСправочникаСертификатов) Или Сертификаты.Количество() = 0 Тогда
		Возврат ПаролиСертификатов;
	КонецЕсли;
	
	Для Каждого Сертификат Из Сертификаты Цикл
		
		Если ЗначениеЗаполнено(Сертификат) Тогда
			Пароль = ПарольСертификата(Сертификат);
		Иначе
			Пароль = Неопределено;
		КонецЕсли;
		
		ПаролиСертификатов.Вставить(Сертификат, Пароль);
	КонецЦикла;
	
	Возврат ПаролиСертификатов;
	
КонецФункции

// Проверяет доступность сертификата текущему пользователю.
// 
// Параметры:
// 	Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// Возвращаемое значение:
// 	Булево - сертификат доступен
Функция ПользователюДоступенСертификат(Сертификат) Экспорт
	
	ПользовательСертификата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сертификат, "Пользователь");
	Возврат ПользователиДляОтбораСертификатов().Найти(ПользовательСертификата) <> Неопределено;
	
КонецФункции

// Определяет есть ли доступный текущему пользователю сертификат или заявление на выпуск сертификата.
// 
// Параметры:
// 	Организация - ОпределяемыйТип.Организация
// 	Отпечатки - см. ПолучитьОтпечаткиСертификатов
// Возвращаемое значение:
// 	Структура:
// * Результат - Булево - если Истина, у пользователя есть доступные сертификаты
// * ОжидаетсяВыпускСертификата - Булево - если Истина, ожидается выпуск сертификата
Функция ПользователюДоступенСертификатИлиЗаявлениеНаВыпуск(Организация, Отпечатки) Экспорт
	
	Отбор = НовыйОтборСертификатов();
	Отбор.Организация = Организация;
	Отбор.Отпечатки = "&Отпечатки";
	Отбор.ВключатьЗаявленияНаВыпускСертификатов = Истина;
	ЗапросСертификатов = ЗапросДействующихСертификатов("Сертификаты", Отбор);
	
	Запросы = Новый Массив;
	Запросы.Добавить(ЗапросСертификатов);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ Сертификаты.Действует,
	|Сертификаты.ОжидаетсяВыпускСертификата
	|ИЗ
	|Сертификаты КАК Сертификаты";
	
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	
	ИтоговыйЗапрос.УстановитьПараметр("Отпечатки", ПолучитьВсеОтпечаткиСертификатов(Отпечатки));
	
	ДоступныеСертификаты = ИтоговыйЗапрос.Выполнить().Выгрузить();
	
	Ответ                           = Новый Структура("ЕстьДоступныеСертификаты, ОжидаетсяВыпускСертификата", Ложь, Ложь);
	
	ОтборОптимистичный              = Новый Структура("Действует", Истина);
	ОтборОжидаетсяВыпускСертификата = Новый Структура("ОжидаетсяВыпускСертификата", Истина);
	
	Если ДоступныеСертификаты.НайтиСтроки(ОтборОптимистичный).Количество() > 0 Тогда
		Ответ.ЕстьДоступныеСертификаты = Истина;
	КонецЕсли;
	Если ДоступныеСертификаты.НайтиСтроки(ОтборОжидаетсяВыпускСертификата).Количество() > 0 Тогда
		Ответ.ОжидаетсяВыпускСертификата = Истина;
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

// Инициализирует структуру для отбора сертификатов.
// 
// Возвращаемое значение:
// 	Структура:
// * Отпечатки - Строка - параметр или выражение для отбора по отпечаткам
// * Организация - ОпределяемыйТип.Организация
// * ВключатьСертификатыСПустойОрганизацией - Булево
// * Облачные - Неопределено, Булево - если Истина, отобрать только облачные сертификаты, 
//              Ложь - только необлачные, Неопределено - без отбора.
// * ДоступныеТекущемуПользователю - Неопределено, Булево - если Истина, отобрать только доступные текущему
//                                   пользователю сертификаты, Ложь - только недоступные, Неопределено - без отбора.
// * ДополнительныеПользователиДляОтбора - Массив из СправочникСсылка.Пользователи - пользователи, которым доступен
//                                         сертификат (параметр дополняет основной отбор по пользователям).
// * ВключатьЗаявленияНаВыпускСертификатов - Булево - если Истина, в результате запроса будет содержаться
//                                   заявления на выпуск сертификатов.
// * ТолькоДействительные - Булево - если Истина, в результате запроса будут содержаться только непросроченные
//                                   не отозванные сертификаты.
// * ТолькоНепомеченныеНаУдаление - Булево - если Истина, в результате запроса будут содержаться только непомеченные
//                                  на удаление сертификаты.
Функция НовыйОтборСертификатов() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Отпечатки", "");
	Отбор.Вставить("Организация", Неопределено);
	Отбор.Вставить("ВключатьСертификатыСПустойОрганизацией", Истина);
	Отбор.Вставить("Облачные", Неопределено);
	Отбор.Вставить("ДоступныеТекущемуПользователю", Истина);
	Отбор.Вставить("ДополнительныеПользователиДляОтбора", Новый Массив);
	Отбор.Вставить("ВключатьЗаявленияНаВыпускСертификатов", Ложь);
	Отбор.Вставить("ТолькоДействительные", Истина);
	Отбор.Вставить("ТолькоНепомеченныеНаУдаление", Истина);
	
	Возврат Отбор;
	
КонецФункции

// Инициализирует структуру для отбора недействующих или недоступных сертификатов.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * ОпечаткиДоступныхСертификатов - Строка - отпечатки сертификатов, доступные текущем пользователю.
Функция НовыйОтборНедействующихИлиНедоступныхСертификатов() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("ОпечаткиДоступныхСертификатов", "");
	
	Возврат Отбор;
	
КонецФункции

// Возвращает пароли сертификатов.
// 
// Возвращаемое значение:
// 	Соответствие из КлючИЗначение:
//    * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат
//    * Значение - Строка - пароль
Функция НовыеПаролиСертификатов() Экспорт
	
	Возврат Новый Соответствие;
	
КонецФункции

// Определяет, являются ли сертификаты облачными.
// 
// Параметры:
// 	Сертификаты - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// Возвращаемое значение:
// 	Соответствие из КлючИЗначение:
//   * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//   * Значение - Булево - сертификат облачный
Функция ЭтоСертификатыОблачногоСервиса(Сертификаты) Экспорт
	
	ВозвращаемоеЗначение = Новый Соответствие;
	
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СертификатыКлючейЭлектроннойПодписиИШифрования.Отпечаток КАК Отпечаток,
		|	&ЭтоПрограммаОблачногоСервиса КАК ЭтоПрограммаОблачногоСервиса,
		|	СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК СертификатыКлючейЭлектроннойПодписиИШифрования
		|ГДЕ
		|	СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка В (&Ссылка)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ЭтоПрограммаОблачногоСервиса", ТекстПоляПрограммаОблачногоСервиса());
	Запрос.УстановитьПараметр("Ссылка", Сертификаты);
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	РезультатПолученияОтпечатков = КриптографияБЭДСлужебный.ПолучитьОтпечаткиСертификатовВСервисе();
	
	Пока Выборка.Следующий() Цикл
		
		ЭтоОблачныйСертификат = Ложь;
		
		Если Выборка.ЭтоПрограммаОблачногоСервиса Тогда
			ЭтоОблачныйСертификат = Истина;
		Иначе
			Если РезультатПолученияОтпечатков.Отпечатки.Найти(Выборка.Отпечаток) <> Неопределено Тогда
				ЭтоОблачныйСертификат = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ВозвращаемоеЗначение.Вставить(Выборка.Ссылка, ЭтоОблачныйСертификат);
		
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Выполняет поиск в справочнике сертификатов.
// 
// Параметры:
// 	Отпечатки - Массив из Строка
// Возвращаемое значение:
// 	Массив - Описание
Функция НайтиСертификаты(Отпечатки) Экспорт
	
	Сертификаты = Новый Массив;
	
	Запросы = Новый Массив;
	Отбор = НовыйОтборСертификатов();
	Отбор.Отпечатки = "&Отпечатки";
	ЗапросСертификатов = ЗапросДействующихСертификатов("Сертификаты", Отбор);
	
	Запросы.Добавить(ЗапросСертификатов);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|Сертификаты.Ссылка КАК Ссылка
	|ИЗ
	|Сертификаты";
	
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	ИтоговыйЗапрос.УстановитьПараметр("Отпечатки", Отпечатки);
	РезультатЗапроса = ИтоговыйЗапрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Сертификаты;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Сертификаты.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Сертификаты;
	
КонецФункции

// Возвращает пустую ссылку на справочник сертификатов.
// 
// Возвращаемое значение:
// 	СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
Функция ПустойСертификат() Экспорт
	
	Возврат Справочники.СертификатыКлючейЭлектроннойПодписиИШифрования.ПустаяСсылка();
	
КонецФункции

// Определяет наличие ошибки получения отпечатков.
// 
// Параметры:
// 	РезультатыПолученияОтпечатков - см. КриптографияБЭДКлиентСервер.НовыеРезультатыПолученияОтпечатков
// 	ОшибкаПолученияОтпечатков - Строка - в случае наличия ошибки возвращается строка, содержащая тексты ошибок клиента,
// 	                            сервера и облака.
// Возвращаемое значение:
// 	Булево - есть ошибка
Функция ЕстьОшибкаПолученияОтпечатков(РезультатыПолученияОтпечатков, ОшибкаПолученияОтпечатков = "") Экспорт
	
	КлиентскиеОтпечатки = РезультатыПолученияОтпечатков.Клиент;
	СерверныеОтпечатки = РезультатыПолученияОтпечатков.Сервер;
	ОблачныеОтпечатки = РезультатыПолученияОтпечатков.Облако;
	
	ЕстьОшибкаПолученияОтпечатков = КлиентскиеОтпечатки.Ошибка И Не СерверныеОтпечатки.Доступность И Не ОблачныеОтпечатки.Доступность
		Или СерверныеОтпечатки.Ошибка И Не КлиентскиеОтпечатки.Доступность И Не ОблачныеОтпечатки.Доступность
		Или ОблачныеОтпечатки.Ошибка И Не КлиентскиеОтпечатки.Доступность И Не СерверныеОтпечатки.Доступность;
		
	Если ЕстьОшибкаПолученияОтпечатков Тогда
		Ошибки = Новый Массив;
		Если КлиентскиеОтпечатки.Ошибка Тогда
			Ошибки.Добавить(КлиентскиеОтпечатки.ТекстОшибки);
		КонецЕсли;
		Если СерверныеОтпечатки.Ошибка Тогда
			Ошибки.Добавить(СерверныеОтпечатки.ТекстОшибки);
		КонецЕсли;
		Если ОблачныеОтпечатки.Ошибка Тогда
			Ошибки.Добавить(ОблачныеОтпечатки.ТекстОшибки);
		КонецЕсли;
		ОшибкаПолученияОтпечатков = СтрСоединить(Ошибки, Символы.ПС);
	КонецЕсли;
	
	Возврат ЕстьОшибкаПолученияОтпечатков;
	
КонецФункции

// Находит существующий или создает новый элемент справочника СертификатыКлючейЭлектроннойПодписиИШифрования.
//
// Параметры:
//  ДвоичныеДанныеСертификата - ДвоичныеДанные - содержимое сертификата;
//  Организация - СправочникСсылка.Организации - организация;
//  ИнформацияОПрограммеКриптографии - Строка - название криптосредства;
//                                - СправочникСсылка.ПрограммыЭлектроннойПодписиИШифрования - ссылка на программу криптографии.
//
// Возвращаемое значение:
//  СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - ссылка на новый сертификат.
//
Функция НайтиСоздатьСертификатЭП(ДвоичныеДанныеСертификата, Организация, ИнформацияОПрограммеКриптографии = Неопределено) Экспорт
	
	Возврат КриптографияБЭДСлужебныйВызовСервера.НайтиСоздатьСертификатЭП(ДвоичныеДанныеСертификата, Организация, ИнформацияОПрограммеКриптографии);
	
КонецФункции

// Возвращает двоичные данные сертификата по ссылке.
// 
// Параметры:
//  Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  
// Возвращаемое значение:
//  - ДвоичныеДанные - двоичные данные сертификата
//  - Неопределено - если данные отсутствуют
Функция ДвоичныеДанныеСертификата(Сертификат) Экспорт
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сертификат, "ДанныеСертификата").Получить();
КонецФункции

#КонецОбласти

#Область Подписи

// Возвращает данные подписи в виде строки в кодировке base64 без переносов.
// 
// Параметры:
// 	ДанныеПодписи - ДвоичныеДанные
// Возвращаемое значение:
// 	Строка
Функция ДанныеПодписиВСтрокуБезПереносов(ДанныеПодписи) Экспорт
	
	Возврат СтрЗаменить(Base64Строка(ДанныеПодписи), Символы.ВК + Символы.ПС, "");
	
КонецФункции

// Возвращает хеш-сумму подписи.
// 
// Параметры:
//  Подпись - ДвоичныеДанные
// 
// Возвращаемое значение:
//  Строка - хеш-сумма
Функция ХешПодписи(Подпись) Экспорт
	Возврат ОбщегоНазначения.КонтрольнаяСуммаСтрокой(Подпись, ХешФункция.MD5);
КонецФункции

// Возвращает свойства подписи соответствующие переданному хешу подписи.
// 
// Параметры:
//  СвойстваПодписей - Массив из см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
//  ХешПодписи - См. ХешПодписи
// 
// Возвращаемое значение:
//  Неопределено, См. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
Функция НайтиСвойстваПодписиПоХешу(СвойстваПодписей, ХешПодписи) Экспорт
	
	Для Каждого СвойстваПодписи Из СвойстваПодписей Цикл
		
		Если ХешПодписи(СвойстваПодписи.Подпись) = ХешПодписи Тогда
			Возврат СвойстваПодписи;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает установленные подписи объектов.
// 
// Параметры:
//  ПодписанныеОбъекты - ОпределяемыйТип.ПодписанныйОбъект
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - ОпределяемыйТип.ПодписанныйОбъект
//  * Значение - Массив из см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
//  
Функция УстановленныеПодписиОбъектов(ПодписанныеОбъекты) Экспорт
	
	СвойстваПодписейОбъектов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЭлектронныеПодписи.ПодписанныйОбъект КАК ПодписанныйОбъект,
		|	ЭлектронныеПодписи.Подпись КАК Подпись,
		|	ЭлектронныеПодписи.ПорядковыйНомер КАК ПорядковыйНомер,
		|	ЭлектронныеПодписи.УстановившийПодпись КАК УстановившийПодпись,
		|	ЭлектронныеПодписи.Комментарий КАК Комментарий,
		|	ЭлектронныеПодписи.ИмяФайлаПодписи КАК ИмяФайлаПодписи,
		|	ЭлектронныеПодписи.ДатаПодписи КАК ДатаПодписи,
		|	ЭлектронныеПодписи.ДатаПроверкиПодписи КАК ДатаПроверкиПодписи,
		|	ЭлектронныеПодписи.ПодписьВерна КАК ПодписьВерна,
		|	ЭлектронныеПодписи.Сертификат КАК Сертификат,
		|	ЭлектронныеПодписи.Отпечаток КАК Отпечаток,
		|	ЭлектронныеПодписи.КомуВыданСертификат КАК КомуВыданСертификат
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|ГДЕ
		|	ЭлектронныеПодписи.ПодписанныйОбъект В (&ПодписанныеОбъекты)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПодписанныйОбъект,
		|	ПорядковыйНомер";
	
	Запрос.УстановитьПараметр("ПодписанныеОбъекты", ПодписанныеОбъекты);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат СвойстваПодписейОбъектов;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СвойстваПодписей = СвойстваПодписейОбъектов[Выборка.ПодписанныйОбъект];
		Если СвойстваПодписей = Неопределено Тогда
			СвойстваПодписей = Новый Массив;
			СвойстваПодписейОбъектов.Вставить(Выборка.ПодписанныйОбъект, СвойстваПодписей);
		КонецЕсли;
		
		СвойстваПодписи = КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи();
		ЗаполнитьЗначенияСвойств(СвойстваПодписи, Выборка);
		СвойстваПодписи.Подпись = СвойстваПодписи.Подпись.Получить();
		СвойстваПодписи.Сертификат = СвойстваПодписи.Сертификат.Получить();
		СвойстваПодписей.Добавить(СвойстваПодписи);
		
	КонецЦикла;
	
	Возврат СвойстваПодписейОбъектов;
	
КонецФункции

// Обновляет подпись объекта.
// 
// Параметры:
//  Объект - ОпределяемыйТип.ПодписанныйОбъект - ссылка на подписанный объект,
//             для которого требуется обновить подпись.
//
//  СвойстваПодписи - Структура - см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
//
Процедура ОбновитьПодпись(Объект, Знач СвойстваПодписи) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПроверитьИзменениеРазрешено(Объект);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЭлектронныеПодписи");
	ЭлементБлокировки.УстановитьЗначение("ПодписанныйОбъект", Объект);
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		ПодписиОбъекта = ЭлектроннаяПодпись.УстановленныеПодписи(Объект);
		
		Для Каждого ПодписьОбъекта Из ПодписиОбъекта Цикл
			ДвоичныеДанныеПодписи = ПодписьОбъекта.Подпись;
			Если ПодписьОбъекта.ПорядковыйНомер = СвойстваПодписи.ПорядковыйНомер 
				// Если двоичные данные совпадают, то обновляем подпись.
				Или ДвоичныеДанныеПодписи = СвойстваПодписи.Подпись Тогда
				ОбновляемаяПодпись = РегистрыСведений.ЭлектронныеПодписи.СоздатьМенеджерЗаписи();
				ОбновляемаяПодпись.ПорядковыйНомер   = СвойстваПодписи.ПорядковыйНомер;
				ОбновляемаяПодпись.ПодписанныйОбъект = Объект;
				ОбновляемаяПодпись.Прочитать();
				Для Каждого КлючИЗначение Из СвойстваПодписи Цикл
					Если КлючИЗначение.Ключ = "Подпись"
						Или КлючИЗначение.Ключ = "ПодписанныйОбъект"
						Или КлючИЗначение.Ключ = "ДатаПодписи"
						Или КлючИЗначение.Ключ = "ПорядковыйНомер" Тогда
						Продолжить;
					КонецЕсли;
					Если КлючИЗначение.Значение <> Неопределено Тогда
						Если КлючИЗначение.Ключ = "Сертификат" Тогда
							ОбновляемаяПодпись[КлючИЗначение.Ключ] = Новый ХранилищеЗначения(КлючИЗначение.Значение);
						Иначе
							ОбновляемаяПодпись[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				ОбновляемаяПодпись.Записать(Истина);
			КонецЕсли;
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область Настройки

// Определяет, доступно ли выполнение криптоопераций на сервере.
// 
// Возвращаемое значение:
// 	Булево - если Истина - криптооперации доступны на сервере.
Функция КриптооперацииДоступныНаСервере() Экспорт
	
	Результат = Ложь;

	Если Не ЭлектроннаяПодпись.СоздаватьЭлектронныеПодписиНаСервере() Тогда
		Возврат Результат;
	КонецЕсли;
	
	МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("", Ложь);
	
	Результат = МенеджерКриптографии <> Неопределено;
	
	Возврат Результат;
	
КонецФункции

// Включает настройку "Использовать электронные подписи".
// 
Процедура ВключитьИспользованиеЭлектронныхПодписей() Экспорт
	
	Попытка
		Если ПравоДоступа("Изменение", Метаданные.Константы.ИспользоватьЭлектронныеПодписи) Тогда
			Константы.ИспользоватьЭлектронныеПодписи.Установить(Истина);
		Иначе
			ТекстСообщения = НСтр("ru = 'Для работы 1С-ЭДО необходимо включить использование электронных подписей. Обратитесь к администратору.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	Исключение
		ВидОперации = НСтр("ru = 'Включение использования электронных подписей'");
		ТекстСообщения = НСтр("ru = 'Не удалось включить использование электронных подписей.'");
		ТекстОшибки = ТекстСообщения + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействие.ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

// Возвращает возможность использования облачной электронной подписи.
// 
// Возвращаемое значение:
// 	Булево
Функция ИспользоватьЭлектроннуюПодписьВМоделиСервиса() Экспорт
	
	Возврат ИспользованиеЭлектроннойПодписиВМоделиСервисаВозможно()
		И КриптографияБЭДСлужебный.ИспользованиеСертификатовОблачногоСервисаВозможно();
	
КонецФункции

// Устанавливает настройки электронных подписей на сервере: "Создавать электронные подписи на сервере" и
// "Проверять электронные подписи на сервере".
// 
// Параметры:
// 	СоздаватьЭлектронныеПодписи - Булево - значение настройки, если не передано, значение не будет изменено.
// 	ПроверятьЭлектронныеПодписи - Булево - значение настройки, если не передано, значение не будет изменено.
Процедура УстановитьНастройкиСервернойКриптографии(СоздаватьЭлектронныеПодписи = Неопределено,
	ПроверятьЭлектронныеПодписи = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Если СоздаватьЭлектронныеПодписи <> Неопределено Тогда
		Константы.СоздаватьЭлектронныеПодписиНаСервере.Установить(СоздаватьЭлектронныеПодписи);
	ИначеЕсли ПроверятьЭлектронныеПодписи <> Неопределено Тогда 
		Константы.ПроверятьЭлектронныеПодписиНаСервере.Установить(ПроверятьЭлектронныеПодписи);
	КонецЕсли;
	ОбновитьПовторноИспользуемыеЗначения();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаНеисправностей

// Добавляет ошибку "Для сертификата нет подписываемого вида документов" в контекст диагностики.
// 
// Параметры:
// 	КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 	ВидОперации - Строка
// 	Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 	ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
Процедура ДобавитьОшибкуДляСертификатаНетПодписываемогоВидаДокумента(КонтекстДиагностики, ВидОперации, Сертификат, ВидДокумента) Экспорт
	
	ДополнительныеПараметрыОшибки = Новый Структура;
	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("Сертификат", Сертификат);
	ДополнительныеДанные.Вставить("ВидДокумента", ВидДокумента);
	ДополнительныеПараметрыОшибки.Вставить("ДополнительныеДанные", ДополнительныеДанные);
	
	ТекстОшибки = НСтр("ru = 'Для сертификата в списке ""Подписываемые виды документов"" нет данного вида документа.'");
	
	Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(ВидОперации,
		КриптографияБЭДСлужебныйКлиентСервер.ВидОшибкиДляСертификатаНетПодписываемогоВидаДокумента(),
		ТекстОшибки, ТекстОшибки, ДополнительныеПараметрыОшибки);
	ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
		ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().Криптография);
	
КонецПроцедуры

// Добавляет ошибку получения отпечатков сертификатов в контекст диагностики.
// 
// Параметры:
// 	КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 	ВидОперации - Строка 
// 	РезультатыПолученияОтпечатков - см. КриптографияБЭДКлиентСервер.НовыеРезультатыПолученияОтпечатков
Процедура ДобавитьОшибкуПолученияОтпечатковСертификатов(КонтекстДиагностики, ВидОперации,
	РезультатыПолученияОтпечатков) Экспорт
	
	ТекстОшибки = "";
	Если ЕстьОшибкаПолученияОтпечатков(РезультатыПолученияОтпечатков, ТекстОшибки) Тогда
		Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(ВидОперации,
			КриптографияБЭДКлиентСервер.ВидОшибкиКриптография(), ТекстОшибки,
			НСтр("ru = 'Ошибка при получении отпечатков сертификатов.'"));
		ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().Криптография);
	КонецЕсли;
	
КонецПроцедуры


// Возвращает вид ошибки, описывающий ситуацию, когда объект содержит невалидные подписи.
// 
// Возвращаемое значение:
// 	См. ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки
Функция ВидОшибкиПодписьНеверна() Экспорт
	
	ВидОшибки = КриптографияБЭДКлиентСервер.ВидОшибкиКриптография();
	ВидОшибки.ЗаголовокПроблемы = НСтр("ru = 'Не удалось обработать объект'");
	ВидОшибки.ОписаниеПроблемы = НСтр("ru = 'Объект содержит невалидные подписи'");
	ОписаниеРешения = НСтр("ru = '<a href = ""Выполните"">Выполните</a> диагностику криптографии. Если это не помогло решить проблему,
			|ознакомьтесь со статьями <a href = ""http://1c-edo.ru/handbook/24/2595/#1"">здесь</a> и <a href = ""https://its.1c.ru/db/metbud81#content:5784:hdoc"">здесь</a>.'");
	ВидОшибки.ОписаниеРешения = ОписаниеРешения;
	ВидОшибки.ОбработчикиНажатия.Вставить("Выполните", "ОбработкаНеисправностейБЭДКлиент.ОткрытьМастерДиагностики");
	
	Возврат ВидОшибки;
	
КонецФункции

#КонецОбласти

#Область Прочее

// Возвращает описания программ криптографии, добавленные в справочник ПрограммыЭлектроннойПодписиИШифрования.
// 
// Возвращаемое значение:
// 	Массив из см. ЭлектроннаяПодпись.НовоеОписаниеПрограммы
Функция ОписанияПрограммЭлектроннойПодписиИШифрования() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПрограммыЭлектроннойПодписиИШифрования.ИмяПрограммы КАК ИмяПрограммы,
		|	ПрограммыЭлектроннойПодписиИШифрования.ТипПрограммы КАК ТипПрограммы
		|ИЗ
		|	Справочник.ПрограммыЭлектроннойПодписиИШифрования КАК ПрограммыЭлектроннойПодписиИШифрования
		|ГДЕ
		|	ПрограммыЭлектроннойПодписиИШифрования.ИмяПрограммы <> """"
		|	И НЕ ПрограммыЭлектроннойПодписиИШифрования.ПометкаУдаления";
	
	НаборОписаний = Новый Массив;
	
	Для Каждого Программа Из Запрос.Выполнить().Выгрузить() Цикл
		
		Описание = ЭлектроннаяПодпись.НовоеОписаниеПрограммы();
		ЗаполнитьЗначенияСвойств(Описание, Программа);
		НаборОписаний.Добавить(Описание);
		
	КонецЦикла;
	
	Возврат НаборОписаний;
	
КонецФункции

// Возвращает данные для формирования штампа электронной подписи.
// 
// Возвращаемое значение:
//  Структура:
// * ИдентификаторДокумента - Строка
// * ЭтоИнформацияОтправителя - Булево
// * Организация - Неопределено
// * ЭтоИсходящийДокумент - Булево
// * ПодписиОтправителя - Массив -
// * ПодписиПолучателя - Массив -
// * ОсновноеСостояние - Строка - см. ОсновныеСостоянияДокументов
// * ДополнительноеСостояние - Строка - см. ДополнительныеСостоянияДокументов
// * ЕстьОтветнаяПодпись - Булево
Функция НовыеДанныеДляФормированияШтампа() Экспорт
	
	Данные = Новый Структура;
	Данные.Вставить("ИдентификаторДокумента", "");
	Данные.Вставить("ЭтоИнформацияОтправителя", Ложь);
	Данные.Вставить("Организация", Неопределено);
	Данные.Вставить("ЭтоИсходящийДокумент", Ложь);
	Данные.Вставить("ПодписиОтправителя", Новый Массив);
	Данные.Вставить("ПодписиПолучателя", Новый Массив);
	Данные.Вставить("ОсновноеСостояние", "");
	Данные.Вставить("ДополнительноеСостояние", "");
	Данные.Вставить("ЕстьОтветнаяПодпись", Ложь);
	
	Возврат Данные;
	
КонецФункции

// Возвращает табличный документ со штампом электронной подписи.
// 
// Параметры:
// 	ДанныеДляФормирования - см. НовыеДанныеДляФормированияШтампа
// Возвращаемое значение:
// 	ТабличныйДокумент
Функция ШтампЭлектроннойПодписи(ДанныеДляФормирования) Экспорт
	
	Если ДанныеДляФормирования.ЭтоИнформацияОтправителя Тогда
		
		ЭтоИсходящийДокумент = ДанныеДляФормирования.ЭтоИсходящийДокумент;
		Если ЭтоИсходящийДокумент
			И ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов().Подписан Тогда
			ТекстШтампа = НСтр("ru = 'Документ подписан в 1С-ЭДО'");
		ИначеЕсли ЭтоИсходящийДокумент
			И ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов().Отправлен Тогда 
			ТекстШтампа = НСтр("ru = 'Документ подписан и передан через 1С-ЭДО'");
		ИначеЕсли Не ЭтоИсходящийДокумент
			И ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов().Получен Тогда
			ТекстШтампа = НСтр("ru = 'Документ получен через 1С-ЭДО'");
		ИначеЕсли Не ЭтоИсходящийДокумент И ДанныеДляФормирования.ЕстьОтветнаяПодпись Тогда
			ТекстШтампа = НСтр("ru = 'Документ получен и подписан в 1С-ЭДО'");
		ИначеЕсли ЭтоИсходящийДокумент Тогда 
			ТекстШтампа = НСтр("ru = 'Документ подписан в 1С-ЭДО'");
		ИначеЕсли Не ЭтоИсходящийДокумент Тогда
			ТекстШтампа = НСтр("ru = 'Документ получен через 1С-ЭДО'");
		КонецЕсли;
	Иначе
		Если ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов().Подписан Тогда
			ТекстШтампа = НСтр("ru = 'Документ подписан в 1С-ЭДО'");
		ИначеЕсли ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов().Отправлен Тогда
			ТекстШтампа = НСтр("ru = 'Документ подписан и передан через 1С-ЭДО'");
		ИначеЕсли ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов().Получен Тогда
			ТекстШтампа = НСтр("ru = 'Документ получен через 1С-ЭДО'");
		Иначе
			ТекстШтампа = НСтр("ru = 'Документ подписан в 1С-ЭДО'");
		КонецЕсли;
		
	КонецЕсли;
	
	ВсеПодписи = Новый Массив;
	Для Каждого Подпись Из ДанныеДляФормирования.ПодписиОтправителя Цикл
		Подпись.Вставить("ПредставлениеПодписи", НСтр("ru = 'Отправитель'"));
		ВсеПодписи.Добавить(Подпись);
	КонецЦикла;
	Для Каждого Подпись Из ДанныеДляФормирования.ПодписиПолучателя Цикл
		Подпись.Вставить("ПредставлениеПодписи", НСтр("ru = 'Получатель'"));
		ВсеПодписи.Добавить(Подпись);
	КонецЦикла;
		
	Штамп = Новый ТабличныйДокумент;
	Если Не ЗначениеЗаполнено(ВсеПодписи) Тогда
		Возврат Штамп;
	КонецЕсли;
	
	СвойстваПодписи = ВсеПодписи[0].СвойстваПодписи;
	
	ВидПодписи = СвойстваПодписи.ВидПодписи;
	
	Если ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.Простая Тогда
		ТекстШтампа = НСтр("ru = 'Документ подписан простой электронной подписью'");
	КонецЕсли;
	
	ИмяМакета = ?(ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.Простая, "ШтампПЭП_%1", "ШтампЭП_%1");

	МакетЭП = Обработки.КриптографияБЭД.ПолучитьМакет(
		СтрШаблон(ИмяМакета, ОбщегоНазначения.КодОсновногоЯзыка()));
	МакетЭП.КодЯзыка = Метаданные.Языки.Русский.КодЯзыка;

	КоличествоПодписей = ВсеПодписи.Количество();

	ОбластьШапка = МакетЭП.ПолучитьОбласть("ШтампТабличныйШапка|ОбластьШтампТабличный");
	ЗначенияПараметров = Новый Структура;
	
	ДополнительноеСостояние = ДанныеДляФормирования.ДополнительноеСостояние;
	ДобавкаТекста = "";
	Если ДополнительноеСостояние = ДополнительныеСостоянияДокументов().ВПроцессеАннулирования Тогда
		ДобавкаТекста = НСтр("ru = ', в процессе аннулирования.'");
	ИначеЕсли ДополнительноеСостояние = ДополнительныеСостоянияДокументов().Аннулирован Тогда
		ДобавкаТекста = НСтр("ru = ', аннулирован.'");
	ИначеЕсли ДополнительноеСостояние = ДополнительныеСостоянияДокументов().Отклонен Тогда 
		ДобавкаТекста = НСтр("ru = ', отклонен.'");
	КонецЕсли;
	ТекстШтампа = ТекстШтампа + ДобавкаТекста;
	ЗначенияПараметров.Вставить("ДокументПодписан", ТекстШтампа);
	ОбластьШапка.Параметры.Заполнить(ЗначенияПараметров);
	ОбщегоНазначенияБЭД.ВывестиОбластьВТабличныйДокумент(Штамп, ОбластьШапка, "ШтампТабличныйШапка");

	Разделитель = МакетЭП.ПолучитьОбласть("ШтампТабличныйРазделитель|ОбластьШтампТабличный");

	Счетчик = 0;
	
	Для Каждого Подпись Из ВсеПодписи Цикл
		
		СвойстваПодписи = Подпись.СвойстваПодписи;
		
		Счетчик = Счетчик + 1;

		ОбластьШтамп = МакетЭП.ПолучитьОбласть("ШтампТабличныйСтрока|ОбластьШтампТабличный");
		
		ПодписьВерна = СвойстваПодписи.ПодписьВерна;
		Если Подпись.ЭтоПодписьПоДоверенности Тогда
			ПодписьВерна = ПодписьВерна 
							И Подпись.РезультатПроверкиПоМЧД <> Неопределено
							И Подпись.РезультатПроверкиПоМЧД.ПодписьВерна;
		КонецЕсли;
		
		Если Не ПодписьВерна
			Или (Подпись.ЭтоПодписьПоДоверенности И ЕстьОшибкаВДоверенности(Подпись.РезультатПроверкиПоМЧД)) Тогда
			
			Ячейки = ОбластьШтамп.НайтиТекст("[ДатаПодписи]");
			Если Ячейки <> Неопределено Тогда
				Ячейки.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
			КонецЕсли;	
			
			Ячейки = ОбластьШтамп.НайтиТекст("[НомерСертификата]");
			Если Ячейки <> Неопределено Тогда
				Ячейки.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
			КонецЕсли;	
			
			Ячейки = ОбластьШтамп.НайтиТекст("ОрганизацияПодписант");
			Если Ячейки <> Неопределено Тогда
				Ячейки.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
			КонецЕсли;	
			
		КонецЕсли;	
		
		Если СвойстваПодписи.ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.Простая Тогда
			ЗаполнитьОбластьШтампаПЭП(ОбластьШтамп, Подпись, ДанныеДляФормирования.Организация);
		Иначе
			ЗаполнитьОбластьШтампа(ОбластьШтамп, Подпись);
		КонецЕсли;

		ОбщегоНазначенияБЭД.ВывестиОбластьВТабличныйДокумент(Штамп, ОбластьШтамп,
			"ШтампТабличныйСтрока");

		Если Счетчик <> КоличествоПодписей Тогда
			ОбщегоНазначенияБЭД.ВывестиОбластьВТабличныйДокумент(Штамп, Разделитель,
				"ШтампТабличныйРазделитель");
		КонецЕсли;

	КонецЦикла;

	ОбластьПодвал = МакетЭП.ПолучитьОбласть("ШтампТабличныйПодвал|ОбластьШтампТабличный");
	ОбластьПодвал.Параметры.ИдентификаторДокумента = СтрШаблон(НСтр("ru = 'Идентификатор документа: %1'"),
		ДанныеДляФормирования.ИдентификаторДокумента);
	ОбщегоНазначенияБЭД.ВывестиОбластьВТабличныйДокумент(Штамп, ОбластьПодвал,
		"ШтампТабличныйПодвал");

	Штамп.Область().СоздатьФорматСтрок();
	
	Возврат Штамп;
	
КонецФункции

// Возвращает картинку со штампом электронной подписи.
// 
// Параметры:
// 	ДанныеДляФормирования - см. НовыеДанныеДляФормированияШтампа
// Возвращаемое значение:
// 	Картинка
Функция ШтампЭлектроннойПодписиКартинкой(ДанныеДляФормирования) Экспорт
	
	ОсновныеСостоянияДокументов = ОсновныеСостоянияДокументов();
	
	Если ДанныеДляФормирования.ЭтоИнформацияОтправителя Тогда
		
		ЭтоИсходящийДокумент = ДанныеДляФормирования.ЭтоИсходящийДокумент;		
		
		Если ЭтоИсходящийДокумент
			И ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов.Подписан Тогда
			ТекстШтампа = НСтр("ru = 'Документ подписан в 1С-ЭДО'");
		ИначеЕсли ЭтоИсходящийДокумент
			И ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов.Отправлен Тогда 
			ТекстШтампа = НСтр("ru = 'Документ подписан и передан через 1С-ЭДО'");
		ИначеЕсли Не ЭтоИсходящийДокумент
			И ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов.Получен Тогда
			ТекстШтампа = НСтр("ru = 'Документ получен через 1С-ЭДО'");
		ИначеЕсли Не ЭтоИсходящийДокумент И ДанныеДляФормирования.ЕстьОтветнаяПодпись Тогда
			ТекстШтампа = НСтр("ru = 'Документ получен и подписан в 1С-ЭДО'");
		ИначеЕсли ЭтоИсходящийДокумент Тогда 
			ТекстШтампа = НСтр("ru = 'Документ подписан в 1С-ЭДО'");
		ИначеЕсли Не ЭтоИсходящийДокумент Тогда
			ТекстШтампа = НСтр("ru = 'Документ получен через 1С-ЭДО'");
		КонецЕсли;
	Иначе
		Если ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов.Подписан Тогда
			ТекстШтампа = НСтр("ru = 'Документ подписан в 1С-ЭДО'");
		ИначеЕсли ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов.Отправлен Тогда
			ТекстШтампа = НСтр("ru = 'Документ подписан и передан через 1С-ЭДО'");
		ИначеЕсли ДанныеДляФормирования.ОсновноеСостояние = ОсновныеСостоянияДокументов.Получен Тогда
			ТекстШтампа = НСтр("ru = 'Документ получен через 1С-ЭДО'");
		Иначе
			ТекстШтампа = НСтр("ru = 'Документ подписан в 1С-ЭДО'");
		КонецЕсли;
		
	КонецЕсли;
	
	ВсеПодписи = Новый Массив;
	Для Каждого Подпись Из ДанныеДляФормирования.ПодписиОтправителя Цикл
		Подпись.Вставить("ПредставлениеПодписи", НСтр("ru = 'Отправитель'"));
		ВсеПодписи.Добавить(Подпись);
	КонецЦикла;
	Для Каждого Подпись Из ДанныеДляФормирования.ПодписиПолучателя Цикл
		Подпись.Вставить("ПредставлениеПодписи", НСтр("ru = 'Получатель'"));
		ВсеПодписи.Добавить(Подпись);
	КонецЦикла;
	
	Штамп = Новый Картинка;
	Если Не ЗначениеЗаполнено(ВсеПодписи) Тогда
		Возврат Штамп;
	КонецЕсли;
	
	СвойстваПодписи = ВсеПодписи[0].СвойстваПодписи;
	
	ВидПодписи = СвойстваПодписи.ВидПодписи;
	
	Если ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.Простая Тогда
		ТекстШтампа = НСтр("ru = 'Документ подписан простой электронной подписью'");
	КонецЕсли;
	
	ДополнительныеСостоянияДокументов = ДополнительныеСостоянияДокументов();
	ДополнительноеСостояние = ДанныеДляФормирования.ДополнительноеСостояние;
	ДобавкаТекста = "";
	Если ДополнительноеСостояние = ДополнительныеСостоянияДокументов.ВПроцессеАннулирования Тогда
		ДобавкаТекста = НСтр("ru = ', в процессе аннулирования.'");
	ИначеЕсли ДополнительноеСостояние = ДополнительныеСостоянияДокументов.Аннулирован Тогда
		ДобавкаТекста = НСтр("ru = ', аннулирован.'");
	ИначеЕсли ДополнительноеСостояние = ДополнительныеСостоянияДокументов.Отклонен Тогда 
		ДобавкаТекста = НСтр("ru = ', отклонен.'");
	КонецЕсли;
	ТекстШтампа = ТекстШтампа + ДобавкаТекста;
	
	Если ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.Простая Тогда
		ТекстШтампа = ТекстШтампаПЭП(ДанныеДляФормирования, ВсеПодписи, ТекстШтампа);
	Иначе
		ТекстШтампа = ТекстШтампаЭП(ДанныеДляФормирования, ВсеПодписи, ТекстШтампа);
	КонецЕсли;	
	
	Поток = Новый ПотокВПамяти();
	ЗаписьТекста = Новый ЗаписьТекста(Поток, "utf-8");
	ЗаписьТекста.Записать(ТекстШтампа);
	ЗаписьТекста.Закрыть();
	
	ДанныеФайла = Поток.ЗакрытьИПолучитьДвоичныеДанные();
	ШтампSvg = Новый Картинка(ДанныеФайла);
	ШтампPng = ШтампSvg.Преобразовать(ФорматКартинки.PNG);
	
	ДанныеФайлаPng = ШтампPng.ПолучитьДвоичныеДанные();
	Штамп = Новый Картинка(ДанныеФайлаPng, Истина);
		
	Возврат Штамп;
	
КонецФункции

// Возвращает основные состояния документов для вывода в штамп электронной подписи.
// 
// Возвращаемое значение:
//  Структура:
// * Подписан - Строка
// * Отправлен - Строка
// * Получен - Строка
Функция ОсновныеСостоянияДокументов() Экспорт
	
	Состояния = Новый Структура;
	Состояния.Вставить("Подписан", "Подписан");
	Состояния.Вставить("Отправлен", "Отправлен");
	Состояния.Вставить("Получен", "Получен");
	
	Возврат Состояния;
	
КонецФункции

// Возвращает дополнительные состояния документов для вывода в штамп электронной подписи.
// 
// Возвращаемое значение:
//  Структура:
// * ВПроцессеАннулирования - Строка
// * Аннулирован - Строка
// * Отклонен - Строка
Функция ДополнительныеСостоянияДокументов() Экспорт
	
	Состояния = Новый Структура;
	Состояния.Вставить("ВПроцессеАннулирования", "ВПроцессеАннулирования");
	Состояния.Вставить("Аннулирован", "Аннулирован");
	Состояния.Вставить("Отклонен", "Отклонен");
	
	Возврат Состояния;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗапросСертификатов(ИмяВременнойТаблицы, ВыбираемыеПоля, ПоляУсловия,
	ТолькоНепомеченныеНаУдаление = Истина, ТолькоДействительные = Истина, ОтборПоПользователям = Неопределено) 
	
	Если ПоляУсловия = Неопределено Тогда
		ПоляУсловия = Новый Массив;
	КонецЕсли;
	Если ТолькоНепомеченныеНаУдаление Тогда
		ПоляУсловия.Добавить("НЕ Сертификаты.ПометкаУдаления");
	КонецЕсли;
	Если ТолькоДействительные Тогда
		ПоляУсловия.Добавить("НЕ Сертификаты.Отозван");
	КонецЕсли;
	
	ТекстыЗапросов = Новый Массив;
	Если ОтборПоПользователям <> Неопределено
		И (ОтборПоПользователям.ДоступныеТекущемуПользователю <> Неопределено
			Или ЗначениеЗаполнено(ОтборПоПользователям.ДополнительныеПользователиДляОтбора)) Тогда
		ТекстЗапросаПоПользователям = 
		"ВЫБРАТЬ
		|	Сертификаты.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ СертификатыПоПользователям
		|ИЗ
		|	Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК Сертификаты
		|ГДЕ
		|	&УсловиеПоПользователям
		|	И НЕ Сертификаты.Пользователи.НомерСтроки > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Сертификаты.Ссылка
		|ИЗ
		|	Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.Пользователи КАК Сертификаты
		|ГДЕ
		|	&УсловиеПоПользователям";
		Условие = ?(ОтборПоПользователям.ДоступныеТекущемуПользователю <> Ложь,
			"Сертификаты.Пользователь В (&ДоступенПользователям)",
			"НЕ Сертификаты.Пользователь  В (&ДоступенПользователям)");
		ТекстЗапросаПоПользователям = СтрЗаменить(ТекстЗапросаПоПользователям, "&УсловиеПоПользователям", Условие);
		ТекстыЗапросов.Добавить(ТекстЗапросаПоПользователям);
		ПоляУсловия.Добавить("Ссылка В (ВЫБРАТЬ Ссылка ИЗ СертификатыПоПользователям)");
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ВыбираемыеПоля
	|ПОМЕСТИТЬ ИмяВременнойТаблицы
	|ИЗ
	|	Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК Сертификаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаявленияНаВыпускСертификата КАК ЗаявленияНаВыпускСертификата
	|		ПО Сертификаты.Ссылка = ЗаявленияНаВыпускСертификата.Сертификат
	|ГДЕ
	|	&ПоляУсловия";
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
	ИтоговыйТекстЗапроса = СтрСоединить(ТекстыЗапросов, Символы.ПС + ОбщегоНазначения.РазделительПакетаЗапросов());
	
	ИтоговыйТекстЗапроса = ОбщегоНазначенияБЭД.ТекстЗапросаИзШаблона(ИтоговыйТекстЗапроса, ИмяВременнойТаблицы, 
		ВыбираемыеПоля, ПоляУсловия, "Сертификаты");
	
	ОписаниеЗапроса = ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса();
	ОписаниеЗапроса.Текст = ИтоговыйТекстЗапроса;
	
	ОписаниеЗапроса.СлужебныеПараметры.Вставить("ТекущаяДата", ТекущаяДатаСеанса());
	Если ОтборПоПользователям <> Неопределено Тогда
		ОписаниеЗапроса.СлужебныеПараметры.Вставить("ДоступенПользователям", ОтборПоПользователям.Пользователи);
	КонецЕсли;
	
	Возврат ОписаниеЗапроса;
	
КонецФункции

// Возвращает текст поля запроса, в котором вычисляется действительность сертификата.
// 
// Возвращаемое значение:
// 	Строка
Функция ТекстПоляСертификатДействителен() 
	
	Возврат "ВЫБОР
	|			КОГДА Сертификаты.ДействителенДо = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			КОГДА РАЗНОСТЬДАТ(&ТекущаяДата, Сертификаты.ДействителенДо, МИНУТА) > 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ";
	
КонецФункции 

Функция ПользователиДляОтбораСертификатов(ДобавлятьТекущегоПользователя = Истина)
	
	ПользователиДляОтбора = Новый Массив;
	Если ДобавлятьТекущегоПользователя Тогда
		ПользователиДляОтбора.Добавить(Пользователи.АвторизованныйПользователь());
	КонецЕсли;
	ПользователиДляОтбора.Добавить(Пользователи.СсылкаНеуказанногоПользователя());
	ПользователиДляОтбора.Добавить(Справочники.Пользователи.ПустаяСсылка());
	
	Возврат ПользователиДляОтбора;
	
КонецФункции

Функция СертификатПоОтпечатку(МенеджерКриптографии, Отпечаток)
	
	ДвоичныеДанныеОтпечатка = Base64Значение(Отпечаток);
	
	ХранилищеСертификатовКриптографии = МенеджерКриптографии.ПолучитьХранилищеСертификатов(
		ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
	
	Сертификат = ХранилищеСертификатовКриптографии.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
	
	Возврат Сертификат;
	
КонецФункции

Функция ЭтоСертификатОблачногоСервиса(Сертификат)
	
	Сертификаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сертификат);
	Возврат ЭтоСертификатыОблачногоСервиса(Сертификаты)[Сертификат];
	
КонецФункции

Функция ПарольСертификата(Сертификат)
	
	Если Сертификат <> Неопределено Тогда
		
		Данные = КриптографияБЭДСлужебный.ДанныеПароляСертификата(Сертификат);
		
		Если ТипЗнч(Данные) <> Тип("Соответствие") Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Пользователь = Пользователи.ТекущийПользователь();
		Пароль = Данные.Получить(Пользователь);
		
		Если ЗначениеЗаполнено(Пароль) Тогда
			Возврат Пароль;
		КонецЕсли;
		
		Пользователь = Справочники.Пользователи.ПустаяСсылка();
		Пароль = Данные.Получить(Пользователь);
		
		Если ЗначениеЗаполнено(Пароль) Тогда
			Возврат Пароль;
		КонецЕсли;
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Заполняет область штампа ЭД.
// 
// Параметры:
//  Область - ОбластьЯчеекТабличногоДокумента
//  Подпись - см. ЭлектронныеДокументыЭДО.НовыеДанныеПодписиСУчетомДоверенности
Процедура ЗаполнитьОбластьШтампа(Область, Подпись)
	
	ЗначенияПараметров = ПараметрыДляЗаполненияШтампа(Подпись);
	Область.Параметры.Заполнить(ЗначенияПараметров);
	
КонецПроцедуры

// Возвращает параметры для заполнения штампа.
// 
// Параметры:
// 	Подпись - см. НовыеДанныеДляФормированияШтампа
// Возвращаемое значение:
// 	Структура:
// * ВладелецСертификата - Строка
// * ПредставлениеПодписи - Строка
// * ДатаПодписи - Строка
// * ОрганизацияПодписант - Строка
// * НомерСертификата - Строка
// * ВыдалСертификат - Строка
// * СрокДействия - Строка
// * ТекстОтметки - Строка
Функция ПараметрыДляЗаполненияШтампа(Подпись)
	
	СвойстваПодписи = Подпись.СвойстваПодписи; 
	СвойстваДоверенности = Подпись.СвойстваДоверенности;
	ДоверенностьОпределена = СвойстваДоверенности <> Неопределено;
	
	ПодписьВерна = ПодписьВерна(Подпись);
		
	Сертификат = Неопределено;	
	ДанныеСертификата = СвойстваПодписи.Сертификат.Получить();
	Если ДанныеСертификата <> Неопределено Тогда
		Сертификат = Новый СертификатКриптографии(ДанныеСертификата);
	КонецЕсли;
	
	ОтметкаВерна   = НСтр("ru='Подпись верна'");
	ОтметкаНеВерна = НСтр("ru='Подпись неверна'");
	ТекстОтметки = Символы.ПС + ?(ПодписьВерна, ОтметкаВерна, ОтметкаНеВерна);
	ПредставлениеПодписи = ?(Подпись.ПредставлениеПодписи = "Отправитель",
		НСтр("ru='Отправитель'"), НСтр("ru='Получатель'"));  
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ВладелецСертификата",  СвойстваПодписи.Владелец);
	ЗначенияПараметров.Вставить("ПредставлениеПодписи", ПредставлениеПодписи);
	ЗначенияПараметров.Вставить("ДатаПодписи",          Формат(СвойстваПодписи.ДатаПодписи, "ДЛФ=D;"));
	ЗначенияПараметров.Вставить("ОрганизацияПодписант", "");
	ЗначенияПараметров.Вставить("НомерСертификата",     "");
	ЗначенияПараметров.Вставить("ВыдалСертификат",      "");
	ЗначенияПараметров.Вставить("СрокДействия",         "");
	Если Сертификат <> Неопределено Тогда
		Шаблон = НСтр("ru='с %1 по %2'");
		ЛокализованныйФорматДатаВремя = НСтр("ru='ДФ=''dd.MM.yyyy HH:mm'''");
		СрокДействия = СтрШаблон(Шаблон, Формат(Сертификат.ДатаНачала, ЛокализованныйФорматДатаВремя),
			Формат(Сертификат.ДатаОкончания, ЛокализованныйФорматДатаВремя));
			
			Если Подпись.ЭтоПодписьПоДоверенности Тогда  
				Если ДоверенностьОпределена Тогда
					
					ДоверенностьДействует = Не ЕстьОшибкаВДоверенности(Подпись.РезультатПроверкиПоМЧД);
					
					ОрганизацияПодписант = СвойстваДоверенности.Доверитель
					+ Символы.ПС 
					+ ПредставлениеВладельцаСертификата(Сертификат, СвойстваПодписи.Владелец) 
					+ Символы.ПС 
					+ НСтр("ru='По доверенности:'") 
					+ Символы.ПС 
					+ СвойстваДоверенности.НомерДоверенности;
					
					ТекстОтметки = ТекстОтметки 
					+ Символы.ПС
					+ ?(ДоверенностьДействует, НСтр("ru='Доверенность действительна'"),
													НСтр("ru='Доверенность недействительна'"));
				Иначе
					ОрганизацияПодписант = Сертификат.Субъект.CN
					+ Символы.ПС 
					+ ПредставлениеВладельцаСертификата(Сертификат, СвойстваПодписи.Владелец) 
					+ Символы.ПС 
					+ НСтр("ru='Доверенность не проверена'");
					
					ТекстОтметки = ТекстОтметки 
					+ Символы.ПС 
					+ НСтр("ru='Доверенность не найдена'");
					
				КонецЕсли;
			Иначе
				ОрганизацияПодписант = Сертификат.Субъект.CN
				+ Символы.ПС + ПредставлениеВладельцаСертификата(Сертификат, СвойстваПодписи.Владелец);	
			КонецЕсли;
			
		ЗначенияПараметров.Вставить("ОрганизацияПодписант", ОрганизацияПодписант);
		ЗначенияПараметров.Вставить("НомерСертификата",     Сертификат.СерийныйНомер);
		ЗначенияПараметров.Вставить("ВыдалСертификат",      Сертификат.Издатель.CN);
		ЗначенияПараметров.Вставить("СрокДействия",         СрокДействия);
	КонецЕсли;
	
	ЗначенияПараметров.Вставить("ТекстОтметки",         ТекстОтметки);
	
	Возврат ЗначенияПараметров;
	
КонецФункции	

Функция ПредставлениеВладельцаСертификата(Сертификат, ВладелецПодписи)
	
	Субъект = ЭлектроннаяПодпись.СвойстваСубъектаСертификата(Сертификат);
	ВладелецСертификата = "";
	Если ЗначениеЗаполнено(Субъект.Фамилия) И ЗначениеЗаполнено(Субъект.Имя) Тогда
		ВладелецСертификата = Субъект.Фамилия + " " + Субъект.Имя;
	ИначеЕсли ЗначениеЗаполнено(Субъект.Фамилия) Тогда
		ВладелецСертификата = Субъект.Фамилия;
	ИначеЕсли ЗначениеЗаполнено(Субъект.Имя) Тогда
		ВладелецСертификата = Субъект.Имя;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Субъект.Отчество) Тогда
		ВладелецСертификата = ВладелецСертификата + " " + Субъект.Отчество;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВладелецСертификата) И ЗначениеЗаполнено(Субъект.Должность)Тогда
		ВладелецСертификата = ВладелецСертификата + ", " + Субъект.Должность;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ВладелецСертификата) Тогда
		ВладелецСертификата = ВладелецПодписи;
	КонецЕсли;
	
	Возврат ВладелецСертификата;
	
КонецФункции

// Заполняет область штампа ЭД.
// 
// Параметры:
//  Область - ОбластьЯчеекТабличногоДокумента
//  Подпись - см. ЭлектронныеДокументыЭДО.НовыеДанныеПодписиСУчетомДоверенности
//  Организация - ОпределяемыйТип.Организация
Процедура ЗаполнитьОбластьШтампаПЭП(Область, Подпись, Организация)
	
	ЗначенияПараметров = ПараметрыДляЗаполненияШтампаПЭП(Подпись, Организация);
	Область.Параметры.Заполнить(ЗначенияПараметров);
	
КонецПроцедуры

// Возвращает параметры для заполнения штампа простой электронной подписи.
// 
// Параметры:
//  Подпись - см. ЭлектронныеДокументыЭДО.НовыеДанныеПодписиСУчетомДоверенности
//  Организация - ОпределяемыйТип.Организация
// Возвращаемое значение:
// 	Структура:
// * Организация - Строка
// * ДатаПодписи - Строка
// * Подписант - Строка
// * Должность - Строка
// * ИмяПользователя - Строка
// * ТекстОтметки - Строка
// * ФИОПодписанта - Строка
Функция ПараметрыДляЗаполненияШтампаПЭП(Подпись, Организация)
	
	СвойстваПодписи = Подпись.СвойстваПодписи;
	
	Пользователь = СвойстваПодписи.УстановившийПодпись;
	
	ИмяРеквизитаНаименованиеОрганизации =
		ЭлектронноеВзаимодействие.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("НаименованиеОрганизации");
	НаименованиеОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, ИмяРеквизитаНаименованиеОрганизации);
	
	ТекстОтметки = НСтр("ru='Подпись верна'");

	ИдентификаторПользователяИБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "ИдентификаторПользователяИБ");
	
	УстановитьПривилегированныйРежим(Истина);
	СвойстваПользователяИБ = Пользователи.СвойстваПользователяИБ(ИдентификаторПользователяИБ);
	УстановитьПривилегированныйРежим(Ложь);

	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("Организация",     НаименованиеОрганизации);
	ЗначенияПараметров.Вставить("ДатаПодписи",     Формат(СвойстваПодписи.ДатаПодписи, "ДЛФ=D;"));
	ЗначенияПараметров.Вставить("Подписант",       СвойстваПодписи.Владелец);
	ЗначенияПараметров.Вставить("Должность",       СвойстваПодписи.Должность);
	ЗначенияПараметров.Вставить("ИмяПользователя", СвойстваПользователяИБ.Имя);
	ЗначенияПараметров.Вставить("ТекстОтметки",    ТекстОтметки);
	МодульФизическиеЛицаКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ФизическиеЛицаКлиентСервер");
	Если МодульФизическиеЛицаКлиентСервер = Неопределено Тогда
		ЗначенияПараметров.Вставить("ФИОПодписанта", СвойстваПодписи.Владелец);
	Иначе
		ЗначенияПараметров.Вставить("ФИОПодписанта",
	МодульФизическиеЛицаКлиентСервер.ФамилияИнициалы(СвойстваПодписи.Владелец));
	КонецЕсли;
	
	Возврат ЗначенияПараметров;
	
КонецФункции	

// Определяет верность подписи.
// 
// Параметры:
//  Подпись - см. МашиночитаемыеДоверенности.НовыеДанныеПодписиСУчетомДоверенности
// 
// Возвращаемое значение:
//  Булево
Функция ПодписьВерна(Подпись)
	
	СвойстваПодписи = Подпись.СвойстваПодписи;
	ПодписьВерна = СвойстваПодписи.ПодписьВерна;
	Если Подпись.ЭтоПодписьПоДоверенности Тогда
		ПодписьВерна = ПодписьВерна 
						И Подпись.РезультатПроверкиПоМЧД <> Неопределено
						И Подпись.РезультатПроверкиПоМЧД.ПодписьВерна;
	КонецЕсли;
	
	Возврат ПодписьВерна;
	
КонецФункции	

Функция ИспользованиеЭлектроннойПодписиВМоделиСервисаВозможно()
	
	Результат = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса.ЭлектроннаяПодписьВМоделиСервиса") Тогда
		МодульЭлектроннаяПодписьВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("ЭлектроннаяПодписьВМоделиСервиса");
		Если МодульЭлектроннаяПодписьВМоделиСервиса.ИспользованиеВозможно() Тогда
			Результат = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТекстПоляПрограммаОблачногоСервиса()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЭлектроннаяПодписьСервисаDSS") Тогда
		Возврат "Программа ССЫЛКА Справочник.УчетныеЗаписиDSS";
	Иначе
		Возврат "ЛОЖЬ";
	КонецЕсли;
	
КонецФункции

// Определяет наличие ошибки в МЧД.
// 
// Параметры:
//  РезультатПроверкиПоМЧД - см. МашиночитаемыеДоверенности.НовыйРезультатПроверкиПодписи
// 
// Возвращаемое значение:
//  Булево
Функция ЕстьОшибкаВДоверенности(РезультатПроверкиПоМЧД)
	
	Возврат РезультатПроверкиПоМЧД <> Неопределено
		И РезультатПроверкиПоМЧД.Свойство("ПротоколПроверки")
		И РезультатПроверкиПоМЧД.ПротоколПроверки <> Неопределено
		И Не МашиночитаемыеДоверенности.ДоверенностьПроверенаУспешно(РезультатПроверкиПоМЧД.ПротоколПроверки.ПроверкаМЧД);
	
КонецФункции

// Возвращает строку в формате svg, содержащую штамп.
// 
// Параметры:
// 	ДанныеДляФормирования - см. НовыеДанныеДляФормированияШтампа
// 	Подписи - Массив из см. НовыеДанныеПодписиСУчетомДоверенности
// 	ТекстШтампа - Строка
// Возвращаемое значение:
// 	Строка
Функция ТекстШтампаЭП(ДанныеДляФормирования, Подписи, ТекстШтампа)
	
	МакетЭП = Обработки.КриптографияБЭД.ПолучитьМакет(
		СтрШаблон("ШтампЭП_svg_%1", ОбщегоНазначения.КодОсновногоЯзыка()));
	МакетЭП.КодЯзыкаМакета = Метаданные.Языки.Русский.КодЯзыка;		
	
	ТекстЭП = Новый ТекстовыйДокумент;
	
	// Шапка.
	СтрокаЭП = МакетЭП.ПолучитьСтроку(1);	
	
	ТекстЭП.ДобавитьСтроку(СтрокаЭП);	
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(2));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(3));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(4));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(5));
	
	СтрокаЭП = МакетЭП.ПолучитьСтроку(6);	
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ДокументПодписан%", ТекстШтампа);	
	ТекстЭП.ДобавитьСтроку(СтрокаЭП);	
	
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(7));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(8));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(9));	
	
	// Таблица.	
	Счетчик = 0;
	ИтоговоеСмещениеПоКоличествуСтрок = 0;
	СинийЦвет = "rgb(10, 100, 220);";
	КрасныйЦвет = "rgb(255, 60, 10);";
	КоличествоПодписей = Подписи.Количество();
	
	Для Каждого Подпись Из Подписи Цикл
		
		ПодписьВерна = ПодписьВерна(Подпись);
		
		ЦветТекста = СинийЦвет;		
		Если Не ПодписьВерна
			Или (Подпись.ЭтоПодписьПоДоверенности И ЕстьОшибкаВДоверенности(Подпись.РезультатПроверкиПоМЧД)) Тогда			
			ЦветТекста = КрасныйЦвет;			
		КонецЕсли;
		
		ПараметрыДляЗаполненияШтампа = ПараметрыДляЗаполненияШтампа(Подпись);
		ОрганизацияПодписант = ПараметрыДляЗаполненияШтампа.ОрганизацияПодписант;
		ОрганизацияПодписант = СформироватьТекстИзСтрокОграниченнойДлины(ОрганизацияПодписант, 35);
		ОрганизацияПодписант = СтрРазделить(ОрганизацияПодписант, Символы.ПС);
		
		СмещениеПоКоличествуСтрок = 0;
		Если ОрганизацияПодписант.Количество() > 2 Тогда
		    СмещениеПоКоличествуСтрок = 10 * (ОрганизацияПодписант.Количество() - 2);			
		КонецЕсли;
		
		НачальноеСмещенияПоВертикали = 40 * Счетчик + ИтоговоеСмещениеПоКоличествуСтрок;
		КонечноеСмещенияПоВертикали = НачальноеСмещенияПоВертикали + СмещениеПоКоличествуСтрок; 
		
		ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(10));
		
		// Левая граница.
		СтрокаЭП = МакетЭП.ПолучитьСтроку(11);		
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", Формат(НачальноеСмещенияПоВертикали + 50, "ЧРД=.; ЧГ=0"));
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", Формат(КонечноеСмещенияПоВертикали + 90, "ЧРД=.; ЧГ=0"));
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		// Центр.
		СтрокаЭП = МакетЭП.ПолучитьСтроку(12);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY%", Формат(НачальноеСмещенияПоВертикали + 60, "ЧРД=.; ЧГ=0"));
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПредставлениеПодписи%", ПараметрыДляЗаполненияШтампа.ПредставлениеПодписи);
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(13);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY%", Формат(НачальноеСмещенияПоВертикали + 60, "ЧРД=.; ЧГ=0"));
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ЦветТекста%", ЦветТекста);
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		Если ОрганизацияПодписант.Количество() > 0 Тогда
			
			СтрокаЭП = МакетЭП.ПолучитьСтроку(14);
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ОрганизацияПодписант%",
				СтрокаСЗамененнымиСимволамиXML(ОрганизацияПодписант[0]));
			ТекстЭП.ДобавитьСтроку(СтрокаЭП);
			
			Если ОрганизацияПодписант.Количество() > 1 Тогда
			    Для Индекс = 1 По ОрганизацияПодписант.Количество() - 1 Цикл
				    СтрокаЭП = МакетЭП.ПолучитьСтроку(15);
					СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ОрганизацияПодписант%",
						СтрокаСЗамененнымиСимволамиXML(ОрганизацияПодписант[Индекс]));
					ТекстЭП.ДобавитьСтроку(СтрокаЭП);
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;	
		
		ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(16));
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(17);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY%", Формат(НачальноеСмещенияПоВертикали + 60, "ЧРД=.; ЧГ=0"));
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ЦветТекста%", ЦветТекста);
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(18);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%НомерСертификата%", ПараметрыДляЗаполненияШтампа.НомерСертификата);
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(19);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%СрокДействия%", ПараметрыДляЗаполненияШтампа.СрокДействия);
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);

		ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(20));
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(21);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY%", Формат(НачальноеСмещенияПоВертикали + 60, "ЧРД=.; ЧГ=0"));
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ЦветТекста%", ЦветТекста);
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(22);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ДатаПодписи%", ПараметрыДляЗаполненияШтампа.ДатаПодписи);
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(23);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ТекстОтметки%", СокрЛП(ПараметрыДляЗаполненияШтампа.ТекстОтметки));
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(24));
					
		// Правая граница.
		СтрокаЭП = МакетЭП.ПолучитьСтроку(25);		
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", Формат(НачальноеСмещенияПоВертикали + 50, "ЧРД=.; ЧГ=0"));
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", Формат(КонечноеСмещенияПоВертикали + 90, "ЧРД=.; ЧГ=0"));
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);		
		
		Счетчик = Счетчик + 1;
		
		Если Счетчик <> КоличествоПодписей Тогда
			
			// Разделитель.
			ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(26));
			
			СтрокаЭП = МакетЭП.ПолучитьСтроку(27);
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", Формат(НачальноеСмещенияПоВертикали + 90, "ЧРД=.; ЧГ=0"));
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", Формат(КонечноеСмещенияПоВертикали + 100, "ЧРД=.; ЧГ=0"));
			ТекстЭП.ДобавитьСтроку(СтрокаЭП);
			
			СтрокаЭП = МакетЭП.ПолучитьСтроку(28);
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", Формат(КонечноеСмещенияПоВертикали + 85, "ЧРД=.; ЧГ=0"));
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", Формат(КонечноеСмещенияПоВертикали + 85, "ЧРД=.; ЧГ=0"));
			ТекстЭП.ДобавитьСтроку(СтрокаЭП);
			
			СтрокаЭП = МакетЭП.ПолучитьСтроку(29);
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", Формат(НачальноеСмещенияПоВертикали + 90, "ЧРД=.; ЧГ=0"));
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", Формат(КонечноеСмещенияПоВертикали + 100, "ЧРД=.; ЧГ=0"));
			ТекстЭП.ДобавитьСтроку(СтрокаЭП);
			
		КонецЕсли;
		
		ИтоговоеСмещениеПоКоличествуСтрок = ИтоговоеСмещениеПоКоличествуСтрок + СмещениеПоКоличествуСтрок;
		
	КонецЦикла;
	
	// Подвал.	
	ВысотаШтампаБезПодвала = 35 + 40*КоличествоПодписей + ИтоговоеСмещениеПоКоличествуСтрок;
	
	ШиринаViewBox = 660;
	ВысотаViewBox = ВысотаШтампаБезПодвала + 22;
	ШиринаViewPort = ШиринаViewBox * 6;
	ВысотаViewPort = ВысотаViewBox * 6;
	
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(30));
	
	СтрокаЭП = МакетЭП.ПолучитьСтроку(31);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY%", ВысотаШтампаБезПодвала + 10);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ИдентификаторДокумента%",
		СтрокаСЗамененнымиСимволамиXML(ДанныеДляФормирования.ИдентификаторДокумента));
	ТекстЭП.ДобавитьСтроку(СтрокаЭП);
	
	СтрокаЭП = МакетЭП.ПолучитьСтроку(32);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", ВысотаШтампаБезПодвала);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", ВысотаШтампаБезПодвала + 20);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY3%", ВысотаШтампаБезПодвала + 20);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY4%", ВысотаШтампаБезПодвала);
	ТекстЭП.ДобавитьСтроку(СтрокаЭП);	
	
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(33));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(34));
	
	Результат = ТекстЭП.ПолучитьТекст();
	Результат = СтрЗаменить(Результат, "%ШиринаViewPort%", Формат(ШиринаViewPort, "ЧРД=.; ЧГ=0"));
	Результат = СтрЗаменить(Результат, "%ВысотаViewPort%", Формат(ВысотаViewPort, "ЧРД=.; ЧГ=0"));
	Результат = СтрЗаменить(Результат, "%ШиринаViewBox%", Формат(ШиринаViewBox, "ЧРД=.; ЧГ=0"));
	Результат = СтрЗаменить(Результат, "%ВысотаViewBox%", Формат(ВысотаViewBox, "ЧРД=.; ЧГ=0"));
	
	Возврат Результат;
	
КонецФункции

// Возвращает строку в формате svg, содержащую штамп простой электронной подписи.
// 
// Параметры:
// 	ДанныеДляФормирования - см. НовыеДанныеДляФормированияШтампа
// 	Подписи - Массив из см. НовыеДанныеПодписиСУчетомДоверенности
// 	ТекстШтампа - Строка
// Возвращаемое значение:
// 	Строка
Функция ТекстШтампаПЭП(ДанныеДляФормирования, Подписи, ТекстШтампа)
	
	МакетЭП = Обработки.КриптографияБЭД.ПолучитьМакет(
		СтрШаблон("ШтампПЭП_svg_%1", ОбщегоНазначения.КодОсновногоЯзыка()));
	МакетЭП.КодЯзыкаМакета = Метаданные.Языки.Русский.КодЯзыка;		
	
	КоличествоПодписей = Подписи.Количество();
	
	ВысотаШтампаБезПодвалы = 35 + 40*КоличествоПодписей;
	
	ШиринаViewBox = 692;
	ВысотаViewBox = ВысотаШтампаБезПодвалы + 22;
	ШиринаViewPort = ШиринаViewBox * 6;
	ВысотаViewPort = ВысотаViewBox * 6;	
	
	ТекстЭП = Новый ТекстовыйДокумент;
	
	// Шапка.
	СтрокаЭП = МакетЭП.ПолучитьСтроку(1);	
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ШиринаViewPort%", Формат(ШиринаViewPort, "ЧРД=.; ЧГ=0"));
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ВысотаViewPort%", Формат(ВысотаViewPort, "ЧРД=.; ЧГ=0"));
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ШиринаViewBox%", Формат(ШиринаViewBox, "ЧРД=.; ЧГ=0"));
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ВысотаViewBox%", Формат(ВысотаViewBox, "ЧРД=.; ЧГ=0"));
	ТекстЭП.ДобавитьСтроку(СтрокаЭП);
	
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(2));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(3));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(4));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(5));
	
	СтрокаЭП = МакетЭП.ПолучитьСтроку(6);	
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ДокументПодписан%", ТекстШтампа);	
	ТекстЭП.ДобавитьСтроку(СтрокаЭП);	
	
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(7));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(8));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(9));	
	
	// Таблица.	
	Счетчик = 0;
	СинийЦвет = "rgb(10, 100, 220);";
	КрасныйЦвет = "rgb(255, 60, 10);";
	
	Для Каждого Подпись Из Подписи Цикл
		
		Счетчик = Счетчик + 1;
		СвойстваПодписи = Подпись.СвойстваПодписи;			
		ПодписьВерна = СвойстваПодписи.ПодписьВерна;
		Если Подпись.ЭтоПодписьПоДоверенности Тогда
			ПодписьВерна = ПодписьВерна 
							И Подпись.РезультатПроверкиПоМЧД <> Неопределено
							И Подпись.РезультатПроверкиПоМЧД.ПодписьВерна;
		КонецЕсли;
		
		ЦветТекста = СинийЦвет;		
		Если Не ПодписьВерна
			Или (Подпись.ЭтоПодписьПоДоверенности И ЕстьОшибкаВДоверенности(Подпись.РезультатПроверкиПоМЧД)) Тогда			
			ЦветТекста = КрасныйЦвет;			
		КонецЕсли;
			
		ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(10));
		
		СмещениеПоВертикали = 40 * (Счетчик - 1);
		
		// Левая граница.
		СтрокаЭП = МакетЭП.ПолучитьСтроку(11);		
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", Формат(СмещениеПоВертикали + 50, "ЧРД=.; ЧГ=0"));
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", Формат(СмещениеПоВертикали + 90, "ЧРД=.; ЧГ=0"));
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		// Центр.
		ПараметрыДляЗаполненияШтампа = ПараметрыДляЗаполненияШтампаПЭП(Подпись, ДанныеДляФормирования.Организация);
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(12);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY%", Формат(СмещениеПоВертикали + 60, "ЧРД=.; ЧГ=0"));
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ЦветТекста%", ЦветТекста);
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(13);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%Организация%",
			СтрокаСЗамененнымиСимволамиXML(ПараметрыДляЗаполненияШтампа.Организация));
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(14);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ФИОПодписанта%",
			СтрокаСЗамененнымиСимволамиXML(ПараметрыДляЗаполненияШтампа.ФИОПодписанта));
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(15));
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(16);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY%", Формат(СмещениеПоВертикали + 60, "ЧРД=.; ЧГ=0"));
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ЦветТекста%", ЦветТекста);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ИмяПользователя%",
			СтрокаСЗамененнымиСимволамиXML(ПараметрыДляЗаполненияШтампа.ИмяПользователя));
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(17);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY%", Формат(СмещениеПоВертикали + 60, "ЧРД=.; ЧГ=0"));
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ЦветТекста%", ЦветТекста);
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(18);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ДатаПодписи%", ПараметрыДляЗаполненияШтампа.ДатаПодписи);
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		СтрокаЭП = МакетЭП.ПолучитьСтроку(19);
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ТекстОтметки%", СокрЛП(ПараметрыДляЗаполненияШтампа.ТекстОтметки));
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
		
		ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(20));
		
		// Правая граница.
		СтрокаЭП = МакетЭП.ПолучитьСтроку(21);		
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", Формат(СмещениеПоВертикали + 50, "ЧРД=.; ЧГ=0"));
		СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", Формат(СмещениеПоВертикали + 90, "ЧРД=.; ЧГ=0"));
		ТекстЭП.ДобавитьСтроку(СтрокаЭП);
				
		Если Счетчик <> КоличествоПодписей Тогда
			
			// Разделитель.
			ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(22));
			
			СтрокаЭП = МакетЭП.ПолучитьСтроку(23);
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", Формат(СмещениеПоВертикали + 90, "ЧРД=.; ЧГ=0"));
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", Формат(СмещениеПоВертикали + 100, "ЧРД=.; ЧГ=0"));
			ТекстЭП.ДобавитьСтроку(СтрокаЭП);
			
			СтрокаЭП = МакетЭП.ПолучитьСтроку(24);
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", Формат(СмещениеПоВертикали + 85, "ЧРД=.; ЧГ=0"));
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", Формат(СмещениеПоВертикали + 85, "ЧРД=.; ЧГ=0"));
			ТекстЭП.ДобавитьСтроку(СтрокаЭП);
			
			СтрокаЭП = МакетЭП.ПолучитьСтроку(25);
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", Формат(СмещениеПоВертикали + 90, "ЧРД=.; ЧГ=0"));
			СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", Формат(СмещениеПоВертикали + 100, "ЧРД=.; ЧГ=0"));
			ТекстЭП.ДобавитьСтроку(СтрокаЭП);
			
		КонецЕсли;

	КонецЦикла;
	
	// Подвал.
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(26));
	
	СтрокаЭП = МакетЭП.ПолучитьСтроку(27);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY%", ВысотаШтампаБезПодвалы + 10);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ИдентификаторДокумента%",
		СтрокаСЗамененнымиСимволамиXML(ДанныеДляФормирования.ИдентификаторДокумента));
	ТекстЭП.ДобавитьСтроку(СтрокаЭП);
	
	СтрокаЭП = МакетЭП.ПолучитьСтроку(28);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY1%", ВысотаШтампаБезПодвалы);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY2%", ВысотаШтампаБезПодвалы + 20);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY3%", ВысотаШтампаБезПодвалы + 20);
	СтрокаЭП = СтрЗаменить(СтрокаЭП, "%ПозицияY4%", ВысотаШтампаБезПодвалы);
	ТекстЭП.ДобавитьСтроку(СтрокаЭП);	
	
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(29));
	ТекстЭП.ДобавитьСтроку(МакетЭП.ПолучитьСтроку(30));
	
	Возврат ТекстЭП.ПолучитьТекст();
		
КонецФункции

// Возвращает строку, пригодную для вставки в текст XML, т.е. заменяет символы <, >, &, " и ' на их именнованные сущности
// 
// Параметры:
//  ОригинальнаяСтрока - Строка
// 
// Возвращаемое значение:
//  Строка
Функция СтрокаСЗамененнымиСимволамиXML(ОригинальнаяСтрока)
	
	СтрокаДляXML = ОригинальнаяСтрока;
	
	СтрокаДляXML = СтрЗаменить(СтрокаДляXML, "&", "&amp;");
	СтрокаДляXML = СтрЗаменить(СтрокаДляXML, """", "&quot;");
	СтрокаДляXML = СтрЗаменить(СтрокаДляXML, ">", "&gt;");
	СтрокаДляXML = СтрЗаменить(СтрокаДляXML, "<", "&lt;");
	СтрокаДляXML = СтрЗаменить(СтрокаДляXML, "'", "&apos;");
	
	Возврат СтрокаДляXML;
	
КонецФункции

// На основании входного текста формирует текст из строк ограниченной длины.
// Переносы строк во входном тексте сохраняют свои позиции.  
//
// Параметры:
// 	ВходнойТекст - Строка
// 	МаксимальнаяДлинаСтроки - Число
// Возвращаемое значение:
// 	Строка
Функция СформироватьТекстИзСтрокОграниченнойДлины(ВходнойТекст, МаксимальнаяДлинаСтроки)

	ИндексСтроки = 0;
	ИндексСлова = 0;
	РазделителиСлов = " " + Символы.НПП;
	МассивВходныхСтрок = СтрРазделить(ВходнойТекст, Символы.ПС);	
	МассивВыходныхСтрок = Новый Массив(МассивВходныхСтрок.Количество());
	
	Для Каждого ВходнаяСтрока Из МассивВходныхСтрок Цикл
		
		ИндексСлова = 0;
		ВыходнаяСтрока = "";
		МассивВыходныхСтрок[ИндексСтроки] = "";
		МассивСлов = СтрРазделить(СокрЛП(ВходнаяСтрока), РазделителиСлов);
		
		Для Каждого ТекущееСлово Из МассивСлов Цикл
			
			Если СтрДлина(ВыходнаяСтрока + ТекущееСлово) + 1 > МаксимальнаяДлинаСтроки Тогда
				МассивВыходныхСтрок[ИндексСтроки] = МассивВыходныхСтрок[ИндексСтроки] + ВыходнаяСтрока + Символы.ПС;
				ВыходнаяСтрока = "";
			КонецЕсли;	
			
			ВыходнаяСтрока = ВыходнаяСтрока + ТекущееСлово + " ";			
			ИндексСлова = ИндексСлова + 1;
			
			Если ИндексСлова = МассивСлов.Количество() Тогда
				МассивВыходныхСтрок[ИндексСтроки] = МассивВыходныхСтрок[ИндексСтроки] + ВыходнаяСтрока;
			КонецЕсли;	
			
		КонецЦикла;	
		
		МассивВыходныхСтрок[ИндексСтроки] = СокрЛП(МассивВыходныхСтрок[ИндексСтроки]);
		
		ИндексСтроки = ИндексСтроки + 1;
		
	КонецЦикла;	
	
	Возврат СтрСоединить(МассивВыходныхСтрок, Символы.ПС);
	
КонецФункции

// Обрабатывает ошибки криптографической операции.
// 
// Параметры:
//  ВидОперации - Строка
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  ПодробноеПредставлениеОшибки - Строка
//  КраткоеПредставлениеОшибки - Строка
Процедура ОбработатьОшибкиКриптографическойОперации(ВидОперации, КонтекстДиагностики, ПодробноеПредставлениеОшибки,
	КраткоеПредставлениеОшибки)
	
	КриптографияБЭДСобытия.ПриОшибкеВыполненияКриптографическойОперации(КонтекстДиагностики,
		ПодробноеПредставлениеОшибки, КраткоеПредставлениеОшибки);
	
	ВидОшибкиКриптография = КриптографияБЭДКлиентСервер.ВидОшибкиКриптография();
	Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(ВидОперации, ВидОшибкиКриптография,
		ПодробноеПредставлениеОшибки, КраткоеПредставлениеОшибки);
	ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
		ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().Криптография);
	
КонецПроцедуры

#КонецОбласти
