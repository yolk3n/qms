
#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытийФормы

// Возникает на клиенте перед выполнением записи объекта из формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма записываемого объекта,
//  Отказ - Булево - признак отказа от записи,
//  ПараметрыЗаписи - Структура - структура, содержащая параметры записи.
//
Процедура ПередЗаписью(Форма, Отказ, ПараметрыЗаписи) Экспорт
	
	
	
КонецПроцедуры

// Вызывается во всех созданных формах при вызове метода Оповестить.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - оповещаемая форма,
//  ИмяСобытия - Строка - имя события,
//  Параметр - Произвольный - параметр сообщения. Могут быть переданы любые необходимые данные,
//  Источник - Произвольный - источник события.
//
Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	// БольничнаяАптека
	ИнтеграцияМДЛПБольничнаяАптекаКлиент.ОповещениеОбработкиНенайденныхШтрихкодов(Форма, ИмяСобытия, Параметр, Источник);
	// Конец БольничнаяАптека
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// Выполняет действия при изменении номенклатуры в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыЗаполнения - Структура - см. функцию ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//
Процедура ПриИзмененииНоменклатуры(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	// БольничнаяАптека
	Строка = Новый Структура;
	Строка.Вставить("Номенклатура", ТекущаяСтрока.Номенклатура);
	
	ЕстьЕдиницаИзмерения = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "ЕдиницаИзмерения");
	Если ЕстьЕдиницаИзмерения Тогда
		Строка.Вставить("ЕдиницаИзмерения", ТекущаяСтрока.ЕдиницаИзмерения);
	КонецЕсли;
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	СтруктураДействий = Новый Структура;
	Если ЕстьЕдиницаИзмерения Тогда
		СтруктураДействий.Вставить(Действия.Действие_ПроверитьУпаковкуПоВладельцу(), ТекущаяСтрока.ЕдиницаИзмерения);
		СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЕдиницуИзмерения(), НоменклатураКлиентСервер.ВидЕдиницы_ПотребительскаяУпаковка());
	КонецЕсли;
	
	Если ПараметрыЗаполнения.ПроверитьСериюРассчитатьСтатус Тогда
		Строка.Вставить("СерияНоменклатуры", ТекущаяСтрока.Серия);
		СтруктураДействий.Вставить(Действия.Действие_ПроверитьСериюНоменклатурыПоВладельцу(), ТекущаяСтрока.Серия);
	КонецЕсли;
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(Строка, СтруктураДействий, КэшированныеЗначения);
	
	Если ЕстьЕдиницаИзмерения Тогда
		ТекущаяСтрока.ЕдиницаИзмерения = Строка.ЕдиницаИзмерения;
	КонецЕсли;
	
	Если Строка.Свойство("СерияНоменклатуры") Тогда
		ТекущаяСтрока.Серия = Строка.СерияНоменклатуры;
	КонецЕсли;
	// Конец БольничнаяАптека
	
КонецПроцедуры

// Выполняет действия при изменении характеристики в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыЗаполнения - Структура - см. функцию ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//
Процедура ПриИзмененииХарактеристики(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	
	
КонецПроцедуры

// Выполняет действия при изменении серии в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыЗаполнения - Структура - см. функцию ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//
Процедура ПриИзмененииСерии(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	
	
КонецПроцедуры

// Выполняет действия при изменении упаковки в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыЗаполнения - Структура - см. функцию ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//
Процедура ПриИзмененииУпаковки(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	
	
КонецПроцедуры

// Выполняет действия при изменении количества упаковок в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыЗаполнения - Структура - см. функцию ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//
Процедура ПриИзмененииКоличестваУпаковок(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	// БольничнаяАптека
	Если ПараметрыЗаполнения.ПересчитатьКоличествоЕдиниц Тогда
		ТекущаяСтрока.Количество = ТекущаяСтрока.КоличествоУпаковок;
	КонецЕсли;
	Если ПараметрыЗаполнения.ПересчитатьЦену Тогда
		ТекущаяСтрока.Цена = ТекущаяСтрока.Сумма / ?(ТекущаяСтрока.КоличествоУпаковок = 0, 1, ТекущаяСтрока.КоличествоУпаковок);
	КонецЕсли;
	Если ПараметрыЗаполнения.ПересчитатьСумму Тогда
		ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена * ТекущаяСтрока.КоличествоУпаковок;
	КонецЕсли;
	// Конец БольничнаяАптека
	
КонецПроцедуры

// Выполняет действия при изменении количества в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыЗаполнения - Структура - см. функцию ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//
Процедура ПриИзмененииКоличества(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	// БольничнаяАптека
	Если ПараметрыЗаполнения.ПересчитатьКоличествоУпаковок Тогда
		ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.Количество;
	КонецЕсли;
	Если ПараметрыЗаполнения.ПересчитатьЦену Тогда
		ТекущаяСтрока.Цена = ТекущаяСтрока.Сумма / ?(ТекущаяСтрока.КоличествоУпаковок = 0, 1, ТекущаяСтрока.КоличествоУпаковок);
	КонецЕсли;
	Если ПараметрыЗаполнения.ПересчитатьСумму Тогда
		ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена * ТекущаяСтрока.КоличествоУпаковок;
	КонецЕсли;
	// Конец БольничнаяАптека
	
КонецПроцедуры

// Выполняет действия при изменении цены в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыЗаполнения - Структура - см. функцию ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//
Процедура ПриИзмененииЦены(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	// БольничнаяАптека
	Если ПараметрыЗаполнения.ПересчитатьСумму Тогда
		ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена * ТекущаяСтрока.КоличествоУпаковок;
	КонецЕсли;
	// Конец БольничнаяАптека
	
КонецПроцедуры

// Выполняет действия при изменении суммы в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыЗаполнения - Структура - см. функцию ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//
Процедура ПриИзмененииСуммы(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	// БольничнаяАптека
	Если ПараметрыЗаполнения.ПересчитатьЦену Тогда
		ТекущаяСтрока.Цена = ТекущаяСтрока.Сумма / ?(ТекущаяСтрока.КоличествоУпаковок = 0, 1, ТекущаяСтрока.КоличествоУпаковок);
	КонецЕсли;
	// Конец БольничнаяАптека
	
КонецПроцедуры

// Выполняет действия при начале выбора номенклатуры в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  Элемент - ПолеВвода - элемент формы Характеристика,
//  ДанныеВыбора - СписокЗначений - в обработчике можно сформировать и передать в этом параметре данные для выбора,
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура НачалоВыбораНоменклатуры(Форма, ТекущаяСтрока, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	// БольничнаяАптека
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ТипНоменклатуры", ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар"));
	
	ПараметрыФормы.Вставить("Отбор"               , Отбор);
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
	ПараметрыФормы.Вставить("ТекущаяСтрока"       , ТекущаяСтрока.Номенклатура);
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, Элемент);
	// Конец БольничнаяАптека
	
КонецПроцедуры

// Выполняет действия при начале выбора характеристики в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  Элемент - ПолеВвода - элемент формы Характеристика,
//  ДанныеВыбора - СписокЗначений - в обработчике можно сформировать и передать в этом параметре данные для выбора,
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура НачалоВыбораХарактеристики(Форма, ТекущаяСтрока, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	
	
КонецПроцедуры

// Выполняет действия при начале выбора серии в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров;
//  ПараметрыУказанияСерий - Структура - параметры указания серий, возвращаемые соответствующей
//         процедурой модуля менеджера документа;
//  Элемент - ПолеВвода - элемент формы Характеристика;
//  ДанныеВыбора - СписокЗначений - в обработчике можно сформировать и передать в этом параметре данные для выбора;
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура НачалоВыбораСерии(ЭтотОбъект, ТекущаяСтрока, ПараметрыУказанияСерий, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	
	
КонецПроцедуры

// Выполняет действия при начале выбора упаковки в таблице Товары.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  Элемент - ПолеВвода - элемент формы Упаковка,
//  ДанныеВыбора - СписокЗначений - в обработчике можно сформировать и передать в этом параметре данные для выбора,
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура НачалоВыбораУпаковки(Форма, ТекущаяСтрока, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик переопределяемой команды формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта справочника или документа,
//  Команда - КомандаФормы - команда формы.
//
Процедура ВыполнитьПереопределяемуюКоманду(Форма, Команда) Экспорт
	
	// БольничнаяАптека
	ИнтеграцияМДЛПБольничнаяАптекаКлиент.ВыполнитьКомандуЗарегистрироватьGTIN(Форма, Команда);
	ИнтеграцияМДЛПБольничнаяАптекаКлиент.ВыполнитьКомандуОбработатьНенайденныеШтрихкоды(Форма, Команда);
	
	СвойстваКоманды = ИнтеграцияСМобильнымПриложениемКлиентСервер.СвойстваКомандыЗагрузкиШтрихкодовИзМобильногоПриложения();
	Если СтрЗаканчиваетсяНа(Команда.Имя, СвойстваКоманды.Имя) Тогда
		Оповестить = Новый ОписаниеОповещения("ЗагрузитьИзТСДЗавершение", Форма);
		ИнтеграцияСМобильнымПриложениемКлиент.ВыполнитьКомандуЗагрузкиДанныхИзМобильногоПриложения(Форма, Команда, Оповестить);
	КонецЕсли;
	// Конец БольничнаяАптека
	
КонецПроцедуры

// Открывает форму создания нового документа поступления товаров на основании уведомления о приемке.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой вызывается команда создания документа. Для автоматической подстановки
//         создаваемого документа в поле "Основание", форма должна быть передана в качестве владельца открываемой формы;
//  Ссылка - ДокументСсылка.Уведомление - ссылка на уведомление о поступлении.
//
Процедура ОткрытьФормуСозданияДокументаПоступленияТоваровНаОсновании(Форма, Ссылка) Экспорт
	
	// БольничнаяАптека
	ОткрытьФорму("Документ.ПоступлениеТоваров.ФормаОбъекта", Новый Структура("Основание", Ссылка), Форма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	// Конец БольничнаяАптека
	
КонецПроцедуры

// Открывает форму выбора документа поступления товаров.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой вызывается команда выбора документа;
//  Объект - ДокументОбъект.Уведомление - уведомление для которого подбирается поступление;
//  ОповещениеПриЗавершении - ОписаниеОповещения - процедура, вызываемая после закрытия формы выбора;
//
Процедура ОткрытьФормуВыбораДокументаПоступлениеТоваров(Форма, Объект, ОповещениеПриЗавершении) Экспорт
	
	// БольничнаяАптека
	Если ИнтеграцияМДЛПБольничнаяАптекаКлиент.ЭтоСсылкаИлиОбъектФормы(Объект, Тип("ДокументСсылка.УведомлениеОПриемкеМДЛП")) Тогда
		ИнтеграцияМДЛПБольничнаяАптекаКлиент.ВыбратьДокументПоступления(Форма, Объект, ОповещениеПриЗавершении);
	ИначеЕсли ИнтеграцияМДЛПБольничнаяАптекаКлиент.ЭтоСсылкаИлиОбъектФормы(Объект, Тип("ДокументСсылка.УведомлениеОбОприходованииМДЛП")) Тогда
		ИнтеграцияМДЛПБольничнаяАптекаКлиент.ВыбратьДокументПоступленияДляОприходования(Форма, Объект, ОповещениеПриЗавершении);
	ИначеЕсли ИнтеграцияМДЛПБольничнаяАптекаКлиент.ЭтоСсылкаИлиОбъектФормы(Объект, Тип("ДокументСсылка.УведомлениеОПеремещенииМДЛП")) Тогда
		ИнтеграцияМДЛПБольничнаяАптекаКлиент.ВыбратьДокументПоступленияДляПеремещения(Форма, Объект, ОповещениеПриЗавершении);
	КонецЕсли;
	// Конец БольничнаяАптека
	
КонецПроцедуры

// Открывает форму выбора документа выбытия товаров.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой вызывается команда выбора документа;
//  Объект - ДокументОбъект.Уведомление - уведомление для которого подбирается документ выбытия;
//  ОповещениеПриЗавершении - ОписаниеОповещения - процедура, вызываемая после закрытия формы выбора;
//
Процедура ОткрытьФормуВыбораДокументаВыбытияТоваров(Форма, Объект, ОповещениеПриЗавершении) Экспорт
	
	
	
КонецПроцедуры

// Открывает форму создания нового документа инвентаризация товаров на основании инвентаризации МДЛП.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой вызывается команда создания документа. Для автоматической подстановки
//         создаваемого документа в поле "Основание", форма должна быть передана в качестве владельца открываемой формы;
//  Ссылка - ДокументСсылка - ссылка на документ МДЛП.
//
Процедура ОткрытьФормуСозданияДокументаИнвентаризацияТоваровНаОсновании(Форма, Ссылка) Экспорт
	
	
	
КонецПроцедуры

// Открывает форму выбора документа инвентаризация товаров.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой вызывается команда выбора документа;
//  Объект - ДокументОбъект - документ-объект МДЛП для которого подбирается инвентаризация;
//  ОповещениеПриЗавершении - ОписаниеОповещения - процедура, вызываемая после закрытия формы выбора;
//
Процедура ОткрытьФормуВыбораДокументаИнвентаризацияТоваров(Форма, Объект, ОповещениеПриЗавершении) Экспорт
	
	
	
КонецПроцедуры

// Открывает форму создания нового документа инвентаризационная опись товаров на основании инвентаризации МДЛП.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой вызывается команда создания документа. Для автоматической подстановки
//         создаваемого документа в поле "Основание", форма должна быть передана в качестве владельца открываемой формы;
//  Ссылка - ДокументСсылка - ссылка на документ МДЛП.
//
Процедура ОткрытьФормуСозданияДокументаИнвентаризационнаяОписьТоваровНаОсновании(Форма, Ссылка) Экспорт
	
	
	
КонецПроцедуры

// Открывает форму выбора документа инвентаризационная опись товаров.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой вызывается команда выбора документа;
//  Объект - ДокументОбъект - документ-объект МДЛП для которого подбирается инвентаризационная опись;
//  ОповещениеПриЗавершении - ОписаниеОповещения - процедура, вызываемая после закрытия формы выбора;
//
Процедура ОткрытьФормуВыбораДокументаИнвентаризационнаяОписьТоваров(Форма, Объект, ОповещениеПриЗавершении) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#Область ШтрихкодированиеНоменклатуры

// В функции нужно реализовать подготовку данных для дальнейшей обработки штрихкодов.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой происходит обработка,
//  ДанныеШтрихкодовПоТипам - Структура
//     * НомераКиЗ - Массив - разобранные данные штрихкодов вторичных упаковок;
//     * НомераТранспортныхУпаковок - Массив - разобранные данные штрихкодов транспортных упаковок;
//     * НеизвестныеШтрихкоды - Массив - полученные данные прочих штрихкодов.
//  КэшированныеЗначения - Структура - используется механизмом обработки изменения реквизитов ТЧ,
//  ПараметрыЗаполнения - Структура - параметры заполнения (см. ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти()).
//  ОтборСтрок - Структура - отбор строк списков товары и транспортные упаковки, для заполения по штрихкодам.
//
// Возвращаемое значение:
//  Структура - подготовленные данные.
//
Функция ПодготовитьДанныеДляОбработкиШтрихкодов(Форма, ДанныеШтрихкодовПоТипам, КэшированныеЗначения, ПараметрыЗаполнения, ОтборСтрок = Неопределено) Экспорт
	
	// БольничнаяАптека
	ПараметрыДействия = ОбработкаТабличнойЧастиКлиент.ПолучитьПараметрыОбработкиШтрихкодов(Новый Массив, ПараметрыЗаполнения, ПараметрыЗаполнения);
	
	ПараметрыДействия.Штрихкоды = ДанныеШтрихкодовПоТипам.НомераКиЗ;
	ПараметрыДействия.НеизвестныеШтрихкоды = ОбработкаТабличнойЧастиКлиент.ПодготовитьВходящиеШтирхкодыДляОбработки(ДанныеШтрихкодовПоТипам.НеизвестныеШтрихкоды);
	
	ДанныеШтрихкодовПоТипам.НомераКиЗ = Новый Массив;
	ДанныеШтрихкодовПоТипам.НеизвестныеШтрихкоды = Новый Массив;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект")
	   И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Объект, "Ссылка")Тогда
		
		ИсключаемыеТипы = Новый Массив;
		ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.УведомлениеОбАгрегированииИТрансформацииУпаковокМДЛП"));
		ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.УведомлениеОбОтгрузкеМДЛП"));
		ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.УведомлениеОПриемкеМДЛП"));
		ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.УведомлениеОбОприходованииМДЛП"));
		ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.УведомлениеОПеремещенииМДЛП"));
		ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.УведомлениеОПеремещенииГЛОМДЛП"));
		
		Если ИсключаемыеТипы.Найти(ТипЗнч(Форма.Объект.Ссылка)) = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыДействия.НеизвестныеШтрихкоды, ДанныеШтрихкодовПоТипам.НомераТранспортныхУпаковок);
			ДанныеШтрихкодовПоТипам.НомераТранспортныхУпаковок.Очистить();
		КонецЕсли;
		Если ОтборСтрок <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыДействия.ОтборСтрок, ОтборСтрок, Истина);
		КонецЕсли;
		
		ТипыПовторногоПоиска = Новый Массив;
		ТипыПовторногоПоиска.Добавить(Тип("ДокументСсылка.УведомлениеОПриемкеМДЛП"));
		ТипыПовторногоПоиска.Добавить(Тип("ДокументСсылка.УведомлениеОбОприходованииМДЛП"));
		
		Если ТипыПовторногоПоиска.Найти(ТипЗнч(Форма.Объект.Ссылка)) <> Неопределено Тогда
			ПараметрыДействия.ПовторитьПоискТовараБезУчетаСерии = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Форма.Объект.Основание) Тогда
			// Если документ-уведомление создано на основании прикладного документа,
			// тогда табличная часть Товары наследуется из прикладного документа
			// и при добавлении в нее новых строк или изменении существующих (при обработке штрихкодов) необходимо сообщать пользователю.
			// Пример: если штрихкод не привязан ни к одной номенклатуре
			// или привязан к номенклатуре, которой нет в табличной части, тогда будет добавлена новая строка.
			ПараметрыДействия.СообщатьОДобавленииИзмененииСтрокиТоваров = Истина;
		КонецЕсли;
		
		Если ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ИнвентаризацияПотребительскихУпаковокМДЛП") Тогда
			ПараметрыДействия.ЗаполнитьНезаполненныеНомерСерииИГоденДо = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПараметрыДействия;
	// Конец БольничнаяАптека
	
КонецФункции

// Вызывается после загрузки данных по штрихкодам.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой были обработаны штрихкоды,
//  ОбработанныеДанные - Структура - подготовленные ранее данные штрихкодов,
//  КэшированныеЗначения - Структура - используется механизмом обработки изменения реквизитов ТЧ.
//
Процедура ПослеОбработкиШтрихкодов(Форма, ОбработанныеДанные, КэшированныеЗначения) Экспорт
	
	// БольничнаяАптека
	ОбработкаТабличнойЧастиКлиент.СообщитьОНеизвестныхШтрихкодах(ОбработанныеДанные);
	// Конец БольничнаяАптека
	
КонецПроцедуры

// Требуется реализовать получение данных связанной номенклатуры.
//
// Параметры:
//  Данные - Структура - 
//    * Штрихкод - Строка - представление исходного штрихкода;
//    * GTIN - Строка - код товара;
//    * SGTIN - Строка - уникальный код упаковки;
// 
// Возвращаемое значение:
//   - Структура
//      * Номенклатура
//      * Характеристика
//      * Упаковка
//
Функция ПолучитьДанныеНоменклатурыПоКиЗ(Данные) Экспорт
	
	
	
КонецФункции

#КонецОбласти

#Область ЗапросИнформацииОбУпаковках

// В процедуре можно переопределить стандартный алгоритм запроса информации о потребительских упаковках для документов.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой происходит обработка.
//  ПараметрыПодключения - Структура - параметры подключения к ИС "Маркировка.МДЛП".
//  НомераУпаковок - Массив - запрашиваемые номера упаковок.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной обработки события.
//
Процедура ЗапроситьИнформациюОПотребительскихУпаковках(Форма, ПараметрыПодключения, НомераУпаковок, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// В процедуре можно переопределить стандартный алгоритм обработки получения информации о потребительских упаковках для документов.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой происходит обработка.
//  Результат - Структура - результат функции ТранспортМДЛПАПИКлиентСервер.ПолучитьИнформациюОПотребительскихУпаковках.
//  Контекст - Структура - дополнительные параметры, переданные из функции ЗапроситьИнформациюОПотребительскихУпаковках формы документа.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной обработки события.
//
Процедура ОбработатьПолучениеИнформацииОПотребительскихУпаковках(Форма, Результат, Контекст, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// В процедуре можно переопределить стандартный алгоритм запроса информации о транспортных упаковках для документов.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой происходит обработка.
//  ПараметрыПодключения - Структура - параметры подключения к ИС "Маркировка.МДЛП".
//  НомераУпаковок - Массив - запрашиваемые номера упаковок.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной обработки события.
//
Процедура ЗапроситьИнформациюОТранспортныхУпаковках(Форма, ПараметрыПодключения, НомераУпаковок, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// В процедуре можно переопределить стандартный алгоритм обработки получения информации о транспортных упаковках для документов.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой происходит обработка.
//  Результат - Структура - результат функции ТранспортМДЛПАПИКлиентСервер.ПолучитьИнформациюОТранспортныхУпаковках.
//  Контекст - Структура - дополнительные параметры, переданные из функции ЗапроситьИнформациюОПотребительскихУпаковках формы документа.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной обработки события.
//
Процедура ОбработатьПолучениеИнформацииОТранспортныхУпаковках(Форма, Результат, Контекст, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
