
#Область СлужебныеПроцедурыИФункции

Функция ПолучитьКлючДанныхНастроекОбмена(Знач НастройкаОбмена) Экспорт

	ЕстьОбменСКонтрагентами = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами");
	
	КлючДанных = НастройкаОбмена;
	
	Если ЕстьОбменСКонтрагентами Тогда
		МодульПодсистемы = ОбщегоНазначения.ОбщийМодуль("НастройкиЭДО");
		ИмяТипаНастройкиОтправкиЭДО = МодульПодсистемы.ИмяТипаНастройкиОтправки();
		Если ТипЗнч(НастройкаОбмена) = Тип(ИмяТипаНастройкиОтправкиЭДО) Тогда
			КлючДанных = МодульПодсистемы.СсылкаНаОбъектНастройкиЭДО(НастройкаОбмена.Отправитель,
				НастройкаОбмена.Получатель, НастройкаОбмена.Договор);
		КонецЕсли;
	КонецЕсли;
	
	Возврат КлючДанных;
	
КонецФункции


Функция ИмяПоляДляОтображенияОшибкиПоМаршруту(Знач НастройкаОбмена, Знач НомерСтроки) Экспорт
	
	ЕстьОбменСКонтрагентами = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами");
	ЕстьОбменСБанками = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСБанками");
	
	ИмяТипаНастройкиОбменСБанками = "СправочникСсылка.НастройкиОбменСБанками";
	ИмяТипаНастройкиОтправкиЭДО   = "РегистрСведенийКлючЗаписи.НастройкиОтправкиЭлектронныхДокументов";
	
	Если ЕстьОбменСБанками И ТипЗнч(НастройкаОбмена) = Тип(ИмяТипаНастройкиОбменСБанками) Тогда
		МодульПодсистемы = ОбщегоНазначения.ОбщийМодуль("ОбменСБанкамиСлужебный");
		Возврат МодульПодсистемы.ИмяПоляДляОтображенияОшибкиПоМаршруту(НастройкаОбмена, НомерСтроки);	
	ИначеЕсли ЕстьОбменСКонтрагентами И ТипЗнч(НастройкаОбмена) = Тип(ИмяТипаНастройкиОтправкиЭДО) Тогда
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"ИсходящиеДокументы[%1].ДополнительныеНастройки", НомерСтроки - 1);
	КонецЕсли;	
		
КонецФункции

Процедура ЗаполнитьПараметрыОтправкиЭлектронныхДокументов(Знач НастройкаОбмена, ТаблицаНастроек, ИмяКолонкиТаблицыНастроек) Экспорт

	ЕстьОбменСКонтрагентами = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами");
	ЕстьОбменСБанками = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСБанками");
	
	ИмяТипаНастройкиОбменСБанками = "СправочникСсылка.НастройкиОбменСБанками";
	ИмяТипаНастройкиОтправкиЭДО   = "РегистрСведенийКлючЗаписи.НастройкиОтправкиЭлектронныхДокументов";
	
	Если ЕстьОбменСБанками И ТипЗнч(НастройкаОбмена) = Тип(ИмяТипаНастройкиОбменСБанками) Тогда
		ТаблицаНастроек = НастройкаОбмена.ИсходящиеДокументы.Выгрузить();
		ИмяКолонкиТаблицыНастроек = "ИсходящийДокумент";
	ИначеЕсли ЕстьОбменСКонтрагентами И ТипЗнч(НастройкаОбмена) = Тип(ИмяТипаНастройкиОтправкиЭДО) Тогда
		МенеджерНастроек = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени("РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам");
		ТаблицаНастроек = МенеджерНастроек.СоздатьНастройкиОтправкиДокументов();
		ИмяКолонкиТаблицыНастроек = "ВидДокумента";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти