#Область ПрограммныйИнтерфейс

// Открыть форму контракта ЕИС.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  СсылкаНаКонтракт - ОпределяемыйТип.ГосударственныеКонтрактыБЭД
//  ОповещениеОЗавершении - ОписаниеОповещения
Процедура ОткрытьФормуКонтракта(Форма, СсылкаНаКонтракт, ОповещениеОЗавершении) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СсылкаНаКонтракт) Тогда
		Возврат;
	КонецЕсли;
		
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Ключ", СсылкаНаКонтракт);
	
	ПутьКФорме = "Справочник.ГосударственныеКонтрактыЕИС.ФормаОбъекта";
	ПутьКФорме = ЭлектронноеАктированиеЕИСВызовСервера.ЗаменитьИмяСправочникаГосКонтрактов(ПутьКФорме);
	ОткрытьФорму(ПутьКФорме,
		СтруктураПараметров, Форма, , , , ОповещениеОЗавершении);
	
КонецПроцедуры

// Открыть форму настроек обмена ЕИС для поставщиков.
Процедура ОткрытьФормуНастройкиОбменаЕИС() Экспорт

	ОткрытьФорму("РегистрСведений.НастройкиОбменаЕИС.ФормаСписка");
		
КонецПроцедуры

// Открыть форму настроек обмена ЕИС для заказчиков.
Процедура ОткрытьФормуНастройкиОбменаЕИСДляЗаказчиков() Экспорт
	
	Если НЕ ЭлектронноеАктированиеЕИСВызовСервера.ЕстьПодсистемаДляЗаказчиков() Тогда
		Возврат;
	КонецЕсли;
	
	МодульПодсистемы = ОбщегоНазначенияКлиент.ОбщийМодуль("ЭлектронноеАктированиеЗаказчикаЕИСКлиент");
	МодульПодсистемы.ОткрытьФормуНастройкиОбменаЕИСДляЗаказчиков();
		
КонецПроцедуры

// Открыть форму выбора контракта.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация.
//  Контрагент - ОпределяемыйТип.КонтрагентБЭД - контрагент.
//  ФормаВладелец - ФормаКлиентскогоПриложения - форма владелец.
//  ОповещениеОЗакрытии - ОписаниеОповещения, Неопределено - оповещение о закрытии.
Процедура ОткрытьФормуВыбораКонтракта(Организация,
		Контрагент,
		ФормаВладелец,
		ОповещениеОЗакрытии = Неопределено) Экспорт

	Отбор = Новый Структура;
	Отбор.Вставить("Контрагент" , Контрагент);
	Отбор.Вставить("Организация", Организация);
	
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	ПутьКФорме = "Справочник.ГосударственныеКонтрактыЕИС.Форма.ФормаВыбора";
	ПутьКФорме = ЭлектронноеАктированиеЕИСВызовСервера.ЗаменитьИмяСправочникаГосКонтрактов(ПутьКФорме);
	ОткрытьФорму(ПутьКФорме,
		ПараметрыФормы,
		ФормаВладелец, , , ,
		ОповещениеОЗакрытии);
	
КонецПроцедуры

// При изменении идентификатора для актирования ЕИС.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ТабЧасть - ТабличнаяЧасть
//  ДанныеФормыКоллекция - ДанныеФормыКоллекция
Процедура ПриИзмененииИдентификатораДляАктированияЕИС(Форма, ТабЧасть, ДанныеФормыКоллекция) Экспорт
	
	ДанныеСтроки = Новый Структура(
		"ИдентификаторДляАктированияЕИС, ПредставлениеДляАктированияЕИС");
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ДанныеФормыКоллекция);
	ЭлектронноеАктированиеЕИСВызовСервера.ПриИзмененииИдентификатораДляАктированияЕИС(
		Форма, ТабЧасть, ДанныеСтроки);
	ЗаполнитьЗначенияСвойств(ДанныеФормыКоллекция, ДанныеСтроки);
	
КонецПроцедуры

// Имя события изменения владельца контракта ЕИС.
// 
// Возвращаемое значение:
//  Строка - Имя события изменения владельца контракта ЕИС
Функция ИмяСобытияИзмененияВладельцаКонтрактаЕИС() Экспорт
	
	Возврат "ИзмененВладелецГосударственногоКонтракта";
	
КонецФункции

// Имя формы выбора государственного контракта.
// 
// Возвращаемое значение:
//  Строка
Функция ИмяФормыВыбораГосударственногоКонтракта() Экспорт
	
	ИмяФормы = "Справочник.ГосударственныеКонтрактыЕИС.Форма.ФормаВыбора";
	ИмяФормы = ЭлектронноеАктированиеЕИСВызовСервера.ЗаменитьИмяСправочникаГосКонтрактов(
		ИмяФормы);
	Возврат ИмяФормы;
	
КонецФункции

// Открыть государственный контракт.
// 
// Параметры:
//  СсылкаНаКонтракт - ОпределяемыйТип.ГосударственныеКонтрактыБЭД
//  Параметры - Неопределено,
//     Структура
Процедура ОткрытьГосударственныйКонтракт(СсылкаНаКонтракт, Параметры = Неопределено) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", СсылкаНаКонтракт);
	ИмяФормы = "Справочник.ГосударственныеКонтрактыЕИС.Форма.ФормаЭлемента";
	ИмяФормы = ЭлектронноеАктированиеЕИСВызовСервера.ЗаменитьИмяСправочникаГосКонтрактов(
		ИмяФормы);
	ОткрытьФорму(ИмяФормы, ПараметрыФормы);
	
КонецПроцедуры

// Открыть подбор товаров по государственному контракту в документе.
// 
// Параметры:
//  ПараметрКоманды - Структура
//  ПараметрыВыполнения - Структура
Процедура ОткрытьПодборТоваровПоГосударственномуКонтрактуВДокументе(ПараметрКоманды, ПараметрыВыполнения) Экспорт
	
	Документ = ПараметрыВыполнения.Источник.Ссылка;
	ДоговорКонтрагента = ЭлектронноеАктированиеЕИСВызовСервера.
		ДоговорКонтрагентаИсточникаКоманды(ПараметрыВыполнения.Источник);
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ГосударственныйКонтракт = ЭлектронноеАктированиеЕИСВызовСервера.
			НайтиГосударственныйКонтрактПоВладельцу(ДоговорКонтрагента);
		Если ЗначениеЗаполнено(ГосударственныйКонтракт) Тогда
			Форма = ПараметрыВыполнения.Форма;
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Документ", Документ);
			ПараметрыФормы.Вставить("ГосударственныйКонтракт", ГосударственныйКонтракт);
			МестаПоставки = ЭлектронноеАктированиеЕИСВызовСервера.МестаПоставкиДокумента(Документ);
			ПараметрыФормы.Вставить("МестаПоставкиДокумента",
				МестаПоставки);
			ОтображатьМестаПоставки =
				ЭлектронноеАктированиеЕИСВызовСервера.ДляДокументаЗаполняютсяМестаПоставки(Документ);
			ПараметрыФормы.Вставить("ОтображатьМестаПоставки", ОтображатьМестаПоставки);
			ОтображатьСудебноеРешение =
				ЭлектронноеАктированиеЕИСВызовСервера.ДляДокументаОтображаетсяСудебноеРешение(Документ);
			ПараметрыФормы.Вставить("ОтображатьСудебноеРешение", ОтображатьСудебноеРешение);
			
			ОткрытьФорму("Обработка.ЭлектронноеАктированиеЕИС.Форма.ФормаПараметровДляДокумента",
				ПараметрыФормы, Форма,
				Форма.УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Открыть список приложенных файлов документа.
// 
// Параметры:
//  Объект - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  Форма - ФормаКлиентскогоПриложения
Процедура ОткрытьСписокПриложенныхФайловДокумента(Объект, Форма) Экспорт
	
	ПараметрыФормы = Новый Структура;
		
	Приложения = Новый Массив;
	ПараметрыФормы.Вставить("Документ", Объект);
	
	ПриложенныеДокументыДляЕИС = ЭлектронноеАктированиеЕИСВызовСервера.ПриложенныеДокументыДляЕИС(Объект);
	
	Для каждого Строка Из ПриложенныеДокументыДляЕИС Цикл
		Документ = Новый Структура;
		Документ.Вставить("ВидВложения");
		Документ.Вставить("ПрисоединенныйФайл");
		ЗаполнитьЗначенияСвойств(Документ, Строка);
		Приложения.Добавить(Документ);
	КонецЦикла;
	ПараметрыФормы.Вставить("ПриложенныеДокументы", Приложения);
	
	ИмяФормы = ИмяФормыПриложенийДляЕИС();
	ОткрытьФорму(
		ИмяФормы,
		ПараметрыФормы,
		Форма,
		Форма.УникальныйИдентификатор);
	
КонецПроцедуры

// Открыть данные обжалования отказа о приемке.
// 
// Параметры:
//  Объект - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  Форма - ФормаКлиентскогоПриложения
Процедура ОткрытьДанныеОбжалованияОтказаОПриемке(Объект, Форма) Экспорт

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидДокументаОбжалованияОтказаОПриемке",
		Форма.ВидДокументаОбжалованияОтказаОПриемке);
	ПараметрыФормы.Вставить("РеквизитыДокументаОбжалованияОтказаОПриемке",
		Форма.РеквизитыДокументаОбжалованияОтказаОПриемке);
	ПараметрыФормы.Вставить("Документ",
		Форма.Документ);
	ПараметрыФормы.Вставить("Организация",
		Форма.Организация);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("СудебноеДосудебноеРешениеЗавершение", Форма);
	
	ОткрытьФорму(
		"Обработка.ЭлектронноеАктированиеЕИС.Форма.ФормаДанныхОбжалованияОтказаОПриемке",
		ПараметрыФормы,
		Форма, 
		Форма.УникальныйИдентификатор, , ,
		ОповещениеОЗакрытии);
	
КонецПроцедуры

// Открыть форму строки контракта.
// 
// Параметры:
//  ГосударственныйКонтракт - ОпределяемыйТип.ГосударственныеКонтрактыБЭД
//  ИдентификаторДляАктированияЕИС - Строка
//  Форма - ФормаКлиентскогоПриложения
//  ТолькоПросмотр - Булево
Процедура ОткрытьФормуСтрокиКонтракта(ГосударственныйКонтракт,
		ИдентификаторДляАктированияЕИС,
		Форма,
		ТолькоПросмотр = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторДляАктированияЕИС) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ГосударственныйКонтракт", ГосударственныйКонтракт);
	ПараметрыФормы.Вставить("Идентификатор", ИдентификаторДляАктированияЕИС);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	
	ОткрытьФорму("Обработка.ЭлектронноеАктированиеЕИС.Форма.ФормаСтрокиКонтракта",
		ПараметрыФормы,
		Форма,
		Форма.УникальныйИдентификатор);

КонецПроцедуры

// Открыть выбор подписантов документа.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация
//  Объект - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  Форма - ФормаКлиентскогоПриложения
Процедура ОткрытьВыборПодписантовДокумента(Организация, Объект, Форма) Экспорт
	
	Подписанты = ПодписантыДокумента(Объект);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр", 			Форма.ТолькоПросмотр);
	ПараметрыФормы.Вставить("Организация", 				Организация);
	ПараметрыФормы.Вставить("Документ", 				Объект);
	ПараметрыФормы.Вставить("Подписанты", 				Подписанты);
	ИмяФормы = ИмяФормыСпискаПодписантов();
	ОткрытьФорму(
		ИмяФормы,
		ПараметрыФормы,
		Форма,
		Форма.УникальныйИдентификатор);
	
КонецПроцедуры

// Возвращает подписантов документа.
// 
// Параметры:
//  Объект - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО -
// 
// Возвращаемое значение:
//  Массив из Структура - Подписанты документа
Функция ПодписантыДокумента(Объект) Экспорт
	
	ПодписантыЕИС = ЭлектронноеАктированиеЕИСВызовСервера.ПодписантыЕИС(Объект);
	
	Подписанты = Новый Массив;
	Для каждого Подписант Из ПодписантыЕИС Цикл
		ДанныеПодписанта = ЭлектронноеАктированиеЕИСКлиентСервер.НовыеСведенияОПодписантеДляДокумента();
		ЗаполнитьЗначенияСвойств(ДанныеПодписанта, Подписант);
		Подписанты.Добавить(ДанныеПодписанта);
	КонецЦикла;
	
	Возврат Подписанты;
	
КонецФункции

// Открыть места доставки документа для ЕИС.
// 
// Параметры:
//  Объект - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  Форма - ФормаКлиентскогоПриложения
Процедура ОткрытьМестаДоставкиДокументаДляЕИС(Объект, Форма) Экспорт
	
	МестаПоставкиДляЕИС = ЭлектронноеАктированиеЕИСВызовСервера.МестаПоставкиДляЕИС(Объект);
	
	МестаПоставки = Новый Массив;
	Для каждого Строка Из МестаПоставкиДляЕИС Цикл
		МестоПоставки = ЭлектронноеАктированиеЕИСКлиентСервер.НовоеМестоПоставки();
		ЗаполнитьЗначенияСвойств(МестоПоставки, Строка);
		МестаПоставки.Добавить(МестоПоставки);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр", 			Форма.ТолькоПросмотр);
	ПараметрыФормы.Вставить("МестаПоставки", 			МестаПоставки);
	ПараметрыФормы.Вставить("Документ", 				Объект);
	
	ИмяФормы = ИмяФормыМестДоставкиДляЕИС();
	ОткрытьФорму(ИмяФормы, ПараметрыФормы, Форма);
	
КонецПроцедуры

// Открыть личный кабинет поставщика ЕИС.
// 
// Параметры:
//  ОповещениеОбратногоВызова - ОписаниеОповещения,
//    Неопределено
Процедура ОткрытьЛичныйКабинетПоставщикаЕИС(ОповещениеОбратногоВызова = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеОбратногоВызова", ОповещениеОбратногоВызова);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьЛичныйКабинетПоставщикаЕИСЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
	ГиперссылкаЛКП = ЭлектронноеАктированиеЕИСКлиентСервер.ГиперссылкаОткрытияЛичногоКабинетаПоставщика();
	НачатьЗапускПриложения(ОписаниеОповещения,
		"powershell -windowStyle hidden Start-Process 'iexplore.exe' -ArgumentList "
			+ ГиперссылкаЛКП + " -Verb RunAs -Wait", ,
		Истина); 
	
КонецПроцедуры

// Открыть мастер настроек.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация
//  УникальныйИдентификаторФормы - Строка,
//     УникальныйИдентификатор
//  ОповещениеОЗакрытии - ОписаниеОповещения,
//     Неопределено
Процедура ОткрытьМастерНастроек(Организация, УникальныйИдентификаторФормы, ОповещениеОЗакрытии = Неопределено) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	
	ОткрытьФорму("РегистрСведений.НастройкиОбменаЕИС.Форма.ФормаМастера", ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификаторФормы, , , ОповещениеОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

// Открыть мастер настроек для заказчика.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация.
//  УникальныйИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор формы.
//  ОповещениеОЗакрытии - Неопределено, ОписаниеОповещения - оповещение о закрытии.
Процедура ОткрытьМастерНастроекДляЗаказчика(Организация,
				УникальныйИдентификаторФормы,
				ОповещениеОЗакрытии = Неопределено) Экспорт
	
	Если НЕ ЭлектронноеАктированиеЕИСВызовСервера.ЕстьПодсистемаДляЗаказчиков() Тогда
		Возврат;
	КонецЕсли;
	
	МодульПодсистемы = ОбщегоНазначенияКлиент.ОбщийМодуль("ЭлектронноеАктированиеЗаказчикаЕИСКлиент");
	МодульПодсистемы.ОткрытьМастерНастроекДляЗаказчика(
		Организация, УникальныйИдентификаторФормы, ОповещениеОЗакрытии);
	
КонецПроцедуры

// Имя события сохранения настроек подключения.
// 
// Возвращаемое значение:
//  Строка - Имя события сохранения настроек подключения
Функция ИмяСобытияСохраненияНастроекПодключения() Экспорт
	
	Возврат "ЕИС_СобытиеСохраненияНастроекПодключения";
		
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Вывести ошибку.
// 
// Параметры:
//  ТекстОшибки - Строка
Процедура ВывестиОшибку(ТекстОшибки) Экспорт

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекстОшибки", ТекстОшибки);
	ОткрытьФорму("Обработка.ЭлектронноеАктированиеЕИС.Форма.ФормаОшибки", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

// Гиперссылка строки контракта.
// 
// Возвращаемое значение:
//  Строка - Гиперссылка строки контракта
Функция ГиперссылкаСтрокиКонтракта() Экспорт
	
	Возврат НСтр("ru='Открыть данные строки контракта'");
	
КонецФункции

// Способ обмена через сервер ЕИС.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СпособыОбменаЭД - Способ обмена через сервер ЕИС
Функция СпособОбменаЧерезСерверЕИС() Экспорт
	
	Возврат ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСерверЕИС");
	
КонецФункции

// Открывает мастер подключения интернет-поддержки.
// 
// Параметры:
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  ПараметрыОбработкиОшибок - См. ОбработкаНеисправностейБЭДКлиент.НовыеПараметрыОбработкиОшибок
Процедура ПодключениеИнтернетПоддержки(КонтекстДиагностики, ПараметрыОбработкиОшибок) Экспорт
	
	ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей();
	
КонецПроцедуры

// Открывает настройку интернет-поддержки.
// 
// Параметры:
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  ПараметрыОбработкиОшибок - См. ОбработкаНеисправностейБЭДКлиент.НовыеПараметрыОбработкиОшибок
Процедура НастройкиИнтернетПоддержки(КонтекстДиагностики, ПараметрыОбработкиОшибок) Экспорт
	
	ИнтернетПоддержкаПользователейКлиент.ОткрытьЛичныйКабинетПользователя();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Имя формы мест доставки для ЕИС.
// 
// Возвращаемое значение:
//  Строка - Имя формы мест доставки для ЕИС
Функция ИмяФормыМестДоставкиДляЕИС()
	
	Возврат "Обработка.ЭлектронноеАктированиеЕИС.Форма.ФормаДоставкаДляЕИС";
	
КонецФункции

// Имя формы списка подписантов.
// 
// Возвращаемое значение:
//  Строка
Функция ИмяФормыСпискаПодписантов()
	
	Возврат "Обработка.ЭлектронноеАктированиеЕИС.Форма.ФормаПодписиДляЕИС";
	
КонецФункции

// Имя формы приложений для ЕИС.
// 
// Возвращаемое значение:
//  Строка
Функция ИмяФормыПриложенийДляЕИС()
	
	Возврат "Обработка.ЭлектронноеАктированиеЕИС.Форма.ФормаПриложенныеДокументыДляЕИС";
	
КонецФункции

// Открыть личный кабинет поставщика ЕИСЗавершение.
// 
// Параметры:
//  КодВозврата - Строка
//  ДополнительныеПараметры - Структура
Процедура ОткрытьЛичныйКабинетПоставщикаЕИСЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	ОповещениеОбратногоВызова = ДополнительныеПараметры.ОповещениеОбратногоВызова;
	Если ЗначениеЗаполнено(ОповещениеОбратногоВызова) Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОбратногоВызова, КодВозврата = 0);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти