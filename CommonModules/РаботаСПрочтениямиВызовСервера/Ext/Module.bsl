////////////////////////////////////////////////////////////////////////////////
// Модуль для работы с пометкой прочтения.
//
////////////////////////////////////////////////////////////////////////////////
#Область СлужебныйПрограммныйИнтерфейс

// Установить свойство прочтен.
// 
// Параметры:
//  Ссылка - ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО - Ссылка
//  Прочтен - Булево - Прочтен
// 
// Возвращаемое значение:
//  Булево - признак, что свойство прочтен установлено
Функция УстановитьСвойствоПрочтен(Ссылка, Прочтен = Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО")
		Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭлектронныйДокументИсходящийЭДО") Тогда
		ПрочтениеУстановлено = УстановитьСвойствоПрочтенОбъект(Ссылка, Прочтен);
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("Массив") Тогда
		ПрочтениеУстановлено = УстановитьСвойствоПрочтенМассив(Ссылка, Прочтен);
		
	Иначе
		ПрочтениеУстановлено = Ложь;
	КонецЕсли;
	
	Возврат ПрочтениеУстановлено;
	
КонецФункции

// Получить свойство прочтен.
// 
// Параметры:
//  Объект - ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО - Ссылка
//  Пользователь - СправочникСсылка.Пользователи - Пользователь
// 
// Возвращаемое значение:
//  Булево - свойство прочтен
Функция ПолучитьСвойствоПрочтен(Объект, Знач Пользователь = Неопределено) Экспорт
	
	Возврат РегистрыСведений.СведенияОПрочтенииДокументовЭДО.ОбъектБылПрочтен(Объект, Пользователь);
	
КонецФункции

Функция ПолучитьДатуПереходаНаВерсиюСПрочтенностью() Экспорт
	
	Возврат Константы.ДатаОбновленияНаВерсиюСПрочтенностью.Получить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции	

Функция УстановитьСвойствоПрочтенОбъект(Объект, Прочтен)
	
	Если ЗначениеЗаполнено(Объект) Тогда
		
		ПрочтенЗначениеВРегистре = РегистрыСведений.СведенияОПрочтенииДокументовЭДО.ОбъектБылПрочтен(Объект);
		
		Если Прочтен <> ПрочтенЗначениеВРегистре ИЛИ Прочтен = Неопределено Тогда
			
			РегистрыСведений.СведенияОПрочтенииДокументовЭДО.УстановитьСведенияОПрочтении(Объект, Прочтен);
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция УстановитьСвойствоПрочтенМассив(Массив, Прочтен)
	
	ПрочтениеУстановлено = Ложь;
	
	Для каждого Объект Из Массив Цикл
		ПрочтениеУстановленоДляОбъекта = УстановитьСвойствоПрочтен(Объект, Прочтен);
		ПрочтениеУстановлено = ПрочтениеУстановлено Или ПрочтениеУстановленоДляОбъекта;
	КонецЦикла;
	
	Возврат ПрочтениеУстановлено;
	
КонецФункции

#КонецОбласти