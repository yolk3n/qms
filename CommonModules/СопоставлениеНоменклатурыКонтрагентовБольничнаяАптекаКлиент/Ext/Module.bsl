
#Область ПрограммныйИнтерфейс

#Область ПереопределяемыеКоманды_НоменклатурыКонтрагентов

// См. СопоставлениеНоменклатурыКонтрагентовКлиентПереопределяемый.Подключаемый_ВыполнитьКомандуИнтеграции_НоменклатурыКонтрагентов.
Процедура Подключаемый_ВыполнитьКомандуИнтеграции_НоменклатурыКонтрагентов(Форма, Команда, Источник) Экспорт
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтотОбъект, Источник);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСФормамиОбъектовИБ

// См. СопоставлениеНоменклатурыКонтрагентовКлиентПереопределяемый.ПриСозданииНоменклатурыПоДаннымКонтрагента.
Процедура ПриСозданииНоменклатурыПоДаннымКонтрагента(Знач НаборНоменклатурыКонтрагентов, Знач ОповещениеОЗавершении, Контекст, СтандартнаяОбработка = Истина) Экспорт
	
	Если Не НаборНоменклатурыКонтрагентов.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	НоменклатураКонтрагента = НаборНоменклатурыКонтрагентов[0];
	
	КонтекстИсходящий = Новый Структура;
	КонтекстИсходящий.Вставить("ОповещениеОЗавершении"  , ОповещениеОЗавершении);
	КонтекстИсходящий.Вставить("НоменклатураКонтрагента", НоменклатураКонтрагента);
	
	Оповестить = Новый ОписаниеОповещения("ОткрытьФормуНовогоЭлементаНоменклатурыПоНоменклатуреКонтрагента", ЭтотОбъект, КонтекстИсходящий);
	
	Тип = ПредопределенноеЗначение("Справочник.ТипыНоменклатурыРасширенные.Товар");
	ВидНоменклатуры = НоменклатураВызовСервера.ВидНоменклатурыПоУмолчанию(Тип);
	
	Если ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		
		ВыполнитьОбработкуОповещения(Оповестить, ВидНоменклатуры);
		
	Иначе
		
		Отбор = Новый Структура;
		Отбор.Вставить("ТипНоменклатурыРасширенный", Тип);
		
		ОткрытьФорму("Справочник.ВидыНоменклатуры.ФормаВыбора", Новый Структура("Отбор", Отбор),,,,, Оповестить);
		
	КонецЕсли;
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовКлиентПереопределяемый.ОткрытьФормуНоменклатуры.
Процедура ОткрытьФормуНоменклатуры(Знач Параметры, Знач Владелец, Знач Уникальность, Знач ОповещениеОЗакрытии) Экспорт
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта",
		Параметры, Владелец, Уникальность,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовКлиентПереопределяемый.ОткрытьФормуВыбораНоменклатуры.
Процедура ОткрытьФормуВыбораНоменклатуры(Знач Параметры, Знач Владелец, Знач Уникальность) Экспорт
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора",
		Параметры, Владелец, Уникальность,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОткрытьФормуНовогоЭлементаНоменклатурыПоНоменклатуреКонтрагента(ВидНоменклатуры, Контекст) Экспорт
	
	Если ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		
		ЗначенияЗаполнения = СопоставлениеНоменклатурыКонтрагентовБольничнаяАптекаВызовСервера.ДанныеДляНоменклатурыПоНоменклатуреКонтрагента(Контекст.НоменклатураКонтрагента);
		ЗначенияЗаполнения.Вставить("ВидНоменклатуры", ВидНоменклатуры);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		ОписаниеОповещенияОЗакрытииФормы = Новый ОписаниеОповещения("ПриСозданииНоменклатурыПоДаннымКонтрагентаЗавершение", ЭтотОбъект, Контекст);
		
		ФормаНоменклатуры = ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", ПараметрыФормы,,,,, ОписаниеОповещенияОЗакрытииФормы, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Если ФормаНоменклатуры <> Неопределено Тогда
			Если Не ЗначениеЗаполнено(ФормаНоменклатуры.Объект.Наименование) Тогда
				ФормаНоменклатуры.Объект.Наименование = ЗначенияЗаполнения.Наименование;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриСозданииНоменклатурыПоДаннымКонтрагентаЗавершение(Знач ДанныеНоменклатуры, Контекст) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныеОбъекты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеНоменклатуры, "ВыбранныеОбъекты");
	Если Не ЗначениеЗаполнено(ВыбранныеОбъекты) Тогда
		Возврат;
	КонецЕсли;
	
	НоменклатураСсылка = Неопределено;
	Для Каждого ВыбранныйОбъект Из ВыбранныеОбъекты Цикл
		НоменклатураСсылка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ВыбранныйОбъект, "Номенклатура");
		Если ЗначениеЗаполнено(НоменклатураСсылка) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(НоменклатураСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Массив;
	
	НоменклатураИБ = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы(НоменклатураСсылка);
	
	СозданныйЭлемент = Новый Структура;
	СозданныйЭлемент.Вставить("НоменклатураКонтрагента", Контекст.НоменклатураКонтрагента);
	СозданныйЭлемент.Вставить("НоменклатураИБ", НоменклатураИБ);
	Результат.Добавить(СозданныйЭлемент);
	
	ВыполнитьОбработкуОповещения(Контекст.ОповещениеОЗавершении, Результат);
	
КонецПроцедуры

#КонецОбласти
