
#Область СлужебныйПрограммныйИнтерфейс

#Область РаботаСоСправочникомНоменклатураКонтрагентов

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ПриИзмененииНоменклатурыСопоставления_НоменклатураКонтрагентов.
Процедура ПриИзмененииНоменклатурыСопоставления_НоменклатураКонтрагентов(Форма, ДанныеСопоставления) Экспорт
	
	ТекущаяСтрока = Новый Структура("Номенклатура, ЕдиницаИзмерения");
	ТекущаяСтрока.Номенклатура     = ДанныеСопоставления.Номенклатура;
	ТекущаяСтрока.ЕдиницаИзмерения = ДанныеСопоставления.Упаковка;
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ПроверитьУпаковкуПоВладельцу(), ТекущаяСтрока.ЕдиницаИзмерения);
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЕдиницуИзмерения(), НоменклатураКлиентСервер.ВидЕдиницы_ПотребительскаяУпаковка());
	
	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТабличнойЧасти(ТекущаяСтрока, СтруктураДействий, Неопределено);
	
	ДанныеСопоставления.Упаковка                    = ТекущаяСтрока.ЕдиницаИзмерения;
	ДанныеСопоставления.ЕдиницаИзмеренияПоУмолчанию = ТекущаяСтрока.ЕдиницаИзмерения;
	ДанныеСопоставления.ИспользоватьХарактеристики  = Ложь;
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ОбработкаПолученияДанныхВыбора_НоменклатураКонтрагентов.
Процедура ОбработкаПолученияДанныхВыбора_НоменклатураКонтрагентов(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	УдаляемыеЭлементыОтбора = Новый Массив;
	Для Каждого КлючИЗначение Из Параметры.Отбор Цикл
		Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			УдаляемыеЭлементыОтбора.Добавить(КлючИЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементыОтбора Цикл
		Параметры.Отбор.Удалить(УдаляемыйЭлемент);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСЕдиницамиИзмерения

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ПриОпределенииСвойствУпаковкиЕдиницыИзмеренияНоменклатуры.
Процедура ПриОпределенииСвойствУпаковкиЕдиницыИзмеренияНоменклатуры(Знач Упаковка, Знач Номенклатура, СвойстваУпаковки) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЕдиницыИзмеренияНоменклатуры.ЕдиницаИзмерения.Наименование               КАК НаименованиеУпаковки,
	|	ЕдиницыИзмеренияНоменклатуры.Номенклатура.ЕдиницаИзмерения.Наименование  КАК НаименованиеБазовойЕдиницыИзмерения,
	|	ЕдиницыИзмеренияНоменклатуры.Номенклатура.ЕдиницаИзмерения.КодОКЕИ       КАК КодОКЕИБазовойЕдиницыИзмерения,
	|	ЕдиницыИзмеренияНоменклатуры.Коэффициент                                 КАК КоличествоБазовойЕдиницыИзмерения,
	|	1                                                                        КАК КоличествоУпаковок
	|ИЗ
	|	РегистрСведений.ЕдиницыИзмеренияНоменклатуры КАК ЕдиницыИзмеренияНоменклатуры
	|ГДЕ
	|	ЕдиницыИзмеренияНоменклатуры.Номенклатура = &Номенклатура
	|	И ЕдиницыИзмеренияНоменклатуры.ЕдиницаИзмерения = &Упаковка
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Упаковка"    , Упаковка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СвойстваУпаковки, Выборка);
	КонецЕсли;
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.НайтиНаименованиеЕдиницыИзмеренияПоКодуОКЕИ.
Процедура НайтиНаименованиеЕдиницыИзмеренияПоКодуОКЕИ(КодОКЕИЕдиницыИзмерения, НаименованиеЕдиницыИзмерения) Экспорт
	
	Если ЗначениеЗаполнено(КодОКЕИЕдиницыИзмерения) Тогда
		ЕдиницыИзмеренияПоКоду = Справочники.ЕдиницыИзмерения.ДобавитьЕдиницыИзмеренияПоКоду(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КодОКЕИЕдиницыИзмерения), Ложь, Истина);
		Если ЗначениеЗаполнено(ЕдиницыИзмеренияПоКоду) Тогда
			НаименованиеЕдиницыИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЕдиницыИзмеренияПоКоду[0], "Наименование");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ВладелецУпаковкиЕдиницыИзмеренияНоменклатура.
Процедура ВладелецУпаковкиЕдиницыИзмеренияНоменклатура(Знач Упаковка, ВладелецУпаковкиНоменклатура) Экспорт
	
	ВладелецУпаковкиНоменклатура = Истина;
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.БазоваяЕдиницаИзмеренияНоменклатуры.
Процедура БазоваяЕдиницаИзмеренияНоменклатуры(Знач Номенклатура, ЕдиницаИзмерения) Экспорт
	
	ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения");
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСДаннымиИБ

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ПриОпределенииВладельцаНоменклатурыКонтрагента.
Процедура ПриОпределенииВладельцаНоменклатурыКонтрагента(Знач Контрагент, Владелец) Экспорт
	
	// По умолчанию
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ПриОпределенииСвойствНоменклатурыИнформационнойБазы.
Процедура ПриОпределенииСвойствНоменклатурыИнформационнойБазы(Знач НаборНоменклатуры, СвойстваНоменклатурИБ) Экспорт
	
	СвойстваНоменклатурИБ = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(НаборНоменклатуры, "ЕдиницаИзмерения");
	Для Каждого КлючИЗначение Из СвойстваНоменклатурИБ Цикл
		СвойстваНоменклатуры = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеСвойстваНоменклатурыИБ();
		СвойстваНоменклатуры.ИспользоватьУпаковки = Истина;
		СвойстваНоменклатуры.ИспользоватьХарактеристики = Ложь;
		СвойстваНоменклатуры.ОбязательноеЗаполнениеХарактеристики = Ложь;
		СвойстваНоменклатуры.ЕдиницаИзмеренияПоУмолчанию = НоменклатураСервер.ОсновнаяЕдиницаИзмерения(КлючИЗначение.Ключ, НоменклатураКлиентСервер.ВидЕдиницы_ПотребительскаяУпаковка());
		СвойстваНоменклатурИБ[КлючИЗначение.Ключ] = СвойстваНоменклатуры;
	КонецЦикла;
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ПриОпределенииПредставленийСопоставленияНоменклатуры.
Процедура ПриОпределенииПредставленийСопоставленияНоменклатуры(Представления) Экспорт
	
	// По умолчанию
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ПриОпределенииСтруктурыНоменклатурыИнформационнойБазы.
Процедура ПриОпределенииСтруктурыНоменклатурыИнформационнойБазы(СтруктураНоменклатуры) Экспорт
	
	СтруктураНоменклатуры.ИмяПараметраСвязиУпаковки = "Отбор.Номенклатура";
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ПриОпределенииШтрихкодовНоменклатурыКонтрагента.
Процедура ПриОпределенииШтрихкодовНоменклатурыКонтрагента(Знач НоменклатураИБ, Штрихкод, ДругиеШтрихкодыНоменклатуры) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Штрихкоды.Штрихкод          КАК Штрихкод,
	|	Штрихкоды.Номенклатура      КАК Номенклатура,
	|	Штрихкоды.ЕдиницаИзмерения  КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ СписокШтрихкодовПоКомбинации
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|ГДЕ
	|	Штрихкоды.Номенклатура = &Номенклатура
	|	И Штрихкоды.ЕдиницаИзмерения = &ЕдиницаИзмерения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Штрихкод,
	|	Номенклатура,
	|	ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыКомбинации.Штрихкод          КАК Штрихкод,
	|	ШтрихкодыКомбинации.Номенклатура      КАК Номенклатура,
	|	ШтрихкодыКомбинации.ЕдиницаИзмерения  КАК ЕдиницаИзмерения
	|ИЗ
	|	СписокШтрихкодовПоКомбинации КАК ШтрихкодыКомбинации
	|ГДЕ
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			СписокШтрихкодовПоКомбинации КАК СписокШтрихкодовПоКомбинации
	|		ГДЕ
	|			СписокШтрихкодовПоКомбинации.Номенклатура = ШтрихкодыКомбинации.Номенклатура
	|			И СписокШтрихкодовПоКомбинации.ЕдиницаИзмерения = ШтрихкодыКомбинации.ЕдиницаИзмерения
	|		ИМЕЮЩИЕ
	|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СписокШтрихкодовПоКомбинации.Штрихкод) = 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Штрихкоды.Штрихкод      КАК Штрихкод,
	|	Штрихкоды.Номенклатура  КАК Номенклатура
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|ГДЕ
	|	Штрихкоды.Номенклатура = &Номенклатура
	|");
	
	Запрос.УстановитьПараметр("Номенклатура"    , НоменклатураИБ.Номенклатура);
	Запрос.УстановитьПараметр("ЕдиницаИзмерения", НоменклатураИБ.Упаковка);
	
	РезультатПакетов = Запрос.ВыполнитьПакет();
	
	ВыборкаШтрихкодаКомбинации = РезультатПакетов[1].Выбрать();
	Если ВыборкаШтрихкодаКомбинации.Следующий() Тогда
		Штрихкод = ВыборкаШтрихкодаКомбинации.Штрихкод;
	КонецЕсли;
	
	ВыборкаШтрихкодыНоменклатуры = РезультатПакетов[2].Выбрать();
	Пока ВыборкаШтрихкодыНоменклатуры.Следующий() Цикл
		ДругиеШтрихкодыНоменклатуры.Добавить(ВыборкаШтрихкодыНоменклатуры.Штрихкод);
	КонецЦикла;
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ПроверитьИспользованиеХарактеристик.
Процедура ПроверитьИспользованиеХарактеристик(ХарактеристикиИспользуются) Экспорт
	
	ХарактеристикиИспользуются = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСоСловарем

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ЗначениеНаименованияДанныхСопоставленияНоменклатурыБЭД.
Процедура ЗначениеНаименованияДанныхСопоставленияНоменклатурыБЭД(Знач СсылкаНаОбъект, ЗначениеНаименования) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Номенклатура") Тогда
		ЗначениеНаименования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "НаименованиеПолное");
	КонецЕсли;
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ЗначенияНаименованийДанныхСопоставленияНоменклатурыБЭД.
Процедура ЗначенияНаименованийДанныхСопоставленияНоменклатурыБЭД(Знач НаборСсылокНаОбъекты, СоответствиеЗначенийНаименований) Экспорт
	
	Если ЗначениеЗаполнено(НаборСсылокНаОбъекты) И ТипЗнч(НаборСсылокНаОбъекты[0]) = Тип("СправочникСсылка.Номенклатура") Тогда
		СоответствиеЗначенийНаименований = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(НаборСсылокНаОбъекты, "НаименованиеПолное");
	КонецЕсли;
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ОпределитьНаименованиеТипаОбъектаСопоставленияНоменклатурыБЭД.
Процедура ОпределитьНаименованиеТипаОбъектаСопоставленияНоменклатурыБЭД(Знач СсылкаНаОбъект, НаименованиеТипаОбъекта) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Номенклатура") Тогда
		НаименованиеТипаОбъекта = "Номенклатура";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПоискСопоставленияНоменклатурыСНоменклатуройИБ

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ТекстЗапросаПоискаВариантовСопоставленияПоШтрихкодамКомбинаций.
Процедура ТекстЗапросаПоискаВариантовСопоставленияПоШтрихкодамКомбинаций(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Штрихкоды.Штрихкод          КАК ШтрихкодКомбинации,
	|	Штрихкоды.Номенклатура      КАК Номенклатура,
	|	Штрихкоды.ЕдиницаИзмерения  КАК Упаковка
	|ПОМЕСТИТЬ НайденныеКомбинации
	|ИЗ
	|	ТаблицаШтрихкодовКомбинации КАК ТаблицаШтрихкодовКомбинации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|		ПО ТаблицаШтрихкодовКомбинации.ШтрихкодКомбинации = Штрихкоды.Штрихкод
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ШтрихкодКомбинации,
	|	Номенклатура,
	|	Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НайденныеКомбинации.ШтрихкодКомбинации КАК ШтрихкодКомбинации
	|ПОМЕСТИТЬ СписокУникальныхШтрихкодовКомбинаций
	|ИЗ
	|	НайденныеКомбинации КАК НайденныеКомбинации
	|
	|СГРУППИРОВАТЬ ПО
	|	НайденныеКомбинации.ШтрихкодКомбинации
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(НайденныеКомбинации.Номенклатура) = 1
	|	И КОЛИЧЕСТВО(НайденныеКомбинации.Упаковка) = 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ШтрихкодКомбинации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НайденныеКомбинации.ШтрихкодКомбинации  КАК ШтрихкодКомбинации,
	|	НайденныеКомбинации.Номенклатура        КАК НоменклатураИБ,
	|	НЕОПРЕДЕЛЕНО                            КАК ХарактеристикаИБ,
	|	НайденныеКомбинации.Упаковка            КАК УпаковкаИБ
	|ИЗ
	|	НайденныеКомбинации КАК НайденныеКомбинации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокУникальныхШтрихкодовКомбинаций КАК СписокУникальныхШтрихкодовКомбинаций
	|		ПО НайденныеКомбинации.ШтрихкодКомбинации = СписокУникальныхШтрихкодовКомбинаций.ШтрихкодКомбинации
	|";
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ИнициализацияТекстаЗапросаПоискаСопоставленияПоНатуральнымКлючам.
Процедура ИнициализацияТекстаЗапросаПоискаСопоставленияПоНатуральнымКлючам(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТаблицаНатуральныхКлючей.ИдентификаторНоменклатуры  КАК ИдентификаторНоменклатуры,
	|	ТаблицаНатуральныхКлючей.Штрихкод                   КАК Штрихкод,
	|	ТаблицаНатуральныхКлючей.ЭтоВнутреннийШтрихкод      КАК ЭтоВнутреннийШтрихкод,
	|	Штрихкоды.Номенклатура                              КАК Номенклатура,
	|	ИСТИНА                                              КАК ШтрихкодСопоставлен,
	|	ТаблицаНатуральныхКлючей.ЭтоНашаНоменклатура        КАК ЭтоНашаНоменклатура
	|ПОМЕСТИТЬ ТаблицаСопоставленныхПоШтрихкодам
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ТаблицаНатуральныхКлючей КАК ТаблицаНатуральныхКлючей
	|		ПО
	|			Штрихкоды.Штрихкод = ТаблицаНатуральныхКлючей.Штрихкод
	|ГДЕ
	|	ТаблицаНатуральныхКлючей.Штрихкод <> """"
	|	И НЕ Штрихкоды.Номенклатура.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторНоменклатуры,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СправочникНоменклатура.Артикул                      КАК Артикул,
	|	ТаблицаНатуральныхКлючей.ИдентификаторНоменклатуры  КАК ИдентификаторНоменклатуры,
	|	СправочникНоменклатура.Ссылка                       КАК Номенклатура,
	|	ИСТИНА                                              КАК АртикулСопоставлен,
	|	ТаблицаНатуральныхКлючей.ЭтоНашаНоменклатура        КАК ЭтоНашаНоменклатура
	|ПОМЕСТИТЬ ТаблицаСопоставленныхПоАртикулам
	|ИЗ
	|	ТаблицаНатуральныхКлючей КАК ТаблицаНатуральныхКлючей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО
	|			ТаблицаНатуральныхКлючей.Артикул = СправочникНоменклатура.Артикул
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ТаблицаСопоставленныхПоШтрихкодам КАК ТаблицаСопоставленныхПоШтрихкодам
	|		ПО
	|			ТаблицаНатуральныхКлючей.ИдентификаторНоменклатуры = ТаблицаСопоставленныхПоШтрихкодам.ИдентификаторНоменклатуры
	|			И НЕ ТаблицаСопоставленныхПоШтрихкодам.ЭтоВнутреннийШтрихкод
	|ГДЕ
	|	ТаблицаНатуральныхКлючей.Артикул <> """"
	|	И ТаблицаСопоставленныхПоШтрихкодам.ЭтоВнутреннийШтрихкод ЕСТЬ NULL
	|	И НЕ СправочникНоменклатура.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторНоменклатуры,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТаблицаНатуральныхКлючей.ИдентификаторНоменклатуры  КАК ИдентификаторНоменклатуры,
	|	СправочникНоменклатура.Ссылка                       КАК Номенклатура,
	|	ИСТИНА                                              КАК НаименованиеСопоставлено,
	|	ТаблицаНатуральныхКлючей.ЭтоНашаНоменклатура        КАК ЭтоНашаНоменклатура
	|ПОМЕСТИТЬ ТаблицаСопоставленныхПоНаименованию
	|ИЗ
	|	ТаблицаНатуральныхКлючей КАК ТаблицаНатуральныхКлючей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО
	|			ТаблицаНатуральныхКлючей.Наименование = СправочникНоменклатура.Наименование
	|ГДЕ
	|	НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ТаблицаСопоставленныхПоШтрихкодам КАК ТаблицаСопоставленныхПоШтрихкодам
	|		ГДЕ
	|			ТаблицаСопоставленныхПоШтрихкодам.ИдентификаторНоменклатуры = ТаблицаНатуральныхКлючей.ИдентификаторНоменклатуры
	|			И НЕ ТаблицаСопоставленныхПоШтрихкодам.ЭтоВнутреннийШтрихкод)
	|	И ТаблицаНатуральныхКлючей.ЭтоНашаНоменклатура
	|	И НЕ СправочникНоменклатура.ЭтоГруппа
	|	И НЕ СправочникНоменклатура.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаНатуральныхКлючей.ИдентификаторНоменклатуры,
	|	СправочникНоменклатура.Ссылка,
	|	ИСТИНА,
	|	ТаблицаНатуральныхКлючей.ЭтоНашаНоменклатура
	|ИЗ
	|	ТаблицаНатуральныхКлючей КАК ТаблицаНатуральныхКлючей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО
	|			ТаблицаНатуральныхКлючей.Наименование = СправочникНоменклатура.НаименованиеПолное
	|ГДЕ
	|	НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ТаблицаСопоставленныхПоШтрихкодам КАК ТаблицаСопоставленныхПоШтрихкодам
	|		ГДЕ
	|			ТаблицаСопоставленныхПоШтрихкодам.ИдентификаторНоменклатуры = ТаблицаНатуральныхКлючей.ИдентификаторНоменклатуры
	|			И НЕ ТаблицаСопоставленныхПоШтрихкодам.ЭтоВнутреннийШтрихкод)
	|	И ТаблицаНатуральныхКлючей.ЭтоНашаНоменклатура
	|	И НЕ СправочникНоменклатура.ЭтоГруппа
	|	И НЕ СправочникНоменклатура.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторНоменклатуры,
	|	Номенклатура
	|";
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ТекстЗапросаПоискаВариантовСопоставленияУпаковкиНоменклатурыБЭД.
Процедура ТекстЗапросаПоискаВариантовСопоставленияУпаковкиНоменклатурыБЭД(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаНоменклатуры.НоменклатураИБ         КАК НоменклатураИБ,
	|	ТаблицаНоменклатуры.ИдентификаторУпаковки  КАК ИдентификаторУпаковки,
	|	УпаковкиНоменклатуры.ЕдиницаИзмерения      КАК УпаковкаИБ,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения <> 0
	|			И (ТаблицаНоменклатуры.КоличествоУпаковок = 1 ИЛИ ТаблицаНоменклатуры.КоличествоУпаковок = 0)
	|			И УпаковкиНоменклатуры.Коэффициент = ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения
	|			ТОГДА 0
	|		КОГДА ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения <> 0
	|			И УпаковкиНоменклатуры.Коэффициент = ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения
	|			ТОГДА 20
	|		ИНАЧЕ 80
	|	КОНЕЦ КАК Порядок
	|ПОМЕСТИТЬ УпаковкаПоНаименованию
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ЕдиницыИзмеренияНоменклатуры КАК УпаковкиНоменклатуры
	|		ПО
	|			УпаковкиНоменклатуры.Номенклатура = ТаблицаНоменклатуры.НоменклатураИБ.Ссылка
	|			И УпаковкиНоменклатуры.ЕдиницаИзмерения.НаименованиеПолное = ТаблицаНоменклатуры.НаименованиеУпаковки
	|ГДЕ
	|	ТаблицаНоменклатуры.НаименованиеУпаковки <> """"
	|	И ТаблицаНоменклатуры.ИспользоватьУпаковки
	|	И ТаблицаНоменклатуры.УпаковкаЗадана
	|	И НЕ УпаковкиНоменклатуры.ЕдиницаИзмерения.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.НоменклатураИБ         КАК НоменклатураИБ,
	|	ТаблицаНоменклатуры.ИдентификаторУпаковки  КАК ИдентификаторУпаковки,
	|	УпаковкиНоменклатуры.ЕдиницаИзмерения      КАК УпаковкаИБ,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения <> 0
	|			И (ТаблицаНоменклатуры.КоличествоУпаковок = 1 ИЛИ ТаблицаНоменклатуры.КоличествоУпаковок = 0)
	|			И УпаковкиНоменклатуры.Коэффициент = ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения
	|			ТОГДА 10
	|		КОГДА ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения <> 0
	|			И УпаковкиНоменклатуры.Коэффициент = ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения
	|			ТОГДА 30
	|		ИНАЧЕ 90
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ЕдиницыИзмеренияНоменклатуры КАК УпаковкиНоменклатуры
	|		ПО
	|			УпаковкиНоменклатуры.Номенклатура = ТаблицаНоменклатуры.НоменклатураИБ.Ссылка
	|			И УпаковкиНоменклатуры.ЕдиницаИзмерения.Наименование = ТаблицаНоменклатуры.НаименованиеУпаковки
	|ГДЕ
	|	ТаблицаНоменклатуры.НаименованиеУпаковки <> """"
	|	И ТаблицаНоменклатуры.ИспользоватьУпаковки
	|	И ТаблицаНоменклатуры.УпаковкаЗадана
	|	И НЕ УпаковкиНоменклатуры.ЕдиницаИзмерения.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаНоменклатуры.НоменклатураИБ         КАК НоменклатураИБ,
	|	ТаблицаНоменклатуры.ИдентификаторУпаковки  КАК ИдентификаторУпаковки,
	|	УпаковкиНоменклатуры.ЕдиницаИзмерения      КАК УпаковкаИБ,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения <> 0
	|			И (ТаблицаНоменклатуры.КоличествоУпаковок = 1 ИЛИ ТаблицаНоменклатуры.КоличествоУпаковок = 0)
	|			И УпаковкиНоменклатуры.Коэффициент = ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения
	|			ТОГДА 40
	|		КОГДА ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения <> 0
	|			И УпаковкиНоменклатуры.Коэффициент = ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения
	|			ТОГДА 50
	|		ИНАЧЕ 100
	|	КОНЕЦ КАК Порядок
	|ПОМЕСТИТЬ УпаковкаПоЕдиницеИзмерения
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ЕдиницыИзмеренияНоменклатуры КАК УпаковкиНоменклатуры
	|		ПО
	|			УпаковкиНоменклатуры.Номенклатура = ТаблицаНоменклатуры.НоменклатураИБ.Ссылка
	|			И ВЫБОР
	|				КОГДА ТаблицаНоменклатуры.ЕдиницаИзмерения <> """"
	|					ТОГДА ТаблицаНоменклатуры.ЕдиницаИзмерения = УпаковкиНоменклатуры.ЕдиницаИзмерения.БазоваяЕдиницаИзмерения.Наименование
	|			КОНЕЦ
	|			И ВЫБОР
	|				КОГДА ТаблицаНоменклатуры.ЕдиницаИзмеренияКод <> """"
	|					ТОГДА ТаблицаНоменклатуры.ЕдиницаИзмеренияКод = УпаковкиНоменклатуры.ЕдиницаИзмерения.БазоваяЕдиницаИзмерения.КодОКЕИ
	|			КОНЕЦ
	|ГДЕ
	|	ТаблицаНоменклатуры.ИспользоватьУпаковки
	|	И ТаблицаНоменклатуры.УпаковкаЗадана
	|	И НЕ УпаковкиНоменклатуры.ЕдиницаИзмерения.ПометкаУдаления
	|	И НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			УпаковкаПоНаименованию КАК УпаковкаПоНаименованию
	|		ГДЕ
	|			УпаковкаПоНаименованию.ИдентификаторУпаковки = ТаблицаНоменклатуры.ИдентификаторУпаковки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаНоменклатуры.НоменклатураИБ         КАК НоменклатураИБ,
	|	ТаблицаНоменклатуры.ИдентификаторУпаковки  КАК ИдентификаторУпаковки,
	|	УпаковкиНоменклатуры.ЕдиницаИзмерения      КАК УпаковкаИБ,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения <> 0
	|			И (ТаблицаНоменклатуры.КоличествоУпаковок = 1 ИЛИ ТаблицаНоменклатуры.КоличествоУпаковок = 0)
	|			И УпаковкиНоменклатуры.Коэффициент = ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения
	|			ТОГДА 60
	|		КОГДА ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения <> 0
	|			И УпаковкиНоменклатуры.Коэффициент = ТаблицаНоменклатуры.КоличествоБазовойЕдиницыИзмерения
	|			ТОГДА 70
	|		ИНАЧЕ 110
	|	КОНЕЦ КАК Порядок
	|ПОМЕСТИТЬ УпаковкаПоНаименованиюЕдиницыИзмерения
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ЕдиницыИзмеренияНоменклатуры КАК УпаковкиНоменклатуры
	|		ПО
	|			УпаковкиНоменклатуры.Номенклатура = ТаблицаНоменклатуры.НоменклатураИБ.Ссылка
	|			И ТаблицаНоменклатуры.ЕдиницаИзмерения = УпаковкиНоменклатуры.ЕдиницаИзмерения.БазоваяЕдиницаИзмерения.Наименование
	|ГДЕ
	|	ТаблицаНоменклатуры.ИспользоватьУпаковки
	|	И ТаблицаНоменклатуры.УпаковкаЗадана
	|	И НЕ УпаковкиНоменклатуры.ЕдиницаИзмерения.ПометкаУдаления
	|	И ТаблицаНоменклатуры.ЕдиницаИзмерения <> """"
	|	И НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			УпаковкаПоНаименованию КАК УпаковкаПоНаименованию
	|		ГДЕ
	|			УпаковкаПоНаименованию.ИдентификаторУпаковки = ТаблицаНоменклатуры.ИдентификаторУпаковки)
	|	И НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			УпаковкаПоЕдиницеИзмерения КАК УпаковкаПоЕдиницеИзмерения
	|		ГДЕ
	|			УпаковкаПоЕдиницеИзмерения.ИдентификаторУпаковки = ТаблицаНоменклатуры.ИдентификаторУпаковки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпаковкаПоНаименованию.НоменклатураИБ         КАК НоменклатураИБ,
	|	УпаковкаПоНаименованию.ИдентификаторУпаковки  КАК ИдентификаторУпаковки,
	|	УпаковкаПоНаименованию.УпаковкаИБ             КАК УпаковкаИБ,
	|	УпаковкаПоНаименованию.Порядок                КАК Порядок
	|ИЗ
	|	УпаковкаПоНаименованию КАК УпаковкаПоНаименованию
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	УпаковкаПоЕдиницеИзмерения.НоменклатураИБ         КАК НоменклатураИБ,
	|	УпаковкаПоЕдиницеИзмерения.ИдентификаторУпаковки  КАК ИдентификаторУпаковки,
	|	УпаковкаПоЕдиницеИзмерения.УпаковкаИБ             КАК УпаковкаИБ,
	|	УпаковкаПоЕдиницеИзмерения.Порядок                КАК Порядок
	|ИЗ
	|	УпаковкаПоЕдиницеИзмерения КАК УпаковкаПоЕдиницеИзмерения
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	УпаковкаПоНаименованиюЕдиницыИзмерения.НоменклатураИБ         КАК НоменклатураИБ,
	|	УпаковкаПоНаименованиюЕдиницыИзмерения.ИдентификаторУпаковки  КАК ИдентификаторУпаковки,
	|	УпаковкаПоНаименованиюЕдиницыИзмерения.УпаковкаИБ             КАК УпаковкаИБ,
	|	УпаковкаПоНаименованиюЕдиницыИзмерения.Порядок                КАК Порядок
	|ИЗ
	|	УпаковкаПоНаименованиюЕдиницыИзмерения КАК УпаковкаПоНаименованиюЕдиницыИзмерения
	|УПОРЯДОЧИТЬ ПО Порядок
	|";
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
