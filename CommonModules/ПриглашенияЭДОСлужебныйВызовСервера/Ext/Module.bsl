
#Область СлужебныеПроцедурыИФункции

#Область ОперацииСПриглашениями

Функция НачатьОтправкуПриглашений(Приглашения, КлючиСинхронизации, ИдентификаторФормы) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Отправка приглашений к обмену электронными документами'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"ПриглашенияЭДОСлужебный.ОтправитьПриглашения",
		Приглашения, КлючиСинхронизации);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

Функция НачатьПринятиеОтклонениеПриглашений(Приглашения, КлючиСинхронизации, ИдентификаторФормы, Принять) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Принятие/отклонение приглашений к обмену электронными документами'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"ПриглашенияЭДОСлужебный.ПринятьОтклонитьПриглашения",
		Приглашения, КлючиСинхронизации, Принять);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

Функция КлючПриглашенияПоНатуральнымКлючам(НатуральныеКлючиПриглашения) Экспорт
	
	Возврат ПриглашенияЭДОСлужебный.КлючПриглашенияПоНатуральнымКлючам(НатуральныеКлючиПриглашения);
	
КонецФункции

Функция КлючЗаписиПриглашения(КлючПриглашения) Экспорт
	
	Возврат ПриглашенияЭДО.КлючЗаписиПриглашения(КлючПриглашения);
	
КонецФункции

#КонецОбласти

#Область СоглашенияНаРоуминг

Функция ЗапуститьПодготовкуШаблонаСоглашенияНаРоуминг(Знач ИдентификаторЭДО) Экспорт
	
	ВидОперации = НСтр("ru = 'Получение шаблона соглашения на роуминг.'");
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтрШаблон(НСтр("ru = 'Обмен с контрагентами. %1'"), ВидОперации);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"ПриглашенияЭДОСлужебный.ШаблонСоглашенияНаРоуминг",
		ИдентификаторЭДО);
	
КонецФункции

Функция ДоступныеРасширенияСканаЗаявленияНаРоуминг(ИдентификаторУчетнойЗаписи) Экспорт
	
	ЗапросОператоров = СервисНастроекЭДО.ЗапросОператоровЭДО("ОператорыЭДО");
	Отбор = УчетныеЗаписиЭДО.НовыйОтборУчетныхЗаписей();
	Отбор.УчетныеЗаписи = "&ИдентификаторУчетнойЗаписи";
	ЗапросУчетныхЗаписей = УчетныеЗаписиЭДО.ЗапросУчетныхЗаписей("УчетныеЗаписиЭДО", Отбор);
	
	Запросы = Новый Массив;
	Запросы.Добавить(ЗапросОператоров);
	Запросы.Добавить(ЗапросУчетныхЗаписей);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОператорыЭДО.РасширениеФайлаСоглашенияНаРоуминг КАК РасширениеФайлаСоглашенияНаРоуминг
		|ИЗ
		|	ОператорыЭДО КАК ОператорыЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|		ПО ОператорыЭДО.ИдентификаторОператора = УчетныеЗаписиЭДО.ОператорЭДО";
	
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	ИтоговыйЗапрос.УстановитьПараметр("ИдентификаторУчетнойЗаписи", ИдентификаторУчетнойЗаписи);
	
	Выборка = ИтоговыйЗапрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.РасширениеФайлаСоглашенияНаРоуминг;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция ЗаписатьДанныеСоглашенияНаРоуминг(Знач ИдентификаторОрганизации, Знач Адрес, Знач ИмяФайла) Экспорт
	
	Возврат ПриглашенияЭДОСлужебный.ЗаписатьДанныеСоглашенияНаРоуминг(ИдентификаторОрганизации, Адрес, ИмяФайла);
	
КонецФункции

#КонецОбласти

#КонецОбласти