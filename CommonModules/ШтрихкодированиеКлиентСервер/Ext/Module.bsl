
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Функция вычисляет контрольный символ кода EAN
//
// Параметры:
//  Штрихкод     - штрихкод (без контрольной цифры)
//  Тип          - тип штрихкода: 13 - EAN13, 8 - EAN8
//
// Возвращаемое значение:
//  Контрольный символ штрихкода
//
Функция КонтрольныйСимволEAN(ШтрихКод, Тип) Экспорт
	
	ЧетныеЦифры   = 0;
	НечетныеЦифры = 0;
	
	КоличествоИтераций = ?(Тип = 13, 6, 4);
	
	Для Индекс = 1 По КоличествоИтераций Цикл
		Если (Тип = 8) И (Индекс = КоличествоИтераций) Тогда
		Иначе
			ЧетныеЦифры = ЧетныеЦифры + Сред(ШтрихКод, 2 * Индекс, 1);
		КонецЕсли;
		НечетныеЦифры = НечетныеЦифры + Сред(ШтрихКод, 2 * Индекс - 1, 1);
	КонецЦикла;
	
	Если Тип = 13 Тогда
		ЧетныеЦифры = ЧетныеЦифры * 3;
	Иначе
		НечетныеЦифры = НечетныеЦифры * 3;
	КонецЕсли;
	
	КонтрольнаяЦифра = 10 - (ЧетныеЦифры + НечетныеЦифры) % 10;
	
	Возврат ?(КонтрольнаяЦифра = 10, "0", Строка(КонтрольнаяЦифра));
	
КонецФункции

// Возвращает числовой код для печати штрихкода
//
// Параметры:
//  Ссылка - ЛюбаяСсылка - ссылка на объект, по которому нужно получить код
//
// Возвращаемое значение:
//  Строка - строка из чисел, соответствующая переданной ссылке
//
Функция ЧисловойКодПоСсылке(Ссылка) Экспорт
	
	ШестнадцатеричноеЧисло = СтрЗаменить(Строка(Ссылка.УникальныйИдентификатор()), "-", "");
	Возврат ПреобразоватьЧислоИзШестнадцатеричнойСистемыСчисленияВДесятичную(ШестнадцатеричноеЧисло);
	
КонецФункции

// Возвращает уникальный идентификатор по значению штрихкода
//
// Параметры:
//  Штрихкод - Строка - значение штрихкода
//
// Возвращаемое значение:
//  УникальныйИдентификатор или Неопределено
//
Функция УникальныйИдентификаторПоШтрихкоду(Штрихкод) Экспорт
	
	Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Штрихкод, Ложь, Ложь) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ШтрихкодВШестнадцатеричномВиде = ПреобразоватьЧислоИзДесятичнойСистемуСчисленияВШестнадцатеричную(Число(Штрихкод));
	Пока СтрДлина(ШтрихкодВШестнадцатеричномВиде) < 32 Цикл
		ШтрихкодВШестнадцатеричномВиде = "0" + ШтрихкодВШестнадцатеричномВиде;
	КонецЦикла;
	
	Идентификатор =
		Сред(ШтрихкодВШестнадцатеричномВиде, 1,  8)
		+ "-"
		+ Сред(ШтрихкодВШестнадцатеричномВиде, 9,  4)
		+ "-"
		+ Сред(ШтрихкодВШестнадцатеричномВиде, 13, 4)
		+ "-"
		+ Сред(ШтрихкодВШестнадцатеричномВиде, 17, 4)
		+ "-"
		+ Сред(ШтрихкодВШестнадцатеричномВиде, 21, 12);
		
	Возврат Новый УникальныйИдентификатор(Идентификатор);
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Функция ПреобразоватьЧислоИзДесятичнойСистемуСчисленияВШестнадцатеричную(Знач ДесятичноеЧисло)
	
	ШестнадцатеричноеЧисло = "";
	
	Пока ДесятичноеЧисло > 0 Цикл
		ОстатокОтДеления = ДесятичноеЧисло % 16;
		ДесятичноеЧисло = (ДесятичноеЧисло - ОстатокОтДеления) / 16;
		ШестнадцатеричноеЧисло = Сред("0123456789abcdef", ОстатокОтДеления + 1, 1) + ШестнадцатеричноеЧисло;
	КонецЦикла;
	
	Возврат ШестнадцатеричноеЧисло;
	
КонецФункции

Функция ПреобразоватьЧислоИзШестнадцатеричнойСистемыСчисленияВДесятичную(Знач ШестнадцатеричноеЧисло)
	
	ШестнадцатеричноеЧисло = НРег(ШестнадцатеричноеЧисло);
	ДлинаСтроки = СтрДлина(ШестнадцатеричноеЧисло);
	
	ДесятичноеЧисло = 0;
	Для НомерСимвола = 1 По ДлинаСтроки Цикл
		ДесятичноеЧисло = ДесятичноеЧисло * 16 + Найти("0123456789abcdef", Сред(ШестнадцатеричноеЧисло, НомерСимвола, 1)) - 1;
	КонецЦикла;
	
	Возврат Формат(ДесятичноеЧисло, "ЧГ=0");
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
