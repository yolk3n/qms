
#Область ПрограммныйИнтерфейс

// Общая процедура обработки навигационной ссылки текста счета-фактуры в документах закупки
// В зависимости от выбранной ссылки вызывает создание/открытие счетов-фактуры документа.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма, в которой располагается элемент.
//  НавигационнаяСсылка - Строка - Значение гиперссылки форматированной строки.
//  СтандартнаяОбработка - Булево - Признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработатьНавигационнуюСсылкуСчетаФактуры(Форма, НавигационнаяСсылка, СтандартнаяОбработка, ОбработкаОповещения = Неопределено) Экспорт
	
	Если НавигационнаяСсылка = "ВвестиСчетФактуру" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		РеквизитыСчетаФактуры = Новый Структура("ПредъявленСчетФактура, НомерВходящегоСчетаФактуры, ДатаВходящегоСчетаФактуры");
		ЗаполнитьЗначенияСвойств(РеквизитыСчетаФактуры, Форма.Объект);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ДополнительныеПараметры.Вставить("ОбработкаОповещения", ОбработкаОповещения);
		Оповестить = Новый ОписаниеОповещения("ОбработатьВводСчетаФактуры", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Документ.ПоступлениеТоваров.Форма.ВвестиСчетФактуру", РеквизитыСчетаФактуры, Форма,,,, Оповестить);
		
	КонецЕсли;
	
КонецПроцедуры

#Область ПроцедурыИФункцииРаботыСНоменклатуройПоставщикаВДокументахЗакупки

// Открывает форму заполнения номенклатуры по номенклатуре поставщика.
//
// Параметры:
//  ОписаниеОповещения - ОписаниеОповещения - описание оповещения.
//  АдресТоваровВХранилище - Строка - адрес товаров в хранилище.
//  Форма - ФормаКлиентскогоПриложения - владелец открываемой формы.
//
Процедура ОткрытьФормуЗаполненияНоменклатурыПоНоменклатуреПоставщика(ОписаниеОповещения, АдресТоваровВХранилище, Форма) Экспорт
	
	Если АдресТоваровВХранилище = Неопределено Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'Отсутствуют строки, в которых указана номенклатура поставщика, но не указана номенклатура'"));
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Неопределено);
		Возврат;
		
	КонецЕсли;
	
	ОткрытьФорму(
		"ОбщаяФорма.ЗаполнениеНоменклатурыПоНоменклатуреПоставщика",
		Новый Структура("АдресТоваровВХранилище", АдресТоваровВХранилище),
		Форма,
		,
		,
		,
		ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеРеквизитыИСведения

Процедура ОбработатьВводСчетаФактуры(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		Объект = ДополнительныеПараметры.Форма.Объект;
		ЗаполнитьЗначенияСвойств(Объект, Результат);
		ДополнительныеПараметры.Форма.Модифицированность = Истина;
		
		Если ДополнительныеПараметры.ОбработкаОповещения <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработкаОповещения, Результат);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти