
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Устанавливает вид номенклатуры.
//
// Параметры:
//  Форма           - ФормаКлиентскогоПриложения - форма, установки фильтра по виду и свойствам номенклатуры.
//  ВидНоменклатуры - устанавливаемый вид номенклатуры.
//
Процедура УстановитьВидНоменклатуры(Форма, ВидНоменклатуры) Экспорт
	
	Форма[Идентификатор() + "ВидНоменклатуры"] = ВидНоменклатуры;
	
КонецПроцедуры

// Настраивает форму в соответствии со значением ПоказыватьВидыНоменклатуры.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//
Процедура ПоказатьСкрытьВидыНоменклатуры(Форма) Экспорт
	
	Префикс = Идентификатор();
	Форма.Элементы[Префикс + "ВидыНоменклатуры"].Видимость = Форма[Префикс + "ПоказыватьВидыНоменклатуры"];
	Форма.Элементы[Префикс + "ВидНоменклатуры"].Доступность = Не Форма[Префикс + "ПоказыватьВидыНоменклатуры"];
	
	Если Форма[Префикс + "ПоказыватьВидыНоменклатуры"] Тогда
		Заголовок = НСтр("ru = 'Скрыть виды'");
	Иначе
		Заголовок = НСтр("ru = 'Показать виды'");
	КонецЕсли;
	
	Форма.Элементы[Префикс + "ПоказатьСкрытьВидыНоменклатуры"].Заголовок = Заголовок;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает идентификатор фильтра по виду и свойствам номенклатуры.
//
// Возвращаемое значение:
//  Строка - идентификатор фильтра лекарственных средств.
//
Функция Идентификатор() Экспорт
	
	Возврат "ФильтрНоменклатурыПоВидуИСвойствам";
	
КонецФункции

Функция ПредставлениеИнтервалаОтбора(ИнтервалОт, ИнтервалДо, ФорматЗначения) Экспорт
	
	СтрокаИнтервалОт = ?(ЗначениеЗаполнено(ИнтервалОт),       НСтр("ru = 'от'") + " " + Формат(ИнтервалОт, ФорматЗначения), "");
	СтрокаИнтервалДо = ?(ЗначениеЗаполнено(ИнтервалДо), " " + НСтр("ru = 'до'") + " " + Формат(ИнтервалДо, ФорматЗначения), "");
	
	ПредставлениеОтбора = СтрокаИнтервалОт + СтрокаИнтервалДо;
	ПредставлениеОтбора = ?(ЗначениеЗаполнено(ПредставлениеОтбора), ПредставлениеОтбора, НСтр("ru = '<не задано>'"));
	
	Возврат ПредставлениеОтбора;
	
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс