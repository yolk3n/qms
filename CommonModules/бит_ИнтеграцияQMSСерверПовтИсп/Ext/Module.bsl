
// Настройки подключения для МИС qMS
//  
// Возвращаемое значение:
//  Структура - Данные подключения:
//   * АдресСервера 
//   * ИмяПользователя 
//   * Пароль  
//
Функция НастройкиПодключения() Экспорт  
	
	Результат = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	бит_НастройкиИнтеграцииQMS.ИмяПредопределенныхДанных КАК Ключ,
		|	бит_НастройкиИнтеграцииQMS.Значение КАК Значение
		|ИЗ
		|	Справочник.бит_НастройкиИнтеграцииQMS КАК бит_НастройкиИнтеграцииQMS
		|ГДЕ
		|	бит_НастройкиИнтеграцииQMS.Родитель = &Родитель";
	
	Запрос.УстановитьПараметр("Родитель", Справочники.бит_НастройкиИнтеграцииQMS.НастройкиПодключения);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.Ключ,Выборка.Значение);		
	КонецЦикла;  

	Возврат Результат;  
			
КонецФункции


Функция ДополнительноеСведениеХэш() Экспорт
	
	Возврат	бит_ИнтеграцияQMSСервер.ДополнительноеСведениеПоИмени("Хэш_qMS"); 	
	
КонецФункции


Функция ОбъектМодифицирован(Объект) Экспорт
	МетаданныеОбъекта = Объект.Метаданные();
	Если Метаданные.Справочники.Содержит(МетаданныеОбъекта) Тогда
		Вид = "Справочник";
	ИначеЕсли Метаданные.Документы.Содержит(МетаданныеОбъекта) Тогда
		Вид = "Документ";
	Иначе
		ВызватьИсключение СтрШаблон(
			НСтр("ru='Проверка модифицированности для объекта %1 не предусмотрена.'"),
			МетаданныеОбъекта
		);
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	" + Вид + "." + МетаданныеОбъекта.Имя + "
	|ГДЕ
	|	Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат ПроверитьМодифицированностьРеквизитов(Объект, РезультатЗапроса, "");
КонецФункции

Функция ПроверитьМодифицированностьРеквизитов(Объект, РезультатЗапроса, Префикс)
	НеобрабатываемыеРеквизиты = СтрРазделить("Ссылка,НомерСтроки", ",");
	
	Выборка = РезультатЗапроса.Выбрать();
	НомерСтроки = 0;
	Пока Выборка.Следующий() Цикл
		ТабличнаяЧасть = ?(Не ПустаяСтрока(Префикс), Объект[Префикс], Неопределено); 
		Если ТабличнаяЧасть <> Неопределено И Выборка.Количество() <> ТабличнаяЧасть.Количество() Тогда
			Возврат Истина;
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		Для каждого Колонка Из РезультатЗапроса.Колонки Цикл
			Если НеобрабатываемыеРеквизиты.Найти(Колонка.Имя) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначениеДоРедактирования = Выборка[Колонка.Имя];
			ТипЗначения = ТипЗнч(ЗначениеДоРедактирования);
			Если ТипЗначения = Тип("ХранилищеЗначения") Тогда
				// ХранилищеЗначения не проверяем.
				Продолжить;
			ИначеЕсли ТипЗначения = Тип("РезультатЗапроса") Тогда
				Если ПроверитьМодифицированностьРеквизитов(Объект, ЗначениеДоРедактирования, Колонка.Имя) Тогда
					Возврат Истина;
				КонецЕсли;
			Иначе
				ЗначениеПослеРедактирования = ?(ТабличнаяЧасть = Неопределено,
					Объект[Колонка.Имя],
					ТабличнаяЧасть[НомерСтроки - 1][Колонка.Имя]
				);
				Если ЗначениеДоРедактирования <> ЗначениеПослеРедактирования Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции