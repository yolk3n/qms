
#Область СлужебныйПрограммныйИнтерфейс

// Подготовить данные операции.
// 
// Параметры:
//  ПараметрыПодключения - Структура -Параметры подключения
//  Команда - Строка - Команда
//  ПараметрыОперации - Структура -Параметры операции
// 
// Возвращаемое значение:
//  Неопределено.
Функция ПодготовитьДанныеОперации(ПараметрыПодключения, Команда, ПараметрыОперации) Экспорт
	
	ДанныеОперации = ПараметрыОперацииДисплеиПокупателя(); 
	ДанныеОперации.ТекстHTML = "<p>No information</p>";
	ФорматЧисла = "ЧРД=.;ЧЦ=12;ЧДЦ=2;ЧН=0.00;ЧГ=0";
	
	Если Команда = "DisplayInfo" Тогда      
		
		Если ПараметрыОперации.Операция = "СервисныйРежим" Тогда
			
			Если ПараметрыОперации.ГотовностьКРаботе Тогда
				СтрокиТекста = НСтр("ru='ДОБРО ПОЖАЛОВАТЬ!'"); // АПК: 374 - интерфейсная особенность
				СтатусОперации = 1;
			ИначеЕсли НЕ ПараметрыОперации.ГотовностьКРаботе Тогда
				СтрокиТекста = НСтр("ru='КАССА НЕ РАБОТАЕТ!'"); // АПК: 374 - интерфейсная особенность
				СтатусОперации = 2;
			Иначе                                                    
				СтрокиТекста = НСтр("ru='СЕРВИСНЫЙ РЕЖИМ'");
				СтатусОперации = 3;
			КонецЕсли;
			
			ШаблонДисплеиПокупателя = МенеджерОборудованияПовтИсп.СодержимоеОбщегоМакета("ШаблонДисплеяПокупателя");     
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%TITLE%", " ");
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%CAPTION%", XMLСтрока(ПараметрыОперации.НазваниеОрганизации));
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%OPERATION%", " ");     
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%DESC%", " ");     
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%INFO%", СтрокиТекста);  
			ОбновитьСтатус(ШаблонДисплеиПокупателя, СтатусОперации);   
			ДанныеОперации.СтатусОперации = СтатусОперации;
			ДанныеОперации.ТекстHTML = ШаблонДисплеиПокупателя;   
			
			// Текст на обычный дисплей покупателя
			ДанныеОперации.СтрокиТекста = СтрокиТекста; 
			
		ИначеЕсли ПараметрыОперации.Операция = "Информация" Тогда   
			
			СтрокиТекста = ПараметрыОперации.СтрокиТекста;
			СтрокиТекста = СтрЗаменить(СтрокиТекста, Символы.ПС, "<br>"); 
			
			ШаблонДисплеиПокупателя = МенеджерОборудованияПовтИсп.СодержимоеОбщегоМакета("ШаблонДисплеяПокупателя");     
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%TITLE%", " ");
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%CAPTION%", XMLСтрока(ПараметрыОперации.НазваниеОрганизации));
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%OPERATION%", " ");     
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%DESC%", " ");     
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%INFO%", СтрокиТекста);   
			Если ПустаяСтрока(ПараметрыОперации.КартинкаBase64) Тогда                                   
				ОбновитьСтатус(ШаблонДисплеиПокупателя, ПараметрыОперации.СтатусОперации);    
			Иначе
				ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%QR%", ПараметрыОперации.КартинкаBase64);    
			КонецЕсли;                                            
			ДанныеОперации.СтатусОперации = ПараметрыОперации.СтатусОперации;
			ДанныеОперации.ТекстHTML = ШаблонДисплеиПокупателя;
			
			// Текст на обычный дисплей покупателя
			ДанныеОперации.СтрокиТекста = ПараметрыОперации.СтрокиТекста; 
			
		ИначеЕсли ПараметрыОперации.Операция = "ПлатежнаяОперация" Тогда   
			
			ШаблонДисплеиПокупателя = МенеджерОборудованияПовтИсп.СодержимоеОбщегоМакета("ШаблонДисплеяПокупателя");   
			
			Если ПараметрыОперации.СтатусОперации = 1 Тогда
				СтатусТекс = НСтр("ru='УСПЕШНО'");
			ИначеЕсли ПараметрыОперации.СтатусОперации = 2 Тогда
				СтатусТекс = НСтр("ru='ОТКАЗ'");
			ИначеЕсли ПараметрыОперации.СтатусОперации = 3 Тогда
				СтатусТекс = НСтр("ru='ОЖИДАНИЕ ОПЛАТЫ'");
			ИначеЕсли ПараметрыОперации.СтатусОперации = 4 Тогда
				СтатусТекс = НСтр("ru='ИНФОРМАЦИЯ'");         
			КонецЕсли; 
					
			Если ПараметрыОперации.НаличнаяОплата Тогда
				ТипПлатежнойСистемы = НСтр("ru='НАЛИЧНАЯ ОПЛАТА'"); 
			ИначеЕсли Не ПустаяСтрока(ПараметрыОперации.ТипПлатежнойСистемы) Тогда
				ТипПлатежнойСистемы = Строка(ПараметрыОперации.ТипПлатежнойСистемы)
			Иначе
				ТипПлатежнойСистемы = НСтр("ru='БЕЗНАЛИЧНАЯ ОПЛАТА'");      
			КонецЕсли;                                                     
			
			Если ПараметрыОперации.ТипПлатежнойСистемы = Перечисления.ТипыПлатежнойСистемыККТ.СистемаБыстрыхПлатежей 
				И ПараметрыОперации.СтатусОперации = 3 Тогда      
				ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%DESC%", 
					НСтр("ru='Для совершения платежа отсканируйте QR-код'"));
				Если Не ПустаяСтрока(ПараметрыОперации.ЗначениеQRКода) Тогда
					ПараметрыШтрихкода = ГенерацияШтрихкода.ПараметрыГенерацииШтрихкода();
					ПараметрыШтрихкода.Ширина = 300;
					ПараметрыШтрихкода.Высота = 300;
					ПараметрыШтрихкода.ТипКода = 16; // QR
					ПараметрыШтрихкода.Штрихкод = ПараметрыОперации.ЗначениеQRКода;
					ПараметрыШтрихкода.УбратьЛишнийФон = Истина;    
					РезультатШтрихкод = ГенерацияШтрихкода.ИзображениеШтрихкода(ПараметрыШтрихкода);
					КартинкаBase64 = Base64Строка(РезультатШтрихкод.ДвоичныеДанные);       
					ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%QR%", КартинкаBase64);    
					ДанныеОперации.ЗначениеQRКода = ПараметрыОперации.ЗначениеQRКода;
				КонецЕсли;                                                     
			Иначе
				ОбновитьСтатус(ШаблонДисплеиПокупателя, ПараметрыОперации.СтатусОперации);
			КонецЕсли; 
			
			Если ПустаяСтрока(ПараметрыОперации.ДополнительныйТекст) Тогда             
				Если ПараметрыОперации.СтатусОперации = 1 И ПараметрыОперации.НаличнаяОплата И ПараметрыОперации.Сдача > 0 Тогда
					ОперацияОписание = НСтр("ru='СДАЧА'");     
					ОперацияИнфо = Формат(ПараметрыОперации.Сдача, ФорматЧисла);  
				Иначе
					ОперацияОписание = НСтр("ru='СУММА'");     
					ОперацияИнфо = Формат(ПараметрыОперации.Сумма, ФорматЧисла);  
				КонецЕсли;   
			Иначе
				ОперацияОписание = ПараметрыОперации.ДополнительныйТекст;  
				ОперацияИнфо = " ";     
			КонецЕсли;                                                     
			
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%DESC%", ОперацияОписание);  
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%INFO%", ОперацияИнфо);     
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%TITLE%", НСтр("ru='ОПЛАТА'"));
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%CAPTION%", ТипПлатежнойСистемы);
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%OPERATION%", СтатусТекс);    
			ДанныеОперации.СтатусОперации = ПараметрыОперации.СтатусОперации;
			ДанныеОперации.ТекстHTML = ШаблонДисплеиПокупателя;         
			
			// Текст на обычный дисплей покупателя
			ДанныеОперации.СтрокиТекста = НСтр("ru='ПЛАТЕЖНАЯ ОПЕРАЦИЯ'") + Символы.ПС + СтатусТекс;
			
		ИначеЕсли ПараметрыОперации.Операция = "СписокТоваров" Тогда 
			
			ШаблонДисплеиПокупателя = МенеджерОборудованияПовтИсп.СодержимоеОбщегоМакета("ШаблонДисплеяПокупателяЧек");
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%TITLE%", НСтр("ru='Список покупок'"));
			
			КолонкиТабличнойЧасти = ПараметрыОперации.КолонкиТабличнойЧасти;
			ЗаголовокТаблицы = ""; 
			ШаблонСтроки = "<tr>";  
			Для Каждого КолонкаТабличнойЧасти Из ПараметрыОперации.КолонкиТабличнойЧасти Цикл                                        
				Ширина = Строка(?(КолонкаТабличнойЧасти.Ширина > 0, КолонкаТабличнойЧасти.Ширина, 10));
				Заголовок = ?(ПустаяСтрока(КолонкаТабличнойЧасти.Заголовок), КолонкаТабличнойЧасти.Имя, КолонкаТабличнойЧасти.Заголовок);           
				ЗаголовокТаблицы = ЗаголовокТаблицы + СтрШаблон("<th align=""center"" width=""%1"">%2</th>", Ширина + "%", Заголовок);   
				ШаблонСтроки = ШаблонСтроки + СтрШаблон("<td %1>%2</td>", СтрокаВыравнивания(КолонкаТабличнойЧасти.Выравнивание), "%" + КолонкаТабличнойЧасти.Имя + "%");   
			КонецЦикла;                                                        
			ШаблонСтроки = ШаблонСтроки + "</tr>";
			ТабличнаяЧасть = "";
			
			Для Каждого СтрокаТабличнаяЧасть Из ПараметрыОперации.ТабличнаяЧасть Цикл 
				ТекстСтроки = ШаблонСтроки;
				Для Каждого СтрокаТаблицы Из СтрокаТабличнаяЧасть Цикл    
					ТекстСтроки = СтрЗаменить(ТекстСтроки, "%" + СтрокаТаблицы.Ключ + "%", СтрокаТаблицы.Значение);
				КонецЦикла;                
				ТабличнаяЧасть = ТабличнаяЧасть + ТекстСтроки;
			КонецЦикла;     
			
			ПодвалТаблицы = СтрШаблон("<th colspan=""%1"">%2</th><th>%3</th>", 
				КолонкиТабличнойЧасти.Количество() - 1, 
				НСтр("ru='Итого'"),
				Формат(ПараметрыОперации.ПодвалСумма, ФорматЧисла));
			
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "<!--Head-->" , ЗаголовокТаблицы);
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "<!--Table-->", ТабличнаяЧасть);        
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "<!--Foot-->" , ПодвалТаблицы);  
			ДанныеОперации.ТекстHTML = ШаблонДисплеиПокупателя;
			
			// Текст на обычный дисплей покупателя
			ДанныеОперации.СтрокиТекста = СтрШаблон(НСтр("ru='ИТОГО: %1'"), Формат(ПараметрыОперации.ПодвалСумма, ФорматЧисла));
				
		ИначеЕсли ПараметрыОперации.Операция = "ФискальныйЧек" Тогда
			
			ШаблонДисплеиПокупателя = МенеджерОборудованияПовтИсп.СодержимоеОбщегоМакета("ШаблонДисплеяПокупателяЧек");
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "%TITLE%", НСтр("ru='Получите электронный чек'"));
			
			ЗаголовокТаблицы = "<tr><th align=""center"" width=""50%"">%TEXT1%</th><th align=""center"" width=""50%"" colspan=""2"">%TEXT2%</th>";
			ЗаголовокТаблицы = СтрЗаменить(ЗаголовокТаблицы, "%TEXT1%", НСтр("ru='Сканируйте QR-код в мобильном приложении'"));
			ЗаголовокТаблицы = СтрЗаменить(ЗаголовокТаблицы, "%TEXT2%", НСтр("ru='На сайте ФНС mco.nalog.ru по реквизитам'"));   
			
			ПодвалТаблицы = "<tr><th align=""center"" colspan=""3"">%1</th></tr>";
			ПодвалТаблицы = СтрШаблон(ПодвалТаблицы, НСтр("ru='СПАСИБО ЗА ПОКУПКУ'")); 
			
			ТабличнаяЧасть = "";
			 
			Если Не ПустаяСтрока(ПараметрыОперации.ЗначениеQRКода) Тогда          
				ТабличнаяЧасть = "<tr><td align=""center""><br><br><img src=""data:image/png;base64, %QR%""><br><br></td><td>%TEXT1%</td><td>%TEXT2%</td></tr>";
				ПараметрыQRКод = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.РасшифроватьQRКодЧекаККТ(ПараметрыОперации.ЗначениеQRКода);    
				Если ПараметрыQRКод.Расшифрован Тогда                    
					ЗаголовкиПолей = СтрШаблон("%1:<br>%2:<br>%3:<br>%4:<br>%5:<br>",
						НСтр("ru='№ чека'"),
						НСтр("ru='Дата/Время'"),
						НСтр("ru='Сумма'"),
						НСтр("ru='ФН'"),
						НСтр("ru='ФП'"));   
					ЗначениеПолей = СтрШаблон("%1<br>%2<br>%3<br>%4<br>%5<br>",
						ПараметрыQRКод.НомерФискальногоДокумента,
						ПараметрыQRКод.ДатаВремяРасчета,
						Формат(ПараметрыQRКод.СуммаРасчета, ФорматЧисла),
						ПараметрыQRКод.ФискальныйПризнак,
						ПараметрыQRКод.НомерФискальногоНакопителя);       
					ТабличнаяЧасть = СтрЗаменить(ТабличнаяЧасть, "%TEXT1%", ЗаголовкиПолей); 
					ТабличнаяЧасть = СтрЗаменить(ТабличнаяЧасть, "%TEXT2%", ЗначениеПолей); 
				КонецЕсли;
				ПараметрыШтрихкода = ГенерацияШтрихкода.ПараметрыГенерацииШтрихкода();
				ПараметрыШтрихкода.Ширина = 200;
				ПараметрыШтрихкода.Высота = 200;
				ПараметрыШтрихкода.ТипКода = 16; // QR
				ПараметрыШтрихкода.Штрихкод = ПараметрыОперации.ЗначениеQRКода;
				ПараметрыШтрихкода.УбратьЛишнийФон = Истина;    
				РезультатШтрихкод = ГенерацияШтрихкода.ИзображениеШтрихкода(ПараметрыШтрихкода);
				КартинкаBase64 = Base64Строка(РезультатШтрихкод.ДвоичныеДанные);       
				ТабличнаяЧасть = СтрЗаменить(ТабличнаяЧасть, "%QR%", КартинкаBase64);     
				ДанныеОперации.ЗначениеQRКода = ПараметрыОперации.ЗначениеQRКода;
			КонецЕсли;       
			
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "<!--Head-->" , ЗаголовокТаблицы);
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "<!--Table-->", ТабличнаяЧасть);        
			ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "<!--Foot-->" , ПодвалТаблицы);  
			
			ДанныеОперации.ТекстHTML = ШаблонДисплеиПокупателя;
			
			// Текст на обычный дисплей покупателя
			ДанныеОперации.СтрокиТекста = ?(ПустаяСтрока(ПараметрыОперации.СтрокиТекста), НСтр("ru='СПАСИБО ЗА ПОКУПКУ!'"), ПараметрыОперации.СтрокиТекста);   // АПК: 374 - интерфейсная особенность
			
		КонецЕсли;
			
	КонецЕсли;
	
	Возврат ДанныеОперации;       
	
КонецФункции

Процедура ОбработатьДанныеОперации(ПараметрыПодключения, Команда, РезультатВыполнения, ДанныеОперации) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции   

// Заполнить структуру операции
// 
// Возвращаемое значение:
//  Структура - Параметры операции:
// * Результат - Булево.
// * ТекстОшибки - Строка.
Функция ПараметрыОперацииДисплеиПокупателя()
	
	ПараметрыОперации = Новый Структура();
	ПараметрыОперации.Вставить("Результат", Истина); 
	ПараметрыОперации.Вставить("СтрокиТекста");    
	ПараметрыОперации.Вставить("ЗначениеQRКода"); // Строка - значение QR кода
	ПараметрыОперации.Вставить("КартинкаQRКода"); // Строка - строка с base64 представлением png картинки логотипа.      
	ПараметрыОперации.Вставить("СтатусОперации", 0); // Число - Статус: 0-Не установлены, 1-Выполнено успешно, 2-Операция не выполнена
	ПараметрыОперации.Вставить("ТекстОшибки");     
	ПараметрыОперации.Вставить("ТекстHTML");
	Возврат ПараметрыОперации;
	
КонецФункции 

Процедура ОбновитьСтатус(ШаблонДисплеиПокупателя, СтатусОперации) 
	
	Если СтатусОперации = 1 Тогда
		ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "<!--OK", "");
		ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "OK-->", "");
	ИначеЕсли СтатусОперации = 2 Тогда
		ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "<!--FAIL", "");
		ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "FAIL-->", "");
	ИначеЕсли СтатусОперации = 3 Тогда
		ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "<!--WAIT", "");
		ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "WAIT-->", "");
	ИначеЕсли СтатусОперации = 4 Тогда
		ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "<!--INFO", "");
		ШаблонДисплеиПокупателя = СтрЗаменить(ШаблонДисплеиПокупателя, "INFO-->", "");
	КонецЕсли; 
	
КонецПроцедуры

Функция СтрокаВыравнивания(Выравнивание)
	
	Если Выравнивание = ГоризонтальноеПоложение.Лево Тогда 
		Результат = "align=""left""";
	ИначеЕсли Выравнивание = ГоризонтальноеПоложение.Право Тогда 
		Результат = "align=""right""";
	Иначе
		Результат = "align=""center""";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
