
#Область СлужебныеПроцедурыИФункции

// Серверный обработчик события ОбработкаПолученияФормы справочника Организации.
//
Процедура ОрганизацииОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	
	Если ВидФормы = "ФормаСписка" И Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		
		Параметры.Вставить("Ключ", ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОрганизациюПоУмолчанию());
		ВыбраннаяФорма = "ФормаЭлемента";
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

// Серверный обработчик события ОбработкаПолученияДанныхВыбора справочника Организации.
//
Процедура ОрганизацииОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	Если Параметры.Свойство("ХозяйственнаяОперация") Или Параметры.Свойство("РежимВыбораВзаимосвязанныхОрганизаций") Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
		|	ОбособленныеПодразделения.ГоловнаяОрганизация КАК Ссылка
		|ПОМЕСТИТЬ ГоловныеОрганизации
		|ИЗ
		|	Справочник.Организации КАК ОбособленныеПодразделения
		|ГДЕ
		|	ОбособленныеПодразделения.ОбособленноеПодразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА Организации.ОбособленноеПодразделение
		|				ИЛИ ГоловныеОрганизации.Ссылка ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЭтоГоловнаяОрганизация,
		|	Организации.ОбособленноеПодразделение КАК ЭтоОбособленноеПодразделение,
		|	Организации.ГоловнаяОрганизация
		|ПОМЕСТИТЬ ДанныеОрганизаций
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			ГоловныеОрганизации КАК ГоловныеОрганизации
		|		ПО
		|			Организации.Ссылка = ГоловныеОрганизации.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляФильтрации.Ссылка                        КАК Организация,
		|	ДанныеДляФильтрации.ЭтоГоловнаяОрганизация        КАК ЭтоГоловнаяОрганизация,
		|	ДанныеДляФильтрации.ЭтоОбособленноеПодразделение  КАК ЭтоОбособленноеПодразделение,
		|	ВЫБОР
		|		КОГДА ДанныеДляФильтрации.Ссылка <> &Организация
		|				И (ДанныеДляФильтрации.ЭтоГоловнаяОрганизация
		|					ИЛИ ДанныеДляФильтрации.ЭтоОбособленноеПодразделение)
		|				И (&Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|					ИЛИ ДанныеДляФильтрации.ГоловнаяОрганизация = Организация.ГоловнаяОрганизация)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ                                             КАК Взаимосвязана
		|ПОМЕСТИТЬ ДанныеДляФильтрации
		|ИЗ
		|	ДанныеОрганизаций КАК ДанныеДляФильтрации
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			Справочник.Организации КАК Организация
		|		ПО
		|			Организация.Ссылка = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляФильтрации.Организация КАК Организация
		|ИЗ
		|	ДанныеДляФильтрации КАК ДанныеДляФильтрации
		|ГДЕ
		|	&РежимВыбораВзаимосвязанныхОрганизаций = ""ТолькоВзаимосвязанные""
		|	И ДанныеДляФильтрации.Взаимосвязана
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляФильтрации.Организация КАК Организация
		|ИЗ
		|	ДанныеДляФильтрации КАК ДанныеДляФильтрации
		|ГДЕ
		|	&РежимВыбораВзаимосвязанныхОрганизаций = ""ТолькоНеВзаимосвязанные""
		|	И НЕ ДанныеДляФильтрации.Взаимосвязана
		|	И ДанныеДляФильтрации.Организация <> &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляФильтрации.Организация КАК Организация
		|ИЗ
		|	ДанныеДляФильтрации КАК ДанныеДляФильтрации
		|ГДЕ
		|	&РежимВыбораВзаимосвязанныхОрганизаций = ""БезОбособленныхПодразделений""
		|	И НЕ ДанныеДляФильтрации.ЭтоОбособленноеПодразделение
		|	И ДанныеДляФильтрации.Организация <> &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляФильтрации.Организация КАК Организация
		|ИЗ
		|	ДанныеДляФильтрации КАК ДанныеДляФильтрации
		|ГДЕ
		|	&РежимВыбораВзаимосвязанныхОрганизаций = """"
		|";
		
		Организация = ?(Параметры.Свойство("Организация"), Параметры.Организация, Справочники.Организации.ПустаяСсылка());
		
		РежимВыбораВзаимосвязанныхОрганизаций = "";
		Если Параметры.Свойство("РежимВыбораВзаимосвязанныхОрганизаций") Тогда
			РежимВыбораВзаимосвязанныхОрганизаций = Параметры.РежимВыбораВзаимосвязанныхОрганизаций;
		ИначеЕсли Параметры.Свойство("ХозяйственнаяОперация") Тогда
			Если Параметры.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами Тогда
				РежимВыбораВзаимосвязанныхОрганизаций = "ТолькоВзаимосвязанные";
			КонецЕсли;
		КонецЕсли;
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("РежимВыбораВзаимосвязанныхОрганизаций", РежимВыбораВзаимосвязанныхОрганизаций);
		
		СвязанныеОрганизации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
		
		Параметры.Отбор.Вставить("Ссылка", Новый ФиксированныйМассив(СвязанныеОрганизации));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
