
#Область СлужебныйПрограммныйИнтерфейс

// Открывает настройку внутреннего электронного документооборота.
// 
// Параметры:
// 	Организация - ОпределяемыйТип.Организация
//  СоздатьНовую - Булево
Процедура ОткрытьНастройкуВнутреннегоЭлектронногоДокументооборота(Организация) Экспорт 
	
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", Организация);
	ОткрытьФорму("РегистрСведений.НастройкиВнутреннегоЭДО.Форма.НастройкиВнутреннегоЭДО", Параметры);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// ЭлектронноеВзаимодействие.БазоваяФункциональность.ОбработкаНеисправностей

// Открывает форму исправления ошибок с отображением настроек внутреннего электронного документооборота по организациям.
// 
// Параметры:
// 	КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
// 	ДополнительныеПараметры - Произвольный - см. ключ ПараметрыОбработчиков структуры, возвращаемой методом
//                                           см. ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки
Процедура ОткрытьОшибкиНастроекВнутреннегоЭДО(КонтекстДиагностики, ДополнительныеПараметры) Экспорт
	
	Ошибки = ОбработкаНеисправностейБЭДКлиентСервер.ПолучитьОшибки(КонтекстДиагностики);
	ДополнительныеДанные = ОбработкаНеисправностейБЭДКлиентСервер.ЗначенияСвойствОшибок(Ошибки, "ДополнительныеДанные");
	
	Организации = Новый Массив;
	
	Для каждого Данные Из ДополнительныеДанные Цикл
		Если Организации.Найти(Данные.Организация) = Неопределено Тогда
			Организации.Добавить(Данные.Организация);
		КонецЕсли;
	КонецЦикла;
	
	СоответствиеОшибокДанным = Новый Соответствие;
	Для каждого Ошибка Из Ошибки Цикл
		Если СоответствиеОшибокДанным[Ошибка.ДополнительныеДанные] = Неопределено Тогда
			СоответствиеОшибокДанным.Вставить(Ошибка.ДополнительныеДанные, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ошибка));
		Иначе 
			СоответствиеОшибокДанным[Ошибка.ДополнительныеДанные].Добавить(Ошибка);
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыИсправленияОшибок = ОбработкаНеисправностейБЭДКлиент.НовыеПараметрыИсправленияОшибок();
	ПараметрыИсправленияОшибок.СкрытьКнопкуПросмотреть = Истина;
	
	Если Ошибки.Количество() > 0 Тогда
		ПараметрыИсправленияОшибок.Заголовок = Ошибки[0].ВидОшибки.ЗаголовокПроблемы;
	КонецЕсли;
	
	Команда = ОбработкаНеисправностейБЭДКлиент.НовоеОписаниеКомандыФормыИсправленияОшибок();
	Команда.Заголовок = НСтр("ru = 'Открыть настройку'");
	Команда.Обработчик = "НастройкиВнутреннегоЭДОКлиент.ОткрытьНастройкуВнутреннегоЭДО";
	
	ПараметрыИсправленияОшибок.Команды.Добавить(Команда);
	
	ПараметрыИсправленияОшибок.ОбработчикСобытияВыбор = Команда;
	ПараметрыИсправленияОшибок.ДополнительныеПараметрыОбработчиков = СоответствиеОшибокДанным;
	
	ОбработкаНеисправностейБЭДКлиент.ИсправитьОшибки(Организации, ПараметрыИсправленияОшибок);
	
КонецПроцедуры

// Обработчик команды, открывающий настройку внутреннего электронного документооборота из формы исправления ошибок.
// 
// Параметры:
// 	Организации - Массив из см. ОпределяемыйТип.Организация
// 	ДополнительныеПараметры - Произвольный - см. ключ ДополнительныеПараметрыОбработчиков структуры, возвращаемой методом
//                                           см. НовыеПараметрыИсправленияОшибок
Процедура ОткрытьНастройкуВнутреннегоЭДО(Организации, ДополнительныеПараметры) Экспорт
	
	Если Организации.Количество() Тогда
		ОткрытьНастройкуВнутреннегоЭлектронногоДокументооборота(Организации[0]);
	КонецЕсли;
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.БазоваяФункциональность.ОбработкаНеисправностей

#КонецОбласти

#КонецОбласти