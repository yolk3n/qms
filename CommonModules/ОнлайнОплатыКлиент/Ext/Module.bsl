///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ОнлайнОплаты".
// ОбщийМодуль.ОнлайнОплатыКлиент.
//
// Клиентские процедуры настройки использования интеграции и настройки регламентных заданий онлайн оплат:
//  - алгоритмы настройки формы "Интернет-поддержка и сервисы";
//  - открытие формы платежной ссылки.
//
////////////////////////////////////////////////////////////////////////////////


#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область БСПНастройкиПрограммы

// Обработчик команды на форме панели администрирования "Интернет-поддержка и сервисы"
// Библиотеки стандартных подсистем.
//
// Параметры:
//  УникальныйИдентификатор - УникальныйИдентификатор - идентификатор родительской формы.
//
Процедура ИнтернетПоддержкаИСервисы_ОткрытьШаблоныСообщенийОнлайнОплат(УникальныйИдентификатор) Экспорт
	
	ОнлайнОплатыСлужебныйКлиент.ОткрытьШаблоныСообщенийОнлайнОплат(УникальныйИдентификатор);
	
КонецПроцедуры

// Обработчик элемента БИПОнлайнОплатыПриИзменении на форме панели администрирования "Интернет-поддержка и сервисы"
// Библиотеки стандартных подсистем.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма панели администрирования;
//  Элемент - ЭлементФормы - поле ввода информации.
//
Процедура ИнтернетПоддержкаИСервисы_БИПОнлайнОплатыПриИзменении(Форма, Элемент) Экспорт
	
	ОнлайнОплатыВызовСервера.УстановитьИспользованиеИнтеграции(Форма.БИПОнлайнОплаты);
	
	Форма.Элементы.ГруппаПолучениеУведомлений.Доступность = Форма.БИПОнлайнОплаты;
	Форма.Элементы.ГруппаНастройкиОнлайнОплатыПраво.Доступность = Форма.БИПОнлайнОплаты;
	Форма.Элементы.ДекорацияРасписаниеПолученияУведомленийОтОнлайнОплат.Доступность = 
		Форма.БИПИспользоватьАвтоматическоеПолучениеУведомленияОтОнлайнОплат;
	
	ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	
КонецПроцедуры

// Обработчик события ПриИзменении элемента ПолучениеУведомленияОтОнлайнОплат на форме панели администрирования 
// "Интернет-поддержка и сервисы" Библиотеки стандартных подсистем.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма панели администрирования;
//  Элемент - ПолеФормы - элементы управления формы панели администрирования.
//
Процедура ИнтернетПоддержкаИСервисы_ПолучениеУведомленияОтОнлайнОплатПриИзменении(Форма, Элемент) Экспорт
	
	Форма.Элементы.ДекорацияРасписаниеПолученияУведомленийОтОнлайнОплат.Доступность = 
		Форма.БИПИспользоватьАвтоматическоеПолучениеУведомленияОтОнлайнОплат;
	
	ОнлайнОплатыВызовСервера.ИспользоватьАвтоматическоеПолучениеУведомленияОтОнлайнОплат(
		Форма.БИПИспользоватьАвтоматическоеПолучениеУведомленияОтОнлайнОплат);

	
КонецПроцедуры

// Обработчик события Нажатие элемента ДекорацияРасписаниеПолученияУведомленийОтОнлайнОплатНажатие
// на форме панели администрирования "Интернет-поддержка и сервисы" Библиотеки стандартных подсистем.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма панели администрирования;
//  Элемент - ПолеФормы - элементы управления формы панели администрирования.
//
Процедура ИнтернетПоддержкаИСервисы_ДекорацияРасписаниеПолученияУведомленийОтОнлайнОплатНажатие(Форма, Элемент) Экспорт
	
	// АПК:574-выкл код не используется в мобильном клиенте
	Расписание = ОнлайнОплатыВызовСервера.РасписаниеЗаданияПолучениеУведомленияОтОнлайнОплат();
	ДиалогРасписание = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПриИзмененииРасписанияПолученияУведомлений",
		ЭтотОбъект,
		Форма);
	ДиалогРасписание.Показать(ОписаниеОповещения);
	// АПК:574-вкл
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс




// Устанавливает и заполняет представление расписание регламентного задания на форме панели администрирования
// "Интернет-поддержка и сервисы" Библиотеки стандартных подсистем.
//
// Параметры:
//  Расписание - РасписаниеРегламентногоЗадания, Неопределено - содержит результат заполнения расписания;
//  Форма - ФормаКлиентскогоПриложения - форма панели администрирования.
//
Процедура ПриИзмененииРасписанияПолученияУведомлений(Расписание, Форма) Экспорт
	
	Если Расписание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОнлайнОплатыВызовСервера.УстановитьРасписаниеЗаданияПолучениеУведомленияОтОнлайнОплат(Расписание);
	
	Форма.Элементы.ДекорацияРасписаниеПолученияУведомленийОтОнлайнОплат.Заголовок =
		ИнтернетПоддержкаПользователейКлиентСервер.ПредставлениеРасписания(Расписание);
	
КонецПроцедуры

// Открывает форму формирования ссылки на оплату.
// Позволяет: 
//   * Сформировать новую ссылку.
//   * Просмотреть сформированную ранее ссылку.
//   * Обновить данные в сервисе, если они изменились.
//
// Параметры:
//  ОснованиеПлатежа - Произвольный - основание платежа, для которого нужно сформировать/открыть/обновить ссылку.
//
Процедура ОткрытьФормуПлатежнойСсылки(Знач ОснованиеПлатежа) Экспорт
	
	ОнлайнОплатыСлужебныйКлиент.ОткрытьФормуПлатежнойСсылки(ОснованиеПлатежа);
	
КонецПроцедуры

#КонецОбласти



