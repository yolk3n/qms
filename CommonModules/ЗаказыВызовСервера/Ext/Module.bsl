
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

Функция ПолучитьПараметрыЗакрытияЗаказа(Заказ) Экспорт
	
	МенеджерЗаказа = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Заказ);
	
	ПараметрыЗакрытия = Новый Структура;
	
	ЕстьНеотработанныеСтроки = Ложь;
	Если ОбщегоНазначенияБольничнаяАптека.ИспользоватьСтатусы(Заказ) Тогда
		СписокЗаказов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Заказ);
		ДополнительныеПараметры = Новый Структура("КонтрольВыполненияЗаказа", Истина);
		
		Запрос = МенеджерЗаказа.СформироватьЗапросПроверкиПриСменеСтатуса(СписокЗаказов, "Закрыт", ДополнительныеПараметры);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ПроверкаПройдена = МенеджерЗаказа.ПроверкаПередСменойСтатуса(Выборка, "Закрыт", ДополнительныеПараметры);
			Если Не ПроверкаПройдена Тогда
				ПолучитьСообщенияПользователю(Истина);
				ЕстьНеотработанныеСтроки = Истина;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		ЕстьНеотработанныеСтроки = Не МенеджерЗаказа.ПолучитьРезультатЗапросаПоОстаткам(Заказ).Пустой();
		
	КонецЕсли;
	ПараметрыЗакрытия.Вставить("ЕстьНеотработанныеСтроки", ЕстьНеотработанныеСтроки);
	
	ИспользоватьПричиныОтмены = МенеджерЗаказа.ИспользоватьПричиныОтмены();
	ПараметрыЗакрытия.Вставить("ИспользоватьПричиныОтмены", ИспользоватьПричиныОтмены);
	Если ИспользоватьПричиныОтмены Тогда
		ПараметрыЗакрытия.Вставить("ФормаВыбораПричиныОтмены", МенеджерЗаказа.ИмяОбъектаПричиныОтмены() + ".ФормаВыбора");
	КонецЕсли;
	
	Возврат ПараметрыЗакрытия;
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
