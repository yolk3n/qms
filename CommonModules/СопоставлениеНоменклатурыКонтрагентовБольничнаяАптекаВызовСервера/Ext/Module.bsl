
#Область СлужебныйПрограммныйИнтерфейс

Функция ДанныеДляНоменклатурыПоНоменклатуреКонтрагента(НоменклатураКонтрагента) Экспорт
	
	СтавкаНДС = СтавкаНДСПоДаннымНоменклатурыКонтрагента(НоменклатураКонтрагента.СтавкаНДС);
	
	ЕдиницаИзмеренияКонтрагента = Новый Структура;
	ЕдиницаИзмеренияКонтрагента.Вставить("ОКЕИ", НоменклатураКонтрагента.ЕдиницаИзмеренияКод);
	ЕдиницаИзмеренияКонтрагента.Вставить("Наименование", НоменклатураКонтрагента.ЕдиницаИзмерения);
	СсылкаНаЕдиницуИзмерения = ЕдиницаИзмеренияПоДаннымНоменклатурыКонтрагента(ЕдиницаИзмеренияКонтрагента);
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Артикул"           , НоменклатураКонтрагента.Артикул);
	ДанныеЗаполнения.Вставить("Наименование"      , НоменклатураКонтрагента.Наименование);
	ДанныеЗаполнения.Вставить("НаименованиеПолное", НоменклатураКонтрагента.Наименование);
	ДанныеЗаполнения.Вставить("ЕдиницаИзмерения"  , СсылкаНаЕдиницуИзмерения);
	ДанныеЗаполнения.Вставить("СтавкаНДС"         , СтавкаНДС);
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтавкаНДСПоДаннымНоменклатурыКонтрагента(Знач Значение)
	
	Если СтрНайти(Значение, "10") Тогда
		Возврат Перечисления.СтавкиНДС.НДС10;
	ИначеЕсли СтрНайти(Значение, "18") Тогда
		Возврат Перечисления.СтавкиНДС.НДС18;
	ИначеЕсли СтрНайти(Значение, "20") Тогда
		Возврат Перечисления.СтавкиНДС.НДС20;
	Иначе
		Возврат Перечисления.СтавкиНДС.БезНДС;
	КонецЕсли;
	
КонецФункции

Функция ЕдиницаИзмеренияПоДаннымНоменклатурыКонтрагента(ЕдиницаИзмеренияСервиса)
	
	ЕдиницаПоДаннымСервиса = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	
	КодЕдиницыВСервисе = ЕдиницаИзмеренияСервиса.ОКЕИ;
	Если ЗначениеЗаполнено(КодЕдиницыВСервисе) Тогда
		ПолученныеЕдиницы = Справочники.ЕдиницыИзмерения.ДобавитьЕдиницыИзмеренияПоКоду(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КодЕдиницыВСервисе),, Истина);
		Если ПолученныеЕдиницы.Количество() > 0 Тогда
			ЕдиницаПоДаннымСервиса = ПолученныеЕдиницы[0];
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЕдиницаПоДаннымСервиса;
	
КонецФункции

#КонецОбласти
