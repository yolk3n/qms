
#Область СлужебныйПрограммныйИнтерфейс

// Проверяет корректность идентификатора абонента.
// 
// Параметры:
// 	Идентификатор - Строка
// Возвращаемое значение:
// 	Булево
Функция ИдентификаторАбонентаКорректный(Идентификатор) Экспорт
	
	ДлинаИдентификатора = СтрДлина(Идентификатор);
	Если ДлинаИдентификатора < 4 ИЛИ ДлинаИдентификатора > 46 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Индекс = 1 По ДлинаИдентификатора Цикл
		КодСимвола = КодСимвола(Сред(Идентификатор, Индекс, 1));
		Если (КодСимвола < 48 И КодСимвола <> 45 И КодСимвола <> 46)
			Или (КодСимвола > 57 И КодСимвола < 65)
			Или (КодСимвола > 90 И КодСимвола < 97)
			Или КодСимвола > 122 Тогда
				
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Проверяет, является ли абонент абонентом данного оператора.
// 
// Параметры:
// 	ИдентификаторАбонента - Строка
// 	Оператор - Строка
// Возвращаемое значение:
// 	Булево
Функция ЭтоАбонентОператора(ИдентификаторАбонента, Оператор) Экспорт
	
	Возврат ЗначениеЗаполнено(ИдентификаторАбонента)
		И ЗначениеЗаполнено(Оператор)
		И ОператорАбонента(ИдентификаторАбонента) = Оператор;
	
КонецФункции

// Возвращает идентификатор оператора электронного документооборота.
// 
// Параметры:
// 	ИдентификаторАбонента - Строка
// Возвращаемое значение:
// 	Строка
Функция ОператорАбонента(ИдентификаторАбонента) Экспорт
	
	Возврат ВРег(Лев(ИдентификаторАбонента, 3));
	
КонецФункции

#КонецОбласти