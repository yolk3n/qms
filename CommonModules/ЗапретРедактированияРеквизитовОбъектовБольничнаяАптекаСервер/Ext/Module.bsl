
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Настраивает форму разблокирования реквизитов объекта
//
// Параметры:
//  Форма                    - ФормаКлиентскогоПриложения - форма разблокирования реквизитов объекта
//  ЗаблокированныеРеквизиты - Массив           - список заблокированных реквизитов
//
Процедура НастроитьФормуРазблокированияРеквизитов(Форма) Экспорт
	
	Если Форма.Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПрефиксИмениРеквизита = "РазрешитьРедактирование";
	ДлинаПрефикса = СтрДлина(ПрефиксИмениРеквизита);
	
	Элементы = Форма.Элементы;
	ИменаРеквизитовФормы = Новый СписокЗначений;
	
	ЗаблокированныеРеквизиты = Неопределено;
	Форма.Параметры.Свойство("ЗаблокированныеРеквизиты", ЗаблокированныеРеквизиты);
	
	РеквизитыФормы = Форма.ПолучитьРеквизиты();
	Для Каждого Реквизит Из РеквизитыФормы Цикл
		Если СтрНайти(Реквизит.Имя, ПрефиксИмениРеквизита) <> 1 Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяРеквизита = Сред(Реквизит.Имя, ДлинаПрефикса + 1);
		НужноРазблокировать = (Не ЗначениеЗаполнено(ЗаблокированныеРеквизиты) Или ЗаблокированныеРеквизиты.Найти(ИмяРеквизита) <> Неопределено);
		
		Форма[Реквизит.Имя]                = НужноРазблокировать;
		Элементы[Реквизит.Имя].Доступность = НужноРазблокировать;
		
		ИменаРеквизитовФормы.Добавить(Реквизит.Имя, ИмяРеквизита);
		
	КонецЦикла;
	
	Реквизиты = Новый Массив;
	
	Если ЗначениеЗаполнено(ЗаблокированныеРеквизиты) Тогда
		Реквизиты.Добавить(Новый РеквизитФормы("ЗаблокированныеРеквизиты", Новый ОписаниеТипов("СписокЗначений")));
	КонецЕсли;
	Если ИменаРеквизитовФормы.Количество() > 0 Тогда
		Реквизиты.Добавить(Новый РеквизитФормы("ИменаРазблокируемыхРеквизитов", Новый ОписаниеТипов("СписокЗначений")));
	КонецЕсли;
	
	Если Реквизиты.Количество() > 0 Тогда
		Форма.ИзменитьРеквизиты(Реквизиты);
		
		Если ЗначениеЗаполнено(ЗаблокированныеРеквизиты) Тогда
			Форма.ЗаблокированныеРеквизиты.ЗагрузитьЗначения(ЗаблокированныеРеквизиты);
		КонецЕсли;
		
		Если ИменаРеквизитовФормы.Количество() > 0 Тогда
			Форма.ИменаРазблокируемыхРеквизитов = ИменаРеквизитовФормы;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс