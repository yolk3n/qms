////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интеграция с 1С:Документооборотом"
// Модуль ИнтеграцияС1СДокументооборотВызовСервера: сервер, вызов сервера
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбщиеПроцедурыИФункции

// Возвращает правила интеграции по объекту ДО в виде списка значений для предъявления пользователю.
//
// Параметры:
//   ОбъектДО - ОбъектXDTO - объект ДО, источник данных заполнения.
//
// Возвращаемое значение:
//   СписокЗначений - правила интеграции и представления создаваемых объектов ИС.
//
Функция ПравилаЗаполненияИнтегрированныхОбъектовСписком(ОбъектДО) Экспорт
	
	Результат = Новый СписокЗначений;
	
	Правила = ПодходящиеПравила(, ОбъектДО);
	
	Для Каждого Правило Из Правила Цикл
		Результат.Добавить(Правило.Ссылка, Правило.ПредставлениеОбъектаИС);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив подходящих правил. Обращается к сервису за объектом ДО при неоднозначности
// выбора правила, если объект не передан, а передан его тип и идентификатор.
//
// Параметры:
//   ОбъектИС - ОпределяемыйТип.ИнтеграцияС1СДокументооборотВсеСсылкиПереопределяемый,
//              ОпределяемыйТип.ИнтеграцияС1СДокументооборотДокументыОбъектыПереопределяемый,
//              ОпределяемыйТип.ИнтеграцияС1СДокументооборотСправочникиОбъектыПереопределяемый - объект ИС.
//   ОбъектДО - ОбъектXDTO - объект Документооборота.
//   ТипОбъектаИС - Строка - тип объекта ИС.
//   ТипОбъектаДО - Строка - тип объекта Документооборота.
//   ИдентификаторОбъектаДО - Строка - идентификатор объекта Документооборота.
//
// Возвращаемое значение:
//   Массив из Структура:
//     * Ссылка - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом - правило.
//     * ПредставлениеОбъектаДО - Строка - представление объекта ДО.
//     * ПредставлениеОбъектаИС - Строка - представление объекта ИС.
//     * ТипОбъектаДО - Строка - тип объекта ДО.
//     * ТипОбъектаИС - Строка - тип объекта ИС.
//     * ИдентификаторВидаДокумента - Строка - идентификатор вида документа ДО.
//     * ТипВидаДокумента - Строка - тип вида документа ДО.
//
Функция ПодходящиеПравила(ОбъектИС = Неопределено, ОбъектДО = Неопределено, ТипОбъектаИС = Неопределено,
		ТипОбъектаДО = Неопределено, ИдентификаторОбъектаДО = Неопределено) Экспорт
	
	Если ТипЗнч(ОбъектДО) = Тип("ОбъектXDTO") Тогда
		ОбъектXDTO = ОбъектДО;
	Иначе
		ОбъектXDTO = Неопределено;
	КонецЕсли;
	
	Достаточно = Ложь;
	
	Пока Не Достаточно Цикл
		
		Правила = Новый Массив;
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ПравилаВыгрузки.Ссылка КАК Ссылка,
			|	ИСТИНА КАК ПравилоВыгрузки,
			|	ПравилаВыгрузки.ИмяРеквизитаОбъектаДО КАК ИмяРеквизита,
			|	ПравилаВыгрузки.ЭтоДополнительныйРеквизитДО КАК ЭтоДополнительныйРеквизит,
			|	ПравилаВыгрузки.ДополнительныйРеквизитДОID КАК ДополнительныйРеквизитID,
			|	ПравилаВыгрузки.ДополнительныйРеквизитДОТип КАК ДополнительныйРеквизитТип,
			|	ВЫБОР
			|		КОГДА ПравилаВыгрузки.Вариант = ЗНАЧЕНИЕ(Перечисление.ВариантыПравилЗаполненияРеквизитов.ИзШаблона)
			|			ТОГДА ПравилаВыгрузки.ШаблонЗначение
			|		ИНАЧЕ ПравилаВыгрузки.ЗначениеРеквизитаДО
			|	КОНЕЦ КАК ЗначениеРеквизита,
			|	ВЫБОР
			|		КОГДА ПравилаВыгрузки.Вариант = ЗНАЧЕНИЕ(Перечисление.ВариантыПравилЗаполненияРеквизитов.ИзШаблона)
			|			ТОГДА ПравилаВыгрузки.ШаблонТип
			|		ИНАЧЕ ПравилаВыгрузки.ЗначениеРеквизитаДОТип
			|	КОНЕЦ КАК ЗначениеРеквизитаДОТип,
			|	ВЫБОР
			|		КОГДА ПравилаВыгрузки.Вариант = ЗНАЧЕНИЕ(Перечисление.ВариантыПравилЗаполненияРеквизитов.ИзШаблона)
			|			ТОГДА ПравилаВыгрузки.ШаблонID
			|		ИНАЧЕ ПравилаВыгрузки.ЗначениеРеквизитаДОID
			|	КОНЕЦ КАК ЗначениеРеквизитаДОID
			|ПОМЕСТИТЬ КлючевыеРеквизиты
			|ИЗ
			|	Справочник.ПравилаИнтеграцииС1СДокументооборотом.ПравилаЗаполненияРеквизитовДО КАК ПравилаВыгрузки
			|ГДЕ
			|	ПравилаВыгрузки.Ключевой
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПравилаЗагрузки.Ссылка,
			|	ЛОЖЬ,
			|	ПравилаЗагрузки.ИмяРеквизитаОбъектаИС,
			|	ПравилаЗагрузки.ЭтоДополнительныйРеквизитИС,
			|	ПравилаЗагрузки.ДополнительныйРеквизитИС,
			|	НЕОПРЕДЕЛЕНО,
			|	ПравилаЗагрузки.ЗначениеРеквизитаИС,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО
			|ИЗ
			|	Справочник.ПравилаИнтеграцииС1СДокументооборотом.ПравилаЗаполненияРеквизитовИС КАК ПравилаЗагрузки
			|ГДЕ
			|	ПравилаЗагрузки.Ключевой
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Правила.Ссылка КАК Ссылка,
			|	Правила.ТипОбъектаИС КАК ТипОбъектаИС,
			|	Правила.ТипОбъектаДО КАК ТипОбъектаДО,
			|	Правила.ПредставлениеОбъектаИС КАК ПредставлениеОбъектаИС,
			|	Правила.ПредставлениеОбъектаДО КАК ПредставлениеОбъектаДО,
			|	Правила.УсловиеПрименимостиПриВыгрузке КАК УсловиеПрименимостиПриВыгрузке,
			|	Правила.УсловиеПрименимостиПриЗагрузке КАК УсловиеПрименимостиПриЗагрузке,
			|	КлючевыеРеквизиты.ПравилоВыгрузки КАК ПравилоВыгрузки,
			|	КлючевыеРеквизиты.ИмяРеквизита КАК ИмяРеквизита,
			|	КлючевыеРеквизиты.ЗначениеРеквизита КАК ЗначениеРеквизита,
			|	КлючевыеРеквизиты.ЗначениеРеквизитаДОТип КАК ЗначениеРеквизитаДОТип,
			|	КлючевыеРеквизиты.ЗначениеРеквизитаДОID КАК ЗначениеРеквизитаДОID,
			|	КлючевыеРеквизиты.ЭтоДополнительныйРеквизит КАК ЭтоДополнительныйРеквизит,
			|	КлючевыеРеквизиты.ДополнительныйРеквизитID КАК ДополнительныйРеквизитID,
			|	КлючевыеРеквизиты.ДополнительныйРеквизитТип КАК ДополнительныйРеквизитТип
			|ИЗ
			|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК Правила
			|		ЛЕВОЕ СОЕДИНЕНИЕ КлючевыеРеквизиты КАК КлючевыеРеквизиты
			|		ПО Правила.Ссылка = КлючевыеРеквизиты.Ссылка
			|ГДЕ
			|	НЕ Правила.ПометкаУдаления
			|	И &УсловиеОбъектИС
			|	И &УсловиеОбъектДО
			|ИТОГИ ПО
			|	Ссылка");
			
		Если ЗначениеЗаполнено(ОбъектИС) Тогда
			ОбъектИСЭтоСсылка = ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ОбъектИС));
			ТипОбъектаИС = ОбъектИС.Метаданные().ПолноеИмя();
			УсловиеОбъектИС = "Правила.ТипОбъектаИС = &ТипОбъектаИС";
			Запрос.УстановитьПараметр("ТипОбъектаИС", ТипОбъектаИС);
		ИначеЕсли ЗначениеЗаполнено(ТипОбъектаИС) Тогда
			УсловиеОбъектИС = "Правила.ТипОбъектаИС = &ТипОбъектаИС";
			Запрос.УстановитьПараметр("ТипОбъектаИС", ТипОбъектаИС);
		Иначе
			ОбъектИСЭтоСсылка = Ложь;
			УсловиеОбъектИС = "ИСТИНА";
		КонецЕсли;
		
		Если ТипЗнч(ОбъектXDTO) = Тип("ОбъектXDTO") Тогда
			ТипОбъектаДО = ОбъектXDTO.objectID.type;
			УсловиеОбъектДО = "Правила.ТипОбъектаДО = &ТипОбъектаДО";
			Запрос.УстановитьПараметр("ТипОбъектаДО", ТипОбъектаДО);
		ИначеЕсли ЗначениеЗаполнено(ТипОбъектаДО) Тогда
			УсловиеОбъектДО = "Правила.ТипОбъектаДО = &ТипОбъектаДО";
			Запрос.УстановитьПараметр("ТипОбъектаДО", ТипОбъектаДО);
		Иначе
			УсловиеОбъектДО = "ИСТИНА";
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОбъектИС", УсловиеОбъектИС);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОбъектДО", УсловиеОбъектДО);
		
		ВыборкаПравила = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПравила.Следующий() Цикл
			
			ИдентификаторВидаДокумента = Неопределено;
			ТипВидаДокумента = Неопределено;
			
			// Проверка ключевых реквизитов.
			
			ПодходитПоКлючевымРеквизитам = Истина;
			
			ВыборкаКлючевыеРеквизиты = ВыборкаПравила.Выбрать();
			Пока ВыборкаКлючевыеРеквизиты.Следующий() Цикл
				
				Если ВыборкаКлючевыеРеквизиты.ПравилоВыгрузки = Истина Тогда
					
					Если ВыборкаКлючевыеРеквизиты.ИмяРеквизита = "documentType" Тогда
						ИдентификаторВидаДокумента = ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОID;
						ТипВидаДокумента = ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип;
					КонецЕсли;
					
					Если ТипЗнч(ОбъектXDTO) <> Тип("ОбъектXDTO") Тогда
						Продолжить;
					КонецЕсли;
					
					Если Не ВыборкаКлючевыеРеквизиты.ЭтоДополнительныйРеквизит Тогда
						Если Не ОбъектXDTO.Установлено(ВыборкаКлючевыеРеквизиты.ИмяРеквизита) Тогда
							ЗначениеРеквизита = Неопределено;
						Иначе
							ЗначениеРеквизита = ОбъектXDTO.Получить(ВыборкаКлючевыеРеквизиты.ИмяРеквизита);
						КонецЕсли;
					Иначе
						Если ОбъектXDTO.Установлено("additionalProperties") Тогда
							ЗначениеРеквизита = Неопределено;
							Для Каждого ДопРеквизит Из ОбъектXDTO.additionalProperties Цикл
								Если ДопРеквизит.objectID.ID = ВыборкаКлючевыеРеквизиты.ДополнительныйРеквизитID
									И ДопРеквизит.objectID.type = ВыборкаКлючевыеРеквизиты.ДополнительныйРеквизитТип Тогда
									Если ДопРеквизит.Установлено("propertySimpleValue") Тогда
										ЗначениеРеквизита = ДопРеквизит.propertySimpleValue;
									Иначе
										ЗначениеРеквизита = ДопРеквизит.propertyObjectValue;
									КонецЕсли;
								КонецЕсли;
							КонецЦикла;
						Иначе
							ЗначениеРеквизита = Неопределено;
						КонецЕсли;
					КонецЕсли;
					
					Если ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип = "Строка"
						Или ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип = "Число"
						Или ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип = "Дата"
						Или ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип = "Булево" Тогда
						
						Если ЗначениеРеквизита <> ВыборкаКлючевыеРеквизиты.ЗначениеРеквизита Тогда
							ПодходитПоКлючевымРеквизитам = Ложь;
							Прервать;
						КонецЕсли;
						
					ИначеЕсли ЗначениеРеквизита = Неопределено Тогда
						
						ПодходитПоКлючевымРеквизитам = Ложь;
						Прервать;
						
					Иначе // ссылочный тип
						
						Если Не ЗначениеРеквизита.Установлено("objectID")
								Или ЗначениеРеквизита.objectID.ID <> ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОID
								Или ЗначениеРеквизита.objectID.type <> ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип Тогда
							ПодходитПоКлючевымРеквизитам = Ложь;
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
					
				ИначеЕсли ВыборкаКлючевыеРеквизиты.ПравилоВыгрузки = Ложь Тогда // правило загрузки
					
					Отказ = Ложь;
					ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриПроверкеСоответствияПравилаФункциональнымОпциям(
						ВыборкаПравила.Ссылка,
						ВыборкаПравила.ТипОбъектаДО,
						ВыборкаПравила.ТипОбъектаИС,
						Отказ,
						ВыборкаКлючевыеРеквизиты.ИмяРеквизита,
						ВыборкаКлючевыеРеквизиты.ЗначениеРеквизита);
						
					Если Отказ Тогда
						ПодходитПоКлючевымРеквизитам = Ложь;
						Прервать;
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ОбъектИС) Тогда
						Продолжить;
					КонецЕсли;
					
					Если Не ВыборкаКлючевыеРеквизиты.ЭтоДополнительныйРеквизит Тогда
						
						Если ОбъектИСЭтоСсылка Тогда
							ЗначениеРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
								ОбъектИС,
								ВыборкаКлючевыеРеквизиты.ИмяРеквизита);
						Иначе // объект
							ЗначениеРеквизита = ОбъектИС[ВыборкаКлючевыеРеквизиты.ИмяРеквизита];
						КонецЕсли;
						
					Иначе // доп. реквизит
						
						ЗначениеРеквизита = Неопределено;
						
						Если ОбъектИСЭтоСсылка Тогда
							
							ЗапросЗначениеРеквизита = Новый Запрос(
								"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
								|	ТаблицаСвойств.Значение КАК ЗначениеРеквизита
								|ИЗ
								|	&ИмяОбъектаСоСвойствами КАК ТаблицаСвойств
								|ГДЕ
								|	ТаблицаСвойств.Ссылка = &Ссылка
								|	И ТаблицаСвойств.Свойство = &Свойство");
							
							ЗапросЗначениеРеквизита.Текст = СтрЗаменить(ЗапросЗначениеРеквизита.Текст,
								"&ИмяОбъектаСоСвойствами",
								СтрШаблон("%1.ДополнительныеРеквизиты", 
									ОбщегоНазначения.ИмяТаблицыПоСсылке(ОбъектИС.Ссылка)));
							
							ЗапросЗначениеРеквизита.УстановитьПараметр("Ссылка", ОбъектИС.Ссылка);
							ЗапросЗначениеРеквизита.УстановитьПараметр("Свойство", ВыборкаКлючевыеРеквизиты.ДополнительныйРеквизитID);
							
							ВыборкаЗначениеРеквизита = ЗапросЗначениеРеквизита.Выполнить().Выбрать();
							Если ВыборкаЗначениеРеквизита.Следующий() Тогда
								ЗначениеРеквизита = ВыборкаЗначениеРеквизита.ЗначениеРеквизита;
							КонецЕсли;
							
						Иначе // объект
							
							СтруктураПоиска = Новый Структура("Свойство", ВыборкаКлючевыеРеквизиты.ДополнительныйРеквизитID);
							СтрокиРеквизита = ОбъектИС.ДополнительныеРеквизиты.НайтиСтроки(СтруктураПоиска);
							Если СтрокиРеквизита.Количество() <> 0 Тогда
								ЗначениеРеквизита = СтрокиРеквизита[0].Значение;
							КонецЕсли;
							
						КонецЕсли;
					КонецЕсли;
					
					Если ЗначениеРеквизита <> ВыборкаКлючевыеРеквизиты.ЗначениеРеквизита Тогда
						ПодходитПоКлючевымРеквизитам = Ложь;
						Прервать;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если Не ПодходитПоКлючевымРеквизитам Тогда
				Продолжить;
			КонецЕсли;
			
			// Проверка условий применимости.
			
			Если ЗначениеЗаполнено(ОбъектИС) И ЗначениеЗаполнено(ВыборкаПравила.УсловиеПрименимостиПриВыгрузке) Тогда
				
				Параметры = Новый Структура;
				Параметры.Вставить("Источник", ОбъектИС);
				Параметры.Вставить("Результат", Истина);
				
				ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ВыборкаПравила.УсловиеПрименимостиПриВыгрузке,
					Параметры);
				
				Если Параметры.Результат <> Истина Тогда
					Продолжить;
				КонецЕсли;
				
			КонецЕсли;
			
			Если ТипЗнч(ОбъектXDTO) = Тип("ОбъектXDTO") И ЗначениеЗаполнено(ВыборкаПравила.УсловиеПрименимостиПриЗагрузке) Тогда
				
				Параметры = Новый Структура;
				Параметры.Вставить("Источник", ОбъектXDTO);
				Параметры.Вставить("Результат", Истина);
				
				ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ВыборкаПравила.УсловиеПрименимостиПриЗагрузке, Параметры);
				
				Если Параметры.Результат <> Истина Тогда
					Продолжить;
				КонецЕсли;
				
			КонецЕсли;
			
			Правило = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеПравилаИнтеграции();
			Правило.Ссылка = ВыборкаПравила.Ссылка;
			Правило.ТипОбъектаИС = ВыборкаПравила.ТипОбъектаИС;
			Правило.ТипОбъектаДО = ВыборкаПравила.ТипОбъектаДО;
			Правило.ПредставлениеОбъектаИС = ВыборкаПравила.ПредставлениеОбъектаИС;
			Правило.ПредставлениеОбъектаДО = ВыборкаПравила.ПредставлениеОбъектаДО;
			Правило.ИдентификаторВидаДокумента = ИдентификаторВидаДокумента;
			Правило.ТипВидаДокумента = ТипВидаДокумента;
			
			Правила.Добавить(Правило);
			
		КонецЦикла;
		
		// Возможно, следует уточнить выборку, обратившись к сервису за самим объектом.
		Если Правила.Количество() > 1
				И ТипЗнч(ОбъектXDTO) <> Тип("ОбъектXDTO")
				И ЗначениеЗаполнено(ИдентификаторОбъектаДО)
				И ЗначениеЗаполнено(ТипОбъектаДО) Тогда
			
			Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
			
			ОбъектXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
				Прокси,
				ТипОбъектаДО,
				ИдентификаторОбъектаДО)
			
		Иначе
			
			Достаточно = Истина;
			
		КонецЕсли;
	
	КонецЦикла; // Пока Не Достаточно
	
	Возврат Правила;
	
КонецФункции

// Проверяет, является ли текущий сеанс неразделенным при работе в модели сервиса.
//
// Возвращаемое значение:
//   Булево - Истина, если сеанс неразделенный, и Ложь в разделенном сеансе и в локальном режиме.
//
Функция ЭтоНеразделенныйСеансРазделеннойИБ() Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаВМоделиСервиса") Тогда
		МодульРаботаВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервиса");
		ЕстьРазделители = МодульРаботаВМоделиСервиса.ЭтоРазделеннаяКонфигурация();
	Иначе
		ЕстьРазделители = Ложь;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.РазделениеВключено()
		И ЕстьРазделители;
	
КонецФункции

// Возвращает значение реквизита, прочитанного из информационной базы по ссылке на объект.
// Если доступа к реквизиту нет, возникнет исключение прав доступа.
// Если необходимо зачитать реквизит независимо от прав текущего пользователя,
// то следует использовать предварительный переход в привилегированный режим.
//
// Параметры:
//   Ссылка - ЛюбаяСсылка- ссылка на объект.
//   ИмяРеквизита - Строка - имя получаемого реквизита.
//
// Возвращаемое значение:
//   Произвольный - зависит от типа значения прочитанного реквизита.
//
Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
	
КонецФункции

// Создает правила интеграции для указанного типа объекта ИС.
//
// Параметры:
//   ИмяТипаОбъекта - Строка, ЛюбаяСсылка - полное имя типа объекта ИС, как в метаданных,
//     или ссылка на объект.
//
// Возвращаемое значение:
//   Массив из Структура:
//     * Ссылка - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом
//     * ТипОбъектаИС - Строка
//     * ТипОбъектаДО - Строка
//     * ПредставлениеОбъектаИС - Строка
//     * ПредставлениеОбъектаДО - Строка
//     * ИдентификаторВидаДокумента - Строка
//     * ТипВидаДокумента - Строка
//
Функция СоздатьПравилаИнтеграцииАвтоматически(Знач ИмяТипаОбъекта) Экспорт
	
	ПравилаИнтеграции = Новый Массив;
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗНЧ(ИмяТипаОбъекта)) Тогда
		ИмяТипаОбъекта = ИмяТипаОбъекта.Метаданные().ПолноеИмя();
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервераПереопределяемый.ПриСозданииПравилИнтеграцииАвтоматически(
		ИмяТипаОбъекта,
		ПравилаИнтеграции);
	
	Возврат ПравилаИнтеграции;
	
КонецФункции

#КонецОбласти

#Область Хронометраж

// Возвращает структуру параметров хронометража для указанного объекта.
//
// Параметры:
//   ВнешнийОбъект - ЛюбаяСсылка - ссылка на объект хронометража.
//
// Возвращаемое значение:
//   Структура:
//     * ВключенХронометраж - Булево
//     * ДатаНачалаХронометража - Дата
//     * ДатаКонцаХронометража - Дата
//     * Источник - Строка
//     * ИсточникID - Строка
//     * ИсточникТип - Строка
//
Функция ПараметрыХронометражаОбъекта(ВнешнийОбъект) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetChronometrationSettingsRequest");
	
	СписокОбъектов = Запрос.objects; // СписокXDTO
	СписокОбъектов.Добавить(
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьExternalObject(Прокси, ВнешнийОбъект));
	
	Результат = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	ПараметрыХронометражаXDTO = Результат.settings[0];
	
	Если ПараметрыХронометражаXDTO.beginDate = Неопределено Тогда
		ДатаНачалаХронометража = '00010101';
	Иначе
		ДатаНачалаХронометража = ПараметрыХронометражаXDTO.beginDate;
	КонецЕсли;
	
	Если ПараметрыХронометражаXDTO.endDate = Неопределено Тогда
		ДатаКонцаХронометража = '00010101';
	Иначе
		ДатаКонцаХронометража = ПараметрыХронометражаXDTO.endDate;
	КонецЕсли;
	
	ПараметрыХронометража = Новый Структура;
	ПараметрыХронометража.Вставить("ВключенХронометраж", ПараметрыХронометражаXDTO.chronometrationOn);
	ПараметрыХронометража.Вставить("ДатаНачалаХронометража", ДатаНачалаХронометража);
	ПараметрыХронометража.Вставить("ДатаКонцаХронометража", ДатаКонцаХронометража);
	Если ПараметрыХронометражаXDTO.objectID <> Неопределено Тогда
		ПараметрыХронометража.Вставить("ИсточникID", ПараметрыХронометражаXDTO.objectID.ID);
		ПараметрыХронометража.Вставить("ИсточникТип", ПараметрыХронометражаXDTO.objectID.type);
		ПараметрыХронометража.Вставить("Источник", ПараметрыХронометражаXDTO.objectID.presentation);
	КонецЕсли;
	
	Возврат ПараметрыХронометража;
	
КонецФункции

// Возвращает массив активных записей хронометража.
//
// Возвращаемое значение:
//   Массив из Структура:
//     * ВключенХронометраж - Булево
//     * ДатаНачалаХронометража - Дата
//     * ДатаКонцаХронометража - Дата
//     * Источник - Строка
//     * ИсточникID - Строка
//     * ИсточникТип - Строка
//
Функция АктивныеЗаписиХронометража() Экспорт
	
	АктивныеЗаписи = Новый Массив;
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetChronometrationSettingsRequest");
	
	Результат = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	Для Каждого ПараметрыХронометражаXDTO Из Результат.settings Цикл
		
		ПараметрыХронометража = Новый Структура;
		ПараметрыХронометража.Вставить("ВключенХронометраж", ПараметрыХронометражаXDTO.chronometrationOn);
		ПараметрыХронометража.Вставить("ДатаНачалаХронометража", ПараметрыХронометражаXDTO.beginDate);
		ПараметрыХронометража.Вставить("ДатаКонцаХронометража", ПараметрыХронометражаXDTO.endDate);
		Если ПараметрыХронометражаXDTO.objectID <> Неопределено Тогда
			ПараметрыХронометража.Вставить("ИсточникID", ПараметрыХронометражаXDTO.objectID.ID);
			ПараметрыХронометража.Вставить("ИсточникТип",  ПараметрыХронометражаXDTO.objectID.type);
			ПараметрыХронометража.Вставить("Источник",  ПараметрыХронометражаXDTO.objectID.presentation);
		КонецЕсли;
		
		АктивныеЗаписи.Добавить(ПараметрыХронометража);
		
	КонецЦикла;
	
	Возврат АктивныеЗаписи;
	
КонецФункции

// Переключает хронометраж и возвращает результирующие параметры хронометража по внешнему объекту.
//
// Параметры:
//   ВнешнийОбъект - ЛюбаяСсылка - ссылка на объект хронометража.
//   ПараметрыХронометража - см. ИнтеграцияС1СДокументооборотВызовСервера.ПараметрыХронометражаОбъекта
//
// Возвращаемое значение:
//   Структура:
//     * ВключенХронометраж - Булево
//     * ДатаНачалаХронометража - Дата
//     * ДатаКонцаХронометража - Дата
//
Функция ПереключитьХронометражПоВнешнемуОбъекту(ВнешнийОбъект, ПараметрыХронометража) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMSetChronometrationSettingsRequest");
	
	СписокОбъектов = Запрос.objects; // СписокXDTO
	СписокОбъектов.Добавить(
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьExternalObject(Прокси, ВнешнийОбъект));
	
	Результат = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Результат);

	ПараметрыХронометражаXDTO = Результат.settings[0];
	
	Если ПараметрыХронометражаXDTO.beginDate = Неопределено Тогда
		ДатаНачалаХронометража = '00010101';
	Иначе
		ДатаНачалаХронометража = ПараметрыХронометражаXDTO.beginDate;
	КонецЕсли;
	
	Если ПараметрыХронометражаXDTO.endDate = Неопределено Тогда
		ДатаКонцаХронометража = '00010101';
	Иначе
		ДатаКонцаХронометража = ПараметрыХронометражаXDTO.endDate;
	КонецЕсли;
	
	ПараметрыХронометража = Новый Структура;
	ПараметрыХронометража.Вставить("ВключенХронометраж", ПараметрыХронометражаXDTO.chronometrationOn);
	ПараметрыХронометража.Вставить("ДатаНачалаХронометража", ДатаНачалаХронометража);
	ПараметрыХронометража.Вставить("ДатаКонцаХронометража", ДатаКонцаХронометража);
	
	Возврат ПараметрыХронометража; 
	
КонецФункции

// Переключает хронометраж и возвращает параметры хронометража по объектам 1С:Документооборота.
//
// Параметры:
//   Источники - Массив - массив структур, описывающих объекты ДО, со свойствами ИсточникID и ИсточникТип.
//
// Возвращаемое значение:
//   Массив из Структура:
//     * ВключенХронометраж - Булево
//     * ДатаНачалаХронометража - Дата
//     * ДатаКонцаХронометража - Дата
//     * Источник - Строка
//     * ИсточникID - Строка
//     * ИсточникТип - Строка
//
Функция ПереключитьХронометражПоОбъектамДокументооборота(Источники) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMSetChronometrationSettingsRequest");
	СписокОбъектов = Запрос.objects; // СписокXDTO
	
	Для Каждого Источник Из Источники Цикл
		
		ОбъектXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
			Прокси,
			Источник.ИсточникID,
			Источник.ИсточникТип);
		СписокОбъектов.Добавить(ОбъектXDTO);
		
	КонецЦикла;
	
	Результат = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	АктивныеЗаписи = Новый Массив;
	
	Для Каждого ПараметрыХронометражаXDTO Из Результат.settings Цикл
		
		ПараметрыХронометража = Новый Структура;
		ПараметрыХронометража.Вставить("ВключенХронометраж", ПараметрыХронометражаXDTO.chronometrationOn);
		ПараметрыХронометража.Вставить("ДатаНачалаХронометража", ПараметрыХронометражаXDTO.beginDate);
		ПараметрыХронометража.Вставить("ДатаКонцаХронометража", ПараметрыХронометражаXDTO.endDate);
		Если ПараметрыХронометражаXDTO.objectID <> Неопределено Тогда
			ПараметрыХронометража.Вставить("ИсточникID", ПараметрыХронометражаXDTO.objectID.ID);
			ПараметрыХронометража.Вставить("ИсточникТип",  ПараметрыХронометражаXDTO.objectID.type);
			ПараметрыХронометража.Вставить("Источник",  ПараметрыХронометражаXDTO.objectID.presentation);
		КонецЕсли;
		
		АктивныеЗаписи.Добавить(ПараметрыХронометража);
		
	КонецЦикла;
	
	Возврат АктивныеЗаписи; 
	
КонецФункции

#КонецОбласти

#Область ПроцессыИЗадачи

// Получает список шаблонов, определенных в Документообороте для указанного типа БП и предмета.
//
// Параметры:
//   ТипБизнесПроцесса - Строка - имя типа XDTO.
//   ПредметБизнесПроцесса - Структура:
//     * ID - Строка - уникальный идентификатор предмета бизнес-процесса в Документообороте.
//     * type - Строка - имя типа XDTO предмета бизнес-процесса.
//
// Возвращаемое значение:
//   СписокЗначений:
//     * name - Строка - имя шаблона.
//     * ID - Строка - уникальный идентификатор шаблона.
//     * type - Строка - имя типа XDTO шаблона.
//
Функция ШаблоныБизнесПроцесса(Знач ТипБизнесПроцесса, Знач ПредметБизнесПроцесса) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetBusinessProcessTemplatesRequest");
	Запрос.businessProcessType = ТипБизнесПроцесса;
	
	Если ПредметБизнесПроцесса <> Неопределено Тогда
		ПредметБизнесПроцессаИд = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
			Прокси,
			ПредметБизнесПроцесса.ID,
			ПредметБизнесПроцесса.type);
		
		Запрос.businessProcessTargetID = ПредметБизнесПроцессаИд;
	КонецЕсли;
	
	Результат = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	СписокШаблоновДокументов = Новый СписокЗначений;
	Для Каждого Шаблон Из Результат.BusinessProcessTemplates Цикл
		ДанныеШаблона = Новый Структура;
		ДанныеШаблона.Вставить("ID", Шаблон.objectID.ID);
		ДанныеШаблона.Вставить("type", Шаблон.objectID.type);
		ДанныеШаблона.Вставить("name", Шаблон.name);
		СписокШаблоновДокументов.Добавить(ДанныеШаблона);
	КонецЦикла;
	
	Возврат СписокШаблоновДокументов;
	
КонецФункции

// Заполняет форму бизнес-процесса на основании шаблона.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма бизнес-процесса 1С:Документооборота.
//   ДанныеОШаблоне - Структура:
//     * РеквизитID - Строка - идентификатор шаблона 1С:Документооборота.
//     * РеквизитТип - Строка - тип шаблона XDTO.
//
// Возвращаемое значение:
//   ОбъектXDTO - бизнес-процесс, заполненный по шаблону.
//
Функция ЗаполнитьБизнесПроцессПоШаблону(Знач Форма, Знач ДанныеОШаблоне) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetBusinessProcessByTemplateRequest");
	
	Запрос.type = Форма.Тип;
	
	Если ЗначениеЗаполнено(Форма.Предмет)Тогда
		
		ПредметБизнесПроцессаИд = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
			Прокси,
			Форма.ПредметID,
			Форма.ПредметТип);
		
		Запрос.targetID = ПредметБизнесПроцессаИд;
		
	КонецЕсли;
	
	ШаблонБизнесПроцессаИд = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ДанныеОШаблоне.РеквизитID,
		ДанныеОШаблоне.РеквизитТип);
	
	Запрос.businessProcessTemplateID = ШаблонБизнесПроцессаИд;
	
	Результат = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	Возврат Результат;
	
КонецФункции

// Проверяет возможность запуска согласования в ДО.
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект ИС.
//   ТекстПредупреждения - Строка - неявно возвращаемое значение, текст предупреждения.
//
// Возвращаемое значение:
//   Булево - Истина, если запуск согласования разрешен, и Ложь в противном случае 
//
Функция ПользователюРазрешенЗапускСогласования(Знач ПредметСогласования, ТекстПредупреждения) Экспорт
	
	Результат = Неопределено;
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриОпределенииРазрешенияПользователяНаЗапускСогласования(
		ПредметСогласования, ТекстПредупреждения, Результат);
		
	Если ТипЗнч(Результат) = Тип("Булево") Тогда
		Возврат Результат;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Проверяет возможность прерывания согласования в ДО.
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект ИС.
//   ПредметДО - Структура - описание связанного объекта ДО:
//      name - Строка - представление связанного объекта.
//      ID - Строка - идентификатор связанного объекта.
//      type - Строка - имя типа XDTO.
//   ТекстПредупреждения - Строка - неявно возвращаемое значение, текст предупреждения.
//
// Возвращаемое значение:
//   Булево - Истина, если прерывание согласования разрешено, и Ложь в противном случае 
//
Функция ПользователюРазрешеноПрерываниеСогласования(Знач ПредметСогласования, Знач ПредметДО,
		ТекстПредупреждения) Экспорт
	
	Результат = Неопределено;
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриОпределенииРазрешенияПользователяНаПрерываниеСогласования(
		ПредметСогласования, ПредметДО, ТекстПредупреждения, Результат);
		
	Если ТипЗнч(Результат) = Тип("Булево") Тогда
		Возврат Результат;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Проверяет, существуют ли адресованные пользователю задачи согласования по ссылке на объект ИС.
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект ИС.
//
// Возвращаемое значение:
//   Булево - Истина, если есть задачи согласования, адресованные текущему пользователю.
//
Функция ПользователюАдресованыЗадачиСогласования(Знач ПредметСогласования) Экспорт
	
	СвязанныйОбъектДО = ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ДанныеОбъектаДОПоВнешнемуОбъекту(
		ПредметСогласования);
	
	Если СвязанныйОбъектДО = Неопределено Тогда
		Возврат Ложь;
	Иначе
		ЗадачиСогласования = ПолучитьЗадачиСогласования(СвязанныйОбъектДО);
		Возврат ЗадачиСогласования.Количество() <> 0;
	КонецЕсли;
	
КонецФункции

// Проверяет, запущены ли уже процессы согласования в ДО по ссылке на объект ИС.
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект ИС.
//
// Возвращаемое значение:
//   Булево - Истина, если есть активные процессы согласования, и Ложь в противном случае.
//
Функция ЗапущеноСогласование(Знач ПредметСогласования) Экспорт
	
	СвязанныйОбъектДО = ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ДанныеОбъектаДОПоВнешнемуОбъекту(
		ПредметСогласования);
	
	Если СвязанныйОбъектДО = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Возврат ЗапущеноСогласованиеПоПредметуДО(СвязанныйОбъектДО);
	КонецЕсли;
	
КонецФункции

// Проверяет, запущены ли уже процессы согласования в ДО по описанию предмета в ДО.
//
// Параметры:
//   ПредметДО - Структура:
//     * name - Строка - представление предмета.
//     * ID - Строка - идентификатор связанного объекта ДО.
//     * type - Строка - имя типа XDTO.
//
// Возвращаемое значение:
//   Булево - Истина, если есть активные процессы согласования, и Ложь в противном случае.
//
Функция ЗапущеноСогласованиеПоПредметуДО(Знач ПредметДО) Экспорт
	
	// Выберем активные процессы типа "согласование" по указанному предмету.
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	query = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListQuery");
	УсловияОтбора = query.conditions; // СписокXDTO
	
	Условие = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "target";
	Условие.value = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ПредметДО.ID,
		ПредметДО.type);
	УсловияОтбора.Добавить(Условие);
	
	Условие = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "withExecuted";
	Условие.value = Ложь;
	УсловияОтбора.Добавить(Условие);
	
	Условие = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "withDelayed";
	Условие.value = Ложь;
	УсловияОтбора.Добавить(Условие);
	
	Попытка
		Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.НайтиСписокОбъектов(
			Прокси,
			"DMBusinessProcessApproval",
			query);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат (Ответ.items.Количество() <> 0);
	
КонецФункции

// Запускает согласование по указанному шаблону и предмету и возвращает признак успешности запуска.
//
// Параметры:
//    Шаблон - Структура:
//      * name - Строка - представление шаблона.
//      * ID - Строка - идентификатор шаблона.
//      * type - Строка - имя типа XDTO.
//    Предмет - Структура:
//      * name - Строка - представление предмета.
//      * ID - Строка - идентификатор предмета.
//      * type - Строка - имя типа XDTO.
//
// Возвращаемое значение:
//    Булево - Истина, если процесс запущен успешно.
//
Функция ЗапуститьСогласованиеПоШаблону(Знач Шаблон, Знач Предмет) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	ПроцессПоШаблону = ИнтеграцияС1СДокументооборот.НовыйБизнесПроцессПоШаблону(Прокси, 
		"DMBusinessProcessApproval", Шаблон, Предмет);
	НовыйПроцесс = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMBusinessProcessApproval");
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьЗначенияСвойствXDTO(Прокси, НовыйПроцесс, ПроцессПоШаблону);
	
	Попытка
		РезультатЗапуска = ИнтеграцияС1СДокументооборот.ЗапуститьБизнесПроцесс(Прокси, НовыйПроцесс);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат (ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, РезультатЗапуска, "DMError") = Ложь);
	
КонецФункции

// Прерывает процессы согласования по указанному предмету.
//
// Параметры:
//   ПредметДО - Структура:
//     * name - Строка - представление предмета.
//     * ID - Строка - идентификатор связанного объекта ДО.
//     * type - Строка - имя типа XDTO.
//
// Возвращаемое значение:
//   Булево - Истина, если процесс прерван успешно.
//
Функция ПрерватьСогласование(Знач ПредметДО) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	query = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListQuery");
	УсловияОтбора = query.conditions; // СписокXDTO
	
	Условие = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "target";
	Условие.value = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ПредметДО.ID,
		ПредметДО.type);
	
	УсловияОтбора.Добавить(Условие);
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.НайтиСписокОбъектов(
		Прокси,
		"DMBusinessProcessApproval",
		query);
	
	Если Ответ.items.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Процессов согласования может быть несколько. Прервем все.
	СписокОбъектовНаЗапись = Новый Массив;
	
	Для Каждого Элемент Из Ответ.items Цикл
		
		Процесс = Элемент.object;
		ПрочитанныйПроцесс = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
			Прокси,
			Процесс.objectID.type,
			Процесс.objectID.ID);
		СостояниеПрерван = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
			Прокси, "DMBusinessProcessState");
		СостояниеПрерван.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
			Прокси,
			"Прерван", //@NON-NLS-1
			"DMBusinessProcessState");
		СостояниеПрерван.name = "Прерван";
		ПрочитанныйПроцесс.state = СостояниеПрерван;
		СписокОбъектовНаЗапись.Добавить(ПрочитанныйПроцесс);
		
	КонецЦикла;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьОбъекты(Прокси, СписокОбъектовНаЗапись);
	
	Возврат Истина;
	
КонецФункции

// Получает задачи согласования по предмету, адресованные пользователю.
//
// Параметры:
//   ПредметДО - Структура:
//     * name - Строка - представление предмета.
//     * ID - Строка - идентификатор связанного объекта ДО.
//     * type - Строка - имя типа XDTO.
//
// Возвращаемое значение:
//   Массив из Строка - идентификаторы задач согласования в ДО.
//
Функция ПолучитьЗадачиСогласования(Знач ПредметДО) Экспорт
	
	Результат = Новый Массив;
	
	// Выберем активные задачи пользователя, потребовав указать их тип.
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	query = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListQuery");
	УсловияОтбора = query.conditions; // СписокXDTO
	
	Условие = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "target";
	Условие.value = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ПредметДО.ID,
		ПредметДО.type);
	УсловияОтбора.Добавить(Условие);
	
	Условие = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "byUser";
	Условие.value = Истина;
	УсловияОтбора.Добавить(Условие);
	
	Условие = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "typed";
	Условие.value = Истина;
	УсловияОтбора.Добавить(Условие);
	
	Условие = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "withExecuted";
	Условие.value = Ложь;
	УсловияОтбора.Добавить(Условие);
	
	Условие = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "withDelayed";
	Условие.value = Ложь;
	УсловияОтбора.Добавить(Условие);
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.НайтиСписокОбъектов(
		Прокси,
		"DMBusinessProcessApprovalTaskApproval",
		query);
	
	// Отберем полученные задачи по типу "Согласование".
	Для Каждого Элемент Из Ответ.items Цикл
		Задача = Элемент.object;
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, Задача, 
			"DMBusinessProcessApprovalTaskApproval") Тогда
			Результат.Добавить(Задача.objectID.ID);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Выполняет задачи согласования. Возвращает Истина в случае успеха, Ложь - в случае ошибки.
//
// Параметры:
//   Задачи - Массив из Строка - идентификаторы задач.
//   Результат - Строка - описание результата:
//     "Согласовано",
//     "СогласованоСЗамечаниями" или
//     "НеСогласовано".
//   Комментарий - Строка - комментарий к результатам "НеСогласовано" или "СогласованоСЗамечаниями".
//   ТекстСообщенияОбОшибке - Строка - неявно возвращаемое значение, описание ошибки.
//
// Возвращаемое значение:
//   Булево - Истина, если согласование всех задач выполнено успешно, и Ложь в противном случае.
//
Функция ВыполнитьСогласование(Знач Задачи, Знач Результат, Знач Комментарий, ТекстСообщенияОбОшибке) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Для Каждого ИдентификаторЗадачи Из Задачи Цикл
		
		Задача = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
			Прокси,
			"DMBusinessProcessApprovalTaskApproval",
			ИдентификаторЗадачи);
		
		Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMApprovalResult");
		Ответ.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
			Прокси,
			Результат,
			"DMApprovalResult");
		Если Результат = "Согласовано" Тогда //@NON-NLS-1
			Ответ.name = НСтр("ru = 'Согласовано'");
			
		ИначеЕсли Результат = "СогласованоСЗамечаниями" Тогда //@NON-NLS-1
			Ответ.name = НСтр("ru = 'Согласовано с замечаниями'");
			
		ИначеЕсли Результат = "НеСогласовано" Тогда //@NON-NLS-1
			Ответ.name = НСтр("ru = 'Не согласовано'");
			
		КонецЕсли;
		Задача.approvalResult = Ответ;
		Задача.executed = Истина;
		Задача.endDate = ТекущаяДатаСеанса();
		Задача.executionComment = Комментарий;
		
		РезультатЗаписи = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьОбъект(Прокси, Задача);
		
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, РезультатЗаписи, "DMError") Тогда
			ТекстСообщенияОбОшибке = РезультатЗаписи.description;
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Получает состояние согласования в ДО по ссылке на объект ИС
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - ссылка на объект ИС.
//   Пояснение - Строка - неявно возвращаемое значение, пояснение к состоянию.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.СостоянияСогласованияВДокументообороте - состояние согласования или
//   Неопределено - если согласование по объекту не запущено.
//
Функция ПолучитьСостояниеСогласования(Знач ПредметСогласования, Пояснение = "") Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	СостоянияСогласования.Состояние КАК Состояние,
		|	СостоянияСогласования.Установил КАК Установил,
		|	СостоянияСогласования.ДатаУстановки КАК ДатаУстановки
		|ИЗ
		|	РегистрСведений.СостоянияСогласованияВДокументообороте КАК СостоянияСогласования
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом КАК ОбъектыИнтегрированныеС1СДокументооборотом
		|		ПО СостоянияСогласования.ИдентификаторОбъектаДО = ОбъектыИнтегрированныеС1СДокументооборотом.ИдентификаторОбъектаДО
		|ГДЕ
		|	ОбъектыИнтегрированныеС1СДокументооборотом.Объект = &ПредметСогласования");
	Запрос.УстановитьПараметр("ПредметСогласования", ПредметСогласования);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Пояснение = ИнтеграцияС1СДокументооборот.ПояснениеСостоянияСогласования(
			Выборка.Установил, Выборка.ДатаУстановки);
		Возврат Выборка.Состояние;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает структуру, описывающую основные действия над объектом Документооборота
//
// Параметры:
//   Тип - Строка - тип XDTO объекта Документооборота.
//   Идентификатор - Строка - идентификатор объекта Документооборота.
//
// Возвращаемое значение:
//   Структура:
//     * КомандыСоздания - Массив из Структура:
//         ** Тип - Строка - полное имя типа, например, Справочник.Контрагенты
//         ** Представление - Строка - представление типа, например, "Поступление товаров и услуг"
//     * ВидДокументаID - Строка - для документов, вид документа предмета
//
Функция СтруктураКомандСозданияИОткрытия(Знач Тип, Знач Идентификатор) Экспорт
	
	Если Не ЗначениеЗаполнено(Тип) Или Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура;
	
	// существует ли связанный объект?
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	Реквизиты = Новый Массив;
	Реквизиты.Добавить("externalObject");
	
	Если ИнтеграцияС1СДокументооборотКлиентСервер.ЭтоДокумент(Тип) Тогда
		
		Реквизиты.Добавить("documentType");
		ОбъектXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
			Прокси,
			Тип,
			Идентификатор,
			Реквизиты);
		ВидДокументаID = ОбъектXDTO.documentType.ObjectID.ID;
		
	Иначе // объект иного типа, для которого не имеют смысла виды документов
		
		ОбъектXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
			Прокси,
			Тип,
			Идентификатор,
			Реквизиты);
		ВидДокументаID = Неопределено;
		
	КонецЕсли;
	
	КомандыСоздания = Новый Массив;
	Если ОбъектXDTO.externalObject = Неопределено Тогда
		ПравилаЗаполнения = ПодходящиеПравила(,ОбъектXDTO);
		Для Каждого ПравилоЗаполнения Из ПравилаЗаполнения Цикл
			КомандаСоздания = Новый Структура;
			КомандаСоздания.Вставить("Тип", ПравилоЗаполнения.ТипОбъектаИС);
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПравилоЗаполнения.ТипОбъектаИС);
			Если ОбъектМетаданных = Неопределено Тогда
				Представление = ПравилоЗаполнения.ТипОбъектаИС;
			Иначе
				Если Метаданные.Справочники.Содержит(ОбъектМетаданных) Тогда
					Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Элемент справочника %1'"),
						ОбъектМетаданных.Представление());
				Иначе
					Представление = ОбъектМетаданных.Представление();
				КонецЕсли;
			КонецЕсли;
			КомандаСоздания.Вставить("Представление", Представление);
			КомандыСоздания.Добавить(КомандаСоздания);
		КонецЦикла;
	КонецЕсли;
	Результат.Вставить("КомандыСоздания", КомандыСоздания);
	Результат.Вставить("ВидДокументаID", ВидДокументаID);
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру, описывающую варианты исполнения задачи.
//
// Параметры:
//   РеквизитыЗадачи - Структура:
//     * ЗадачаТип - Строка - тип задачи строкой.
//     * ЗадачаID - Строка - идентификатор задачи.
//     * ПроцессТип - Строка - имя тип процесса.
//     * ПроцессID - Строка - идентификатор процесса.
//     * ТочкаМаршрутаКратко - Строка - точка маршрута.
//     * Выполнена - Булево - признак выполнения задачи.
//     * ВидВопросаID - Строка - вид вопроса точки маршрута "Рассмотрение инициатором".
//     * РезультатID - Строка - результат выполнения задачи.
//
// Возвращаемое значение:
//   Структура:
//     * ФункционалНеДоступен - Булево - признак доступности функционала выполнения задачи в данной версии ДО.
//     * ВыполнитьЗадачуПервая - Строка - заголовок первой кнопки.
//     * ЦветТекстаПервая - Цвет - цвет первой кнопки.
//     * ВыполнитьЗадачуВторая - Строка - заголовок второй кнопки.
//     * ЦветТекстаВторая - Цвет - цвет второй кнопки.
//     * ВыполнитьЗадачуТретья - Строка - заголовок третьей кнопки.
//     * ЦветТекстаТретья - Цвет - цвет третьей кнопки.
//
Функция СтруктураИсполненияЗадачи(Знач РеквизитыЗадачи) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ФункционалНеДоступен", Ложь);
	Результат.Вставить("ВыполнитьЗадачуПервая", "");
	Результат.Вставить("ВыполнитьЗадачуВторая", "");
	Результат.Вставить("ВыполнитьЗадачуТретья", "");
	Результат.Вставить("ЦветТекстаПервая", ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи);
	Результат.Вставить("ЦветТекстаВторая", Новый Цвет);
	Результат.Вставить("ЦветТекстаТретья", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
	
	Если РеквизитыЗадачи.Выполнена Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "РассмотрениеИнициатором" И РеквизитыЗадачи.ВидВопросаID = "ПереносСрока" //@NON-NLS-1 @NON-NLS-2
			И Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP") Тогда
		Результат.ФункционалНеДоступен = Истина;
		Возврат Результат;
	КонецЕсли;
	
	Если РеквизитыЗадачи.Состояние = "Остановлен" Или РеквизитыЗадачи.Состояние = "Прерван" Тогда //@NON-NLS-1 @NON-NLS-2
		Возврат Результат;
	КонецЕсли;
	
	// Согласование.
	Если РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessApproval" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Согласовать" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Согласовано'"));
			Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Согласовано
																|с замечаниями'"));
			Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи);
			Результат.Вставить("ВыполнитьЗадачуТретья", НСтр("ru='Не согласовано'"));
			
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Ознакомился'"));
			
			Если РеквизитыЗадачи.РезультатID = "НеСогласовано" Тогда //@NON-NLS-1
				
				Результат.ВыполнитьЗадачуПервая = НСтр("ru='Завершить согласование'");
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Повторить согласование...'"));
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
				
			КонецЕсли;
		КонецЕсли;
		
	// Утверждение.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessConfirmation" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Утвердить" Тогда //@NON-NLS-1
			
			Если РеквизитыЗадачи.ЭтоПодписание Тогда
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Подписать'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Отклонить'"));
			Иначе
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Утверждено'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Не утверждено'"));
			КонецЕсли;
			Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
			
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Ознакомился'"));
			Если РеквизитыЗадачи.РезультатID = "НеУтверждено" Тогда //@NON-NLS-1
				
				Если РеквизитыЗадачи.ЭтоПодписание Тогда
					Результат.ВыполнитьЗадачуПервая = НСтр("ru='Завершить подписание'");
					Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Повторить подписание...'"));
				Иначе
					Результат.ВыполнитьЗадачуПервая = НСтр("ru='Завершить утверждение'");
					Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Повторить утверждение...'"));
				КонецЕсли;
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
				
			КонецЕсли;
			
		КонецЕсли;
		
	// Рассмотрение.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessConsideration" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Рассмотреть" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Рассмотрено'"));
			
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Обработано'"));
			
		КонецЕсли;
		
	// Поручение.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessOrder" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Выполнить" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Выполнено'"));
			
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Проверить" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Завершить поручение'"));
			Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Вернуть на доработку'"));
			Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
			
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Контролировать" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Снять с контроля'"));
			
		КонецЕсли;
		
	// Исполнение.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessPerformance" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Исполнить" //@NON-NLS-1
				Или РеквизитыЗадачи.ТочкаМаршрутаКратко = "ОтветственноеИсполнение" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Исполнено'"));
			
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Контролировать" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Снять с контроля'"));
			
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Проверить" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Завершить'"));
			Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Вернуть...'"));
			Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
			
		КонецЕсли;
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMComplexBusinessProcess" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Контролировать" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Проконтролировано'"));
			
		КонецЕсли;
		
	// Регистрация.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessRegistration" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Зарегистрировать" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Зарегистрировать'"));
			Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Не регистрировать'"));
			Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
			
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда //@NON-NLS-1
			Если РеквизитыЗадачи.РезультатID = "НеЗарегистрировано" Тогда //@NON-NLS-1
				
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Завершить регистрацию'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Повторить регистрацию'"));
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
				
			Иначе
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Ознакомился'"));
			КонецЕсли;
		КонецЕсли;
		
	// Ознакомление.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessAcquaintance" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда //@NON-NLS-1
			
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Ознакомился'"));
			
		КонецЕсли;
		
	// Решение вопросов.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessIssuesSolution" Тогда
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.2.7.3") Тогда
			Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "РассмотрениеИнициатором" //@NON-NLS-1
					И РеквизитыЗадачи.ВидВопросаID = "Иное" Тогда //@NON-NLS-1
				
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Рассмотрено'"));
				
			ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "РассмотрениеИнициатором" //@NON-NLS-1
					И РеквизитыЗадачи.ВидВопросаID = "ПереносСрока" Тогда //@NON-NLS-1
				
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Перенести срок'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Отказать в переносе'"));
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
				
			ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "ОзнакомлениеСРезультатомРассмотрения" Тогда //@NON-NLS-1
				
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Закрыть вопрос'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Уточнить'"));
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
				
			Иначе
				Результат.ФункционалНеДоступен = Истина;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		
	// Приглашение.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessInvitation" Тогда
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.2.7.3") Тогда
			Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Пригласить" Тогда //@NON-NLS-1
				
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Принять приглашение'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Отклонить приглашение'"));
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
				
			ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда //@NON-NLS-1
				// Зависит от результата принятия.
				Если РеквизитыЗадачи.РезультатID = "ПринятоВсемиУчастниками" Тогда //@NON-NLS-1
					
					Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Подтвердить приглашения'"));
					
				ИначеЕсли РеквизитыЗадачи.РезультатID = "ПринятоОбязательнымиУчастниками" Тогда //@NON-NLS-1
					
					Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Подтвердить приглашения'"));
					Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Повторить приглашение...'"));
					Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
					
				ИначеЕсли РеквизитыЗадачи.РезультатID = "НеПринятоОбязательнымиУчастниками" //@NON-NLS-1
						Или РеквизитыЗадачи.РезультатID = "НеПринятоВсемиУчастниками" Тогда //@NON-NLS-1
					
					Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Повторить приглашение...'"));
					Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Отменить приглашение'"));
					Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
					
				КонецЕсли;
			ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Оповестить" Тогда //@NON-NLS-1
				
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Ознакомился'"));
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Останавливает процесс ДО. Вызывает исключение в случае невозможности выполнить это в случае,
// когда прав недостаточно или текущее состояние процесса не позволяет выполнить это.
//
// Параметры:
//   ID - Строка - идентификатор процесса.
//   Тип - Строка - тип XDTO-типа процесса.
//
Процедура ОстановитьПроцесс(ID, Тип) Экспорт
	
	ИмяСостояния = "Остановлен"; //@NON-NLS-1
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	ПрочитанныйПроцесс = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
		Прокси,
		Тип,
		ID);
	
	НовоеСостояние = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
		Прокси,
		"DMBusinessProcessState");
	НовоеСостояние.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ИмяСостояния,
		"DMBusinessProcessState");
	НовоеСостояние.name = ИмяСостояния;
	ПрочитанныйПроцесс.state = НовоеСостояние;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьОбъект(Прокси, ПрочитанныйПроцесс);
	
КонецПроцедуры

// Прерывает процесс ДО. Вызывает исключение в случае невозможности выполнить это в случае,
// когда прав недостаточно или текущее состояние процесса не позволяет выполнить это.
//
// Параметры:
//   ID - Строка - идентификатор процесса.
//   Тип - Строка - тип XDTO-типа процесса.
//
Процедура ПрерватьПроцесс(ID, Тип) Экспорт
	
	ИмяСостояния = "Прерван"; //@NON-NLS-1
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	ПрочитанныйПроцесс = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
		Прокси,
		Тип,
		ID);
	
	НовоеСостояние = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
		Прокси,
		"DMBusinessProcessState");
	НовоеСостояние.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ИмяСостояния,
		"DMBusinessProcessState");
	НовоеСостояние.name = ИмяСостояния;
	ПрочитанныйПроцесс.state = НовоеСостояние;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьОбъект(Прокси, ПрочитанныйПроцесс);
	
КонецПроцедуры

// Продолжает процесс ДО. Вызывает исключение в случае невозможности выполнить это в случае,
// когда прав недостаточно или текущее состояние процесса не позволяет выполнить это.
//
// Параметры:
//   ID - Строка - идентификатор процесса.
//   Тип - Строка - тип XDTO-типа процесса.
//
Процедура ПродолжитьПроцесс(ID, Тип) Экспорт
	
	ИмяСостояния = "Активен"; //@NON-NLS-1
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	ПрочитанныйПроцесс = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
		Прокси,
		Тип,
		ID);
	
	НовоеСостояние = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
		Прокси,
		"DMBusinessProcessState");
	НовоеСостояние.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ИмяСостояния,
		"DMBusinessProcessState");
	НовоеСостояние.name = ИмяСостояния;
	ПрочитанныйПроцесс.state = НовоеСостояние;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьОбъект(Прокси, ПрочитанныйПроцесс);
	
КонецПроцедуры

// Возвращает подпись длительности переноса срока.
//
// Параметры:
//   Автор - Строка - представление пользователя.
//   АвторID - Строка - идентификатор пользователя.
//   АвторТип - Строка - имя тип пользователя.
//   СтарыйСрок - Дата - старый срок исполнения.
//   НовыйСрок - Дата - новый срок исполнения.
//
// Возвращаемое значение:
//   Строка - подпись длительности переноса срока.
//
Функция ПодписьДлительностиПереносаСрока(Автор, АвторID, АвторТип, СтарыйСрок, НовыйСрок) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMPostponementDurationRequest");
	Запрос.user = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMUser");
	Запрос.user.name = Автор;
	Запрос.user.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(Прокси, АвторID, АвторТип);
	Запрос.oldDueDate = СтарыйСрок;
	Запрос.newDueDate = НовыйСрок;
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	Возврат Ответ.postponementDuration;
	
КонецФункции

#КонецОбласти

#Область Документы

// Возвращает список XDTO документов по объекту Документооборота.
//
// Параметры:
//   ИдентификаторВладельца - УникальныйИдентификатор - уникальный идентификатор объекта Документооборота.
//   ИмяВладельца - Строка - представление объекта Документооборота.
//   ТипВладельца - Строка - тип XDTO объекта Документооборота
//
// Возвращаемое значение:
//   ОбъектXDTO - Список объектов XDTO типа DMInternalDocument.
//
Функция ДокументыПоВладельцу(ИдентификаторВладельца, ИмяВладельца, ТипВладельца) Экспорт

	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetDocumentListByOwnerRequest");
	Владельцы = Запрос.owners; // СписокXDTO
	ПолучаемыеПоля = Запрос.columnSet; // СписокXDTO
	
	ОбъектВладелец =  ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObject");
	ОбъектВладелец.name = ИмяВладельца;
	ОбъектВладелец.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		Строка(ИдентификаторВладельца),
		ТипВладельца);
	
	Владельцы.Добавить(ОбъектВладелец);
	
	ПолучаемыеПоля.Добавить("objectID");
	ПолучаемыеПоля.Добавить("signatures");
	ПолучаемыеПоля.Добавить("name");
	ПолучаемыеПоля.Добавить("author");
	
	Файлы = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Файлы);
	
	Возврат Файлы;
	
КонецФункции

// Добавляет связь указанного типа между двумя документами.
//
// Параметры:
//   ИсходныйДокумент - Структура - исходный документ:
//     * ID - Строка - идентификатор объекта ДО.
//     * Тип - Строка - тип объекта ДО.
//     * Представление - Строка - представление объекта ДО.
//   СвязанныйДокумент - Структура - связываемый документ:
//     * ID - Строка - идентификатор объекта ДО.
//     * Тип - Строка - тип объекта ДО.
//     * Представление - Строка - представление объекта ДО.
//   ТипСвязи - Структура - тип связи (необязательный):
//     * ID - Строка - идентификатор объекта ДО.
//     * Тип - Строка - тип объекта ДО.
//     * Представление - Строка - представление объекта ДО.
//
Процедура ДобавитьСвязьДокументов(ИсходныйДокумент, СвязанныйДокумент, ТипСвязи = Неопределено) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Связь = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMDocumentRelation");
	
	Document = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMDocument");
	Document.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ИсходныйДокумент.ID,
		ИсходныйДокумент.Тип);
	Document.name = ИсходныйДокумент.Представление;
	Связь.document = Document;
	
	RelatedDocument = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMDocument");
	RelatedDocument.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		СвязанныйДокумент.ID,
		СвязанныйДокумент.Тип);
	RelatedDocument.name = СвязанныйДокумент.Представление;
	Связь.relatedDocument = RelatedDocument;
	
	Если ЗначениеЗаполнено(ТипСвязи) Тогда
		RelationType = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMRelationType");
		RelationType.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
			Прокси,
			ТипСвязи.ID,
			ТипСвязи.Тип);
		RelationType.name = ТипСвязи.Представление;
		Связь.relationType = RelationType;
	КонецЕсли;
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMAddDocumentRelationRequest");
	Запрос.relation = Связь;
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

// Удаляет связь указанного типа между двумя документами.
//
// Параметры:
//   ИсходныйДокумент - Структура - исходный документ.
//   СвязанныйДокумент - Структура - связанный документ.
//   ТипСвязи - Структура - тип связи. Свойства структур:
//     ID - Строка - идентификатор объекта ДО.
//     Тип - Строка - тип объекта ДО.
//     Представление - Строка - представление объекта ДО.
//
Процедура УдалитьСвязьДокументов(ИсходныйДокумент, СвязанныйДокумент, ТипСвязи) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Document = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMDocument");
	Document.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ИсходныйДокумент.ID,
		ИсходныйДокумент.Тип);
	Document.name = ИсходныйДокумент.Представление;
	
	RelatedDocument = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMDocument");
	RelatedDocument.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		СвязанныйДокумент.ID,
		СвязанныйДокумент.Тип);
	RelatedDocument.name = СвязанныйДокумент.Представление;
	
	RelationType = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMRelationType");
	RelationType.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ТипСвязи.ID,
		ТипСвязи.Тип);
	RelationType.name = ТипСвязи.Представление;
	
	Связь = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMDocumentRelation");
	Связь.document = Document;
	Связь.relationType = RelationType;
	Связь.relatedDocument = RelatedDocument;
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMRemoveDocumentRelationRequest");
	Запрос.relation = Связь;
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

// Обновляет HTML-представление резолюций на форме документа.
//
// Параметры:
//   РезолюцииXDTO - СписокXDTO - список резолюций, объектов типа DMResolution.
//   ПредставлениеHTML - Строка - обновляемое HTML-представление.
//   ЭлементФормы - ГруппаФормы - элемент, заголовок которого следует обновить.
//
Процедура ОбновитьПредставлениеРезолюций(РезолюцииXDTO, ПредставлениеHTML, ЭлементФормы) Экспорт
	
	Если РезолюцииXDTO.Количество() = 0 Тогда
		ПредставлениеHTML = "";
		ЭлементФормы.Заголовок = НСтр("ru = 'Резолюции'");
		Возврат;
	КонецЕсли;
	
	Представление = "";
	Для Каждого РезолюцияXDTO Из РезолюцииXDTO Цикл
		Если РезолюцияXDTO.Установлено("reviewer") Тогда
			ПредставлениеИмени = РезолюцияXDTO.reviewer.name;
		Иначе
			ПредставлениеИмени = "";
		КонецЕсли;
		Если РезолюцияXDTO.signed Тогда
			Если РезолюцияXDTO.signatureChecked Тогда
				Если РезолюцияXDTO.signatureValid Тогда
					СтатусПроверки = "true";
				Иначе
					СтатусПроверки = "false";
				КонецЕсли;
			Иначе // не проверялась
				СтатусПроверки = "not";
			КонецЕсли;
		Иначе // не подписана
			СтатусПроверки = "";
		КонецЕсли;
		Представление = СтрШаблон(
			"%1<div class=""field""><p>%2</p><p><span class=""status %3"">%4</span></p><p>%5</p></div>",
			Представление, // 1
			СтрЗаменить(РезолюцияXDTO.text, Символы.ПС, "<br/>"), // 2
			СтатусПроверки, // 3
			ПредставлениеИмени, // 4
			Формат(РезолюцияXDTO.date, НСтр("ru = 'ДФ='dd.MM.yyyy ЧЧ:мм''"))); // 5
	КонецЦикла;
		
	ПредставлениеHTML = СтрШаблон(
		"<html>
		|<head><meta http-equiv=""X-UA-Compatible"" content=""IE=edge""></head>
		|<body>
		|<style>
		|* { font-family: sans-serif; font-size: 9pt; }
		|body { padding: 0; margin: 8px; overflow: auto; }
		|a { color: #1E90FF; }
		|p { padding: 0 0 4px 0; margin: 0; }
		|.field { margin-bottom: 6px; padding: 6px 8px 2px 8px; }
		|.active { background-color: #EEEEEE; }
		|.status { background-position: center right; background-repeat: no-repeat; padding: 1px 24px 2px 0; }
		|.not { background-image: url(""data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAABAAAAAQCAYAAAAf8/9hAAAACXBIWXMAAA7EAAAOxAGVKw4bAAAAB3RJTUUH3gcIDAYg91pdKgAAAAd0RVh0QXV0aG9yAKmuzEgAAAAMdEVYdERlc2NyaXB0aW9uABMJISMAAAAKdEVYdENvcHlyaWdodACsD8w6AAAADnRFWHRDcmVhdGlvbiB0aW1lADX3DwkAAAAJdEVYdFNvZnR3YXJlAF1w/zoAAAALdEVYdERpc2NsYWltZXIAt8C0jwAAAAh0RVh0V2FybmluZwDAG+aHAAAAB3RFWHRTb3VyY2UA9f+D6wAAAAh0RVh0Q29tbWVudAD2zJa/AAAABnRFWHRUaXRsZQCo7tInAAACAklEQVQ4ja2TsWsUQRjF35udOZNKK5tDAgtXyFleaxLBFPkDUooEJKTKXpWrlJAuVrtdECGFdvkDRFJ4mvbaw+Lg8AhnoZXdejs7zyJz4YhREPzK4f2+ed98bygJi5Vl2RrJdQAdSS0AIDkCMJDUL4ri46Ke8wbdbveepG2S9yX1SQ5ms9kIABqNRktSh+S6pM8kT/I8v7hqEOF9AMMkSd5UVbWSJElbUhodjOu6HjrnJnVdPwHQJvkyz/MLCwCStgEMrbWnVVVtGmOaAIYATqPT1Biz4b2fOudOvfdz5pB7e3trJHeNMc+895skfywtLX06Ojr6uThrr9e7VZblqqTb1tp3IYTXko5tnKtfVdWKMaYpqSzL8mmWZbhekr6SbEZtn+S6BdAheZAkSTvaTouiePUbfbmhHZLDqB0AODCSWrPZbCQplTS+CbzmYiwpjUzL3qCZZFm28wd+cv3Akhw1Go0WgDGANM/z939z0O12HwMYR2ZkcZmwTgjh3Biz0ev1zsuyXAXwBcADY8x3AKjr+s7y8vKZpHYI4cwY85DkwMbU7Trn3nrvp2VZri6scTS/eWGNU+fcJITwXNIxJSHLshcAvsUgPSLZJDmcPyrJVFI7wh+891sA7hZFcWij4ETSvvce1tqrKAPYigbGIYQz59zEe38VZeB/fabF+tfv/AvEjVUkU5cRDAAAAABJRU5ErkJggg==""); }
		|.true { background-image: url(""data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAABAAAAAQCAYAAAAf8/9hAAAACXBIWXMAAA7EAAAOxAGVKw4bAAAAB3RJTUUH3gcIDAcDTCYdGQAAAAd0RVh0QXV0aG9yAKmuzEgAAAAMdEVYdERlc2NyaXB0aW9uABMJISMAAAAKdEVYdENvcHlyaWdodACsD8w6AAAADnRFWHRDcmVhdGlvbiB0aW1lADX3DwkAAAAJdEVYdFNvZnR3YXJlAF1w/zoAAAALdEVYdERpc2NsYWltZXIAt8C0jwAAAAh0RVh0V2FybmluZwDAG+aHAAAAB3RFWHRTb3VyY2UA9f+D6wAAAAh0RVh0Q29tbWVudAD2zJa/AAAABnRFWHRUaXRsZQCo7tInAAABwElEQVQ4ja2TMWsUURSFv7nz8hQbrWwWCQxsIWOZ1s0KpvAHpBQJSCZdunRKSGlnlxEhhXb7A0S2cDXttoPFwOIS1kIru3H2zX0W+0Z3l0UQPOXl3nPePfe8yHvPMqIs3gX6wA7QDeUSGAMjnzcfV/pbgiiL7wAHwF1gBIypKQGwdANhH/gMXPi8ufpNEIZPgMIYfeNq2caQoiQACBMchbE6dU4eAynwwufNlQkvOQAKjA5cLY8QOigFMABASRD2XC0zrA5w0s6cRRzKLnBkjD4Nwz+2ruun+qX/ubyrPY6uzSvpodw0Vt85J6+BcxP2GrlatoNyNa/kSZTFrEJA+YrQCb0joG+COadh5wIh8Xnzig2IsvhwsSopyhg4FaBLTRkMm2waXMMEJQkX6poNDdOgtAnT9YIBSixdZMHs8+b93+SjLH6IMAnZKA2LhO3guETYs8fR5bySHvAFuIfwHQDHra0bOgRJcQwR7gNjwyJ1R8bqW1fLbF5Jb+mMZau8dMZZCNQz4LxN4nPgG0YH1PIAoQMU/DE1AVKUGVY/4GQfuO3z5qw18QI4wQnGrkR5H2ijPFyPMvyvz7Tm8j9951+Qy9eWUE15/AAAAABJRU5ErkJggg==""); }
		|.false { background-image: url(""data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAABAAAAAQCAYAAAAf8/9hAAAACXBIWXMAAA7EAAAOxAGVKw4bAAAAB3RJTUUH3gcIDAcaKE212QAAAAd0RVh0QXV0aG9yAKmuzEgAAAAMdEVYdERlc2NyaXB0aW9uABMJISMAAAAKdEVYdENvcHlyaWdodACsD8w6AAAADnRFWHRDcmVhdGlvbiB0aW1lADX3DwkAAAAJdEVYdFNvZnR3YXJlAF1w/zoAAAALdEVYdERpc2NsYWltZXIAt8C0jwAAAAh0RVh0V2FybmluZwDAG+aHAAAAB3RFWHRTb3VyY2UA9f+D6wAAAAh0RVh0Q29tbWVudAD2zJa/AAAABnRFWHRUaXRsZQCo7tInAAACBUlEQVQ4ja2TsWtTURTGf/fm3ZcSClZBl1BDX/IGiWNX0wh26B/QUaQgpX9BN6V07F9QROigW0cHkQ5Gu2YNgi99+ihxsIMRpLzcE+91yEuNWgTBbzz3fOd+55zvKO89s0hUbUVj2sAySDyJmgToOqQT++zNbL6aFjhRtUVgA8wtBx2NdL/hE4B5VOwwyxraIO+Ag7rPTi8KnKjaosNsg/RCwmfnjGohYRMkKhSkFturUM4s9j6Ypkb26j47DQolGyC9OdThOXYtRFWBnoNDAA1RiFrNsYMK6jBHCg676j03VzRmK0A9zLFrGvN1ge9vr/sPo9lez9RSeUip5ZArc4Qvx/inDtkPNKbtoDORrapjJB9iHgxVg19RwiGfAqieM6oFhB2Naas+9RcgO2Ai4IuDKPb9J1yCRDU2NaTAVZAUzI4GiSfTlsgh6WXEWUxyJCo4cfBnimSJamxeTpfs90gAJplHYjCphqju+6/+puBENe4BacFJAqDrMMtj7HGIWj1TS8dDSi2Qjxpua8IzAItduIY+Ate0+KOA8I6GbuCQjsZsVSg/z7GDIaXWZI3ZCEimP/9coxtUKGdj/COH7E+d+NjB54mR/N0AqmB606FqTATSHMOggnqd49c13Kj7bHc6xAMw2znC3IWVaWpYL95Tiz+qUM7ywsoge/C/jmkW/3rOPwBzWQNrm2r0hAAAAABJRU5ErkJggg==""); }
		|</style>
		|%1
		|</body>
		|</html>",
		Представление);
		
	ЭлементФормы.Заголовок = СтрШаблон(
		НСтр("ru = 'Резолюции (%1)'"),
		РезолюцииXDTO.Количество());
	
КонецПроцедуры

// Обновляет таблицу виз согласования на форме документа.
//
// Параметры:
//   ВизыXDTO - СписокXDTO - список виз, объектов типа DMVisa.
//   ВизыСогласования - ДанныеФормыКоллекция - обновляемая таблица виз.
//   ЭлементФормы - ГруппаФормы - элемент, заголовок которого следует обновить.
//
Процедура ОбновитьВизыСогласования(ВизыXDTO, ВизыСогласования, ЭлементФормы) Экспорт
	
	ВизыСогласования.Очистить();
	
	Если ВизыXDTO.Количество() = 0 Тогда
		ЭлементФормы.Заголовок = НСтр("ru = 'Визы'");
		Возврат;
	КонецЕсли;
	
	Для Каждого ВизаXDTO Из ВизыXDTO Цикл
		
		Виза = ВизыСогласования.Добавить();
		
		Виза.Наименование = ВизаXDTO.name;
		Виза.ID = ВизаXDTO.objectID.ID;
		Виза.Тип = ВизаXDTO.objectID.type;
		
		Если ВизаXDTO.Установлено("addedBy") Тогда
			Виза.Внес = ВизаXDTO.addedBy.name;
			Виза.ВнесID = ВизаXDTO.addedBy.objectID.ID;
			Виза.ВнесТип = ВизаXDTO.addedBy.objectID.type;
		КонецЕсли;
		
		Если ВизаXDTO.Установлено("reviewer") Тогда
			Виза.СогласующееЛицо = ВизаXDTO.reviewer.name;
			Виза.СогласующееЛицоID = ВизаXDTO.reviewer.objectID.ID;
			Виза.СогласующееЛицоТип = ВизаXDTO.reviewer.objectID.type;
		КонецЕсли;
		
		Если ВизаXDTO.Установлено("result") Тогда
			Виза.Результат = ВизаXDTO.result.name;
			Виза.РезультатID = ВизаXDTO.result.objectID.ID;
			Виза.РезультатТип = ВизаXDTO.result.objectID.type;
		КонецЕсли;
		
		Виза.Дата = ВизаXDTO.date;
		Виза.Комментарий = ВизаXDTO.comment;
		
		Если ВизаXDTO.signed Тогда
			Если ВизаXDTO.signatureChecked Тогда
				Если ВизаXDTO.signatureValid Тогда
					Виза.КартинкаСтатусаПодписи = 2;
				Иначе
					Виза.КартинкаСтатусаПодписи = 3;
				КонецЕсли;
			Иначе
				Виза.КартинкаСтатусаПодписи = 1;
			КонецЕсли;
		Иначе
			Виза.КартинкаСтатусаПодписи = 0;
		КонецЕсли;
		Виза.Комментарий = ВизаXDTO.comment;
		
	КонецЦикла;
		
	ЭлементФормы.Заголовок = СтрШаблон(
		НСтр("ru = 'Визы (%1)'"),
		ВизыXDTO.Количество());
	
КонецПроцедуры

// Регистрирует документ, если он еще не зарегистрирован.
//
// Параметры:
//   ДокументТип - Строка - тип документа.
//   ДокументID - Строка - тип документа.
//
Процедура ЗарегистрироватьДокументПриНеобходимости(ДокументТип, ДокументID) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	ОбъектXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
		Прокси,
		ДокументТип,
		ДокументID,
		"regNumber,regDate");
	
	Если ЗначениеЗаполнено(ОбъектXDTO.regNumber) И ЗначениеЗаполнено(ОбъектXDTO.regDate) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, ДокументТип);
	ОбъектXDTO.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ДокументID,
		ДокументТип);
	ОбъектXDTO.name = "";
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMDocumentRegistrationRequest");
	Запрос.document = ОбъектXDTO;
	
	Результат = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
КонецПроцедуры

#КонецОбласти

#Область Автообновление

// Загружает параметры Автообновление и ПериодАвтообновления списка из настроек.
//
// Параметры:
//   Форма - форма
//   ИмяСписка - Строка - имя элемента формы
//
Процедура ЗагрузитьНастройкиАвтообновленияСписка(Форма, ИмяСписка) Экспорт
	
	КлючОбъекта = Форма.ИмяФормы + ".Автообновление." + ПолучитьСкоростьКлиентскогоСоединения();
	Настройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта, ИмяСписка);
	Если ЗначениеЗаполнено(Настройки) Тогда
		Форма.Элементы[ИмяСписка].Автообновление = Настройки.Автообновление;
		Форма.Элементы[ИмяСписка].ПериодАвтоОбновления = Настройки.ПериодАвтоОбновления;
	КонецЕсли;
	
КонецПроцедуры

// Сохраняет настройки автообновления списка.
//
// Параметры:
//   ИмяФормы - Строка - имя формы, настройки которой должны быть сохранены.
//   ИмяСписка - Строка - имя списка формы, который должен обновляться.
//   Настройки - Структура:
//     * Автообновление - Булево - признак, что автообновление включено.
//     * ПериодАвтообновления - Число - период автообновления в секундах.
//
Процедура СохранитьНастройкиАвтообновленияСписка(ИмяФормы, ИмяСписка, Настройки) Экспорт
	
	КлючОбъекта = ИмяФормы + ".Автообновление." + ПолучитьСкоростьКлиентскогоСоединения();
	ХранилищеСистемныхНастроек.Сохранить(КлючОбъекта, ИмяСписка, Настройки);
	
КонецПроцедуры

// Сохраняет настройки автообновления Формы.
//
// Параметры:
//   ИмяФормы - Строка - имя формы, настройки которой должны быть сохранены.
//   Настройки - Структура:
//     * Автообновление - Булево - признак, что автообновление включено.
//     * ПериодАвтообновления - Число - период автообновления в секундах.
//
Процедура СохранитьНастройкиАвтообновленияФормы(ИмяФормы, Настройки) Экспорт
	
	КлючОбъекта = ИмяФормы + ".Автообновление";
	КлючНастройки = Строка(ПолучитьСкоростьКлиентскогоСоединения());
	ХранилищеСистемныхНастроек.Сохранить(КлючОбъекта, КлючНастройки, Настройки);
	
КонецПроцедуры

// Получает параметры Автообновление и ПериодАвтообновления списка из настроек.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения, Строка - форма или имя формы
//
// Возвращаемое значение:
//   Структура:
//     * Автообновление - Булево
//     * ПериодАвтоОбновления - Число
//
Функция НастройкиАвтообновленияФормы(Форма) Экспорт
	
	Результат = Новый Структура("Автообновление, ПериодАвтоОбновления", Ложь, 0);
	
	Если ТипЗнч(Форма) = Тип("Строка") Тогда
		КлючОбъекта = Форма + ".Автообновление";
	Иначе
		КлючОбъекта = Форма.ИмяФормы + ".Автообновление";
	КонецЕсли;
	
	КлючНастройки = Строка(ПолучитьСкоростьКлиентскогоСоединения());
	Настройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта, КлючНастройки);
	Если ЗначениеЗаполнено(Настройки) Тогда
		Результат.Автообновление = Настройки.Автообновление;
		Результат.ПериодАвтоОбновления = Настройки.ПериодАвтоОбновления;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Файлы

// Возвращает список XDTO файлов по объекту потребителю.
//
// Параметры:
//   ИдентификаторВладельца - УникальныйИдентификатор - уникальный идентификатор объекта потребителя.
//   ИмяВладельца - Строка - представление объекта потребителя.
//   ТипВладельца - Строка - тип объекта потребителя (используется полное имя метаданного объекта).
//
// Возвращаемое значение:
//   ОбъектXDTO - Список объектов XDTO типа DMFile.
//
Функция ФайлыПоОбъектуПотребителю(ИдентификаторВладельца, ИмяВладельца, ТипВладельца) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetFileListRequest");
	
	Владельцы = Запрос.externalObjects; // СписокXDTO
	Владельцы.Добавить(
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьExternalObject(
			Прокси,,
			Строка(ИдентификаторВладельца),
			ТипВладельца,
			ИмяВладельца));
	
	ПолучаемыеПоля = Запрос.columnSet; // СписокXDTO
	
	ПолучаемыеПоля.Добавить("objectID");
	ПолучаемыеПоля.Добавить("signed");
	ПолучаемыеПоля.Добавить("name");
	ПолучаемыеПоля.Добавить("size");
	ПолучаемыеПоля.Добавить("creationDate");
	ПолучаемыеПоля.Добавить("modificationDateUniversal");
	ПолучаемыеПоля.Добавить("author");
	ПолучаемыеПоля.Добавить("extension");
	ПолучаемыеПоля.Добавить("description");
	ПолучаемыеПоля.Добавить("editing");
	ПолучаемыеПоля.Добавить("encrypted");
	
	ПолучаемыеПоля.Добавить("signatures.author");
	ПолучаемыеПоля.Добавить("signatures.date");
	ПолучаемыеПоля.Добавить("signatures.comment");
	ПолучаемыеПоля.Добавить("signatures.signature");
	ПолучаемыеПоля.Добавить("signatures.thumbprint");
	ПолучаемыеПоля.Добавить("signatures.signer");
	ПолучаемыеПоля.Добавить("signatures.certificate");
	ПолучаемыеПоля.Добавить("signatures.signatureFileName");
	
	Файлы = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Файлы);
	
	Возврат Файлы;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Запускает в фоновом процессе длительную операцию ИнтеграцияС1СДокументооборот.СогласованиеВозможно.
//
// Параметры:
//   ИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор формы,
//     во временное хранилище которой надо поместить результат выполнения процедуры.
//   ПредметСогласования - ЛюбаяСсылка - предмет согласования.
//
// Возвращаемое значение:
//   Структура:
//     * Статус - Строка - "Выполняется", если задание еще не завершилось;
//         "Выполнено", если задание было успешно выполнено;
//         "Ошибка", если задание завершено с ошибкой;
//         "Отменено", если задание отменено пользователем или администратором.
//     * ИдентификаторЗадания - УникальныйИдентификатор - если Статус = "Выполняется", то содержит
//         идентификатор запущенного фонового задания.
//     * АдресРезультата - Строка - адрес временного хранилища, в которое будет
//         помещен (или уже помещен) результат работы процедуры.
//     * АдресДополнительногоРезультата - Строка - если установлен параметр ДополнительныйРезультат,
//         содержит адрес дополнительного временного хранилища,
//         в которое будет помещен (или уже помещен) результат работы процедуры.
//     * КраткоеПредставлениеОшибки - Строка - краткая информация об исключении, если Статус = "Ошибка".
//     * ПодробноеПредставлениеОшибки - Строка - подробная информация об исключении, если Статус = "Ошибка".
//     * Сообщения - ФиксированныйМассив из Строка - если Статус <> "Выполняется", то массив объектов
//         СообщениеПользователю, которые были сформированы в фоновом задании.
//
Функция ДлительнаяОперацияСогласованиеВозможно(ИдентификаторФормы, ПредметСогласования) Экспорт
	
	ПараметрыДлительнойОперации = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПараметрыДлительнойОперации();
	ПараметрыДлительнойОперации.Вставить("ПредметСогласования", ПредметСогласования);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Определение возможности согласования'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияС1СДокументооборот.СогласованиеВозможно",
		ПараметрыДлительнойОперации,
		ПараметрыВыполнения);
	
КонецФункции

// Запускает в фоновом процессе длительную операцию ИнтеграцияС1СДокументооборот.НачатьСогласование.
//
// Параметры:
//   ИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор формы,
//     во временное хранилище которой надо поместить результат выполнения процедуры.
//   ПредметСогласования - ЛюбаяСсылка - предмет согласования.
//
// Возвращаемое значение:
//   Структура:
//     * Статус - Строка - "Выполняется", если задание еще не завершилось;
//         "Выполнено", если задание было успешно выполнено;
//         "Ошибка", если задание завершено с ошибкой;
//         "Отменено", если задание отменено пользователем или администратором.
//     * ИдентификаторЗадания - УникальныйИдентификатор - если Статус = "Выполняется", то содержит
//         идентификатор запущенного фонового задания.
//     * АдресРезультата - Строка - адрес временного хранилища, в которое будет
//         помещен (или уже помещен) результат работы процедуры.
//     * АдресДополнительногоРезультата - Строка - если установлен параметр ДополнительныйРезультат,
//         содержит адрес дополнительного временного хранилища,
//         в которое будет помещен (или уже помещен) результат работы процедуры.
//     * КраткоеПредставлениеОшибки - Строка - краткая информация об исключении, если Статус = "Ошибка".
//     * ПодробноеПредставлениеОшибки - Строка - подробная информация об исключении, если Статус = "Ошибка".
//     * Сообщения - ФиксированныйМассив из Строка - если Статус <> "Выполняется", то массив объектов
//         СообщениеПользователю, которые были сформированы в фоновом задании.
//
Функция ДлительнаяОперацияНачатьСогласование(ИдентификаторФормы, ПредметСогласования) Экспорт
	
	ПараметрыДлительнойОперации = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПараметрыДлительнойОперации();
	ПараметрыДлительнойОперации.Вставить("ПредметСогласования", ПредметСогласования);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отправка документа на согласование в ДО'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияС1СДокументооборот.НачатьСогласование",
		ПараметрыДлительнойОперации,
		ПараметрыВыполнения);
	
КонецФункции

// Запускает в фоновом процессе длительную операцию ИнтеграцияС1СДокументооборот.ПрерываниеСогласованияВозможно.
//
// Параметры:
//   ИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор формы,
//     во временное хранилище которой надо поместить результат выполнения процедуры.
//   ПредметСогласования - ЛюбаяСсылка - предмет согласования.
//
// Возвращаемое значение:
//   Структура:
//     * Статус - Строка - "Выполняется", если задание еще не завершилось;
//         "Выполнено", если задание было успешно выполнено;
//         "Ошибка", если задание завершено с ошибкой;
//         "Отменено", если задание отменено пользователем или администратором.
//     * ИдентификаторЗадания - УникальныйИдентификатор - если Статус = "Выполняется", то содержит
//         идентификатор запущенного фонового задания.
//     * АдресРезультата - Строка - адрес временного хранилища, в которое будет
//         помещен (или уже помещен) результат работы процедуры.
//     * АдресДополнительногоРезультата - Строка - если установлен параметр ДополнительныйРезультат,
//         содержит адрес дополнительного временного хранилища,
//         в которое будет помещен (или уже помещен) результат работы процедуры.
//     * КраткоеПредставлениеОшибки - Строка - краткая информация об исключении, если Статус = "Ошибка".
//     * ПодробноеПредставлениеОшибки - Строка - подробная информация об исключении, если Статус = "Ошибка".
//     * Сообщения - ФиксированныйМассив из Строка - если Статус <> "Выполняется", то массив объектов
//         СообщениеПользователю, которые были сформированы в фоновом задании.
//
Функция ДлительнаяОперацияПрерываниеСогласованияВозможно(ИдентификаторФормы, ПредметСогласования) Экспорт
	
	ПараметрыДлительнойОперации = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПараметрыДлительнойОперации();
	ПараметрыДлительнойОперации.Вставить("ПредметСогласования", ПредметСогласования);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Определение возможности прерывания согласования'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияС1СДокументооборот.ПрерываниеСогласованияВозможно",
		ПараметрыДлительнойОперации,
		ПараметрыВыполнения);
	
КонецФункции

// Запускает в фоновом процессе длительную операцию ИнтеграцияС1СДокументооборот.ПрерватьСогласование.
//
// Параметры:
//   ИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор формы,
//     во временное хранилище которой надо поместить результат выполнения процедуры.
//   ПредметСогласования - ЛюбаяСсылка - предмет согласования.
//
// Возвращаемое значение:
//   Структура:
//     * Статус - Строка - "Выполняется", если задание еще не завершилось;
//         "Выполнено", если задание было успешно выполнено;
//         "Ошибка", если задание завершено с ошибкой;
//         "Отменено", если задание отменено пользователем или администратором.
//     * ИдентификаторЗадания - УникальныйИдентификатор - если Статус = "Выполняется", то содержит
//         идентификатор запущенного фонового задания.
//     * АдресРезультата - Строка - адрес временного хранилища, в которое будет
//         помещен (или уже помещен) результат работы процедуры.
//     * АдресДополнительногоРезультата - Строка - если установлен параметр ДополнительныйРезультат,
//         содержит адрес дополнительного временного хранилища,
//         в которое будет помещен (или уже помещен) результат работы процедуры.
//     * КраткоеПредставлениеОшибки - Строка - краткая информация об исключении, если Статус = "Ошибка".
//     * ПодробноеПредставлениеОшибки - Строка - подробная информация об исключении, если Статус = "Ошибка".
//     * Сообщения - ФиксированныйМассив из Строка - если Статус <> "Выполняется", то массив объектов
//         СообщениеПользователю, которые были сформированы в фоновом задании.
//
Функция ДлительнаяОперацияПрерватьСогласование(ИдентификаторФормы, ПредметСогласования) Экспорт
	
	ПараметрыДлительнойОперации = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПараметрыДлительнойОперации();
	ПараметрыДлительнойОперации.Вставить("ПредметСогласования", ПредметСогласования);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Прерывание бизнес-процесса согласования документа в ДО'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияС1СДокументооборот.ПрерватьСогласование",
		ПараметрыДлительнойОперации,
		ПараметрыВыполнения);
	
КонецФункции

// Запускает в фоновом процессе длительную операцию ИнтеграцияС1СДокументооборот.СостояниеСогласования.
//
// Параметры:
//   ИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор формы,
//     во временное хранилище которой надо поместить результат выполнения процедуры.
//   ПредметСогласования - ЛюбаяСсылка - предмет согласования.
//
// Возвращаемое значение:
//   Структура:
//     * Статус - Строка - "Выполняется", если задание еще не завершилось;
//         "Выполнено", если задание было успешно выполнено;
//         "Ошибка", если задание завершено с ошибкой;
//         "Отменено", если задание отменено пользователем или администратором.
//     * ИдентификаторЗадания - УникальныйИдентификатор - если Статус = "Выполняется", то содержит
//         идентификатор запущенного фонового задания.
//     * АдресРезультата - Строка - адрес временного хранилища, в которое будет
//         помещен (или уже помещен) результат работы процедуры.
//     * АдресДополнительногоРезультата - Строка - если установлен параметр ДополнительныйРезультат,
//         содержит адрес дополнительного временного хранилища,
//         в которое будет помещен (или уже помещен) результат работы процедуры.
//     * КраткоеПредставлениеОшибки - Строка - краткая информация об исключении, если Статус = "Ошибка".
//     * ПодробноеПредставлениеОшибки - Строка - подробная информация об исключении, если Статус = "Ошибка".
//     * Сообщения - ФиксированныйМассив из Строка - если Статус <> "Выполняется", то массив объектов
//         СообщениеПользователю, которые были сформированы в фоновом задании.
//
Функция ДлительнаяОперацияСостояниеСогласования(ИдентификаторФормы, ПредметСогласования) Экспорт
	
	ПараметрыДлительнойОперации = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПараметрыДлительнойОперации();
	ПараметрыДлительнойОперации.Вставить("ПредметСогласования", ПредметСогласования);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Получение текущего состояния согласования документа в ДО'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияС1СДокументооборот.СостояниеСогласования",
		ПараметрыДлительнойОперации,
		ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти