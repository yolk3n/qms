
#Область ПрограммныйИнтерфейс

// Производит инициализацию панели отборов.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ГруппаОтборов - ГруппаФормы - группа формы где размещаются отборы.
//
Процедура ОформитьПанельОтборов(Форма, ГруппаОтборов) Экспорт
	
	Описание = "
	|<Форма>
	|	<Элементы>
	|		<ГруппаФормы Имя='%1ПанельОтборов' Родитель='%2'>
	|				<Свойство Имя='Вид'>ОбычнаяГруппа</Свойство>
	|				<Свойство Имя='ОтображатьЗаголовок'>Ложь</Свойство>
	|				<Свойство Имя='Отображение'>Нет</Свойство>
	|				<Свойство Имя='Группировка'>Горизонтальная</Свойство>
	|				<Свойство Имя='ГоризонтальныйИнтервал'>Нет</Свойство>
	|			<ДекорацияФормы Имя='%1КнопкаСвернуть'>
	|					<Свойство Имя='Вид'>Картинка</Свойство>
	|					<Свойство Имя='Картинка'>СвернутьПанельОтборов</Свойство>
	|					<Свойство Имя='РастягиватьПоВертикали'>Истина</Свойство>
	|					<Свойство Имя='Гиперссылка'>Истина</Свойство>
	|				<События>
	|					<Нажатие Действие='Подключаемый_ПанельОтборов_СвернутьРазвернутьОтбор' />
	|				</События>
	|			</ДекорацияФормы>
	|			<ДекорацияФормы Имя='%1КнопкаРазвернуть'>
	|					<Свойство Имя='Вид'>Картинка</Свойство>
	|					<Свойство Имя='Картинка'>РазвернутьПанельОтборов</Свойство>
	|					<Свойство Имя='РастягиватьПоВертикали'>Истина</Свойство>
	|					<Свойство Имя='Гиперссылка'>Истина</Свойство>
	|				<События>
	|					<Нажатие Действие='Подключаемый_ПанельОтборов_СвернутьРазвернутьОтбор' />
	|				</События>
	|			</ДекорацияФормы>
	|		</ГруппаФормы>
	|	</Элементы>
	|</Форма>
	|";
	
	Префикс = ПанельОтборовКлиентСервер.Идентификатор();
	ИмяГруппыДляРазмещения = ГруппаОтборов.Родитель.Имя;
	
	Описание = УправляемаяФорма.ПрочитатьОписаниеФормыИзСтроки(СтрШаблон(Описание, Префикс, ИмяГруппыДляРазмещения));
	УправляемаяФорма.СоздатьЭлементы(Форма, Описание);
	
	Элементы = Форма.Элементы;
	ГруппаПанельОтборов = Элементы[Префикс + "ПанельОтборов"];
	КнопкаРазвернуть = Элементы[Префикс + "КнопкаРазвернуть"];
	КнопкаРазвернуть.Видимость = Ложь;
	
	Элементы.Переместить(ГруппаОтборов, ГруппаПанельОтборов);
	
	Если ГруппаОтборов.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Авто Тогда
		ГруппаОтборов.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Одинарный;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс
