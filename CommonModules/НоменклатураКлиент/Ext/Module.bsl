
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Регистрирует штрихкоды номенклатуры.
// (см. ЗаписатьШтрихкоды и Справочники.Номенклатура.Формы.ФормаЭлемента).
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура, для которой регистрируются штрихкоды.
//  Штрихкоды    - Массив - регистрируемые штрихкоды.
//
Процедура ЗарегистрироватьШтрихкоды(ДанныеШтрихкодов) Экспорт
	
	НоменклатураВызовСервера.ЗарегистрироватьШтрихкоды(ДанныеШтрихкодов);
	
	КонфликтыРегистрации = Новый Соответствие;
	Для Каждого КлючИЗначение Из ДанныеШтрихкодов Цикл
		ДанныеПоШтрихкоду = КлючИЗначение.Значение;
		Если ДанныеПоШтрихкоду.Статус = "Существует"
		   И ДанныеПоШтрихкоду.СуществующиеДанные.Номенклатура <> ДанныеПоШтрихкоду.Номенклатура Тогда
				КонфликтыРегистрации.Вставить(КлючИЗначение.Ключ, ДанныеПоШтрихкоду);
		КонецЕсли;
	КонецЦикла;
	
	Если КонфликтыРегистрации.Количество() > 0 Тогда
		
		ЗаголовокВопроса = НСтр("ru = 'Регистрация штрихкодов'");
		Текст = НСтр("ru = 'Штрихкоды уже зарегистрированы для другой номенклатуры.'");
		Текст = Текст + Символы.ПС + НСтр("ru = 'Выполнить автоматическую перерегистрацию?'");
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("КонфликтыРегистрации", КонфликтыРегистрации);
		
		Оповестить = Новый ОписаниеОповещения("ОбработатьОтветПеререгистрацииШтрихкодов", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповестить, Текст, РежимДиалогаВопрос.ДаНет,,, ЗаголовокВопроса);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОтветПеререгистрацииШтрихкодов(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		НоменклатураВызовСервера.ЗарегистрироватьШтрихкоды(ДополнительныеПараметры.КонфликтыРегистрации, Истина);
	Иначе
		
		Для Каждого КлючИЗначение Из ДополнительныеПараметры.КонфликтыРегистрации Цикл
			
			Штрихкод     = КлючИЗначение.Ключ;
			Номенклатура = КлючИЗначение.Значение.СуществующиеДанные.Номенклатура;
			
			Текст = НСтр("ru = 'Штрихкод ""%1"" уже зарегистрирован для номенклатуры ""%2""'");
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, Штрихкод, Номенклатура);
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(Текст, Номенклатура);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс
