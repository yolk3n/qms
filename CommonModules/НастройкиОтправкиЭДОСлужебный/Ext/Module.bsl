
#Область СлужебныеПроцедурыИФункции

// Возвращает настройки отправки.
// 
// Параметры:
// 	КлючНастроекОтправки - см. НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки
// Возвращаемое значение:
// 	- Неопределено - настройки не существуют
// 	- См. НастройкиЭДОКлиентСервер.НоваяНастройкаОтправки
Функция НастройкиОтправки(КлючНастроекОтправки) Экспорт
	
	Если КлючНастроекОтправки = Неопределено Тогда
		Возврат КлючНастроекОтправки;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует(
		"ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС") Тогда
		МодульЭлектронноеАктированиеЕИС = ОбщегоНазначения.ОбщийМодуль("ЭлектронноеАктированиеЕИС");
		Если МодульЭлектронноеАктированиеЕИС.ИспользоватьЭлектронноеАктированиеВЕИС()
			ИЛИ МодульЭлектронноеАктированиеЕИС.ИспользоватьЭлектронноеАктированиеЗаказчикаВЕИС() Тогда
			НастройкиОтправкиЕИС = МодульЭлектронноеАктированиеЕИС.НастройкиОтправкиПоКлючу(
				КлючНастроекОтправки);
			Если ЗначениеЗаполнено(НастройкиОтправкиЕИС) Тогда
				Возврат НастройкиОтправкиЕИС;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Настройка = НастройкиЭДОКлиентСервер.НоваяНастройкаОтправки();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиОтправкиПоВидам.Договор = &Договор КАК ЭтоНастройкаПоДоговору,
		|	НастройкиОтправкиПоВидам.Договор КАК Договор
		|ИЗ
		|	РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК НастройкиОтправкиПоВидам
		|ГДЕ
		|	НастройкиОтправкиПоВидам.Отправитель = &Отправитель
		|	И НастройкиОтправкиПоВидам.Получатель = &Получатель
		|	И НастройкиОтправкиПоВидам.Договор В (&Договор, &ПустойДоговор)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭтоНастройкаПоДоговору УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиОтправкиПоВидам.Отправитель КАК Отправитель,
		|	НастройкиОтправкиПоВидам.Получатель КАК Получатель,
		|	НастройкиОтправкиПоВидам.Договор КАК Договор,
		|	НастройкиОтправкиПоВидам.ВидДокумента КАК ВидДокумента,
		|	НастройкиОтправкиПоВидам.ВерсияФормата КАК Формат,
		|	НастройкиОтправкиПоВидам.СпособОбменаЭД КАК СпособОбмена,
		|	НастройкиОтправкиПоВидам.ИдентификаторОтправителя КАК ИдентификаторОтправителя,
		|	НастройкиОтправкиПоВидам.ИдентификаторПолучателя КАК ИдентификаторПолучателя,
		|	НастройкиОтправкиПоВидам.ТребуетсяОтветнаяПодпись КАК ТребуетсяОтветнаяПодпись,
		|	НастройкиОтправкиПоВидам.ТребуетсяИзвещениеОПолучении КАК ТребуетсяИзвещениеОПолучении,
		|	НастройкиОтправкиПоВидам.ВыгружатьДополнительныеСведения КАК ВыгружатьДополнительныеСведения,
		|	НастройкиОтправкиПоВидам.Формировать КАК Формировать,
		|	НастройкиОтправкиПоВидам.ВерсияФорматаУстановленаВручную КАК ВерсияФорматаУстановленаВручную,
		|	НастройкиОтправкиПоВидам.ЗаполнениеКодаТовара КАК ЗаполнениеКодаТовара,
		|	НастройкиОтправкиПоВидам.ОбменБезПодписи КАК ОбменБезПодписи,
		|	НастройкиОтправкиПоВидам.МаршрутПодписания КАК МаршрутПодписания,
		|	НастройкиОтправкиПоВидам.Договор = &Договор КАК ЭтоНастройкаПоДоговору
		|ИЗ
		|	РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК НастройкиОтправкиПоВидам
		|ГДЕ
		|	НастройкиОтправкиПоВидам.Отправитель = &Отправитель
		|	И НастройкиОтправкиПоВидам.Получатель = &Получатель
		|	И НастройкиОтправкиПоВидам.ВидДокумента = &ВидДокумента
		|	И НастройкиОтправкиПоВидам.Договор В (&Договор, &ПустойДоговор)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭтоНастройкаПоДоговору УБЫВ";
		
	Запрос.УстановитьПараметр("ВидДокумента", КлючНастроекОтправки.ВидДокумента);
	Запрос.УстановитьПараметр("Договор", КлючНастроекОтправки.Договор);
	Запрос.УстановитьПараметр("ПустойДоговор", Метаданные.ОпределяемыеТипы.ДоговорСКонтрагентомЭДО.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("Отправитель", КлючНастроекОтправки.Отправитель);
	Запрос.УстановитьПараметр("Получатель", КлючНастроекОтправки.Получатель);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ЕстьНастройкаПоКлючу = Не МассивРезультатов[0].Пустой();
	ЕстьНастройкаПоВидуДокумента = Не МассивРезультатов[1].Пустой();
	
	Если ЕстьНастройкаПоКлючу Тогда
		ОбновляемаяНастройка = МассивРезультатов[0].Выбрать();
		ОбновляемаяНастройка.Следующий();
	КонецЕсли;

	ТребуетсяОбновитьНастройку = ЕстьНастройкаПоКлючу И Не ЕстьНастройкаПоВидуДокумента; 
	
	Если ЕстьНастройкаПоКлючу И ЕстьНастройкаПоВидуДокумента Тогда
		ПолучаемаяНастройка = МассивРезультатов[1].Выбрать();
		ПолучаемаяНастройка.Следующий();
		ТребуетсяОбновитьНастройку = 
			ОбновляемаяНастройка.ЭтоНастройкаПоДоговору 
				И Не ПолучаемаяНастройка.ЭтоНастройкаПоДоговору; 
	КонецЕсли;
	
	Если ТребуетсяОбновитьНастройку Тогда
		
		ВидыЭлектронныхДокументов = ЭлектронныеДокументыЭДО.ИспользуемыеВидыДокументовИсходящиеПрикладные();
	
		ДокументИспользуется = ВидыЭлектронныхДокументов.Найти(КлючНастроекОтправки.ВидДокумента) <> Неопределено;
		Если Не ДокументИспользуется Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнитьНастройкиОтправкиНовымВидомДокумента(КлючНастроекОтправки, ОбновляемаяНастройка.Договор);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		ЕстьНастройкаПоВидуДокумента = Не МассивРезультатов[1].Пустой();
		
	КонецЕсли;
		
	Если Не ЕстьНастройкаПоВидуДокумента Тогда 
		Возврат Неопределено;
	КонецЕсли; 
	
	ВыборкаДетальныеЗаписи = МассивРезультатов[1].Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Настройка, ВыборкаДетальныеЗаписи);
		КлючПриглашенияНаИдентификатор = ПриглашенияЭДОКлиентСервер.КлючПриглашенияНаИдентификатор();
		КлючПриглашенияНаИдентификатор.ИдентификаторОрганизации = Настройка.ИдентификаторОтправителя;
		КлючПриглашенияНаИдентификатор.ИдентификаторКонтрагента = Настройка.ИдентификаторПолучателя;

		КлючПриглашения = ПриглашенияЭДОКлиентСервер.НовыйКлючПриглашения();
		КлючПриглашения.Ключ = ПриглашенияЭДО.КлючПриглашенияПоНатуральнымКлючам(КлючПриглашенияНаИдентификатор);
		КлючПриглашения.ИдентификаторОрганизации = Настройка.ИдентификаторОтправителя;

		ДанныеПриглашения = ПриглашенияЭДО.ДанныеПриглашения(КлючПриглашения);
		Если ДанныеПриглашения = Неопределено Тогда
			СтатусПриглашения = Неопределено;
		Иначе
			СтатусПриглашения = ДанныеПриглашения.Статус;
		КонецЕсли;
		Настройка.ГотовностьКОбмену = ПриглашенияЭДО.ПриглашениеПринято(СтатусПриглашения)
			Или ПриглашенияЭДО.ОжидаетсяОтветНаПриглашение(СтатусПриглашения);
			
	КонецЦикла;
	
	Возврат Настройка;
	
КонецФункции

// Дополняет настройки отправки по ключу настроек новым видом документа в разрезе договора.
// 
// Параметры:
// 	КлючНастроекОтправки - см. НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки
// 	Договор - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//
Процедура ДополнитьНастройкиОтправкиНовымВидомДокумента(КлючНастроекОтправки, Договор)
	
	ТаблицаНастроек = 
		РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьНастройкиОтправкиДокументов();
	ТаблицаНастроек.Колонки.Добавить("ЭтоНовыйВидЭД", Новый ОписаниеТипов("Булево"));
	
	НастройкиОтправки = НастройкиОтправкиЭДО.ТекущиеНастройкиОтправкиЭлектронныхДокументовПоВидам(
		КлючНастроекОтправки.Отправитель,
		КлючНастроекОтправки.Получатель,
		Договор, 
		ТаблицаНастроек);
	
	НастройкаПоВидуДокумента = НастройкиОтправки.Найти(КлючНастроекОтправки.ВидДокумента, "ВидДокумента");
	
	НастройкаПоВидуДокумента.Отправитель = КлючНастроекОтправки.Отправитель; 
	НастройкаПоВидуДокумента.Получатель = КлючНастроекОтправки.Получатель;
	НастройкаПоВидуДокумента.Договор = Договор;
	
	МенеджерЗаписи = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, НастройкаПоВидуДокумента);
	УстановитьПривилегированныйРежим(Истина);
	МенеджерЗаписи.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// См. НастройкиОтправкиЭДО.ИзменитьНастройку
Процедура ИзменитьНастройку(Настройка) Экспорт
	
	Менеджер = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Менеджер, Настройка, "Отправитель, Получатель, Договор, ВидДокумента");
	Менеджер.Прочитать();

	Свойства = "ИдентификаторОтправителя, ИдентификаторПолучателя,
	|МаршрутПодписания, ТребуетсяОтветнаяПодпись, ТребуетсяИзвещениеОПолучении, ВыгружатьДополнительныеСведения,
	|ВерсияФорматаУстановленаВручную, ЗаполнениеКодаТовара";
	МассивСвойств = СтрРазделить(Свойства, ",");
	Для Каждого Свойство Из МассивСвойств Цикл
		ИмяСвойства = СокрЛП(Свойство);
		ЗначениеСвойства = Настройка[ИмяСвойства];
		Если ЗначениеЗаполнено(ЗначениеСвойства) Тогда
			Менеджер[ИмяСвойства] = ЗначениеСвойства;
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(Настройка.Формат) Тогда
		Менеджер.ВерсияФормата = Настройка.Формат;
	КонецЕсли;
	
	Менеджер.Записать();
	
КонецПроцедуры

// См. НастройкиОтправкиЭДО.ЭтоРасширеннаяНастройка
Функция ЭтоРасширеннаяНастройка(КлючНастроекОтправки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторОтправителя КАК ИдентификаторОтправителя,
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторПолучателя КАК ИдентификаторПолучателя
		|ИЗ
		|	РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК НастройкиОтправкиЭлектронныхДокументовПоВидам
		|ГДЕ
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.Отправитель = &Отправитель
		|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.Получатель = &Получатель
		|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.Договор = &Договор";
	
	Запрос.УстановитьПараметр("Отправитель", КлючНастроекОтправки.Отправитель);
	Запрос.УстановитьПараметр("Получатель", КлючНастроекОтправки.Получатель);
	Запрос.УстановитьПараметр("Договор", КлючНастроекОтправки.Договор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Возврат ВыборкаДетальныеЗаписи.Количество() > 1;
	
КонецФункции

// См. НастройкиОтправкиЭДО.ИзменитьТранспортныеНастройки
Процедура ИзменитьТранспортныеНастройки(КлючНастроекОтправки, ИдентификаторОтправителя,
	ИдентификаторПолучателя) Экспорт
	
	НаборЗаписей = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Отправитель.Установить(КлючНастроекОтправки.Отправитель);
	НаборЗаписей.Отбор.Получатель.Установить(КлючНастроекОтправки.Получатель);
	НаборЗаписей.Отбор.Договор.Установить(КлючНастроекОтправки.Договор);
	
	НачатьТранзакцию();
	Попытка
		
		ОбщегоНазначенияБЭД.УстановитьУправляемуюБлокировкуПоНаборуЗаписей(НаборЗаписей);
		НаборЗаписей.Прочитать();
		Таблица = НаборЗаписей.Выгрузить();
		Таблица.ЗаполнитьЗначения(ИдентификаторОтправителя, "ИдентификаторОтправителя");
		Таблица.ЗаполнитьЗначения(ИдентификаторПолучателя, "ИдентификаторПолучателя");
		НаборЗаписей.Загрузить(Таблица);
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Изменение настроек отправки ЭДО'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

Функция ИспользуетсяУПД_УКД(КлючНастроекОтправки) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользуетсяУПД", Ложь);
	Результат.Вставить("ИспользуетсяУКД", Ложь);
	
	Если ОбщегоНазначения.ПодсистемаСуществует(
		"ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС") Тогда
		МодульЭлектронноеАктированиеЕИС = ОбщегоНазначения.ОбщийМодуль("ЭлектронноеАктированиеЕИС");
		Если МодульЭлектронноеАктированиеЕИС.ИспользоватьЭлектронноеАктированиеВЕИС() Тогда
			НастройкиЕИС = МодульЭлектронноеАктированиеЕИС.ИспользуетсяУПД_УКД(КлючНастроекОтправки);
			Если ЗначениеЗаполнено(НастройкиЕИС) Тогда
				Возврат НастройкиЕИС;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиОтправкиЭлектронныхДокументов.ИспользоватьУПД КАК ИспользуетсяУПД,
		|	НастройкиОтправкиЭлектронныхДокументов.ИспользоватьУКД КАК ИспользуетсяУКД,
		|	ВЫБОР
		|		КОГДА НастройкиОтправкиЭлектронныхДокументов.Договор = &Договор
		|			ТОГДА 0
		|		КОГДА НастройкиОтправкиЭлектронныхДокументов.Договор = &ПустойДоговор
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК Порядок
		|ИЗ
		|	РегистрСведений.НастройкиОтправкиЭлектронныхДокументов КАК НастройкиОтправкиЭлектронныхДокументов
		|ГДЕ
		|	НастройкиОтправкиЭлектронныхДокументов.Отправитель = &Отправитель
		|	И НастройкиОтправкиЭлектронныхДокументов.Получатель = &Получатель
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
	
	Запрос.УстановитьПараметр("Договор", КлючНастроекОтправки.Договор);
	Запрос.УстановитьПараметр("ПустойДоговор", Метаданные.ОпределяемыеТипы.ДоговорСКонтрагентомЭДО.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("Отправитель", КлючНастроекОтправки.Отправитель);
	Запрос.УстановитьПараметр("Получатель", КлючНастроекОтправки.Получатель);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Результат, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции 

Функция ОбщиеПараметрыНастроек(КлючНастроекОтправки) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользуетсяУПД", Ложь);
	Результат.Вставить("ИспользуетсяУКД", Ложь);
	
	Если ОбщегоНазначения.ПодсистемаСуществует(
		"ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС") Тогда
		МодульЭлектронноеАктированиеЕИС = ОбщегоНазначения.ОбщийМодуль("ЭлектронноеАктированиеЕИС");
		Если МодульЭлектронноеАктированиеЕИС.ИспользоватьЭлектронноеАктированиеВЕИС() Тогда
			НастройкиЕИС = МодульЭлектронноеАктированиеЕИС.ИспользуетсяУПД_УКД(КлючНастроекОтправки);
			Если ЗначениеЗаполнено(НастройкиЕИС) Тогда
				Возврат НастройкиЕИС;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиОтправкиЭлектронныхДокументов.ИспользоватьУПД КАК ИспользуетсяУПД,
		|	НастройкиОтправкиЭлектронныхДокументов.ИспользоватьУКД КАК ИспользуетсяУКД,
		|	ВЫБОР
		|		КОГДА НастройкиОтправкиЭлектронныхДокументов.Договор = &Договор
		|			ТОГДА 0
		|		КОГДА НастройкиОтправкиЭлектронныхДокументов.Договор = &ПустойДоговор
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК Порядок
		|ИЗ
		|	РегистрСведений.НастройкиОтправкиЭлектронныхДокументов КАК НастройкиОтправкиЭлектронныхДокументов
		|ГДЕ
		|	НастройкиОтправкиЭлектронныхДокументов.Отправитель = &Отправитель
		|	И НастройкиОтправкиЭлектронныхДокументов.Получатель = &Получатель
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
	
	Запрос.УстановитьПараметр("Договор", КлючНастроекОтправки.Договор);
	Запрос.УстановитьПараметр("ПустойДоговор", Метаданные.ОпределяемыеТипы.ДоговорСКонтрагентомЭДО.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("Отправитель", КлючНастроекОтправки.Отправитель);
	Запрос.УстановитьПараметр("Получатель", КлючНастроекОтправки.Получатель);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Результат, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция НоваяТаблицаНастроек() Экспорт
	
	Настройки = Новый ТаблицаЗначений;
	МетаданныеРегистра = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьНаборЗаписей().Метаданные();
	
	Для каждого Измерение Из МетаданныеРегистра.Измерения Цикл
		Настройки.Колонки.Добавить(Измерение.Имя, Измерение.Тип);
	КонецЦикла; 
	
	Для каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
		Настройки.Колонки.Добавить(Ресурс.Имя, Ресурс.Тип);
	КонецЦикла;
	
	Для каждого Реквизит Из МетаданныеРегистра.Реквизиты Цикл
		Настройки.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	
	Настройки.Колонки.Добавить("Приоритет", Новый ОписаниеТипов("Число"));
	Настройки.Колонки.Добавить("Группа", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(100)));
	Настройки.Колонки.Добавить("ПриоритетГруппы", Новый ОписаниеТипов("Число"));
	Настройки.Колонки.Добавить("ДокументУчета", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(100)));
	Настройки.Колонки.Добавить("ДополнительныеНастройки", Новый ОписаниеТипов("Строка"));
	
	Возврат Настройки;
	
КонецФункции

// Возвращает представление настройки отправки.
// 
// Параметры:
// 	КлючНастройки - РегистрСведенийКлючЗаписи.НастройкиОтправкиЭлектронныхДокументов
// Возвращаемое значение:
// 	Строка - Описание
Функция ПредставлениеНастройкиОтправки(КлючНастройки) Экспорт

	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(КлючНастройки.Получатель);
	МассивСтрок.Добавить(КлючНастройки.Отправитель);
	Если ЗначениеЗаполнено(КлючНастройки.Договор) Тогда
		МассивСтрок.Добавить(КлючНастройки.Договор);
	КонецЕсли;
	
	Возврат СтрСоединить(МассивСтрок, " • ");

КонецФункции

Функция СсылкаНаОбъектНастройкиЭДО(Организация, Контрагент, ДоговорКонтрагента) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиЭДО.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НастройкиЭДО КАК НастройкиЭДО
		|ГДЕ
		|	НастройкиЭДО.Организация = &Организация
		|	И НастройкиЭДО.Контрагент = &Контрагент
		|	И НастройкиЭДО.ДоговорКонтрагента = &ДоговорКонтрагента";
	
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ПоляБлокировки = Новый Структура;
	ПоляБлокировки.Вставить("Организация", Организация);
	ПоляБлокировки.Вставить("Контрагент", Контрагент);
	ПоляБлокировки.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
		
	НачатьТранзакцию();
	Попытка
		ОбщегоНазначенияБЭД.УстановитьУправляемуюБлокировку("Справочник.НастройкиЭДО", ПоляБлокировки);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Результат = ВыборкаДетальныеЗаписи.Ссылка;
		Иначе
			НовыйЭлемент = Справочники.НастройкиЭДО.СоздатьЭлемент();
			НовыйЭлемент.Организация        = Организация;
			НовыйЭлемент.Контрагент         = Контрагент;
			НовыйЭлемент.ДоговорКонтрагента = ДоговорКонтрагента;
			НовыйЭлемент.Записать();
			Результат = НовыйЭлемент.Ссылка;
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВидОперации = НСтр("ru = 'Поиск ссылки на настройку электронного документооборота'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ВидОперации, УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
		
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьСписокУчетныхЗаписейПрямогоОбмена(Список, Организация = Неопределено) Экспорт
	
	Запросы = Новый Массив;
	
	ОтборУчетныхЗаписей = СинхронизацияЭДО.НовыйОтборУчетныхЗаписей();
	ОтборУчетныхЗаписей.СпособОбмена = "&СпособОбмена";
	Если Организация <> Неопределено Тогда
		ОтборУчетныхЗаписей.Организация = "&Организация";
	КонецЕсли;
	ЗапросУчетныхЗаписей = СинхронизацияЭДО.ЗапросУчетныхЗаписей("УчетныеЗаписиЭДО", ОтборУчетныхЗаписей);
	Запросы.Добавить(ЗапросУчетныхЗаписей);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО,
		|	УчетныеЗаписиЭДО.НаименованиеУчетнойЗаписи
		|ИЗ
		|	УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО";
	
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	ИтоговыйЗапрос.УстановитьПараметр("Организация", Организация);
	ИтоговыйЗапрос.УстановитьПараметр("СпособОбмена", СинхронизацияЭДО.СпособыПрямогоОбмена());
	РезультатЗапроса = ИтоговыйЗапрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Список.Добавить(ВыборкаДетальныеЗаписи.ИдентификаторЭДО, ВыборкаДетальныеЗаписи.НаименованиеУчетнойЗаписи);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНастройкиПоВидамЭлектронныхДокументов(Настройки, ВидыДокументов, СпособОбмена = Неопределено) Экспорт
	
	ШаблоныНастроекПоВидам = ЭлектронныеДокументыЭДО.ШаблоныНастроекОтправкиВидовДокументов(ВидыДокументов);
	
	Для Каждого ШаблонНастройки Из ШаблоныНастроекПоВидам Цикл
		Настройка = Настройки.Добавить();
		ЗаполнитьЗначенияСвойств(Настройка, ШаблонНастройки);
		Настройка.ВерсияФормата = ШаблонНастройки.Формат;
		
		// Снимаем использование по умолчанию, если тип документа: "Договорной документ"
		Если Настройка.ВидДокумента.ТипДокумента <> Перечисления.ТипыДокументовЭДО.ДоговорнойДокумент Тогда
			Настройка.Формировать = Истина;
		Иначе
			Настройка.Формировать = Ложь;
		КонецЕсли;

		Если СпособОбмена <> Неопределено Тогда
			Настройка.СпособОбменаЭД = СпособОбмена;
		КонецЕсли;
	КонецЦикла;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСГИСЭПД") Тогда
		МодульОбменСГИСЭПД = ОбщегоНазначения.ОбщийМодуль("ОбменСГИСЭПД");	
		МодульОбменСГИСЭПД.ПриЗаполненииНастройкиПоВидамЭлектронныхДокументов(Настройки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНастройкиПоВидамЭлектронныхДокументовИнтеркампани(Настройки, ВидыДокументов, Отправитель,
	Получатель) Экспорт
	
	ОтборФорматов = ЭлектронныеДокументыЭДО.НовыйОтборФорматовЭлектронныхДокументов();
	ОтборФорматов.ВидыДокументов = ВидыДокументов;
	ОтборФорматов.Действует = Истина;
	Форматы = ЭлектронныеДокументыЭДО.ФорматыЭлектронныхДокументов(ОтборФорматов);
	
	УпорядоченныеВидыДокументов = Новый Массив;
	ПервыйВидДокумента = ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(
		Перечисления.ТипыДокументовЭДО.ПередачаТоваровМеждуОрганизациями);
	ИндексЭлемента = ВидыДокументов.Найти(ПервыйВидДокумента);
	Если ИндексЭлемента <> Неопределено Тогда
		УпорядоченныеВидыДокументов.Добавить(ПервыйВидДокумента);
	КонецЕсли;
	Для Каждого ВидДокумента Из ВидыДокументов Цикл
		Если ВидДокумента <> ПервыйВидДокумента Тогда
			УпорядоченныеВидыДокументов.Добавить(ВидДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ВидДокумента Из УпорядоченныеВидыДокументов Цикл
		НоваяСтрока = Настройки.Добавить();
		НоваяСтрока.Отправитель              = Отправитель;
		НоваяСтрока.Получатель               = Получатель;
		НоваяСтрока.ВидДокумента             = ВидДокумента;
		НоваяСтрока.МаршрутПодписания        = МаршрутыПодписанияБЭД.МаршрутОднойДоступнойПодписью();
		НоваяСтрока.СпособОбменаЭД           = Перечисления.СпособыОбменаЭД.Внутренний;
		НоваяСтрока.Формировать              = Истина;
		
		ИдентификаторФормата = "";
		СведенияОФормате = Форматы.Найти(ВидДокумента, "ВидДокумента");
		Если СведенияОФормате <> Неопределено Тогда
			ИдентификаторФормата = СведенияОФормате.ИдентификаторФормата;
		КонецЕсли;
		НоваяСтрока.ВерсияФормата = ИдентификаторФормата;
	КонецЦикла;
	
КонецПроцедуры

Функция ЗаписатьНастройкиИнтеркампани(Настройки, Отправитель, Получатель) Экспорт
	
	Настройки.ЗаполнитьЗначения(Отправитель, "Отправитель");
	Настройки.ЗаполнитьЗначения(Получатель , "Получатель");
	
	ИдентификаторыОрганизацийИнтеркампани = ИдентификаторыОрганизацийИнтеркампани(
		Отправитель, Получатель);
		
	Настройки.ЗаполнитьЗначения(ИдентификаторыОрганизацийИнтеркампани.ИдентификаторОтправителя,
		"ИдентификаторОтправителя");
	Настройки.ЗаполнитьЗначения(ИдентификаторыОрганизацийИнтеркампани.ИдентификаторПолучателя,
		"ИдентификаторПолучателя");
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого СтрокаТЧ Из Настройки Цикл
			
			МенеджерЗаписи = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЧ);
			МенеджерЗаписи.Записать();
			
		КонецЦикла;
		
	ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ВидОперации = НСтр("ru = 'Сохранение настроек отправки между своими организациями ЭДО'");
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(ВидОперации, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке),
			НСтр("ru = 'Не удалось сохранить Настройки между своими организациями
                  |Подробнее см. в журнале регистрации.'"));
			
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ИдентификаторыОрганизацийИнтеркампани(Отправитель, Получатель)
	
	ИмяРеквизитаИННОрганизации = ИнтеграцияЭДО.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	ИмяРеквизитаКППОрганизации = ИнтеграцияЭДО.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
	
	МассивОрганизаций = Новый Массив;
	МассивОрганизаций.Добавить(Отправитель);
	МассивОрганизаций.Добавить(Получатель);
	
	ПараметрыОрганизаций = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивОрганизаций,
		ИмяРеквизитаИННОрганизации + ", " + ИмяРеквизитаКППОрганизации);
		
	ИдентификаторОтправителя = ИдентификаторОрганизацииИнтеркампани(
		ПараметрыОрганизаций.Получить(Отправитель), ИмяРеквизитаИННОрганизации, ИмяРеквизитаКППОрганизации);
				
	ИдентификаторПолучателя = ИдентификаторОрганизацииИнтеркампани(
		ПараметрыОрганизаций.Получить(Получатель), ИмяРеквизитаИННОрганизации, ИмяРеквизитаКППОрганизации);
	
	Идентификаторы = Новый Структура;
	Идентификаторы.Вставить("ИдентификаторОтправителя", ИдентификаторОтправителя);
	Идентификаторы.Вставить("ИдентификаторПолучателя", ИдентификаторПолучателя);
	
	Возврат Идентификаторы;
	
КонецФункции

Процедура ЗаполнитьТаблицуФормыНастроекОтправки(ТаблицаФормы, Настройки) Экспорт
	
	Настройки.Сортировать("ПриоритетГруппы, Приоритет");
	
	ТекущаяГруппа = "";
	Для Каждого СтрокаТаблицы Из Настройки Цикл
		Если СтрокаТаблицы.Группа <> ТекущаяГруппа Тогда
			НоваяСтрока = ТаблицаФормы.Добавить();
			НоваяСтрока.ЭтоГруппа = Истина;
			НоваяСтрока.ДокументУчета = СтрокаТаблицы.Группа;
			ТекущаяГруппа = СтрокаТаблицы.Группа;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ТаблицаФормы.Добавить(), СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьУсловноеОформлениеДляГруппировкиНастроек(УсловноеОформление, СкрываемыеПоля) Экспорт
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсходящиеДокументы.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ИсходящиеДокументыДокументУчета");
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсходящиеДокументы.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ИсходящиеДокументыДокументУчета");
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсходящиеДокументы.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Для Каждого СкрываемоеПоле Из СкрываемыеПоля Цикл
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(СкрываемоеПоле);
	КонецЦикла;
	
КонецПроцедуры

// Проверяет соответствие сертификатов маршрутам в настройке.
// 
// Параметры:
// 	ПроверяемаяНастройка - см. НовоеОписаниеПроверяемойНастройки
// 	КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.
// Возвращаемое значение:
// 	Булево
Функция СертификатыСоответствуютМаршрутам(ПроверяемаяНастройка, КонтекстДиагностики) Экспорт
	
	НаборыСертификатовУчетныхЗаписей = Новый Соответствие;
	
	Запросы = Новый Массив;
	
	Отбор = СинхронизацияЭДО.НовыйОтборСертификатовУчетныхЗаписей();
	Отбор.УчетныеЗаписи = "&УчетныеЗаписи";
	
	ЗапросСертификатовУчетныхЗаписей = СинхронизацияЭДО.ЗапросСертификатовУчетныхЗаписей("СертификатыУчетныхЗаписейЭДО",
		Отбор);
	Запросы.Добавить(ЗапросСертификатовУчетныхЗаписей);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО КАК УчетнаяЗапись,
	|	СертификатыУчетныхЗаписейЭДО.Сертификат КАК Сертификат
	|ИЗ
	|	СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
	|
	|УПОРЯДОЧИТЬ ПО
	|	УчетнаяЗапись,
	|	Сертификат
	|ИТОГИ ПО
	|	УчетнаяЗапись";
	
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	
	ИтоговыйЗапрос.УстановитьПараметр("УчетныеЗаписи", ПроверяемаяНастройка.ИдентификаторыОтправителя);
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаУчетныхЗаписей = ИтоговыйЗапрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	УстановитьПривилегированныйРежим(Ложь);
	Пока ВыборкаУчетныхЗаписей.Следующий() Цикл
		СертификатыУчетныхЗаписей = Новый Массив;
		ИдентификаторНабора = "ИД_";
		Выборка = ВыборкаУчетныхЗаписей.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если СертификатыУчетныхЗаписей.Найти(Выборка.Сертификат) = Неопределено Тогда
				СертификатыУчетныхЗаписей.Добавить(Выборка.Сертификат);
				ИдентификаторНабора = ИдентификаторНабора + Строка(Выборка.Сертификат.УникальныйИдентификатор());
			КонецЕсли; 
		КонецЦикла;
		
		ИдентификаторНабора = СтрЗаменить(ИдентификаторНабора, "-", "_");
		СтруктураОписанияНабораСертификатов = Новый Структура("ИдентификаторНабора, Сертификаты", 
			ИдентификаторНабора, СертификатыУчетныхЗаписей);
		НаборыСертификатовУчетныхЗаписей.Вставить(ВыборкаУчетныхЗаписей.УчетнаяЗапись, СтруктураОписанияНабораСертификатов);
	КонецЦикла;
	
	УникальныеПараметрыПроверки = Новый Структура;
	Для Каждого СтрокаНастройки Из ПроверяемаяНастройка.Настройки Цикл
		Если Не СтрокаНастройки.Формировать
			Или СтрокаНастройки.ОбменБезПодписи Тогда
			Продолжить;
		КонецЕсли;
		МаршрутПодписания        = СтрокаНастройки.МаршрутПодписания;
		
		ПараметрыНабораСертификатов = НаборыСертификатовУчетныхЗаписей[ПроверяемаяНастройка.ИдентификаторОтправителя];
		Если ПараметрыНабораСертификатов <> Неопределено Тогда
			ИдентификаторМаршрута   = СтрЗаменить(Строка(МаршрутПодписания.УникальныйИдентификатор()), "-", "_");
			ИдентификаторПараметров = ПараметрыНабораСертификатов.ИдентификаторНабора + Строка(ИдентификаторМаршрута);
			
			СтруктураОписанияПараметров = Неопределено;
			Если Не УникальныеПараметрыПроверки.Свойство(ИдентификаторПараметров, СтруктураОписанияПараметров) Тогда
				СтруктураОписанияПараметров = Новый Структура;
				СтруктураОписанияПараметров.Вставить("Сертификаты",       ПараметрыНабораСертификатов.Сертификаты);
				СтруктураОписанияПараметров.Вставить("МаршрутПодписания", МаршрутПодписания);
				СтруктураОписанияПараметров.Вставить("ВидыДокументов",    Новый Массив);
				УникальныеПараметрыПроверки.Вставить(ИдентификаторПараметров, СтруктураОписанияПараметров);
			КонецЕсли;
			СтруктураОписанияПараметров.ВидыДокументов.Добавить(СтрокаНастройки.ВидДокумента);
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеНастройки = Новый Структура("Отправитель, Получатель, Договор");
	ОписаниеНастройки.Отправитель = ПроверяемаяНастройка.Отправитель;
	ОписаниеНастройки.Получатель  = ПроверяемаяНастройка.Получатель;
	ОписаниеНастройки.Договор     = ПроверяемаяНастройка.Договор;
	НастройкаОбмена = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументов.СоздатьКлючЗаписи(ОписаниеНастройки);
	ПредставлениеНастройки = ПредставлениеНастройкиОтправки(ОписаниеНастройки);
	
	Отказ = Ложь;
	
	Для Каждого НаборПараметровПроверки Из УникальныеПараметрыПроверки Цикл
		МаршрутПодписания = НаборПараметровПроверки.Значение.МаршрутПодписания;
		Сертификаты       = НаборПараметровПроверки.Значение.Сертификаты;
		ВидыДокументов    = НаборПараметровПроверки.Значение.ВидыДокументов;
		
		РезультатыПроверки = МаршрутыПодписанияБЭД.РезультатыПроверкиМаршрутаПоПараметрамНастройки(
			МаршрутПодписания, Сертификаты, ВидыДокументов);
			
		МаршрутыПодписанияБЭД.ВывестиРезультатыПроверкиМаршрута(РезультатыПроверки, 
			НастройкаОбмена, МаршрутПодписания, Отказ,, ПредставлениеНастройки, КонтекстДиагностики);
	КонецЦикла;
	
	Возврат Не Отказ;
	
КонецФункции

// Возвращает описание настройки для проверки соответствия сертификатов маршрутам, см. СертификатыСоответствуютМаршрутам.
// 
// Возвращаемое значение:
// 	Структура:
// * Настройки - см. НовыеПроверяемыеНастройкиДокументов
// * ИдентификаторОтправителя - Строка
// * ИдентификаторыОтправителя - Массив из Строка
// * Отправитель - ОпределяемыйТип.Организация
// * Получатель - ОпределяемыйТип.КонтрагентБЭД
// * Договор - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
Функция НовоеОписаниеПроверяемойНастройки() Экспорт
	
	ОписаниеНастройки = Новый Структура;
	ОписаниеНастройки.Вставить("Отправитель");
	ОписаниеНастройки.Вставить("Получатель");
	ОписаниеНастройки.Вставить("Договор");
	ОписаниеНастройки.Вставить("ИдентификаторОтправителя", "");
	ОписаниеНастройки.Вставить("ИдентификаторыОтправителя", Новый Массив);
	ОписаниеНастройки.Вставить("Настройки", НовыеПроверяемыеНастройкиДокументов());
	
	Возврат ОписаниеНастройки;
	
КонецФункции

// Возвращает таблицу проверяемых настроек, см. НовоеОписаниеПроверяемойНастройки.
// 
// Возвращаемое значение:
// 	ТаблицаЗначений:
// * Формировать - Булево
// * МаршрутПодписания - СправочникСсылка.МаршрутыПодписания
// * ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
// * ОбменБезПодписи - Булево
Функция НовыеПроверяемыеНастройкиДокументов()
	
	Настройки = Новый ТаблицаЗначений;
	Настройки.Колонки.Добавить("Формировать", Новый ОписаниеТипов("Булево"));
	Настройки.Колонки.Добавить("ОбменБезПодписи", Новый ОписаниеТипов("Булево"));
	Настройки.Колонки.Добавить("МаршрутПодписания", Новый ОписаниеТипов("СправочникСсылка.МаршрутыПодписания"));
	Настройки.Колонки.Добавить("ВидДокумента", Новый ОписаниеТипов("СправочникСсылка.ВидыДокументовЭДО"));
	
	Возврат Настройки;
	
КонецФункции

// Возвращает измерения для поиска настроек по договору
//
// Параметры:
//   Договор - ОпределяемыйТип.ДоговорСКонтрагентомЭДО - Договор контрагента
//
//  Возвращаемое значение:
//   Структура:
//    * ВладелецДоговора - Произвольный
//    * КлючНастроекОтправки - см. НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки
//
Функция НастройкаПоДоговоруКонтрагента(Знач Договор) Экспорт
	
	ИмяРеквизитаДоговора = ИнтеграцияЭДО.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ВладелецДоговораКонтрагента");
	РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, ИмяРеквизитаДоговора);
	
	Результат = Новый Структура;
	Результат.Вставить("ВладелецДоговора", РеквизитыДоговора[ИмяРеквизитаДоговора]);
	Результат.Вставить("КлючНастроекОтправки", НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	НастройкиОтправкиЭлектронныхДокументов.Отправитель КАК Отправитель,
		|	НастройкиОтправкиЭлектронныхДокументов.Получатель КАК Получатель,
		|	НастройкиОтправкиЭлектронныхДокументов.Договор КАК Договор
		|ИЗ
		|	РегистрСведений.НастройкиОтправкиЭлектронныхДокументов КАК НастройкиОтправкиЭлектронныхДокументов
		|ГДЕ
		|	НастройкиОтправкиЭлектронныхДокументов.Договор = &Договор";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Результат.ВладелецДоговора = Неопределено;
			Результат.КлючНастроекОтправки.Отправитель = ВыборкаДетальныеЗаписи.Отправитель;
			Результат.КлючНастроекОтправки.Получатель = ВыборкаДетальныеЗаписи.Получатель;
			Результат.КлючНастроекОтправки.Договор = ВыборкаДетальныеЗаписи.Договор;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИдентификаторОрганизацииИнтеркампани(ПараметрыОрганизации, ИмяРеквизитаИННОрганизации, ИмяРеквизитаКППОрганизации)
	
	Ответ = ПараметрыОрганизации[ИмяРеквизитаИННОрганизации];
	
	Если ЗначениеЗаполнено(ПараметрыОрганизации[ИмяРеквизитаКППОрганизации]) Тогда
		Ответ = СтрШаблон(НСтр("ru = '%1_%2'"), ПараметрыОрганизации[ИмяРеквизитаИННОрганизации],
			ПараметрыОрганизации[ИмяРеквизитаКППОрганизации]);
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция НоваяНастройкаЗаполненияДополнительныхПолей(СтруктураДанных = Неопределено) Экспорт
	
	ТаблицаНастроек = Новый ТаблицаЗначений;
	ТаблицаНастроек.Колонки.Добавить("Идентификатор");
	ТаблицаНастроек.Колонки.Добавить("Имя");
	ТаблицаНастроек.Колонки.Добавить("Представление");
	ТаблицаНастроек.Колонки.Добавить("Описание");
	ТаблицаНастроек.Колонки.Добавить("Правило");
	ТаблицаНастроек.Колонки.Добавить("Заполнение");
	ТаблицаНастроек.Колонки.Добавить("Значение");
	ТаблицаНастроек.Колонки.Добавить("Версия");
	ТаблицаНастроек.Колонки.Добавить("Раздел");
	
	Если СтруктураДанных <> Неопределено Тогда
		Для Каждого НастройкаПоля Из СтруктураДанных Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаНастроек.Добавить(), НастройкаПоля);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТаблицаНастроек;
	
КонецФункции

// Возвращает соглашение об обмене электронными документами.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация
//  Контрагент - ОпределяемыйТип.УчастникЭДО
//  Договор - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
// 
// Возвращаемое значение:
//  Структура:
//  * НастройкаЭДО - СправочникСсылка.НастройкиЭДО
//  * ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
Функция СоглашениеОбОбменеЭлектроннымиДокументами(Организация, Контрагент, Договор) Экспорт
	
	НастройкаЭДО = СсылкаНаОбъектНастройкиЭДО(Организация, Контрагент, Договор);
	ВидДокумента = ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(Перечисления.ТипыДокументовЭДО.СоглашениеОбЭДО);
	
	Результат = Новый Структура;
	Результат.Вставить("НастройкаЭДО", НастройкаЭДО);
	Результат.Вставить("ВидДокумента", ВидДокумента);
	
	Возврат Результат;
	
КонецФункции

// Возвращает параметры каталога товаров для отправки по ЭДО.
// 
// Параметры:
//  Отправитель - ОпределяемыйТип.Организация
//  Получатель - ОпределяемыйТип.УчастникЭДО
//  Договор - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
// 
// Возвращаемое значение:
//  Структура:
//  * НастройкаЭДО - СправочникСсылка.НастройкиЭДО
//  * ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
Функция ПараметрыКаталогаТоваровДляОтправкиПоЭДО(Организация, Контрагент, Договор) Экспорт
	
	НастройкаЭДО = СсылкаНаОбъектНастройкиЭДО(Организация, Контрагент, Договор);
	ВидДокумента = ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(Перечисления.ТипыДокументовЭДО.КаталогТоваров);
	
	Результат = Новый Структура;
	Результат.Вставить("НастройкаЭДО", НастройкаЭДО);
	Результат.Вставить("ВидДокумента", ВидДокумента);
	
	Возврат Результат;
	
КонецФункции

// Выполняет проверку поддержки формата операторами.
// 
// Параметры:
// 	КлючНастроекОтправки - см. НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки
// 	Формат - Строка - формат электронного документа
// Возвращаемое значение:
// 	Структура:
// 		* Формат - Строка
// 		* ОператорОтправителя - Строка
// 		* ОператорПолучателя - Строка
// 		* ФорматПоддержанОператоромОтправителя - Булево
// 		* ФорматПоддержанОператоромПолучателя - Булево
// 	Неопределено
Функция ПроверитьПоддержкуФорматаОператорами(КлючНастроекОтправки, Формат)  Экспорт
	
	Если ПустаяСтрока(Формат) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НастройкиОтправки = НастройкиОтправки(КлючНастроекОтправки);
	Если НастройкиОтправки = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	ДанныеФормата = ФорматыЭДО.ПолучитьСведенияОФормате(Формат);
	Если ДанныеФормата = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПоддерживаемыеФорматы = ПоддерживаемыеОператорамиФорматы();
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("ИдентификаторФормата", ДанныеФормата.ИдентификаторСервиса);
	
	НайденныеСтроки = ПоддерживаемыеФорматы.НайтиСтроки(СтруктураОтбора);
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	Результат = Новый Структура;
	Результат.Вставить("Формат", ДанныеФормата.ПредставлениеФормата);
	Результат.Вставить("ОператорОтправителя", "");
	Результат.Вставить("ОператорПолучателя", "");
	Результат.Вставить("ФорматПоддержанОператоромОтправителя", Истина);
	Результат.Вставить("ФорматПоддержанОператоромПолучателя", Истина);
	
	МассивОператоров = Новый Массив;
	МассивОператоров.Добавить(НастройкиОтправки.ИдентификаторОтправителя);
	МассивОператоров.Добавить(НастройкиОтправки.ИдентификаторПолучателя);
	
	ОператорыЭДО = СервисНастроекЭДО.НайтиОператоровЭДОПоИдентификаторам(МассивОператоров);
	
	// Проверка поддержки формата оператором отправителя.
	ОператорОтправителя = ОператорыЭДО.Найти(НастройкиОтправки.ИдентификаторОтправителя, "ИдентификаторЭДО");
	Если ЗначениеЗаполнено(ОператорОтправителя) Тогда
		СтруктураОтбора.Вставить("ИдентификаторОператора", ОператорОтправителя.ИдентификаторОператора);
		НайденныеСтроки = ПоддерживаемыеФорматы.НайтиСтроки(СтруктураОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Результат.ОператорОтправителя = ОператорОтправителя.ПредставлениеОператора;
			Результат.ФорматПоддержанОператоромОтправителя = Ложь;
			Возврат Результат;
		КонецЕсли;	
	Иначе
		Результат.ФорматПоддержанОператоромОтправителя = Ложь;
		Возврат Результат;
	КонецЕсли;
	
	// Проверка поддержки формата оператором получателя.	
	ОператорПолучателя = ОператорыЭДО.Найти(НастройкиОтправки.ИдентификаторПолучателя, "ИдентификаторЭДО");
	Если ЗначениеЗаполнено(ОператорПолучателя) Тогда
		СтруктураОтбора.Вставить("ИдентификаторОператора", ОператорПолучателя.ИдентификаторОператора);
		НайденныеСтроки = ПоддерживаемыеФорматы.НайтиСтроки(СтруктураОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Результат.ОператорПолучателя = ОператорПолучателя.ПредставлениеОператора;
			Результат.ФорматПоддержанОператоромПолучателя = Ложь;
			Возврат Результат;
		КонецЕсли;	
	Иначе
		Результат.ФорматПоддержанОператоромПолучателя = Ложь;
		Возврат Результат;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Возвращает таблицу форматов, поддержанных операторами.
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - таблица значений с колонками:
// 		* ИдентификаторФормата - Строка
// 		* ИдентификаторОператора - Строка
Функция ПоддерживаемыеОператорамиФорматы()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ИдентификаторФормата", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(40)));
	Результат.Колонки.Добавить("ИдентификаторОператора", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(10)));
	
	ИдентификаторФорматаАктаСверкиВзаиморасчетов = "921e0a05-02fa-49f1-bcc8-44b070926843";
		
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2AL"; // ООО «Такском»
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2BN"; // ООО «Линк-сервис»
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2LB"; // ООО ЭТП ГПБ
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2BE"; // ООО «Тензор»
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2AE"; // АО «Калуга Астрал»
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2MB"; // ООО "ДИСТЭЙТ"
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2AO"; // ООО УЦ "АСКОМ"
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2AK"; // ЗАО "ТаксНет"
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2BM"; // АО "ПФ "СКБ Контур"
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2IJ"; // ООО "Эдисофт"
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2VO"; // Платформа ОФД (Эвотор ОФД)
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаСверкиВзаиморасчетов;
	НоваяСтрока.ИдентификаторОператора = "2LT"; // ООО "Оператор-ЦРПТ"
	
	ИдентификаторФорматаАктаПриемкиСтроительныхРабот = "7481f7be-2841-4580-aaa0-e4cc6daccb54";
	
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.ИдентификаторФормата = ИдентификаторФорматаАктаПриемкиСтроительныхРабот;
	НоваяСтрока.ИдентификаторОператора = "2AL"; // ООО «Такском»
	
	Возврат Результат;
	
КонецФункции	
	
#Область СформироватьСоглашениеПоШаблону

// Возвращает адрес на сформированное соглашение об ЭДО.
// 
// Параметры:
//  НастройкаЭДО - см. СсылкаНаОбъектНастройкиЭДО
// 
// Возвращаемое значение:
//  - Неопределено - если не удалось сформировать соглашение об ЭДО.
//  - ОписаниеФайла - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
Функция СформироватьСоглашениеПоШаблону(НастройкаЭДО) Экспорт
	
	ИмяМакета = "ПФ_DOC_СоглашениеОбОбменеЭлектроннымиДокументами";
	МакетИДанныеОбъекта = ПодготовитьДанныеПечатиСоглашенияПолучитьМакет(НастройкаЭДО, ИмяМакета);
	
	Области               = МакетИДанныеОбъекта.Макеты.ОписаниеОбластей;
	ДвоичныеДанныеМакетов = МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	ДанныеОбъекта = МакетИДанныеОбъекта.Данные[НастройкаЭДО][ИмяМакета];
	
	Макет = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(ДвоичныеДанныеМакетов[ИмяМакета], "");
	Если Макет <> Неопределено Тогда
		Попытка
			ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму("",, Макет);
			Если ПечатнаяФорма <> Неопределено Тогда
				// Вывод обычных областей с параметрами.
				Область = УправлениеПечатью.ОбластьМакета(Макет, Области[ИмяМакета]["Шапка"]);
				УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
				АдресХранилища = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма);
			КонецЕсли;
		Исключение
			ОбщегоНазначения.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЕсли;
	
	УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);
	
	Если Не ЗначениеЗаполнено(АдресХранилища) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
	УдалитьИзВременногоХранилища(АдресХранилища);
	
	Если Не ЗначениеЗаполнено(ДвоичныеДанные) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеФайла = РаботаСФайламиБЭД.НовоеОписаниеФайла();
	ОписаниеФайла.ИмяФайла = НСтр("ru = 'Соглашение об обмене электронными документами.docx'");
	ОписаниеФайла.ДвоичныеДанные = ДвоичныеДанные;
	
	Возврат ОписаниеФайла;
	
КонецФункции

Функция ПодготовитьДанныеПечатиСоглашенияПолучитьМакет(НастройкаЭДО, ИмяМакета)
	
	РеквизитыНастройки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаЭДО, "Организация, Контрагент");
	Сторона1 = ПредставлениеЮрФизЛица(РеквизитыНастройки.Организация);
	Сторона2 = ПредставлениеЮрФизЛица(РеквизитыНастройки.Контрагент);
	
	ДанныеОрганизации = ИнтеграцияЭДО.РегистрационныеДанныеОрганизации(РеквизитыНастройки.Организация);
	
	Город = "";
	Если ДанныеОрганизации.Свойство("Город", Город) И НЕ ЗначениеЗаполнено(Город)
		И ДанныеОрганизации.Свойство("КодРегиона") И ЗначениеЗаполнено(ДанныеОрганизации.КодРегиона) Тогда
		
		Если ДанныеОрганизации.КодРегиона = "77" // Москва
			ИЛИ ДанныеОрганизации.КодРегиона = "78" // Санкт-Петербург
			ИЛИ ДанныеОрганизации.КодРегиона = "92" // Севастополь
			ИЛИ ДанныеОрганизации.КодРегиона = "99" Тогда // Байконур и иные территории
			
			Город = АдресныйКлассификатор.НаименованиеРегионаПоКоду(ДанныеОрганизации.КодРегиона);
		КонецЕсли;
	КонецЕсли;
	
	Субъект = Новый Структура;
	Субъект.Вставить("НастройкаЭДО", НастройкаЭДО);
	Субъект.Вставить("Город",        Город);
	Субъект.Вставить("Дата",         Формат(ТекущаяДатаСеанса(), "ДЛФ=DD"));
	Субъект.Вставить("Сторона1",     Сторона1);
	Субъект.Вставить("Сторона2",     Сторона2);
	
	Субъекты = Новый Массив;
	Субъекты.Добавить(Субъект);
	
	ИмяМенеджераПечати = "Обработка.НастройкиОтправкиЭДО";
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати(ИмяМенеджераПечати,
		ИмяМакета, Субъекты);
	
	Возврат МакетИДанныеОбъекта;
	
КонецФункции

Функция ПредставлениеЮрФизЛица(ЮрФизЛицо)
	
	Результат = "";
	
	Сведения = ИнтеграцияЭДО.ДанныеЮрФизЛица(ЮрФизЛицо);
	
	Если Сведения.Свойство("ПолноеНаименование") И ЗначениеЗаполнено(Сведения.ПолноеНаименование) Тогда
		Результат = Сведения.ПолноеНаименование;
	Иначе
		Результат = Строка(ЮрФизЛицо);
	КонецЕсли;
	
	Если Сведения.Свойство("ИНН") И ЗначениеЗаполнено(Сведения.ИНН) Тогда
		Результат = Результат + " " + СтрШаблон(НСтр("ru = 'ИНН %1'"), Сведения.ИНН);
		Если Сведения.Свойство("КПП") И ЗначениеЗаполнено(Сведения.КПП) Тогда
			Результат = Результат + " " + СтрШаблон(НСтр("ru = 'КПП %1'"), Сведения.КПП);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти