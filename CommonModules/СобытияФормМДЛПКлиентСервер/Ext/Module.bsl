
////////////////////////////////////////////////////////////////////////////////
//
// СобытияФормМДЛПКлиентСервер: процедуры и функции подсистемы "ИнтеграцияМДЛП.БазоваяФункциональность"
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбновитьЗаголовокКоличествоНомеровУпаковок(Форма) Экспорт
	
	// Использование такого подхода обусловлено тем, что сервер вызывается в минимальном количетсве случаев
	// (не явно, в случае изменения значения свойства ОтображатьВШапке).
	// Если вместо этого подхода использовать подход прямого изменения заголовка поля,
	// то каждое изменение заголовка влечет за собой вызов сервера.
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Форма.ПоказыватьВсеНомераУпаковок Тогда
		
		КоличествоВсего = Объект.НомераУпаковок.Количество();
		
		Форма.ЗаголовокКоличествоНомеровУпаковок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '(%1)'"),
			Формат(КоличествоВсего, "ЧН=0; ЧГ=0"));
		
		ОтображатьКоличествоВШапке = КоличествоВсего > 0;
		
	Иначе
		
		КоличествоПоТекущейСтроке = Объект.НомераУпаковок.НайтиСтроки(Новый Структура("ИдентификаторСтроки", Форма.ИдентификаторТекущейСтроки)).Количество();
		КоличествоВсего = Объект.НомераУпаковок.Количество();
		
		Форма.ЗаголовокКоличествоНомеровУпаковок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '(%1 из %2)'"),
			Формат(КоличествоПоТекущейСтроке, "ЧН=0; ЧГ=0"),
			Формат(КоличествоВсего, "ЧН=0; ЧГ=0"));
		
		ОтображатьКоличествоВШапке = КоличествоПоТекущейСтроке > 0;
		
	КонецЕсли;
	
	// Переключение отображения нужно, т.к. если в табличном поле нет строк,
	// то в пользовательском режиме не происходит обновление значения совойства ПутьКДаннымШапки
	// (см. свойство ПутьКДаннымШапки элемента ГруппаНомераУпаковокНомерУпаковки).
	// По этому, если в табличном поле есть строки,
	// то в шапке таблицы отображается заголовок группы ГруппаНомераУпаковокНомерУпаковки,
	// иначе заголовок поля НомераУпаковокНомерУпаковки.
	Если ОтображатьКоличествоВШапке Тогда
		Если Не Элементы.ГруппаНомераУпаковокНомерУпаковки.ОтображатьВШапке Тогда
			Элементы.ГруппаНомераУпаковокНомерУпаковки.ОтображатьВШапке = Истина;
			Элементы.НомераУпаковокНомерУпаковки.ОтображатьВШапке = Ложь;
		КонецЕсли;
	Иначе
		Если Элементы.ГруппаНомераУпаковокНомерУпаковки.ОтображатьВШапке Тогда
			Элементы.ГруппаНомераУпаковокНомерУпаковки.ОтображатьВШапке = Ложь;
			Элементы.НомераУпаковокНомерУпаковки.ОтображатьВШапке = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
