
#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает путь к каталогу временных файлов в операционной системе Windows.
// 
// Параметры:
// 	Путь - Строка
// Возвращаемое значение:
//  Булево - Истина, если установка прошла успешно
Функция УстановитьПутьККаталогуВременныхФайлов(Путь) Экспорт
	
	Если Не ОбщегоНазначенияКлиент.ЭтоWindowsКлиент() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Установка пути к каталогу временных файлов не поддерживается для данной операционной системы'"));
		Возврат Ложь;
	КонецЕсли;
	
	#Если Не МобильныйКлиент Тогда
		
		Если Не ЗначениеЗаполнено(Путь) Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не заполнен путь к каталогу временных файлов'"));
			Возврат Ложь;
		КонецЕсли;
		Оболочка = Новый COMОбъект("Wscript.Shell");
		Оболочка.RegWrite("HKEY_CURRENT_USER\Environment\TMP", Путь, "REG_EXPAND_SZ");
		
		Возврат Истина;
	#КонецЕсли
	
	Возврат Ложь;
	
КонецФункции

// Вызывает РаботаСФайламиКлиент.СохранитьФайлКак() с подготовленными данными файла.
//
// Параметры:
// 	ДанныеФайлаВыгрузки - Структура - см. РаботаСФайламиБЭД.ДанныеФайла().
// 	СсылкаНаДвоичныеДанные - Строка - адрес во временном хранилище, по которому помещены данные.
// 	ОбработчикЗавершения - ОписаниеОповещения, Неопределено - описание процедуры, которая будет вызвана после сохранения.
//
Процедура СохранитьФайлВыгрузкиКак(ДанныеФайлаВыгрузки, СсылкаНаДвоичныеДанныеФайла,
	ОбработчикЗавершения = Неопределено) Экспорт
	
	ДанныеФайла = Новый Структура;
	ДанныеФайла.Вставить("СсылкаНаДвоичныеДанныеФайла", СсылкаНаДвоичныеДанныеФайла);
	ДанныеФайла.Вставить("ОтносительныйПуть", "");
	ДанныеФайла.Вставить("ДатаМодификацииУниверсальная", ОбщегоНазначенияКлиент.ДатаУниверсальная());
	ДанныеФайла.Вставить("ИмяФайла", ДанныеФайлаВыгрузки.ИмяФайла);
	ДанныеФайла.Вставить("Наименование", ДанныеФайлаВыгрузки.ИмяБезРасширения);
	ДанныеФайла.Вставить("Расширение", ДанныеФайлаВыгрузки.Расширение);
	ДанныеФайла.Вставить("Размер", ДанныеФайлаВыгрузки.Размер);
	ДанныеФайла.Вставить("Редактирует", Неопределено);
	ДанныеФайла.Вставить("ПодписанЭП", Ложь);
	ДанныеФайла.Вставить("Зашифрован", Ложь);
	ДанныеФайла.Вставить("ХранитьВерсии", Ложь);
	ДанныеФайла.Вставить("ПометкаУдаления", Ложь);
	ДанныеФайла.Вставить("ДатаЗаема", Неопределено);
	ДанныеФайла.Вставить("ФайлРедактируется", Ложь);
	ДанныеФайла.Вставить("ФайлРедактируетТекущийПользователь", Ложь);
	ДанныеФайла.Вставить("НомерВерсии", 0);
	ДанныеФайла.Вставить("ПолноеНаименованиеВерсии", ДанныеФайлаВыгрузки.ИмяБезРасширения);
	ДанныеФайла.Вставить("ПапкаДляСохранитьКак", Неопределено);
	
	РаботаСФайламиКлиент.СохранитьФайлКак(ДанныеФайла, ОбработчикЗавершения);
	
КонецПроцедуры

// Выполняет корректировку имени файла и предлагает пользователю исправить, если в имени были обнаружены некорректные символы.
//
// Параметры:
// 	ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, которая будет вызвана после проверки имени файла со
// 							следующими параметрами:
// 							 * ИмяФайла - Строка
// 							 			- Неопределено - Если пользователь отказался от ввода имени файла.
// 							 * АдресВХранилище - Строка
// 	ИмяФайла - Строка
//	Кодировка - Строка
//
Процедура СкорректироватьИмяФайлаСУчетомКодировки(ОповещениеОЗавершении, ИмяФайла, Кодировка = "windows-1251") Экспорт 
	
	// Запланированная обработка частного сценария с наличием в имени файла комбинируемых символов.
	// Эти комбинации символов встречаются довольно часто, 
	// поэтому преобразуем их до вызова метода "ПроверитьИмяФайлаСУчетомКодировки" для исключения частого оповещения 
	// пользователей о некорректности данных символов в имени файла.
	ИмяФайла = СтрЗаменить(ИмяФайла, "и" + Символ(774), "й");
	ИмяФайла = СтрЗаменить(ИмяФайла, "е" + Символ(776), "е");
	
	ИмяФайла = 
		ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла, "_");
	
	РезультатПроверки = ПроверитьИмяФайлаСУчетомКодировки(ИмяФайла, Кодировка);
	
	Если РезультатПроверки.ИмеютсяНекорректныеСимволы Тогда 
		
		ПараметрыОповещения = 
			Новый Структура("ОповещениеОЗавершении, Кодировка", ОповещениеОЗавершении, Кодировка);
		
		ОповещениеПослеВводаСтроки = 
			Новый ОписаниеОповещения("СкорректироватьИмяФайлаПослеВводаСтроки", ЭтотОбъект, ПараметрыОповещения); 
			
		ДополнительноеОписание = 
			СтрШаблон(НСтр("ru = 'В имени файла ""%1"" присутствовали недопустимые символы, имя файла было скорректировано системой.
			|Проверьте и при необходимости отредактируйте имя файла.'"), РезультатПроверки.ИмяФайлаДоКорректировки);
			
		КомментарийОбязательностиВвода = 
			НСтр("ru = 'Имя файла не может быть пустым. Введите имя файла.'");
			
		ДополнительныеПараметры = ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки();
		ДополнительныеПараметры.ЗаголовокФормы = НСтр("ru = 'Изменение имени файла'");
		ДополнительныеПараметры.Строка = РезультатПроверки.ИмяФайлаПослеКорректировки;
		ДополнительныеПараметры.ДополнительноеОписание = ДополнительноеОписание;
		ДополнительныеПараметры.Обязательность = Истина;
		ДополнительныеПараметры.КомментарийОбязательностиВвода = КомментарийОбязательностиВвода;
		
		ОбщегоНазначенияБЭДКлиент.ПоказатьВводСтрокиБЭД(ОповещениеПослеВводаСтроки, ДополнительныеПараметры);
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, РезультатПроверки.ИмяФайлаПослеКорректировки);
	КонецЕсли;
	
КонецПроцедуры 

// Продолжение процедуры СкорректироватьИмяФайлаСУчетомКодировки после ввода строки пользователем.
//
// Параметры:
//  ИмяФайла - Строка
//  ДополнительныеПараметры - Структура:
//  * ОповещениеОЗавершении - ОписаниеОповещения 
//  * Кодировка - Строка
//
Процедура СкорректироватьИмяФайлаПослеВводаСтроки(ИмяФайла, ДополнительныеПараметры) Экспорт
	Если ИмяФайла = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, ИмяФайла);
		Возврат;
	КонецЕсли;
	СкорректироватьИмяФайлаСУчетомКодировки(
		ДополнительныеПараметры.ОповещениеОЗавершении, ИмяФайла, ДополнительныеПараметры.Кодировка);
КонецПроцедуры 

// Выполняет преобазование имени файла в латиницу, если это требуется 
//
// Параметры:
//  ИмяФайла - Строка
//  ОтключитьТранслитерацию - Булево - Истина, если не переводить в латиницу.
//  
//  Возвращаемое значение:
//   Строка - Имя файла после преобразования.
//
Функция ПреобразоватьИмяФайлаСУчетомТранслитерации(ИмяФайла, ОтключитьТранслитерацию) Экспорт
	
	Результат = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла, "_");

	Если Не ОтключитьТранслитерацию Тогда
		Результат = СтроковыеФункцииКлиент.СтрокаЛатиницей(Результат);
		Результат = СтрЗаменить(Результат, " ", "_");
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает результат операции проверки и корректировки имени файла.
// 
// Параметры:
// 	ИмяФайла - Строка
// 	Кодировка - Строка - Строковое представление кодировки
// 	
// Возвращаемое значение:
//  Структура:
//  * ИмеютсяНекорректныеСимволы - Булево
//  * ИмяФайлаДоКорректировки - Строка
//  * ИмяФайлаПослеКорректировки - Строка
//
Функция ПроверитьИмяФайлаСУчетомКодировки(Знач ИмяФайла, Кодировка)
		
	ИмяФайлаПослеКорректировки = 
		ПолучитьСтрокуИзДвоичныхДанных(
			ПолучитьДвоичныеДанныеИзСтроки(ИмяФайла, Кодировка), Кодировка);
			
	ИмяФайлаПослеКорректировки = РаботаСФайламиБЭДКлиентСервер.ДопустимоеИмяФайла(ИмяФайлаПослеКорректировки, Истина);
	
	ИмеютсяНекорректныеСимволы = ИмяФайла <> ИмяФайлаПослеКорректировки;
		
	Результат = Новый Структура;
	Результат.Вставить("ИмеютсяНекорректныеСимволы", ИмеютсяНекорректныеСимволы);
	Результат.Вставить("ИмяФайлаДоКорректировки", ИмяФайла);
	Результат.Вставить("ИмяФайлаПослеКорректировки", ИмяФайлаПослеКорректировки);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти