
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Добавляет элементы фильтра по иерархии номенклатуры на форму.
//
// Параметры:
//  Форма -ФормаКлиентскогоПриложения - форма, на которую добавляется фильтр.
//
Процедура ДобавитьФильтрНаФорму(Форма) Экспорт
	
	ИнициализироватьФильтр(Форма);
	
КонецПроцедуры

// Восстанавливает настройки фильтра из хранилища общих настроек.
// Устанавливает отбор из параметров формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, где устанавливаются начальные настройки.
//
Процедура УстановитьНачальныеНастройки(Форма, Параметры) Экспорт
	
	
КонецПроцедуры

// Сохраняет настройки формы с фильтром по иерархии номенклатуры.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, для которой сохраняются настройки.
//
Процедура СохранитьНастройки(Форма) Экспорт
	
КонецПроцедуры

// Возвращает страницу формы с элементами фильтра по иерархии номенклатуры.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, из которой получается страница фильтра.
//
// Возвращаемое значение:
//  ГруппаФормы - страница с элементами фильтра по иерархии номенклатуры.
//
Функция СтраницаФильтра(Форма) Экспорт
	
	Возврат Форма.Элементы[ФильтрНоменклатурыПоИерархииКлиентСервер.Идентификатор() + "ФильтрИерархияНоменклатуры"];
	
КонецФункции

// Возвращает представление фильтра по иерархии номенклатуры.
// Используется для представления пользователю при выборе фильтра.
//
// Возвращаемое значение:
//  Строка - представление фильтра.
//
Функция ПредставлениеФильтра() Экспорт
	
	Возврат НСтр("ru='иерархии'");
	
КонецФункции

// Обрабатывает изменение варианта фильтра.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, у которой обрабатывается фильтр.
//
Процедура ОбработатьВыборФильтра(Форма) Экспорт
	
	УстановитьФильтр(Форма);
	
КонецПроцедуры

// Обрабатывает отмену установленного фильтра.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой обрабатывается отмена фильтра.
//
Процедура ОбработатьОтменуФильтра(Форма) Экспорт
	
	СброситьФильтр(Форма);
	
КонецПроцедуры

// Устанавливает доступность элементов фильтра по иерархии номенклатуры.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, где устанавливается доступность изменения элементов фильтра.
//
Процедура УстановитьДоступность(Форма) Экспорт
	
	ИспользоватьФильтры = ФильтрыСписковКлиентСервер.ИспользоватьФильтры(Форма);
	СтраницаФильтра(Форма).Доступность = ИспользоватьФильтры;
	
КонецПроцедуры

// Устанавливает фильтр в списке передаваемой формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, где устанавливается фильтр.
//
Процедура УстановитьФильтр(Форма) Экспорт
	
	ФильтрНоменклатурыПоИерархииКлиентСервер.УстановитьОтборПоИерархииНоменклатуры(Форма);
	
КонецПроцедуры

// Выключает использование фильтра списка передаваемой формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, где выключается использование фильтра.
//
Процедура СброситьФильтр(Форма) Экспорт
	
	СписокНоменклатура = ФильтрыСписковКлиентСервер.ФильтруемыйСписок(Форма);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокНоменклатура, "Родитель",,,, Ложь);
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьФильтр(Форма)
	
	Описание = УправляемаяФорма.ПрочитатьОписаниеФормыИзСтроки(ОписаниеЭлементовФормы(Форма));
	УправляемаяФорма.СоздатьЭлементы(Форма, Описание);
	
	ИерархияНоменклатуры = Форма[ФильтрНоменклатурыПоИерархииКлиентСервер.Идентификатор() + "ИерархияНоменклатуры"];
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ИерархияНоменклатуры,
		"ЭтоГруппа",
		Истина);
	
КонецПроцедуры

Функция ОписаниеЭлементовФормы(Форма)
	
	ЭтоФормаСписка = ФильтрыСписковКлиентСервер.ЭтоФормаСпискаНоменклатуры(Форма);
	ЭтоФормаВыбора = ФильтрыСписковКлиентСервер.ЭтоФормаВыбораНоменклатуры(Форма);
	
	ДобавлятьГруппы = (ЭтоФормаСписка Или ЭтоФормаВыбора) И ПравоДоступа("Добавление", Метаданные.Справочники.Номенклатура);
	ИзменятьГруппы = (ЭтоФормаСписка Или ЭтоФормаВыбора) И ПравоДоступа("Изменение", Метаданные.Справочники.Номенклатура);
	ГрупповоеИзменение = ЭтоФормаСписка И ИзменятьГруппы;
	
	Описание =
	"<Форма>
	|	<Реквизиты>
	|		<Реквизит Имя='%1ИерархияНоменклатуры'>
	|			<Типы>
	|				<Тип>ДинамическийСписок</Тип>
	|			</Типы>
	|			<Свойства>
	|				<Свойство Имя='ОсновнаяТаблица'>Справочник.Номенклатура</Свойство>
	|			</Свойства>
	|		</Реквизит>
	|		<Реквизит Имя='%1ТекущаяИерархияНоменклатуры'>
	|			<Типы>
	|				<Тип>СправочникСсылка.Номенклатура</Тип>
	|			</Типы>
	|		</Реквизит>
	|	</Реквизиты>
	|	<Команды>
	|"
	+ ?(ДобавлятьГруппы, "
	|		<Команда
	|			Имя='%1СоздатьГруппу'
	|			Заголовок='Создать группу'
	|			Действие='Подключаемый_ФильтрНоменклатурыПоИерархии_СоздатьГруппуНоменклатуры'
	|			Картинка='СоздатьГруппу'/>
	|		<Команда
	|			Имя='%1Скопировать'
	|			Заголовок='Скопировать'
	|			Действие='Подключаемый_ФильтрНоменклатурыПоИерархии_СкопироватьГруппуНоменклатуры'
	|			Картинка='СкопироватьЭлементСписка'/>
	|", "")
	+ ?(ИзменятьГруппы, "
	|		<Команда
	|			Имя='%1Изменить'
	|			Заголовок='Изменить'
	|			Действие='Подключаемый_ФильтрНоменклатурыПоИерархии_ИзменитьГруппуНоменклатуры'
	|			Картинка='Изменить'/>
	|		<Команда
	|			Имя='%1УстановитьПометкуУдаления'
	|			Заголовок='Установить пометку удаления'
	|			Действие='Подключаемый_ФильтрНоменклатурыПоИерархии_УстановитьПометкуУдаленияГруппыНоменклатуры'
	|			Картинка='ПометитьНаУдаление'/>
	|", "")
	+ ?(ГрупповоеИзменение, "
	|		<Команда
	|			Имя='%1ИзменитьВыделенныеГруппы'
	|			Заголовок='Изменить выделенные'
	|			Действие='Подключаемый_ФильтрНоменклатурыПоИерархии_ИзменитьВыделенныеГруппы'/>
	|
	|", "") + "
	|	</Команды>
	|	<Элементы>
	|		<УсловноеОформление>
	|			<Элемент>
	|				<Оформление ЦветФона='ЦветФонаФормы' ЦветТекста='ЦветТекстаОтмененнойСтрокиДокумента' />
	|				<Отбор>
	|					<Элемент ЛевоеЗначение='%3'>
	|						<Значение Тип='Булево'>Ложь</Значение>
	|					</Элемент>
	|				</Отбор>
	|				<Поля>
	|					<Поле Имя='%1ИерархияНоменклатуры' />
	|				</Поля>
	|			</Элемент>
	|		</УсловноеОформление>
	|		<ГруппаФормы Имя='%1ФильтрИерархияНоменклатуры' Родитель='%2'>
	|			<Свойство Имя='Вид'>Страница</Свойство>
	|			<ГруппаФормы Имя='%1КоманднаяПанельИерархияНоменклатуры'>
	|				<Свойство Имя='Вид'>КоманднаяПанель</Свойство>
	|"
	+ ?(ДобавлятьГруппы, "
	|				<КнопкаФормы Имя='%1СоздатьГруппу'>
	|					<Свойство Имя='Вид'>КнопкаКоманднойПанели</Свойство>
	|					<Свойство Имя='ИмяКоманды'>%1СоздатьГруппу</Свойство>
	|					<Свойство Имя='Отображение'>КартинкаИТекст</Свойство>
	|				</КнопкаФормы>
	|", "")
	+ ?(ИзменятьГруппы, "
	|				<КнопкаФормы Имя='%1Изменить'>
	|					<Свойство Имя='Вид'>КнопкаКоманднойПанели</Свойство>
	|					<Свойство Имя='ИмяКоманды'>%1Изменить</Свойство>
	|				</КнопкаФормы>
	|", "")
	+ ?(ДобавлятьГруппы, "
	|				<КнопкаФормы Имя='%1Скопировать'>
	|					<Свойство Имя='Вид'>КнопкаКоманднойПанели</Свойство>
	|					<Свойство Имя='ИмяКоманды'>%1Скопировать</Свойство>
	|				</КнопкаФормы>
	|", "")
	+ ?(ИзменятьГруппы, "
	|				<КнопкаФормы Имя='%1УстановитьПометкуУдаления'>
	|					<Свойство Имя='Вид'>КнопкаКоманднойПанели</Свойство>
	|					<Свойство Имя='ИмяКоманды'>%1УстановитьПометкуУдаления</Свойство>
	|				</КнопкаФормы>
	|", "") + "
	|			</ГруппаФормы>
	|			<ТаблицаФормы Имя='%1ИерархияНоменклатуры'>
	|				<Свойство Имя='ПутьКДанным'>%1ИерархияНоменклатуры</Свойство>
	|				<Свойство Имя='ПоложениеКоманднойПанели'>Нет</Свойство>
	|				<Свойство Имя='Отображение'>Дерево</Свойство>
	|				<Свойство Имя='НачальноеОтображениеДерева'>РаскрыватьВерхнийУровень</Свойство>
	|				<Свойство Имя='Шапка'>Ложь</Свойство>
	|				<Свойство Имя='ЧередованиеЦветовСтрок'>Ложь</Свойство>
	|				<Свойство Имя='ВосстанавливатьТекущуюСтроку'>Истина</Свойство>
	|				<Свойство Имя='ТолькоПросмотр'>Истина</Свойство>
	|				<Свойство Имя='ПоискПриВводе'>НеИспользовать</Свойство>"
	+ ?(ИзменятьГруппы, "", "
	|				<Свойство Имя='ИзменятьСоставСтрок'>Ложь</Свойство>
	|				<Свойство Имя='ИзменятьПорядокСтрок'>Ложь</Свойство>
	|") + "
	|				<КонтекстноеМеню>"
	+ ?(ГрупповоеИзменение, "
	|					<КнопкаФормы Имя='%1ИзменитьВыделенныеГруппы'>
	|						<Свойство Имя='Вид'>КнопкаКоманднойПанели</Свойство>
	|						<Свойство Имя='ИмяКоманды'>%1ИзменитьВыделенныеГруппы</Свойство>
	|					</КнопкаФормы>
	|", "") + "
	|				</КонтекстноеМеню>
	|				<События>
	|					<ПриАктивизацииСтроки Действие='Подключаемый_ФильтрНоменклатурыПоИерархии_ИерархияНоменклатурыПриАктивизацииСтроки' />
	|				</События>
	|				<ПолеФормы Имя='%1ИерархияНоменклатурыСсылка'>
	|						<Свойство Имя='Вид'>ПолеНадписи</Свойство>
	|						<Свойство Имя='ПутьКДанным'>%1ИерархияНоменклатуры.Ссылка</Свойство>
	|				</ПолеФормы>
	|		
	|			</ТаблицаФормы>
	|
	|		</ГруппаФормы>
	|	</Элементы>
	|</Форма>
	|";
	
	Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Описание,
		ФильтрНоменклатурыПоИерархииКлиентСервер.Идентификатор(),
		ФильтрыСписков.СтраницаФильтров(),
		ФильтрыСписковКлиентСервер.ИмяРеквизитаИспользоватьФильтры());
	Возврат Описание;
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
