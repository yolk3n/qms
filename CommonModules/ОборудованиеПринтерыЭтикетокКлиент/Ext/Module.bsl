
#Область ПрограммныйИнтерфейс

// Заполняет структуру дополнительных параметров операции на Оборудовании.
//
// Параметры:
//  ОписаниеЭтикеткиВXML - Строка - XML описание макета этикетки
//  ДанныеДляПечати - Массив
// 
// Возвращаемое значение:
//  Структура.
//
Функция ПараметрыОперацииПечатиЭтикеток(ОписаниеЭтикеткиВXML = Неопределено, ДанныеДляПечати = Неопределено) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ОписаниеЭтикеткиВXML", ОписаниеЭтикеткиВXML);
	Результат.Вставить("ДанныеДляПечати"     , ДанныеДляПечати);
	Возврат Результат;
	
КонецФункции

// Начать печать этикеток.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПечатьЭтикеток(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = Новый Структура();
	КонтекстОперации.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	КонтекстОперации.Вставить("ИдентификаторКлиента"   , ИдентификаторКлиента);   
	КонтекстОперации.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	КонтекстОперации.Вставить("ПараметрыОперации"      , ПараметрыОперации);
	КонтекстОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры); 
	КонтекстОперации.Вставить("ПодготовитьДанные"      , Истина); 
	КонтекстОперации.Вставить("ОбработатьДанные"       , Ложь); 
	КонтекстОперации.Вставить("Команда"  , "PrintLabels"); 
	КонтекстОперации.Вставить("Результат", Истина);
	
	Если ИдентификаторУстройства = Неопределено Или  ПустаяСтрока(ИдентификаторУстройства) Тогда
		
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ПринтерЭтикеток");       
		
		Оповещение = Новый ОписаниеОповещения("НачатьОперациюЗавершение", ЭтотОбъект, КонтекстОперации);
		МенеджерОборудованияКлиент.ВыбратьУстройство(Оповещение, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите принтер этикеток'"),
			НСтр("ru='Принтер этикеток не подключен.'"), 
			НСтр("ru='Принтер этикеток не выбран.'"));
	Иначе
		НачатьОперациюЗавершение(КонтекстОперации, КонтекстОперации);
		
	КонецЕсли
	
КонецПроцедуры

// Начать инициализацию принтера этикеток.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьИнициализацияПринтераЭтикеток(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = Новый Структура();
	КонтекстОперации.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	КонтекстОперации.Вставить("ИдентификаторКлиента"   , ИдентификаторКлиента);   
	КонтекстОперации.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	КонтекстОперации.Вставить("ПараметрыОперации"      , ПараметрыОперации);
	КонтекстОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры); 
	КонтекстОперации.Вставить("ПодготовитьДанные"      , Ложь); 
	КонтекстОперации.Вставить("ОбработатьДанные"       , Ложь); 
	КонтекстОперации.Вставить("Команда"  , "InitializePrinter"); 
	КонтекстОперации.Вставить("Результат", Истина);
	
	Если ИдентификаторУстройства = Неопределено Или  ПустаяСтрока(ИдентификаторУстройства) Тогда
		
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ПринтерЭтикеток");       
		
		Оповещение = Новый ОписаниеОповещения("НачатьОперациюЗавершение", ЭтотОбъект, КонтекстОперации);
		МенеджерОборудованияКлиент.ВыбратьУстройство(Оповещение, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите принтер этикеток'"),
			НСтр("ru='Принтер этикеток не подключен.'"), 
			НСтр("ru='Принтер этикеток не выбран.'"));
	Иначе
		НачатьОперациюЗавершение(КонтекстОперации, КонтекстОперации);
	КонецЕсли
	
КонецПроцедуры

// Начать редактирование макета.
//
// Параметры:
//  ОповещениеПриЗавершенииРедактирования - ОписаниеОповещения 
//  ОписаниеЭтикеткиВXML - Строка - XML описание макета этикетки
//  АдресХранилищаСКД - Строка
//  АдресМакета - Строка
//
Процедура НачатьРедактированиеМакета(ОповещениеПриЗавершенииРедактирования, ОписаниеЭтикеткиВXML, АдресХранилищаСКД, АдресМакета = "") Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("СледующееОповещение", ОповещениеПриЗавершенииРедактирования);
	Контекст.Вставить("XMLОписаниеМакета"  , ОписаниеЭтикеткиВXML);
	Контекст.Вставить("АдресМакета", АдресМакета);
	ОповещениеПриЗавершении = Новый ОписаниеОповещения("НачатьРедактированиеМакета_Завершение", ЭтотОбъект, Контекст);
	
	ИмяФормы = "Справочник.ШаблоныЭтикетокИЦенниковБПО.Форма.РедакторЭтикетокФормаРедактированияМакета";
	Попытка
		ОткрытьФорму(ИмяФормы, Новый Структура("АдресХранилищаСКД, АдресМакета", АдресХранилищаСКД, АдресМакета),,,,, ОповещениеПриЗавершении);
	Исключение
		// АПК: 374-выкл предупреждение об критических действиях
		ТекстСообщения = НСтр("ru='Использование редактора этикеток невозможно! Функциональность не поддерживается.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		// АПК: 374-вкл
	КонецПопытки;
	
КонецПроцедуры

// Завершение редактирования макета
//
// Параметры:
//  Результат - Структура
//  ДополнительныеПараметры - Структура
//
Процедура НачатьРедактированиеМакета_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("РазмерМакета",      ?(Результат <> Неопределено, ДополнительныеПараметры.РазмерМакета, Неопределено));
	ДопПараметры.Вставить("XMLОписаниеМакета", ?(Результат <> Неопределено, ДополнительныеПараметры.XMLОписаниеМакета, Неопределено));
	ДопПараметры.Вставить("Поля",              ?(Результат <> Неопределено, ДополнительныеПараметры.Поля, Неопределено));
	ДопПараметры.Вставить("Макет",             ?(Результат <> Неопределено, ДополнительныеПараметры.Макет, Неопределено));
	
	ОписаниеОповещения = Новый ОписаниеОповещения(ДополнительныеПараметры.СледующееОповещение.ИмяПроцедуры, ДополнительныеПараметры.СледующееОповещение.Модуль, ДопПараметры);
	ВыполнитьОбработкуОповещения(ОписаниеОповещения, ?(Результат <> Неопределено, Результат, КодВозвратаДиалога.Отмена));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НачатьОперациюЗавершение(РезультатВыбора, КонтекстОперации) Экспорт
	
	Если РезультатВыбора.Результат Тогда 
		ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды(КонтекстОперации.Команда, ОборудованиеПринтерыЭтикетокВызовСервера,
		КонтекстОперации.ДополнительныеПараметры, КонтекстОперации.ПодготовитьДанные, КонтекстОперации.ОбработатьДанные);
		МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(КонтекстОперации.ОповещениеПриЗавершении, КонтекстОперации.ИдентификаторКлиента, 
			РезультатВыбора.ИдентификаторУстройства, КонтекстОперации.ПараметрыОперации, ПараметрыВыполнениеКоманды);
	Иначе
		ВыполнитьОбработкуОповещения(КонтекстОперации.ОповещениеПриЗавершении, РезультатВыбора);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти