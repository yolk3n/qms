
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Выводит сообщение об отсутствии данных по штрихкоду.
//
// Параметры:
//  Штрихкод - Строка - штрихкод печатной формы.
//
Процедура СообщитьОбъектНеНайден(Штрихкод) Экспорт
	
	ОчиститьСообщения();
	ОбщегоНазначенияКлиент.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Объект со штрихкодом %1 не найден'"), Штрихкод));
	
КонецПроцедуры

// Возвращает список ссылок по штрихкоду.
//
// Параметры:
//  Штрихкод             - Строка - штрихкод печатной формы.
//  Типы                 - Массив(Тип) - список типов объектов для поиска.
//  ИскатьПоВсемОбъектам - Булево - если Истина, то если в заданной области поиска объект не найден, то поиск продолжится без отбора по типам.
//
// Возвращаемое значение:
//  Массив - список найденных ссылок
//
Функция ПолучитьСсылкуПоШтрихкодуТабличногоДокумента(Штрихкод, Типы = Неопределено, ИскатьПоВсемОбъектам = Истина) Экспорт
	
	Состояние(НСтр("ru = 'Выполняется поиск документа по штрихкоду...'"));
	НайденныеСсылки = ШтрихкодированиеПечатныхФормВызовСервера.ПолучитьСсылкуПоШтрихкодуТабличногоДокумента(Штрихкод, Типы);
	
	Если ИскатьПоВсемОбъектам И НайденныеСсылки.Количество() = 0 Тогда
		Состояние(НСтр("ru = 'Выполняется поиск документа по штрихкоду во всех документах информационной базы..'"));
		НайденныеСсылки = ШтрихкодированиеПечатныхФормВызовСервера.ПолучитьСсылкуПоШтрихкодуТабличногоДокумента(Штрихкод);
	КонецЕсли;
	
	Возврат НайденныеСсылки;
	
КонецФункции

// Открывает ссылку по штрихкоду.
//
// Параметры:
//  СписокДокументов - ТаблицаФормы - список документов формы списка.
//  Штрихкод         - Строка - штрихкод печатной формы.
//  Типы             - Массив(Тип) - список типов объектов для поиска.
//
Процедура ПоказатьСсылкуПоШтрихкодуТабличногоДокумента(СписокДокументов, Штрихкод, Типы) Экспорт
	
	НайденныеСсылки = ПолучитьСсылкуПоШтрихкодуТабличногоДокумента(Штрихкод, Типы);
	Если НайденныеСсылки.Количество() > 0 Тогда
		Ссылка = НайденныеСсылки[0];
		Если Типы.Найти(ТипЗнч(Ссылка)) <> Неопределено Тогда
			СписокДокументов.ТекущаяСтрока = Ссылка;
		КонецЕсли;
		ПоказатьЗначение(Неопределено, Ссылка);
	Иначе
		СообщитьОбъектНеНайден(Штрихкод);
	КонецЕсли;
	
КонецПроцедуры

// Позиционируется на ссылке по штрихкоду.
//
// Параметры:
//  СписокДокументов - ТаблицаФормы - список документов формы списка.
//  Штрихкод         - Строка - штрихкод печатной формы.
//  Типы             - Массив(Тип) - список типов объектов для поиска.
//
Процедура СпозиционироватьсяНаСсылкеПоШтрихкодуТабличногоДокумента(СписокДокументов, Штрихкод, Типы) Экспорт
	
	НайденныеСсылки = ПолучитьСсылкуПоШтрихкодуТабличногоДокумента(Штрихкод, Типы, Ложь);
	Если НайденныеСсылки.Количество() > 0 Тогда
		Ссылка = НайденныеСсылки[0];
		СписокДокументов.ТекущаяСтрока = Ссылка;
	Иначе
		СообщитьОбъектНеНайден(Штрихкод);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает тип объекта по имени его формы.
//
// Параметры:
//  ИмяФормы - Строка - имя формы в формате:
//    "Документ.<ИмяДокумента>.Форма.<ИмяФормы>"
//    "Справочник.<ИмяСправочника>".Форма.<ИмяФормы>
//
// Возвращаемое значение:
//  Тип - полученный тип значения объекта
//
Функция ТипОбъектаПоИмениФормы(ИмяФормы) Экспорт
	
	ЧастиИмениФормы  = СтрРазделить(ИмяФормы, ".");
	Возврат Тип(ЧастиИмениФормы[0] + "Ссылка." + ЧастиИмениФормы[1]);
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
