
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Возвращает элементы доступные для просмотра текущему пользователю
//
// Параметры:
//  ПолноеИмяОбъекта - Строка - полное имя таблицы, для которой определяются доступные элементы
//  Параметры - Структура - параметры получения данных
//  Отбор - Произвольный - 
//
// Возвращаемое значение:
//  ФиксированныйМассив, Неопределено - если для пользователя нет ограничений, то возвращается Неопределено,
//                                      иначе фиксированный массив с доступными элементами
//
Функция ПолучитьДоступныеЭлементыДляОтбора(Знач ПолноеИмяОбъекта, Знач Параметры, Знач Отбор) Экспорт
	
	Если Параметры.Свойство("ПоказыватьВсе")
	 Или Не УправлениеДоступом.ОграничиватьДоступНаУровнеЗаписей()
	 Или Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяОбъекта);
	
	ТаблицаОбъекта = Неопределено;
	Параметры.Свойство("ТаблицаОбъекта", ТаблицаОбъекта);
	ДоступныеЗначения = МенеджерОбъекта.ПолучитьДоступные(Отбор, ТаблицаОбъекта);
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Ссылка") Тогда
		
		ОтборЗначений = Неопределено;
		ОтборСсылка = Параметры.Отбор.Ссылка;
		ТипОтбора = ТипЗнч(ОтборСсылка);
		Если ТипОтбора = Тип("ФиксированныйМассив") Или ТипОтбора = Тип("Массив") Тогда
			ОтборЗначений = ОтборСсылка;
		ИначеЕсли ТипОтбора = Тип("СписокЗначений") Тогда
			ОтборЗначений = ОтборСсылка.ВыгрузитьЗначения();
		ИначеЕсли ТипОтбора = ТипЗнч(МенеджерОбъекта.ПустаяСсылка()) Тогда
			ОтборЗначений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОтборСсылка);
		КонецЕсли;
		
		Если ОтборЗначений <> Неопределено Тогда
			ДоступныеЗначения = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
				ДоступныеЗначения, ОбщегоНазначенияКлиентСервер.РазностьМассивов(ДоступныеЗначения, ОтборЗначений));
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(ДоступныеЗначения);
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
