
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Начинает замер времени проведения документ. Вызывается из формы документа в обработчике события ПередЗаписью.
//
// Параметры:
//  Объект            - ЛюбаяСсылка, Строка - ссылка документа проведения.
//  Отказ             - Булево - соответствует параметру события ПередЗаписью.
//  ПараметрыЗаписи   - Структура - соответствует параметру события ПередЗаписью.
//  УточнениеОперации - Произвольный - уточнение ключевой операции.
//
Процедура НачатьЗамерПроведенияДокумента(Объект, Отказ, ПараметрыЗаписи, УточнениеОперации = Неопределено) Экспорт
	
	Если Не Отказ Тогда
		Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
			КлючеваяОперация = "Проведение" + ИдентификаторОбъекта(Объект, УточнениеОперации);
			ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Начинает замер времени открытия форм при создании объектов.
// Вызывается из списков в обработчике события ПередНачаломДобавления.
//
// Параметры:
//  Объект            - Тип, Строка - тип создаваемого объекта.
//  Отказ             - Булево - соответствует параметру события ПередНачаломДобавления.
//  Копирование       - Булево - соответствует параметру события ПередНачаломДобавления.
//  УточнениеОперации - Произвольный - уточнение ключевой операции.
//
Процедура НачатьЗамерСозданияФормыОбъекта(Объект, Отказ, Копирование, УточнениеОперации = Неопределено) Экспорт
	
	Если Не Отказ Тогда
		КлючеваяОперация = "СозданиеФормы" + ИдентификаторОбъекта(Объект, УточнениеОперации);
		ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	КонецЕсли;
	
КонецПроцедуры

// Начинает замер времени открытия форм существующих объектов.
// Вызывается из списков в обработчике события ПередНачаломИзменения.
//
// Параметры:
//  Объект            - ЛюбаяСсылка, Строка - ссылка открываемого объекта.
//  Отказ             - Булево - соответствует параметру события ПередНачаломИзменения.
//  УточнениеОперации - Произвольный - уточнение ключевой операции.
//
Процедура НачатьЗамерОткрытияФормыОбъекта(Объект, Отказ, УточнениеОперации = Неопределено) Экспорт
	
	Если Не Отказ И ЗначениеЗаполнено(Объект) Тогда
		КлючеваяОперация = "ОткрытиеФормы" + ИдентификаторОбъекта(Объект, УточнениеОперации);
		ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Функция ИдентификаторОбъекта(Объект, УточнениеОперации)
	
	Если ТипЗнч(Объект) = Тип("Тип") Или ТипЗнч(Объект) = Тип("Строка") Тогда
		ТипОбъекта = Объект;
	Иначе
		ТипОбъекта = ТипЗнч(Объект);
	КонецЕсли;
	
	Возврат ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ПолучитьИдентификатор(Строка(ТипОбъекта) + Строка(УточнениеОперации));
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
