
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Открывает форму отбора формы выпуска
//
// Параметры:
//  Форма                - ФормаКлиентскогоПриложения - форма, где устанавливается фильтр по форме выпуска.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура УстановитьОтборПоФормеВыпуска(Форма, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Префикс = ФильтрНоменклатурыЛекарственныхСредствКлиентСервер.Идентификатор();
	
	ПараметрыФормыВыпуска = ФильтрНоменклатурыЛекарственныхСредствКлиентСервер.ПараметрыФормыВыпуска();
	
	ПараметрыФормы = Новый Структура;
	Для Каждого Реквизит Из ПараметрыФормыВыпуска Цикл
		ИмяОтбора = Префикс + Реквизит.Ключ;
		ИмяФлажка = Префикс + "Отбор" + Реквизит.Ключ;
		Если Форма[ИмяФлажка] Тогда
			ПараметрыФормы.Вставить(Реквизит.Ключ, Форма[ИмяОтбора]);
		КонецЕсли;
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения("ФильтрНоменклатурыЛекарственныхСредств_ОбработатьУстановкуОтбораПоФормеВыпуска", Форма);
	ОткрытьФорму("Справочник.ФормыВыпуска.Форма.ОтборФормыВыпуска", ПараметрыФормы, Форма,,,, Оповещение);
	
КонецПроцедуры

// Открывает окно выбора фильтра по принадлежности лекарственного средства к спискам.
//
// Параметры:
//  Форма                - ФормаКлиентскогоПриложения - форма-владелец.
//  ИмяСписка            - Строка - имя списка принадлежности лекарственного средства.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура ВыбратьСписокОтбора(Форма, ИмяСписка, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ОбработчикВыбора = Новый ОписаниеОповещения("ФильтрНоменклатурыЛекарственныхСредств_ОбработатьВыборЭлементовСписка", Форма, ИмяСписка);
	
	РеквизитЗначенияСпискаЗначений = Форма[ИмяСписка + "ДляВыбора"];
	РеквизитЗначенияСпискаЗначений.ПоказатьОтметкуЭлементов(ОбработчикВыбора);
	
КонецПроцедуры

// Обрабатывает выбор элемента фильтра по принадлежности лекарственного средства к спискам.
//
// Параметры:
//  Форма        - ФормаКлиентскогоПриложения - форма-владелец.
//  СписокВыбора - СписокЗначений - список с элементами фильтра по принадлежности лекарственного средства к спискам.
//  ИмяСписка    - Строка - имя списка принадлежности лекарственного средства.
//
Функция ОбработатьВыборЭлементовСписка(Форма, СписокВыбора, ИмяСписка) Экспорт
	
	Если СписокВыбора = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Форма[ИмяСписка].Очистить();
	Для Каждого Элемент Из СписокВыбора Цикл
		Если Элемент.Пометка Тогда
			Форма[ИмяСписка].Добавить(Элемент.Значение, Элемент.Представление);
		КонецЕсли;
	КонецЦикла;
	
	Если Форма[ИмяСписка].Количество() = 0 Тогда
		Элемент = СписокВыбора.НайтиПоЗначению(СписокВыбора.ТипЗначения.ПривестиЗначение());
		Если Элемент <> Неопределено Тогда
			Элемент.Пометка = Истина;
			Форма[ИмяСписка].Добавить(Элемент.Значение, Элемент.Представление);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс