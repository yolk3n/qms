#Область ПрограммныйИнтерфейс

// Возвращает ссылку на общий модуль по имени.
//
// Параметры:
//  Имя          - Строка - имя общего модуля, например:
//                 "ОбщегоНазначения",
//                 "ОбщегоНазначенияКлиент".
//
// Возвращаемое значение:
//  ОбщийМодуль.
//
Функция ОбщийМодуль(Имя) Экспорт
	
	Модуль = Вычислить(Имя);
	
#Если НЕ ВебКлиент Тогда
	Если ТипЗнч(Модуль) <> Тип("ОбщийМодуль") Тогда
		ТекстИсключения = НСтр("ru = 'Общий модуль ""%1"" не найден.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения,"%1", Имя);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
#КонецЕсли
	
	Возврат Модуль;
	
КонецФункции

// Форма списка, в которой устанавливается отбор.
//
// Параметры:
//  ЭтотОбъект - Форма - Справочник.ОчередьЭлектронныхЧековКОтправке.ФормаСписка
//  Список - ДинамическийСписок
//
Процедура ПриОткрытииСпискаОчереди(ЭтотОбъект, Список) Экспорт
	
	Параметры = РассылкаЭлектронныхЧековКлиентПовтИсп.ПараметрыИспользования();
	
	Если Параметры.НаличиеБСП Тогда
		
		// Вставить содержимое обработчика.
		МодульОбщегоНазначенияКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбщегоНазначенияКлиентСервер");
		МодульОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, 
			"ПроизошлаОшибкаПередачи", 
			ЭтотОбъект.ОтборПоОшибкам,
			ВидСравненияКомпоновкиДанных.Равно,
			, 
			ЭтотОбъект.ОтборПоОшибкам);
		
	КонецЕсли;
	
	РассылкаЭлектронныхЧековКлиентПереопределяемый.ПриОткрытииСпискаОчереди(ЭтотОбъект, Список);
	
КонецПроцедуры

// Процедура отправляет электронное сообщение на электронную почта и абонентский номер.
//
// Параметры:
//  ПараметрыЧека - Структура
//  ТекстСообщения - Строка - Текст письма или сообщения
//  ПокупательEmail - Строка - адрес электронной почты покупателя
//  ПокупательНомер - Строка - Номер телефона покупателя
//
Процедура НачатьОтправкуЭлектронногоЧека(ПараметрыЧека, ТекстСообщения, ПокупательEmail, ПокупательНомер) Экспорт
	
	РассылкаЭлектронныхЧековВызовСервера.НачатьОтправкуЭлектронногоЧека(ПараметрыЧека, ТекстСообщения, ПокупательEmail, ПокупательНомер);
	
КонецПроцедуры

// Процедура открывает форму настройки отправки электронных чеков
//
Процедура НастроитьОтправкуЭлектронныхЧеков() Экспорт
	
	РассылкаЭлектронныхЧековКлиентПереопределяемый.НастроитьОтправкуЭлектронныхЧеков();
	
КонецПроцедуры

#КонецОбласти

