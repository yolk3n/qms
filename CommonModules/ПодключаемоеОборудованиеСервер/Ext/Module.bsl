
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Настраивает подключаемое оборудование в форме,
// устанавливает флаг ИспользоватьПодключаемоеОборудование в форме
//
// Параметры
//   Форма - Форма, в которой необходимо настроить подключаемое оборудование
//
Процедура НастроитьФормуДляИспользованияПодключаемогоОборудования(Форма, ПрефиксыЭлементовФормы = "Товары") Экспорт
	
	НовыеРеквизиты = Новый Массив;
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ИспользоватьПодключаемоеОборудование") Тогда
		НовыеРеквизиты.Добавить(Новый РеквизитФормы("ИспользоватьПодключаемоеОборудование", Новый ОписаниеТипов("Булево")));
	КонецЕсли;
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПоддерживаемыеТипыПодключаемогоОборудования") Тогда
		НовыеРеквизиты.Добавить(Новый РеквизитФормы("ПоддерживаемыеТипыПодключаемогоОборудования", Новый ОписаниеТипов("Строка")));
	КонецЕсли;
	
	Если НовыеРеквизиты.Количество() > 0 Тогда
		Форма.ИзменитьРеквизиты(НовыеРеквизиты);
	КонецЕсли;
	
	ТипыПодключенногоОборудования = ПодключаемоеОборудованиеПовтИсп.ТипыПодключенногоОборудованияКлиента();
	Форма.ИспользоватьПодключаемоеОборудование =
		ОбщегоНазначенияБольничнаяАптека.ИспользоватьПодключаемоеОборудование(ТипыПодключенногоОборудования);
	
	ЕстьТСД =
		ТипыПодключенногоОборудования <> Неопределено
		И ТипыПодключенногоОборудования.Найти(Перечисления.ТипыПодключаемогоОборудования.ТерминалСбораДанных) <> Неопределено;
	
	ЕстьВесы =
		ТипыПодключенногоОборудования <> Неопределено
		И ТипыПодключенногоОборудования.Найти(Перечисления.ТипыПодключаемогоОборудования.ЭлектронныеВесы) <> Неопределено;
	
	ЕстьПравоРегистрацииШтрихкодовНоменклатуры = ПравоДоступа(
		"Изменение",
		Метаданные.РегистрыСведений.ШтрихкодыНоменклатуры);
	
	Если ЗначениеЗаполнено(ПрефиксыЭлементовФормы) Тогда
		МассивПрефиксыЭлементовФормы = СтрРазделить(ПрефиксыЭлементовФормы, ", ", Ложь);
	Иначе
		МассивПрефиксыЭлементовФормы = Новый Массив;
		МассивПрефиксыЭлементовФормы.Добавить(ПрефиксыЭлементовФормы);
	КонецЕсли;
	
	Для Каждого Префикс Из МассивПрефиксыЭлементовФормы Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			Префикс + "ЗагрузитьДанныеИзТСД",
			"Видимость",
			Форма.ИспользоватьПодключаемоеОборудование И ЕстьТСД);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			Префикс + "ВыгрузитьДанныеВТСД",
			"Видимость",
			Форма.ИспользоватьПодключаемоеОборудование И ЕстьТСД);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			Префикс + "ПолучитьВес",
			"Видимость",
			Форма.ИспользоватьПодключаемоеОборудование И ЕстьВесы);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			Префикс + "ОбработатьНенайденныеШтрихкоды",
			"Видимость",
			ЕстьПравоРегистрацииШтрихкодовНоменклатуры);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс
