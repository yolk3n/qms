//@strict-types

#Область СлужебныйПрограммныйИнтерфейс

#Область СостояниеДокумента

// Начальное состояние исходящего электронного документа.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция НачальноеСостояниеДокумента() Экспорт
	Возврат Перечисления.СостоянияДокументовЭДО.НеСформирован;
КонецФункции

// Возвращает параметры электронного документа для определения состояния.
// 
// Возвращаемое значение:
//  Структура:
//  * ТипРегламента - ПеречислениеСсылка.ТипыРегламентовЭДО - Тип регламента ЭДО.
//  * СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД - Способ обмена электронными документами.
//  * ОбменБезПодписи - Булево - Обмен без подписи (прямой обмен).
//  * ТребуетсяПодтверждение - Булево - Необходимость формирования ответного титула или ответной подписи.
//  * ТребуетсяИзвещение - Булево - необходимость формирования извещения о получении.
//  * Остановлен - Булево - признак остановленного документа.
//  * ПричинаОстановки - ПеречислениеСсылка.ПричиныОстановкиЭДО - причина остановки документа.
//  * Исправлен - Булево - признак того, что создан документ исправление.
//
Функция НовыеПараметрыДокументаДляОпределенияСостояния() Экспорт
	
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("ТипРегламента", Перечисления.ТипыРегламентовЭДО.ПустаяСсылка());
	ПараметрыДокумента.Вставить("СпособОбмена", Перечисления.СпособыОбменаЭД.ПустаяСсылка());
	ПараметрыДокумента.Вставить("ОбменБезПодписи", Ложь);
	ПараметрыДокумента.Вставить("ТребуетсяИзвещение", Ложь);
	ПараметрыДокумента.Вставить("ТребуетсяПодтверждение", Ложь);
	ПараметрыДокумента.Вставить("Остановлен", Ложь);
	ПараметрыДокумента.Вставить("ПричинаОстановки", Перечисления.ПричиныОстановкиЭДО.ПустаяСсылка());
	ПараметрыДокумента.Вставить("Исправлен", Ложь);
	
	Возврат ПараметрыДокумента;
	
КонецФункции

// Возвращает описание таблицы состояний документов для расчета состояния электронного документа.
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//  * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО - Тип элемента регламента.
//  * Состояние - ПеречислениеСсылка.СостоянияСообщенийЭДО - Состояние электронного документа.
//  * Направление - ПеречислениеСсылка.НаправленияЭДО
//  * Дата - Дата - дата сообщения.
//
Функция НовыеСостоянияЭлементовРегламента() Экспорт
	
	СостоянияЭлементовРегламента = Новый ТаблицаЗначений;
	СостоянияЭлементовРегламента.Колонки.Добавить("ТипЭлементаРегламента",
		Новый ОписаниеТипов("ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО"));
	СостоянияЭлементовРегламента.Колонки.Добавить("Состояние",
		Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияСообщенийЭДО"));
	СостоянияЭлементовРегламента.Колонки.Добавить("Направление",
		Новый ОписаниеТипов("ПеречислениеСсылка.НаправленияЭДО"));
	СостоянияЭлементовРегламента.Колонки.Добавить("Дата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Возврат СостоянияЭлементовРегламента;
	
КонецФункции

// Возвращает состояния электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. НовыеСостоянияЭлементовРегламента
//  ЭтоВходящийЭДО - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента, ЭтоВходящийЭДО = Ложь) Экспорт
	
	Если ПараметрыДокумента.Остановлен Тогда
		Состояние = СостояниеОстановленногоДокумента(ПараметрыДокумента.ПричинаОстановки,
			ПараметрыДокумента.Исправлен);
	ИначеЕсли ЭтоВходящийЭДО Тогда
		Состояние = СостояниеВходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента);
	Иначе
		Состояние = СостояниеИсходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента);
	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции

// Возвращает состояния электронного документа по которым ЭДО является завершенным.
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.СостоянияДокументовЭДО - Состояние электронного документа.
//
Функция СостоянияЗавершенногоЭДО() Экспорт
	Состояния = Новый Массив; // См. СостоянияЗавершенногоЭДО
	Состояния.Добавить(Перечисления.СостоянияДокументовЭДО.ОбменЗавершен);
	Состояния.Добавить(Перечисления.СостоянияДокументовЭДО.ОбменЗавершенСИсправлением);
	Состояния.Добавить(Перечисления.СостоянияДокументовЭДО.Аннулирован);
	Состояния.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытПринудительно);
	Состояния.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонением);
	Состояния.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонениемПриглашения);
	Состояния.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытСОшибкойПередачи);
	Возврат Состояния;
КонецФункции

// Устанавливает приоритетное состояние.
// 
// Параметры:
//  Состояние      - ПеречислениеСсылка.СостоянияДокументовЭДО
//  НовоеСостояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
Процедура УстановитьПриоритетноеСостояние(Состояние, НовоеСостояние) Экспорт
	
	Если СравнитьСостояния(Состояние, НовоеСостояние) = 1 Тогда
		Состояние = НовоеСостояние;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СостояниеСообщения

// Возвращает пустые параметры сообщения для определения состояния.
// 
// Возвращаемое значение:
//  Структура:
//  * Статус - ПеречислениеСсылка.СтатусыСообщенийЭДО
//  * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  * Направление - ПеречислениеСсылка.НаправленияЭДО
//
Функция НовыеПараметрыСообщенияДляОпределенияСостояния() Экспорт
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("ТипЭлементаРегламента", Перечисления.ТипыЭлементовРегламентаЭДО.ПустаяСсылка());
	ПараметрыДокумента.Вставить("Статус", Перечисления.СтатусыСообщенийЭДО.ПустаяСсылка());
	ПараметрыДокумента.Вставить("Направление", Перечисления.НаправленияЭДО.ПустаяСсылка());
	Возврат ПараметрыДокумента;
КонецФункции

// Возвращает параметры электронного документа для определения состояния.
// 
// Возвращаемое значение:
//  Структура:
//  * ТипРегламента          - ПеречислениеСсылка.ТипыРегламентовЭДО
//  * ТребуетсяПодтверждение - Булево
//  * ОбменБезПодписи        - Булево
//  * СпособОбмена           - ПеречислениеСсылка.СпособыОбменаЭД
//
Функция НовыеПараметрыДокументаДляОпределенияСостоянияСообщения() Экспорт
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("ТипРегламента", Перечисления.ТипыРегламентовЭДО.ПустаяСсылка());
	ПараметрыДокумента.Вставить("ТребуетсяПодтверждение", Ложь);
	ПараметрыДокумента.Вставить("ОбменБезПодписи", Ложь);
	ПараметрыДокумента.Вставить("СпособОбмена", Перечисления.СпособыОбменаЭД.ПустаяСсылка());
	Возврат ПараметрыДокумента;
КонецФункции

// Возвращает состояние сообщения.
//
// Параметры:
//  ПараметрыСообщения - См. НовыеПараметрыСообщенияДляОпределенияСостояния
//  ПараметрыДокумента - См. НовыеПараметрыДокументаДляОпределенияСостоянияСообщения
//  ИспользоватьУтверждение  - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияСообщенийЭДО
//
Функция СостояниеСообщения(ПараметрыСообщения, ПараметрыДокумента, ИспользоватьУтверждение = Ложь) Экспорт
	МенеджерРегламента = МенеджерРегламента(ПараметрыДокумента.ТипРегламента);
	Возврат МенеджерРегламента.СостояниеСообщения(ПараметрыСообщения, ПараметрыДокумента, ИспользоватьУтверждение);
КонецФункции

#КонецОбласти

#Область СхемаРегламента

// Возвращает коллекцию добавленных элементов схемы регламента.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  * Значение - СтрокаДереваЗначений:
//  ** ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  ** Дополнительный - Булево
//  ** Данные - СтрокаТаблицыЗначений
//
Функция НоваяКоллекцияЭлементовСхемыРегламента() Экспорт
	Возврат Новый Соответствие;
КонецФункции

// Возвращает пустую структуру настроек регламента.
//
// Возвращаемое значение:
//  Структура:
//  * ТипРегламента          - ПеречислениеСсылка.ТипыРегламентовЭДО - Тип регламента ЭДО.
//  * СпособОбмена           - ПеречислениеСсылка.СпособыОбменаЭД - Способ обмена электронными документами.
//  * ТребуетсяИзвещение     - Булево - необходимость формирования извещения о получении.
//  * ТребуетсяПодтверждение - Булево - Необходимость формирования ответного титула или ответной подписи.
//  * ЭтоВходящийЭДО         - Булево - признак входящего документооборота.
//
Функция НовыеНастройкиСхемыРегламента() Экспорт
	НастройкиРегламента = Новый Структура;
	НастройкиРегламента.Вставить("ТипРегламента", Перечисления.ТипыРегламентовЭДО.ПустаяСсылка());
	НастройкиРегламента.Вставить("СпособОбмена", Перечисления.СпособыОбменаЭД.ПустаяСсылка());
	НастройкиРегламента.Вставить("ТребуетсяИзвещение", Ложь);
	НастройкиРегламента.Вставить("ТребуетсяПодтверждение", Ложь);
	НастройкиРегламента.Вставить("ЭтоВходящийЭДО", Ложь);
	Возврат НастройкиРегламента;
КонецФункции

// Возвращает новую схему регламента.
//
// Параметры:
//  НастройкиСхемыРегламента - См. НовыеНастройкиСхемыРегламента
//  ДанныеЭлементовСхемы     - Неопределено
//                           - ТаблицаЗначений:
//  * ТипЭлементаРегламента  - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
// Возвращаемое значение:
//  ДеревоЗначений:
//  * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО - Тип элемента регламента.
//  * Дополнительный - Булево - Признак дополнительного элемента схемы.
//  * Данные - Неопределено
//           - СтрокаТаблицыЗначений: см. НоваяСхемаРегламента.ДанныеЭлементовСхемы
//
Функция НоваяСхемаРегламента(НастройкиСхемыРегламента, ДанныеЭлементовСхемы = Неопределено) Экспорт
	
	СхемаРегламента = Новый ДеревоЗначений;
	СхемаРегламента.Колонки.Добавить("ТипЭлементаРегламента",
		Новый ОписаниеТипов("ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО"));
	СхемаРегламента.Колонки.Добавить("Дополнительный", Новый ОписаниеТипов("Булево"));
	СхемаРегламента.Колонки.Добавить("Данные");
	
	ДобавитьЭлементыСхемыРегламента(СхемаРегламента, НастройкиСхемыРегламента);
	
	Если ЗначениеЗаполнено(ДанныеЭлементовСхемы) Тогда
		ЗаполнитьДанныеЭлементовСхемыРегламента(СхемаРегламента, НастройкиСхемыРегламента, ДанныеЭлементовСхемы);
	КонецЕсли;
	
	Возврат СхемаРегламента;
	
КонецФункции

// Возвращает элементы схемы регламента у которых не заполнены данные.
// 
// Параметры:
//  СхемаРегламента - См. НоваяСхемаРегламента
//
// Возвращаемое значение:
//  Массив из СтрокаДереваЗначений:
//  * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО - Тип элемента регламента.
//  * Дополнительный - Булево - Признак дополнительного элемента схемы.
//  * Данные - СтрокаТаблицыЗначений - Строка таблицы ДанныеЭлементовСхемы. 
//
Функция ЭлементыСхемыРегламентаБезДанных(СхемаРегламента) Экспорт
	
	Отбор = Новый Структура("Данные", СхемаРегламента.Колонки.Данные.ТипЗначения.ПривестиЗначение());
	ЭлементыСхемы = СхемаРегламента.Строки.НайтиСтроки(Отбор, Истина);
	Возврат ЭлементыСхемы;
	
КонецФункции

// Возвращает признак наличия элемента регламента.
// 
// Параметры:
//  КоллекцияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  ТипЭлементаРегламента        - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  ЭлементРегламента            - Неопределено,
//                                 СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//
// Возвращаемое значение:
//  Булево
//
Функция ЕстьЭлементРегламента(КоллекцияЭлементовРегламента, ТипЭлементаРегламента, ЭлементРегламента = Неопределено) Экспорт
	
	ЭлементРегламента = КоллекцияЭлементовРегламента.Найти(ТипЭлементаРегламента, "ТипЭлементаРегламента");
	Возврат ЭлементРегламента <> Неопределено;
	
КонецФункции

// Возвращает признак наличия и ссылку на актуальный элемента регламента с типом ПОА.
// 
// Параметры:
//  КоллекцияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  АктуальныйЭлемент            - Неопределено
//                               - СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//
// Возвращаемое значение:
//  Булево
//
Функция ЕстьАктуальноеАннулирование(КоллекцияЭлементовРегламента, АктуальныйЭлемент = Неопределено) Экспорт
	
	Результат = Ложь;
	
	Отбор = Новый Структура("ТипЭлементаРегламента", Перечисления.ТипыЭлементовРегламентаЭДО.ПОА);
	ЭлементыРегламента = КоллекцияЭлементовРегламента.НайтиСтроки(Отбор);
	
	Если Не ЗначениеЗаполнено(ЭлементыРегламента) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого ЭлементРегламента Из ЭлементыРегламента Цикл
		
		Если АктуальныйЭлемент = Неопределено
			ИЛИ ЭлементРегламента.Дата > АктуальныйЭлемент.Дата
			ИЛИ ЭлементРегламента.Дата = АктуальныйЭлемент.Дата
				И ЭлементРегламента.Направление = Перечисления.НаправленияЭДО.Входящий Тогда
			
			АктуальныйЭлемент = ЭлементРегламента;
			Результат = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак наличия и ссылку на актуальный элемент регламента с типом ПОА_УОУ.
// 
// Параметры:
//  КоллекцияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  АктуальноеАннулирование      - СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  АктуальноеОтклонение         - Неопределено
//                               - СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//
// Возвращаемое значение:
//  Булево
//
Функция ЕстьАктуальноеОтклонениеАннулирования(КоллекцияЭлементовРегламента, АктуальноеАннулирование, АктуальноеОтклонение = Неопределено) Экспорт
	
	Результат = Ложь;
	
	Отбор = Новый Структура("ТипЭлементаРегламента", Перечисления.ТипыЭлементовРегламентаЭДО.ПОА_УОУ);
	ЭлементыРегламента = КоллекцияЭлементовРегламента.НайтиСтроки(Отбор);
	
	Если Не ЗначениеЗаполнено(ЭлементыРегламента) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого ЭлементРегламента Из ЭлементыРегламента Цикл
		
		Если ЭлементРегламента.Дата > АктуальноеАннулирование.Дата Тогда
			АктуальноеОтклонение = ЭлементРегламента;
			Результат = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак успешности вставки элемента схемы регламента.
// 
// Параметры:
//  ЭлементыСхемы - См. НоваяКоллекцияЭлементовСхемыРегламента
//  ЭлементРодитель - СтрокаДереваЗначений: См. НоваяСхемаРегламента
//                  - См. НоваяСхемаРегламента
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  Дополнительный - Булево
// 
// Возвращаемое значение:
//  СтрокаДереваЗначений: См. НоваяСхемаРегламента
//
Функция ВставитьЭлементСхемыРегламента(ЭлементыСхемы, ЭлементРодитель, ТипЭлементаРегламента, Дополнительный = Ложь) Экспорт
	ЭлементСхемы = ЭлементРодитель.Строки.Добавить(); // См. СтрокаДереваЗначений: См. НоваяСхемаРегламента
	ЭлементСхемы.ТипЭлементаРегламента = ТипЭлементаРегламента;
	ЭлементСхемы.Дополнительный = Дополнительный;
	ЭлементыСхемы.Вставить(ТипЭлементаРегламента, ЭлементСхемы);
	Возврат ЭлементСхемы;
КонецФункции

// Параметры:
//  СхемаРегламента - см. НоваяСхемаРегламента
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
// 
// Возвращаемое значение:
//  - Неопределено
//  - СтрокаДереваЗначений: см. НоваяСхемаРегламента
Функция НайтиЭлементСхемыРегламента(СхемаРегламента, ТипЭлементаРегламента) Экспорт
	Возврат СхемаРегламента.Строки.Найти(ТипЭлементаРегламента, "ТипЭлементаРегламента");
КонецФункции

// Параметры:
//  СхемаРегламента - См. РегламентыЭДО.НоваяСхемаРегламента
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
// Возвращаемое значение:
//  См. НоваяКоллекцияЭлементовСхемыРегламента
Функция ДобавитьПроизвольныйЭлементРегламента(СхемаРегламента, ТипЭлементаРегламента) Экспорт
	
	ЭлементыСхемы = НоваяКоллекцияЭлементовСхемыРегламента();
	
	ИнформацияОтправителя = НайтиЭлементСхемыРегламента(СхемаРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя);
	
	Если ИнформацияОтправителя <> Неопределено Тогда
		ВставитьЭлементСхемыРегламента(ЭлементыСхемы, ИнформацияОтправителя, ТипЭлементаРегламента, Истина);
	КонецЕсли;
	
	Возврат ЭлементыСхемы;
	
КонецФункции

#КонецОбласти

#Область Извещения

// Возвращает пустые параметры документа для определения потребности извещения.
// 
// Возвращаемое значение:
//  Структура:
//  * ТипРегламента      - ПеречислениеСсылка.ТипыРегламентовЭДО
//  * СпособОбмена       - ПеречислениеСсылка.СпособыОбменаЭД
//  * ТребуетсяИзвещение - Булево
//
Функция НовыеПараметрыДокументаДляОпределенияПотребностиИзвещения() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("ТипРегламента", Перечисления.ТипыРегламентовЭДО.ПустаяСсылка());
	Параметры.Вставить("СпособОбмена", Перечисления.СпособыОбменаЭД.ПустаяСсылка());
	Параметры.Вставить("ТребуетсяИзвещение", Ложь);
	Возврат Параметры;
КонецФункции

// Возвращает тип извещения для элемента регламента.
// 
// Параметры:
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  ПараметрыДокумента - См. НовыеПараметрыДокументаДляОпределенияПотребностиИзвещения
//  ЭтоВходящийЭДО - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
Функция ТипИзвещенияДляЭлементаРегламента(ТипЭлементаРегламента, ПараметрыДокумента, ЭтоВходящийЭДО = Ложь) Экспорт
	
	Результат = Перечисления.ТипыЭлементовРегламентаЭДО.ПустаяСсылка();
	
	Если ЭтоВнутреннийОбмен(ПараметрыДокумента.СпособОбмена) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя
		И Не ПараметрыДокумента.ТребуетсяИзвещение Тогда
		Возврат Результат;
	КонецЕсли;
	
	МенеджерРегламента = МенеджерРегламента(ПараметрыДокумента.ТипРегламента);
	
	Если ЭтоВходящийЭДО Тогда
		Результат = МенеджерРегламента.ТипИзвещенияДляЭлементаВходящегоДокумента(ТипЭлементаРегламента);
	Иначе
		Результат = МенеджерРегламента.ТипИзвещенияДляЭлементаИсходящегоДокумента(ТипЭлементаРегламента);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

// Возвращает признак наличия активного аннулирования.
// 
// Параметры:
//  ЭлементыРегламента - ТаблицаЗначений:
//  * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЕстьАктивноеАннулирование(ЭлементыРегламента) Экспорт
	
	Отклонение = ЭлементыРегламента.Найти(Перечисления.ТипыЭлементовРегламентаЭДО.УОУ, "ТипЭлементаРегламента");
	Если Отклонение <> Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	АктуальноеАннулирование = Неопределено; // Неопределено,СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
	
	Если ЕстьАктуальноеАннулирование(ЭлементыРегламента, АктуальноеАннулирование)
		И Не ЕстьАктуальноеОтклонениеАннулирования(ЭлементыРегламента, АктуальноеАннулирование) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает признак наличия информации получателя по регламенту.
// 
// Параметры:
//  ТипРегламента - ПеречислениеСсылка.ТипыРегламентовЭДО
//
// Возвращаемое значение:
//  Булево - признак наличия информации получателя по регламенту.
//
Функция ЕстьИнформацияПолучателя(ТипРегламента) Экспорт
	МенеджерРегламента = МенеджерРегламента(ТипРегламента);
	Возврат МенеджерРегламента.ЕстьИнформацияПолучателя();
КонецФункции

// Возвращает признак доступности отклонения.
// 
// Параметры:
//  ЭлементыРегламента - ТаблицаЗначений:
//  * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  ЭтоВходящийЭДО - Булево
//  ТипЭлементаРегламентаУОУ - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ОтклонениеДоступно(ЭлементыРегламента, ЭтоВходящийЭДО, ТипЭлементаРегламентаУОУ = Неопределено) Экспорт
	
	Результат = Ложь;
	
	МодульОбменСГИСЭПД = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСГИСЭПД") Тогда
		МодульОбменСГИСЭПД = ОбщегоНазначения.ОбщийМодуль("ОбменСГИСЭПД");	
	КонецЕсли;
	
	Если (ЭтоВходящийЭДО И Не ЕстьАктивноеАннулирование(ЭлементыРегламента)
		Или (МодульОбменСГИСЭПД <> Неопределено 
			И МодульОбменСГИСЭПД.ОтклонениеДоступно(ЭлементыРегламента, ЭтоВходящийЭДО, ТипЭлементаРегламентаУОУ))) Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак отношения типа элемента регламента к служебным типам.
// 
// Параметры:
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтоСлужебноеСообщение(ТипЭлементаРегламента) Экспорт
	Возврат Не ЭтоИнформацияУчастникаЭДО(ТипЭлементаРегламента);
КонецФункции

// Возвращает признак является ли тип элемента регламента информацией участника ЭДО.
// 
// Параметры:
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтоИнформацияУчастникаЭДО(ТипЭлементаРегламента) Экспорт
	Возврат ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя
		ИЛИ ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияПолучателя;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СостояниеДокумента

// Возвращает состояние остановленного документа.
// 
// Параметры:
//  ПричинаОстановки - ПеречислениеСсылка.ПричиныОстановкиЭДО
//  Исправлен - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеОстановленногоДокумента(ПричинаОстановки, Исправлен)
	
	Если ПричинаОстановки = Перечисления.ПричиныОстановкиЭДО.ОтклонениеПодписания
		ИЛИ ПричинаОстановки = Перечисления.ПричиныОстановкиЭДО.ОшибкаПередачиБлокирующая Тогда
		
		Состояние = ?(Исправлен, Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонением,
			Перечисления.СостоянияДокументовЭДО.ТребуетсяУточнение);
		
	ИначеЕсли ПричинаОстановки = Перечисления.ПричиныОстановкиЭДО.ОшибкаПередачиНеблокирующая Тогда
		
		Состояние = Перечисления.СостоянияДокументовЭДО.ТребуетсяПовторнаяОтправка;
		
	ИначеЕсли ПричинаОстановки = Перечисления.ПричиныОстановкиЭДО.ТребуетсяОтправкаПриглашения Тогда
		
		Состояние = Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправкаПриглашения;
		
	ИначеЕсли ПричинаОстановки = Перечисления.ПричиныОстановкиЭДО.ОжидаетсяОтветНаПриглашение Тогда
		
		Состояние = Перечисления.СостоянияДокументовЭДО.ОжидаетсяОтветНаПриглашение;
		
	ИначеЕсли ПричинаОстановки = Перечисления.ПричиныОстановкиЭДО.ОтклонениеПриглашения Тогда
		
		Состояние = Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонениемПриглашения;
		
	ИначеЕсли ПричинаОстановки = Перечисления.ПричиныОстановкиЭДО.Аннулирован Тогда
		
		Состояние = Перечисления.СостоянияДокументовЭДО.Аннулирован;
		
	Иначе
		
		Состояние = Перечисления.СостоянияДокументовЭДО.ЗакрытПринудительно;
		
	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции

// Возвращает состояния исходящего электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. НовыеСостоянияЭлементовРегламента
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеИсходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента)
	
	Если Не ЗначениеЗаполнено(СостоянияЭлементовРегламента) Тогда
		Возврат НачальноеСостояниеДокумента();
	КонецЕсли;
	
	МенеджерРегламента = МенеджерРегламента(ПараметрыДокумента.ТипРегламента);
	Возврат МенеджерРегламента.СостояниеИсходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента);
	
КонецФункции

// Возвращает состояния входящего электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. НовыеСостоянияЭлементовРегламента
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеВходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента)
	
	Если Не ЗначениеЗаполнено(СостоянияЭлементовРегламента) Тогда
		Возврат НачальноеСостояниеДокумента();
	КонецЕсли;
	
	МенеджерРегламента = МенеджерРегламента(ПараметрыДокумента.ТипРегламента);
	Возврат МенеджерРегламента.СостояниеВходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента);
	
КонецФункции

// Возвращает результат сравнения двух состояний.
// 
// Параметры:
//  Состояние1 - ПеречислениеСсылка.СостоянияДокументовЭДО
//  Состояние2 - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Число
//
Функция СравнитьСостояния(Состояние1, Состояние2)
	
	ПриоритетСостояния1 = ПриоритетСостояния(Состояние1);
	ПриоритетСостояния2 = ПриоритетСостояния(Состояние2);
	Если ПриоритетСостояния1 > ПриоритетСостояния2 Тогда
		Возврат -1;
	ИначеЕсли ПриоритетСостояния1 < ПриоритетСостояния2 Тогда 
		Возврат 1;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

// Возвращает значение приоритета состояния.
// 
// Параметры:
//  Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Число
//
Функция ПриоритетСостояния(Состояние)
	
	Если Не ЗначениеЗаполнено(Состояние) Тогда
		Возврат 0
	ИначеЕсли Состояние = Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждение Тогда
		Возврат 1;
	ИначеЕсли Состояние = Перечисления.СостоянияДокументовЭДО.ОжидаетсяИзвещениеОПолучении Тогда 
		Возврат 2;
	ИначеЕсли Состояние = Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждениеОператора Тогда 
		Возврат 3;
	Иначе
		Возврат 4;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СостояниеСообщения

// Возвращает состояние сообщения ФНС.
// 
// Параметры:
//  ПараметрыСообщения - Структура:
//  * Статус - ПеречислениеСсылка.СтатусыСообщенийЭДО
//  ПараметрыДокумента - Структура:
//  * ОбменБезПодписи - Булево
//  * СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД
//  ИспользоватьУтверждение - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияСообщенийЭДО
//
Функция СостояниеСообщенияФНС(ПараметрыСообщения, ПараметрыДокумента, ИспользоватьУтверждение) Экспорт

	Состояние = Перечисления.СостоянияСообщенийЭДО.ПустаяСсылка();
	
	Статус = ПараметрыСообщения.Статус;
	
	Если Статус = Перечисления.СтатусыСообщенийЭДО.Получен Тогда
		
		Если ИспользоватьУтверждение Тогда
			Состояние = Перечисления.СостоянияСообщенийЭДО.Утверждение;
		Иначе
			Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение;
		КонецЕсли;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Утвержден Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Сформирован Тогда
		
		Если ПараметрыДокумента.ОбменБезПодписи Тогда
			Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке;
		Иначе
			Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание;
		КонецЕсли;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.ЧастичноПодписан Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Подписан Тогда
		
		Если ЭтоВнутреннийОбмен(ПараметрыДокумента.СпособОбмена) Тогда
			Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение;
		Иначе
			Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке;
		КонецЕсли;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.ПодготовленКОтправке Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Отправка;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Отправлен Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение;
		
	Иначе
		ВызватьИсключение ТекстОшибкиНекорректныйСтатусСообщения();
	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции

// Возвращает состояние сообщения аннулирования.
// 
// Параметры:
//  ПараметрыСообщения - Структура:
//  * Статус - ПеречислениеСсылка.СтатусыСообщенийЭДО
//  * Направление - ПеречислениеСсылка.НаправленияЭДО
//  ПараметрыДокумента - Структура:
//  * ОбменБезПодписи - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияСообщенийЭДО
//
Функция СостояниеСообщенияАннулирования(ПараметрыСообщения, ПараметрыДокумента) Экспорт
	
	Состояние = Перечисления.СостоянияСообщенийЭДО.ПустаяСсылка();
	
	Статус = ПараметрыСообщения.Статус;
	
	Если Статус = Перечисления.СтатусыСообщенийЭДО.Получен Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Подтверждение;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Утвержден Тогда
		
		Если ПараметрыДокумента.ОбменБезПодписи Тогда
			Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение;
		Иначе
			Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание;
		КонецЕсли;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Сформирован Тогда
		
		Если ПараметрыДокумента.ОбменБезПодписи Тогда
			Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке;
		Иначе
			Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание;
		КонецЕсли;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Подписан Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.ПодготовленКОтправке Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Отправка;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Отправлен Тогда
		
		Если ПараметрыСообщения.Направление = Перечисления.НаправленияЭДО.Входящий
			ИЛИ ПараметрыДокумента.ОбменБезПодписи Тогда
			Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение;
		Иначе
			Состояние = Перечисления.СостоянияСообщенийЭДО.ОжидаетсяПодпись;
		КонецЕсли;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Подтвержден Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение;
		
	Иначе
		ВызватьИсключение ТекстОшибкиНекорректныйСтатусСообщения();
	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции

// Возвращает состояние служебного сообщения.
// 
// Параметры:
//  ПараметрыСообщения - Структура:
//  * Статус - ПеречислениеСсылка.СтатусыСообщенийЭДО
//  ПараметрыДокумента - Структура:
//  * ОбменБезПодписи - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияСообщенийЭДО
//
Функция СостояниеСлужебногоСообщения(ПараметрыСообщения, ПараметрыДокумента) Экспорт
	
	Состояние = Перечисления.СостоянияСообщенийЭДО.ПустаяСсылка();
	
	Статус = ПараметрыСообщения.Статус;
	
	Если Статус = Перечисления.СтатусыСообщенийЭДО.Получен Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Сформирован Тогда
		
		Если ПараметрыДокумента.ОбменБезПодписи Тогда
			Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке;
		Иначе
			Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание;
		КонецЕсли;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Подписан Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.ПодготовленКОтправке Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Отправка;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Отправлен Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение;
		
	Иначе
		ВызватьИсключение ТекстОшибкиНекорректныйСтатусСообщения();
	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции

// Возвращает текст ошибки по некорректному статусу.
// 
// Возвращаемое значение:
//  Строка
//
Функция ТекстОшибкиНекорректныйСтатусСообщения() Экспорт
	Возврат НСтр("ru = 'Некорректный статус документа'");
КонецФункции

#КонецОбласти

#Область СхемаРегламента

// Возвращает коллекцию добавленных элементов схемы регламента.
// 
// Параметры:
//  СхемаРегламента - См. НоваяСхемаРегламента
//  НастройкиСхемыРегламента - См. НовыеНастройкиСхемыРегламента
//
// Возвращаемое значение:
//  См. НоваяКоллекцияЭлементовСхемыРегламента
//
Функция ДобавитьЭлементыСхемыРегламента(СхемаРегламента, НастройкиСхемыРегламента)
	
	МенеджерРегламента = МенеджерРегламента(НастройкиСхемыРегламента.ТипРегламента);
	Возврат МенеджерРегламента.ДобавитьЭлементыСхемыРегламента(СхемаРегламента, НастройкиСхемыРегламента);
	
КонецФункции

// Добавить опциональные элементы схемы регламента.
// 
// Параметры:
//  СхемаРегламента - См. НоваяСхемаРегламента
//  НастройкиСхемыРегламента - См. НовыеНастройкиСхемыРегламента
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
// 
// Возвращаемое значение:
//  См. РегламентыЭДО.НоваяКоллекцияЭлементовСхемыРегламента
Функция ДобавитьЭлементыСхемыВложенногоРегламента(СхемаРегламента, НастройкиСхемыРегламента, ТипЭлементаРегламента)
	
	МенеджерРегламента = МенеджерРегламента(НастройкиСхемыРегламента.ТипРегламента);
	Возврат МенеджерРегламента.ДобавитьЭлементыСхемыВложенногоРегламента(СхемаРегламента, НастройкиСхемыРегламента,
		ТипЭлементаРегламента);
	
КонецФункции

// Заполняет данные элементов схемы регламента.
// 
// Параметры:
//  СхемаРегламента - см. НоваяСхемаРегламента
//  НастройкиСхемыРегламента - см. НовыеНастройкиСхемыРегламента
//  ДанныеЭлементовСхемы - ТаблицаЗначений:
//  * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
Процедура ЗаполнитьДанныеЭлементовСхемыРегламента(СхемаРегламента, НастройкиСхемыРегламента, ДанныеЭлементовСхемы)
	
	СтрокиСхемыРегламента = СхемаРегламента.Строки;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ТипЭлементаРегламента", Перечисления.ТипыЭлементовРегламентаЭДО.ПустаяСсылка());
	Отбор.Вставить("Данные", Неопределено);
	
	Для Каждого ДанныеЭлемента Из ДанныеЭлементовСхемы Цикл
		
		Отбор.ТипЭлементаРегламента = ДанныеЭлемента.ТипЭлементаРегламента;
		ЭлементыСхемы = СтрокиСхемыРегламента.НайтиСтроки(Отбор, Истина);
		
		Если ЗначениеЗаполнено(ЭлементыСхемы) Тогда
			ЭлементыСхемы[0].Данные = ДанныеЭлемента;
		Иначе
			НовыеЭлементыСхемы = ДобавитьЭлементыСхемыВложенногоРегламента(СхемаРегламента, НастройкиСхемыРегламента,
				ДанныеЭлемента.ТипЭлементаРегламента);
			ЭлементСхемы = НовыеЭлементыСхемы[ДанныеЭлемента.ТипЭлементаРегламента];
			Если ЭлементСхемы <> Неопределено Тогда
				ЭлементСхемы.Данные = ДанныеЭлемента;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает признак обмена через оператора.
// 
// Параметры:
//  СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтоОбменЧерезОператора(СпособОбмена) Экспорт
	Возврат СинхронизацияЭДОКлиентСервер.ЭтоОбменЧерезОператора(СпособОбмена);
КонецФункции

// Возвращает признак внутреннего обмена
// 
// Параметры:
//  СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтоВнутреннийОбмен(СпособОбмена) Экспорт
	Возврат СинхронизацияЭДО.ЭтоВнутреннийОбмен(СпособОбмена);
КонецФункции

#КонецОбласти

#Область Прочее

// Общий модуль обработки указанного типа регламента.
// 
// Параметры:
//  ТипРегламента - ПеречислениеСсылка.ТипыРегламентовЭДО - Тип регламента ЭДО.
// 
// Возвращаемое значение:
//  - ОбщийМодуль
//
Функция МенеджерРегламента(ТипРегламента)
	
	Если ТипРегламента = Перечисления.ТипыРегламентовЭДО.УПД Тогда
		Возврат РегламентыЭДО_УПД;
	ИначеЕсли ТипРегламента = Перечисления.ТипыРегламентовЭДО.Неформализованный Тогда
		Возврат РегламентыЭДО_Неформализованный;
	ИначеЕсли ТипРегламента = Перечисления.ТипыРегламентовЭДО.Формализованный Тогда
		Возврат РегламентыЭДО_Формализованный;
	ИначеЕсли ТипРегламента = Перечисления.ТипыРегламентовЭДО.ФормализованныйАктСверки Тогда
		Возврат РегламентыЭДО_ФормализованныйАктСверки;
	Иначе
		Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСГИСЭПД") Тогда
			МодульОбменСГИСЭПД = ОбщегоНазначения.ОбщийМодуль("ОбменСГИСЭПД");	
			РегламентЭПД = МодульОбменСГИСЭПД.РегламентЭПД(ТипРегламента);
		КонецЕсли;
		Если РегламентЭПД <> Неопределено Тогда
			Возврат РегламентЭПД;	
		Иначе
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Некорректный тип регламента ЭДО: %1'"), ТипРегламента);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти
