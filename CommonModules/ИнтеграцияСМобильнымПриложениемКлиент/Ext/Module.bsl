
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Обработчик переопределяемой команды формы,
// запускающей загрузку данных из мобильного приложения.
// Создание команды на форме см. в процедуре ИнтеграцияСМобильнымПриложением.СоздатьКомандуЗагрузкиДанныхИзМобильногоПриложения
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта справочника или документа,
//  Команда - КомандаФормы - команда формы.
//  ОповещениеОЗавершении - ОписаниеОповещения - обработчик завершеия выполнения команды загрузки данных.
//
Процедура ВыполнитьКомандуЗагрузкиДанныхИзМобильногоПриложения(Форма, Команда, ОповещениеОЗавершении) Экспорт
	
	СвойстваКоманды = ИнтеграцияСМобильнымПриложениемКлиентСервер.СвойстваКомандыЗагрузкиШтрихкодовИзМобильногоПриложения();
	
	Если Не СтрЗаканчиваетсяНа(Команда.Имя, СвойстваКоманды.Имя) Тогда
		Возврат;
	КонецЕсли;
	
	Данные = ИнтеграцияСМобильнымПриложениемВызовСервера.ПолучитьДанныеМобильногоПриложенияИзОчереди("ШтрихкодBase64");
	Для Каждого ДанныеШтрихкода Из Данные Цикл
		ДанныеШтрихкода.Вставить("Штрихкод"  , ПолучитьСтрокуИзДвоичныхДанных(Base64Значение(ДанныеШтрихкода.ШтрихкодBase64)));
		ДанныеШтрихкода.Вставить("Количество", 1);
	КонецЦикла;
	
	РезультатЗагрузки = Новый Структура;
	РезультатЗагрузки.Вставить("Результат"     , ЗначениеЗаполнено(Данные));
	РезультатЗагрузки.Вставить("ТаблицаТоваров", Данные);
	
	ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, РезультатЗагрузки);
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс
