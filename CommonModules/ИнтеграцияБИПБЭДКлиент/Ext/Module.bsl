
#Область СлужебныйПрограммныйИнтерфейс

// Признак использования библиотеки интернет поддержки пользователей
//
// Возвращаемое значение:
//  Булево - Истина, если используется библиотека интернет поддержки пользователей.
//
Функция ИспользуетсяИнтернетПоддержкаПользователей() Экспорт
	
	Использование = Неопределено;
	ЕстьОбменСКонтрагентами = ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами");
	Если ЕстьОбменСКонтрагентами Тогда
		МодульОбменСКонтрагентамиКлиентПереопределяемый = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСКонтрагентамиКлиентПереопределяемый");
		МодульОбменСКонтрагентамиКлиентПереопределяемый.ПроверитьИспользованиеИнтернетПоддержкаПользователей(Использование);
	КонецЕсли;
	
	Если Использование = Неопределено Тогда
		Использование = Ложь;
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Подключение1СТакском") Тогда
			МодульПодключение1СТакскомВызовСервера = ОбщегоНазначенияКлиент.ОбщийМодуль("Подключение1СТакскомВызовСервера");
			Если МодульПодключение1СТакскомВызовСервера.ДоступноИспользованиеСервиса1СТакском() Тогда
				ОшибкаОбращенияКВебСервису = Ложь;
				МодульПодключение1СТакскомКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("Подключение1СТакскомКлиент");
				Использование = МодульПодключение1СТакскомКлиент.КонфигурацияЗарегистрированаВСервисе1СТакском(
					ОшибкаОбращенияКВебСервису);
				Если ОшибкаОбращенияКВебСервису Тогда
					Использование = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Использование;
	
КонецФункции

#КонецОбласти