///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ИнтеграцияСПлатежнымиСистемами".
// ОбщийМодуль.СервисИнтеграцииССБП.
//
// Серверные процедуры обмена данными с Системой быстрых платежей:
//  - выполнение оплаты, возвратов и получение статус операций;
//  - запрос данных отчетов по сверке взаиморасчетов;
//  - загрузка настроек платежных систем СБП;
//  - проверка данных аутентификации;
//  - настройки аутентификации участников СБП;
//  - регистрация новых интеграции;
//  - загрузка статусов отложенных операций;
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// ИнтернетПоддержкаПользователей.СверкаВзаиморасчетовСБПc2b

// Получает данные отчета по сверке взаиморасчетов из сервиса платежной системы.
//
// Параметры:
//  НачалоПериода - Дата - дата начало выборки данных отчета;
//  КонецПериода - Дата - дата окончания выборки данных отчета;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//    см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДатаЗапросаСтатуса - Дата - для определения периодичности получения статуса
//    формирования отчета.
//
// Возвращаемое значение:
//  Структура - данные сверки взаиморасчетов:
//    *Идентификатор - Строка - идентификатор отчета;
//    *СуммаВозвратов - Число - общая сумма возвратов за период по торговой точке;
//    *СуммаОплат - Число - общая сумма оплат за период по торговой точке;
//    *СуммаКомиссии - Число - рассчитанная сумма возвратов за период по торговой точке;
//    *СтатусОперации - Строка - текущее состояние операции оплаты:
//       - "Выполняется" - подтверждение оплаты от платежной системы не получено;
//       - "Отменена" - оплата по оплата по ранее сформированному QR-коду невозможна;
//       - "Выполнена" - платежная система подтвердила оплату;
//       - "ТребуетсяПодтверждение" - необходимо подтвердить действие;
//       - "Ошибка" - не удалось выполнить проверку оплаты из-за ошибки.
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//     может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ЗапросОтчетаПоСверкеОборотов(ПараметрыИнтеграции, НачалоПериода, КонецПериода, ДатаЗапросаСтатуса) Экспорт
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации();
	РезультатОперации.Вставить("Идентификатор",  "");
	РезультатОперации.Вставить("СуммаВозвратов", 0);
	РезультатОперации.Вставить("СуммаОплат" ,    0);
	РезультатОперации.Вставить("СуммаКомиссии",  0);
	
	РезультатЗапроса = ОперацияЗапросОтчетаПоОборотам(
		НачалоПериода,
		КонецПериода,
		ПараметрыИнтеграции);
	
	Если ЗначениеЗаполнено(РезультатЗапроса.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатЗапроса,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		Возврат РезультатОперации;
	КонецЕсли;
	
	ОбработатьСтатусОтчета(
		РезультатОперации,
		РезультатЗапроса.Статус,
		РезультатЗапроса.ТипОшибки);
	
	ЗаполнитьЗначенияСвойств(
		РезультатОперации,
		РезультатЗапроса,
		"Идентификатор, СуммаВозвратов, СуммаОплат, СуммаКомиссии");
	
	URLФайла = РезультатЗапроса.URLФайла;
	Если РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполняется() Тогда
		
		РезультатЗапроса = СостояниеОтчетаПоСверкеОборотов(
			ПараметрыИнтеграции,
			РезультатОперации.Идентификатор,
			ДатаЗапросаСтатуса);
		
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатЗапроса,
			"СуммаВозвратов,
			|СуммаОплат,
			|СуммаКомиссии,
			|СтатусОперации,
			|КодОшибки,
			|СообщениеОбОшибке,
			|ИнформацияОбОшибке");
		
	ИначеЕсли РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполнена()
			И ЗначениеЗаполнено(URLФайла) Тогда
		
		ДанныеОтчета = ОперацияЗагрузитьФайлОтчетаПоСверкеОборотов(
			URLФайла,
			РезультатОперации.Идентификатор,
			ПараметрыИнтеграции);
		
		Если ЗначениеЗаполнено(ДанныеОтчета.КодОшибки) Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				ДанныеОтчета,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
			РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		Иначе
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				ДанныеОтчета,
				"СуммаВозвратов, СуммаОплат, СуммаКомиссии");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Получает статус и загружает данные отчета по сверке взаиморасчетов из сервиса платежной системы.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//    см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  Идентификатор - Строка - идентификатор отчета в платежной системе;
//  ДатаЗапросаСтатуса - Дата - для определения периодичности получения статуса
//    формирования отчета.
//
// Возвращаемое значение:
//  Структура - данные сверки взаиморасчетов:
//    *СуммаВозвратов - Число - общая сумма возвратов за период по торговой точке;
//    *СуммаОплат - Число - общая сумма оплат за период по торговой точке;
//    *СуммаКомиссии - Число - рассчитанная сумма возвратов за период по торговой точке;
//    *СтатусОперации - Строка - текущее состояние операции оплаты:
//       - "Выполняется" - подтверждение оплаты от платежной системы не получено;
//       - "Отменена" - оплата по оплата по ранее сформированному QR-коду невозможна;
//       - "Выполнена" - платежная система подтвердила оплату;
//       - "ТребуетсяПодтверждение" - необходимо подтвердить действие;
//       - "Ошибка" - не удалось выполнить проверку оплаты из-за ошибки.
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//     может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция СостояниеОтчетаПоСверкеОборотов(ПараметрыИнтеграции, Идентификатор, ДатаЗапросаСтатуса) Экспорт
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации();
	РезультатОперации.Вставить("СуммаВозвратов", 0);
	РезультатОперации.Вставить("СуммаОплат", 0);
	РезультатОперации.Вставить("СуммаКомиссии", 0);
	
	РезультатЗапроса = ОперацияЗапросСтатусаОтчетаПоОборотам(
		Идентификатор,
		ПараметрыИнтеграции,
		ДатаЗапросаСтатуса);
	
	Если ЗначениеЗаполнено(РезультатЗапроса.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатЗапроса,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		Возврат РезультатОперации;
	КонецЕсли;
	
	ОбработатьСтатусОтчета(
		РезультатОперации,
		РезультатЗапроса.Статус,
		РезультатЗапроса.ТипОшибки);
	
	ЗаполнитьЗначенияСвойств(
		РезультатОперации,
		РезультатЗапроса,
		"СуммаВозвратов, СуммаОплат, СуммаКомиссии");
	
	URLФайла = РезультатЗапроса.URLФайла;
	Если РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполнена()
		И ЗначениеЗаполнено(URLФайла) Тогда
		
		ДанныеОтчета = ОперацияЗагрузитьФайлОтчетаПоСверкеОборотов(
			URLФайла,
			Идентификатор,
			ПараметрыИнтеграции);
		
		Если ЗначениеЗаполнено(ДанныеОтчета.КодОшибки) Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				ДанныеОтчета,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
			РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		Иначе
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				ДанныеОтчета,
				"СуммаВозвратов, СуммаОплат, СуммаКомиссии");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Получает данные операций проведенных в платежной системе за период.
//
// Параметры:
//  НачалоПериода - Дата - дата начало выборки данных отчета;
//  КонецПериода - Дата - дата окончания выборки данных отчета;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//    см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДатаЗапросаСтатуса - Дата - для определения периодичности получения статуса
//    формирования отчета.
//
// Возвращаемое значение:
//  Структура - данные сверки взаиморасчетов:
//    *Идентификатор - Строка - идентификатор отчета;
//    *ДанныеОпераций - ТаблицаЗначений - см. ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйДанныеОтчетаПоСверкеОпераций;
//    *СтатусОперации - Строка - текущее состояние операции оплаты:
//       - "Выполняется" - подтверждение оплаты от платежной системы не получено;
//       - "Отменена" - оплата по оплата по ранее сформированному QR-коду невозможна;
//       - "Выполнена" - платежная система подтвердила оплату;
//       - "Ошибка" - не удалось выполнить проверку оплаты из-за ошибки.
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                 может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ЗапросОтчетаПоСверкеОпераций(ПараметрыИнтеграции, НачалоПериода, КонецПериода, ДатаЗапросаСтатуса) Экспорт
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации();
	РезультатОперации.Вставить("Идентификатор", "");
	РезультатОперации.Вставить("ДанныеОпераций", ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйДанныеОтчетаПоСверкеОпераций());
	
	РезультатЗапроса = ОперацияЗапросОтчетаПоСверкеОпераций(
		НачалоПериода,
		КонецПериода,
		ПараметрыИнтеграции);
	
	Если ЗначениеЗаполнено(РезультатЗапроса.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатЗапроса,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		Возврат РезультатОперации;
	КонецЕсли;
	
	ОбработатьСтатусОтчета(
		РезультатОперации,
		РезультатЗапроса.Статус,
		РезультатЗапроса.ТипОшибки);
	
	ЗаполнитьЗначенияСвойств(
		РезультатОперации,
		РезультатЗапроса,
		"Идентификатор");
	
	Если РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполняется() Тогда
		
		РезультатЗапросаСтатуса = СостояниеОтчетаПоСверкеОпераций(
			ПараметрыИнтеграции,
			РезультатОперации.Идентификатор,
			ДатаЗапросаСтатуса);
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатЗапросаСтатуса,
			"СтатусОперации, КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		РезультатОперации.ДанныеОпераций = РезультатЗапросаСтатуса.ДанныеОпераций;
		
	ИначеЕсли РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполнена()
			И ЗначениеЗаполнено(РезультатЗапроса.URLФайла) Тогда
		
		ДанныеОтчета = ОперацияЗагрузитьФайлОтчетаПоСверкеОпераций(
			РезультатЗапроса.URLФайла,
			РезультатОперации.Идентификатор,
			ПараметрыИнтеграции);
		
		Если ЗначениеЗаполнено(ДанныеОтчета.КодОшибки) Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				ДанныеОтчета,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
			РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		Иначе
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				ДанныеОтчета,
				"ДанныеОпераций");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Получает статус и загружает данные отчета по сверке операций из сервиса платежной системы.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//    см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  Идентификатор - Строка - идентификатор отчета в платежной системе;
//  ДатаЗапросаСтатуса - Дата - для определения периодичности получения статуса
//    формирования отчета.
//
// Возвращаемое значение:
//  Структура - данные сверки взаиморасчетов:
//    *ДанныеОпераций - ТаблицаЗначений - см. ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйДанныеОтчетаПоСверкеОпераций;
//    *СтатусОперации - Строка - текущее состояние операции оплаты:
//       - "Выполняется" - подтверждение оплаты от платежной системы не получено;
//       - "Отменена" - оплата по оплата по ранее сформированному QR-коду невозможна;
//       - "Выполнена" - платежная система подтвердила оплату;
//       - "Ошибка" - не удалось выполнить проверку оплаты из-за ошибки.
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//       может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция СостояниеОтчетаПоСверкеОпераций(ПараметрыИнтеграции, Идентификатор, ДатаЗапросаСтатуса) Экспорт
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации();
	РезультатОперации.Вставить("ДанныеОпераций", ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйДанныеОтчетаПоСверкеОпераций());
	
	РезультатЗапроса = ОперацияЗапросСтатусаОтчетаПоОперациям(
		Идентификатор,
		ПараметрыИнтеграции,
		ДатаЗапросаСтатуса);
	
	Если ЗначениеЗаполнено(РезультатЗапроса.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатЗапроса,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		Возврат РезультатОперации;
	КонецЕсли;
	
	ОбработатьСтатусОтчета(
		РезультатОперации,
		РезультатЗапроса.Статус,
		РезультатЗапроса.ТипОшибки);
	
	Если РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполнена() Тогда
		
		ДанныеОтчета = ОперацияЗагрузитьФайлОтчетаПоСверкеОпераций(
			РезультатЗапроса.URLФайла,
			Идентификатор,
			ПараметрыИнтеграции);
		
		Если ЗначениеЗаполнено(ДанныеОтчета.КодОшибки) Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				ДанныеОтчета,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
			РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		Иначе
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				ДанныеОтчета,
				"ДанныеОпераций");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Конец ИнтернетПоддержкаПользователей.СверкаВзаиморасчетовСБПc2b

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПрикладныеОперации

// См. ИнтеграцияСПлатежнымиСистемами.ИдентификаторОплаты.
//
Функция ИдентификаторОплаты(ДокументОплаты, ПараметрыИнтеграции, ДополнительныеПараметры) Экспорт
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатИдентификаторОплаты();
	
	ДанныеЗаказа = ДанныеЗаказаНаОплату(
		РезультатОперации,
		ДокументОплаты,
		"",
		ПараметрыИнтеграции,
		ДополнительныеПараметры);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОперации.QRКод= РегистрыСведений.ИдентификаторыОперацийСБП.QRКодОперацииСБП(
		ДокументОплаты);
	
	Если Не ЗначениеЗаполнено(РезультатОперации.QRКод) Тогда
		
		// Создание нового QR-кода Системы быстрых платежей.
		ДанныеСсылки = ОперацияЗаказНаОплату(
			ДанныеЗаказа.ЗаказНаОплату,
			ДанныеЗаказа.Идентификатор,
			ПараметрыИнтеграции);
		
		Если ЗначениеЗаполнено(ДанныеСсылки.КодОшибки) Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				ДанныеСсылки,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
			Возврат РезультатОперации;
		КонецЕсли;
		
		// Сохранить информацию об идентификаторе СБП.
		РегистрыСведений.ИдентификаторыОперацийСБП.ЗаписатьДанныеОплатыСБП(
			ДокументОплаты,
			ДанныеСсылки.ИдентификаторОплаты,
			ДанныеСсылки.ПериодИспользования,
			ДанныеСсылки.QRКод,
			ИдентификаторСтатусаВПроцессе());
		
		РезультатОперации.QRКод = ДанныеСсылки.QRКод;
		
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// См. ИнтеграцияСПлатежнымиСистемами.КассоваяСсылка.
//
Функция КассоваяСсылка(ПараметрыИнтеграции) Экспорт
	
	Возврат ОперацияРегистрацияКассовойСсылки(ПараметрыИнтеграции);
	
КонецФункции

// См. ИнтеграцияСПлатежнымиСистемами.АктивироватьКассовуюСсылку.
//
Функция АктивироватьКассовуюСсылку(
		ДокументОплаты,
		ДанныеСсылки,
		ПараметрыИнтеграции,
		ДополнительныеПараметры) Экспорт
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатИдентификаторОплаты();
	
	ДанныеЗаказа = ДанныеЗаказаНаОплату(
		РезультатОперации,
		ДокументОплаты,
		ДанныеСсылки.ИдентификаторОплаты,
		ПараметрыИнтеграции,
		ДополнительныеПараметры);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	// Активация кассовой ссылки.
	РезультатАктивации = ОперацияАктивацияКассовойСсылки(
		ДанныеЗаказа.ЗаказНаОплату,
		ДанныеЗаказа.Идентификатор,
		ДанныеСсылки.ИдентификаторОплаты,
		ПараметрыИнтеграции);
	
	Если ЗначениеЗаполнено(РезультатАктивации.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатАктивации,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатОперации;
	КонецЕсли;
	
	// Сохранить информацию об идентификаторе СБП.
	РегистрыСведений.ИдентификаторыОперацийСБП.ЗаписатьДанныеОплатыСБП(
		ДокументОплаты,
		РезультатАктивации.ИдентификаторОплаты,
		РезультатАктивации.ПериодИспользования,
		ДанныеСсылки.КассоваяСсылка,
		ИдентификаторСтатусаВПроцессе(),
		РезультатАктивации.ПараметрыАктивации);
	
	Возврат РезультатАктивации;
	
КонецФункции

// См. ИнтеграцияСПлатежнымиСистемами.ДеактивироватьКассовуюСсылку.
//
Функция ДеактивироватьКассовуюСсылку(
		ПараметрыИнтеграции,
		ДанныеСсылки) Экспорт
	
	Возврат ОперацияДеактивироватьКассовуюСсылку(
		ПараметрыИнтеграции,
		ДанныеСсылки.ИдентификаторОплаты);
	
КонецФункции 

// См. ИнтеграцияСПлатежнымиСистемами.СтатусОплаты.
//
Функция СтатусОплаты(ДокументОплаты, ПараметрыИнтеграции, ДлительныйВызов) Экспорт
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации();
	ПараметрыСтатуса = РегистрыСведений.ИдентификаторыОперацийСБП.ПараметрыОпределенияСтатусаОперации(
		ДокументОплаты);
	
	Если ПараметрыСтатуса = Неопределено Тогда
		РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Идентификатор оплаты по документу не обнаружен.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Идентификатор оплаты по документу не обнаружен.'");
		Возврат РезультатОперации;
	КонецЕсли;
	
	Оплачен = Ложь;
	Отменен = Ложь;
	
	Если ПараметрыСтатуса.СтатусОперации = ИдентификаторСтатусаВыполнена() Тогда
		Оплачен = Истина;
		ЗаполнитьЗначенияСвойств(РезультатОперации.ПараметрыОперации, ПараметрыСтатуса);
		РезультатОперации.ПараметрыОперации.ИдентификаторОперации = ПараметрыСтатуса.ИдентификаторПлатежнойСистемы;
	ИначеЕсли ПараметрыСтатуса.СтатусОперации = ИдентификаторСтатусаОтклонена() Тогда
		Отменен = Истина;
		ЗаполнитьЗначенияСвойств(РезультатОперации.ПараметрыОперации, ПараметрыСтатуса);
		РезультатОперации.ПараметрыОперации.ИдентификаторОперации = ПараметрыСтатуса.ИдентификаторПлатежнойСистемы;
	ИначеЕсли (ПараметрыСтатуса.ПериодИспользования + 600) < ТекущаяДатаСеанса() Тогда
		
		// При окончании срока действия QR-кода, выполняется проверка статуса оплаты
		// и регистрация отмены QR-кода в ИБ, если оплата так и не была совершена.
		РезультатПроверки = ОпределитьСтатусОплаты(
			ПараметрыСтатуса,
			ПараметрыИнтеграции,
			ИдентификаторОперацииПроверкиОплаты());
		
		Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				РезультатПроверки,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
			РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
			Возврат РезультатОперации;
		КонецЕсли;
		
		Если РезультатПроверки.СтатусОплаты = ИдентификаторСтатусаВПроцессе() Тогда
			Отменен = Истина;
			РезультатПроверки.СтатусОплаты = ИдентификаторСтатусаОтклонена();
		КонецЕсли;
		
		РегистрыСведений.ИдентификаторыОперацийСБП.ЗаписатьСтатусОперации(
			ДокументОплаты,
			РезультатПроверки.ИдентификаторПлатежнойСистемы,
			РезультатПроверки.ДатаОперации,
			РезультатПроверки.СтатусОплаты,
			РезультатПроверки.СуммаОперации,
			,
			ПараметрыСтатуса.ДатаЗапросаСтатуса);
			
		ЗаполнитьЗначенияСвойств(РезультатОперации.ПараметрыОперации, РезультатПроверки);
		РезультатОперации.ПараметрыОперации.ИдентификаторОперации = РезультатПроверки.ИдентификаторПлатежнойСистемы;
		
	Иначе
		Если ДлительныйВызов Тогда
			
			НастройкиВызова = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйИтеративныйВызовОперации();
			Пока ИнтеграцияСПлатежнымиСистемамиСлужебный.ВозможенВызовОперации(НастройкиВызова) Цикл
				
				РезультатПроверки = ОпределитьСтатусОплаты(
					ПараметрыСтатуса,
					ПараметрыИнтеграции,
					ИдентификаторОперацииПроверкиОплаты());
				
				Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
					ЗаполнитьЗначенияСвойств(
						РезультатОперации,
						РезультатПроверки,
						"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
					РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
					Возврат РезультатОперации;
				КонецЕсли;
				
				Если РезультатПроверки.ОбновитьДатуЗапросаСтатуса Тогда
					ПараметрыСтатуса.ДатаЗапросаСтатуса = ТекущаяДатаСеанса();
				КонецЕсли;
				
				Оплачен = (РезультатПроверки.СтатусОплаты = ИдентификаторСтатусаВыполнена());
				Отменен = (РезультатПроверки.СтатусОплаты = ИдентификаторСтатусаОтклонена());
				Если Оплачен Или Отменен Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			РезультатПроверки = ОпределитьСтатусОплаты(
				ПараметрыСтатуса,
				ПараметрыИнтеграции,
				ИдентификаторОперацииПроверкиОплаты());
			
			Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
				ЗаполнитьЗначенияСвойств(
					РезультатОперации,
					РезультатПроверки,
					"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
				РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
				Возврат РезультатОперации;
			КонецЕсли;
			
			Оплачен = (РезультатПроверки.СтатусОплаты = ИдентификаторСтатусаВыполнена());
			Отменен = (РезультатПроверки.СтатусОплаты = ИдентификаторСтатусаОтклонена());
			
		КонецЕсли;
		
		РегистрыСведений.ИдентификаторыОперацийСБП.ЗаписатьСтатусОперации(
			ДокументОплаты,
			РезультатПроверки.ИдентификаторПлатежнойСистемы,
			РезультатПроверки.ДатаОперации,
			РезультатПроверки.СтатусОплаты,
			РезультатПроверки.СуммаОперации,
			,
			ПараметрыСтатуса.ДатаЗапросаСтатуса);
			
		ЗаполнитьЗначенияСвойств(РезультатОперации.ПараметрыОперации, РезультатПроверки);
		РезультатОперации.ПараметрыОперации.ИдентификаторОперации = РезультатПроверки.ИдентификаторПлатежнойСистемы;
		
	КонецЕсли;
	
	Если Оплачен Тогда
		РезультатОперации.СтатусОперации
			= ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполнена();
	ИначеЕсли Отменен Тогда
		РезультатОперации.СтатусОперации
			= ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОтменена();
	Иначе
		РезультатОперации.СтатусОперации
			= ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполняется();
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// См. ИнтеграцияСПлатежнымиСистемами.ВозвратОплаты.
//
Функция ВозвратОплаты(
		ДокументВозврата,
		ОбъектОплаты,
		ПлатежнаяСистема,
		ПараметрыИнтеграции,
		ДополнительныеПараметры) Экспорт
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации();
	РезультатОперации.Вставить("Подтверждение", Неопределено);
	
	ЗаказНаВозврат = ОписаниеЗаказаНаВозврат();
	ИнтеграцияПодсистемБИП.ПриФормированииЗаказаНаВозвратСБП(
		ДокументВозврата,
		ЗаказНаВозврат,
		ПараметрыИнтеграции.ТорговаяТочкаСсылка,
		ДополнительныеПараметры);
	ИнтеграцияСПлатежнымиСистемамиПереопределяемый.ПриФормированииЗаказаНаВозвратСБП(
		ДокументВозврата,
		ЗаказНаВозврат,
		ПараметрыИнтеграции.ТорговаяТочкаСсылка,
		ДополнительныеПараметры);
	
	// Валидация данных заказа на оплату и настроек.
	ПроверитьЗаказНаВозврат(
		ЗаказНаВозврат,
		РезультатОперации);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	ИдентификаторыОперации = Неопределено;
	Если Метаданные.ОпределяемыеТипы.ДокументОперацииБИП.Тип.СодержитТип(ТипЗнч(ОбъектОплаты)) Тогда
		
		ИдентификаторыОперации = РегистрыСведений.ИдентификаторыОперацийСБП.ИдентификаторыОперацииСБП(
			ОбъектОплаты);
		
	ИначеЕсли ТипЗнч(ОбъектОплаты) = Тип("Строка") Тогда
		
		// Переданный идентификатор должен соответствовать формату:
		// {Идентификатор системы}:{Идентификатор СПБ}:{Идентификатор платежной системы}
		ДанныеСтроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			ОбъектОплаты,
			":");
		
		Сч = 0;
		Пока Сч < ДанныеСтроки.Количество() Цикл
			ДанныеСтроки[Сч] = СокрЛП(ДанныеСтроки[Сч]);
			Сч = Сч + 1;
		КонецЦикла;
		
		ПроверитьИдентификаторОбъектаОплаты(ДанныеСтроки, РезультатОперации);
		
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			Возврат РезультатОперации;
		КонецЕсли;
		
		ИдентификаторыОперации = РегистрыСведений.ИдентификаторыОперацийСБП.НовыйИдентификаторыОперацииСБП(
			ДанныеСтроки[1],
			ДанныеСтроки[2]);
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Возврат для продажи не поддерживается.'");
	КонецЕсли;
	
	Если ИдентификаторыОперации = Неопределено
		Или Не ЗначениеЗаполнено(ИдентификаторыОперации.ИдентификаторПлатежнойСистемы)
		Или Не ЗначениеЗаполнено(ИдентификаторыОперации.ИдентификаторОплаты) Тогда
		
		РезультатОперации.СтатусОперации  = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Не обнаружен идентификатор документа оплаты в платежной системе.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Не обнаружен идентификатор документа оплаты в платежной системе.'");
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	// Формирование идентификаторов возврата.
	Идентификатор = ОпределитьИдентификаторЗаказаНаВозврат(
		РезультатОперации,
		ДокументВозврата,
		ЗаказНаВозврат,
		ИдентификаторыОперации.ИдентификаторОплаты,
		ПараметрыИнтеграции);
	
	// Прекратить вызов операции, в случае ошибок.
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	// Сохранить данные заказа для проверки
	// идемпотентности.
	РегистрыСведений.ДанныеОперацийСБП.СохранитьДанныеВозврата(
		Идентификатор,
		ЗаказНаВозврат,
		ДокументВозврата,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	ПараметрыВозврата = Новый Структура;
	ПараметрыВозврата.Вставить("СуммаВозврата",                 ЗаказНаВозврат.СуммаВозврата);
	ПараметрыВозврата.Вставить("ДатаВозврата",                  ЗаказНаВозврат.ДатаВозврата);
	ПараметрыВозврата.Вставить("ИдентификаторСПБ",              ИдентификаторыОперации.ИдентификаторОплаты);
	ПараметрыВозврата.Вставить("ИдентификаторПлатежнойСистемы", ИдентификаторыОперации.ИдентификаторПлатежнойСистемы);
	ПараметрыВозврата.Вставить("ПлатежнаяСистема",              ПлатежнаяСистема);
	ПараметрыВозврата.Вставить("ИдентификаторыОперации",        Идентификатор);
	
	РезультатВозврата = ОперацияВозвратОплаты(
		ПараметрыВозврата,
		ПараметрыИнтеграции);
	
	Если ЗначениеЗаполнено(РезультатВозврата.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				РезультатВозврата,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		Возврат РезультатОперации;
	КонецЕсли;
	
	РегистрыСведений.ИдентификаторыОперацийСБП.ЗаписатьСтатусОперации(
		ДокументВозврата,
		РезультатВозврата.ИдентификаторВозврата,
		РезультатВозврата.ДатаОперации,
		РезультатВозврата.СтатусВозврата,
		РезультатВозврата.СуммаОперации,
		РезультатВозврата.ИдентификаторСессии);
	
	ОбработатьСтатусВозврата(
		РезультатОперации,
		РезультатВозврата.СтатусВозврата);
	
	РезультатОперации.Подтверждение = РезультатВозврата.Подтверждение;
	ЗаполнитьЗначенияСвойств(РезультатОперации.ПараметрыОперации, РезультатВозврата);
	РезультатОперации.ПараметрыОперации.ИдентификаторОперации = РезультатВозврата.ИдентификаторВозврата;
	
	Возврат РезультатОперации;
	
КонецФункции

// См. ИнтеграцияСПлатежнымиСистемами.ПодтвердитьВозврат.
//
Функция ПодтвердитьВозврат(ДокументВозврата, ПараметрыИнтеграции) Экспорт
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации();
	
	ИдентификаторыОперации = РегистрыСведений.ИдентификаторыОперацийСБП.ИдентификаторыОперацииСБП(
		ДокументВозврата);
	ИдентификаторСессии = РегистрыСведений.ИдентификаторыОперацийСБП.ИдентификаторСессии(
		ДокументВозврата);
	
	РезультатПодтверждения = ОперацияПодтверждениеВозврата(
		ИдентификаторыОперации,
		ИдентификаторСессии,
		ПараметрыИнтеграции);
	
	Если ЗначениеЗаполнено(РезультатПодтверждения.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатПодтверждения,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		Возврат РезультатОперации;
	КонецЕсли;
	
	ОбработатьСтатусВозврата(
		РезультатОперации,
		РезультатПодтверждения.СтатусВозврата);
	
	РегистрыСведений.ИдентификаторыОперацийСБП.ЗаписатьСтатусОперации(
		ДокументВозврата,
		РезультатПодтверждения.ИдентификаторВозврата,
		РезультатПодтверждения.ДатаОперации,
		РезультатПодтверждения.СтатусВозврата,
		РезультатПодтверждения.СуммаОперации);
		
	ЗаполнитьЗначенияСвойств(РезультатОперации.ПараметрыОперации, РезультатПодтверждения);
	РезультатОперации.ПараметрыОперации.ИдентификаторОперации = РезультатПодтверждения.ИдентификаторВозврата;
	
	Возврат РезультатОперации;
	
КонецФункции

// См. ИнтеграцияСПлатежнымиСистемами.СтатусВозврата.
//
Функция СтатусВозврата(ДокументВозврата, ПараметрыИнтеграции, ДлительныйВызов) Экспорт
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации();
	
	ПараметрыСтатуса = РегистрыСведений.ИдентификаторыОперацийСБП.ПараметрыОпределенияСтатусаОперации(
		ДокументВозврата);
	
	ОбработатьСтатусВозврата(
		РезультатОперации,
		ПараметрыСтатуса.СтатусОперации);
	
	Если РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполняется() Тогда
		
		Если ДлительныйВызов Тогда
			
			НастройкиВызова = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйИтеративныйВызовОперации();
			Пока ИнтеграцияСПлатежнымиСистемамиСлужебный.ВозможенВызовОперации(НастройкиВызова) Цикл
				
				РезультатПроверки = ОперацияСтатусВозврата(
					ПараметрыСтатуса.ИдентификаторПлатежнойСистемы,
					ПараметрыСтатуса.ДатаЗапросаСтатуса,
					ПараметрыИнтеграции,
					ИдентификаторОперацииПроверкиВозврата());
				
				Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
					ЗаполнитьЗначенияСвойств(
						РезультатОперации,
						РезультатПроверки,
						"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
					РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
					Возврат РезультатОперации;
				КонецЕсли;
				
				Если РезультатПроверки.ОбновитьДатуЗапросаСтатуса Тогда
					ПараметрыСтатуса.ДатаЗапросаСтатуса = ТекущаяДатаСеанса();
				КонецЕсли;
				
				ОбработатьСтатусВозврата(
					РезультатОперации,
					РезультатПроверки.СтатусВозврата);
				
				Если РезультатОперации.СтатусОперации <> ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполняется() Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			РезультатПроверки = ОперацияСтатусВозврата(
				ПараметрыСтатуса.ИдентификаторПлатежнойСистемы,
				ПараметрыСтатуса.ДатаЗапросаСтатуса,
				ПараметрыИнтеграции,
				ИдентификаторОперацииПроверкиВозврата());
			
			Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
				ЗаполнитьЗначенияСвойств(
					РезультатОперации,
					РезультатПроверки,
					"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
				РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
				Возврат РезультатОперации;
			КонецЕсли;
			
			ОбработатьСтатусВозврата(
				РезультатОперации,
				РезультатПроверки.СтатусВозврата);
			
		КонецЕсли;
		
		РегистрыСведений.ИдентификаторыОперацийСБП.ЗаписатьСтатусОперации(
			ДокументВозврата,
			ПараметрыСтатуса.ИдентификаторПлатежнойСистемы,
			РезультатПроверки.ДатаОперации,
			РезультатПроверки.СтатусВозврата,
			РезультатПроверки.СуммаОперации,
			,
			ПараметрыСтатуса.ДатаЗапросаСтатуса);
		
		ЗаполнитьЗначенияСвойств(РезультатОперации.ПараметрыОперации, РезультатПроверки);
		РезультатОперации.ПараметрыОперации.ИдентификаторОперации = ПараметрыСтатуса.ИдентификаторПлатежнойСистемы;
		
	ИначеЕсли РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииТребуетсяПодтверждение() Тогда
		ВызватьИсключение НСтр("ru = 'Не правильный порядок вызова операций.'");
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// См. ИнтеграцияСПлатежнымиСистемамиСлужебный.ОбновлениеНастроекУчастниковСБП.
//
Процедура ОбновлениеНастроекУчастниковСБП() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ВызватьИсключение НСтр("ru = 'Обновление настроек участников СБП в модели сервиса
			|выполняется через поставляемые данные'");
	КонецЕсли;
	
	Настройки = ОперацияНастройкиУчастниковСБП();
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьНастройкиУчастниковСБП(Настройки);
	
КонецПроцедуры

// См. ИнтеграцияСПлатежнымиСистемами.УдалитьИдентификаторыОперации.
//
Процедура УдалитьИдентификаторыОперации(ДокументОперации) Экспорт
	
	Идентификатор = РегистрыСведений.ИдентификаторыОперацийСБП.ИдентификаторОперации(
		ДокументОперации);
	
	Если Идентификатор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		Набор = РегистрыСведений.ИдентификаторыОперацийСБП.СоздатьНаборЗаписей();
		Набор.Отбор.ДокументОперации.Установить(ДокументОперации);
		Набор.Записать();
		
		Набор = РегистрыСведений.ДанныеОперацийСБП.СоздатьНаборЗаписей();
		Набор.Отбор.Идентификатор.Установить(Идентификатор);
		Набор.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		
		ВызватьИсключение НСтр("ru = 'Не удалось удалить данные операции СБП.'");
		
	КонецПопытки;
	
КонецПроцедуры

// См. ИнтеграцияСПлатежнымиСистемами.СтатусыОперацийC2B.
//
Функция СтатусыОперацийC2B() Экспорт
	
	ОбработанныеОперации = Новый Массив;
	ДанныеОпераций = ДанныеОперацийСТерминальнымСтатусом(ОбработанныеОперации);
	
	ОбработатьСтатусыСтатусыОплат(ДанныеОпераций, ОбработанныеОперации);
	ОбработатьСтатусыСтатусыВозвратов(ДанныеОпераций, ОбработанныеОперации);
	
	Возврат ОбработанныеОперации;
	
КонецФункции

#КонецОбласти

#Область СлужебныеОперацииОплат

// Формирует и заполняет новое описание заказа на оплату, определяет внешний идентификатор заказа
// и сохраняет параметры заказа для проверки идемпотентности.
//
// Параметры:
//  РезультатОперации - Структура - результат подготовки см.
//    ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатИдентификаторОплаты
//  ДокументОплаты - ОпределяемыйТип.ДокументОперацииБИП - документ, который отражает
//    оплату в информационной базе;
//  КассоваяСсылка - Строка - идентификатор кассовой ссылки;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//    см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДополнительныеПараметры - Структура, Неопределено - дополнительные настройки формирования
//    заказа на оплату.
//
// Возвращаемое значение:
//  Структура - содержит результат подготовки заказа на оплату:
//    * ЗаказНаОплату - Структура - см. ОписаниеЗаказаНаОплату;
//    * Идентификатор - Строка - внешний идентификатор заказа на оплату.
//
Функция ДанныеЗаказаНаОплату(
		РезультатОперации,
		ДокументОплаты,
		КассоваяСсылка,
		ПараметрыИнтеграции,
		ДополнительныеПараметры)
	
	ДанныеЗаказа = Новый Структура;
	ДанныеЗаказа.Вставить("ЗаказНаОплату", Неопределено);
	ДанныеЗаказа.Вставить("Идентификатор", Неопределено);
	
	ЗаказНаОплату = СформироватьДанныеЗаказаНаОплатуПоДокументуОперации(
		ДокументОплаты,
		ПараметрыИнтеграции.ТорговаяТочкаСсылка,
		ДополнительныеПараметры,
		РезультатОперации);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Возврат ДанныеЗаказа;
	КонецЕсли;
	
	// Формирование идентификаторов оплаты.
	Идентификатор = ОпределитьИдентификаторЗаказаНаОплату(
		РезультатОперации,
		ДокументОплаты,
		ЗаказНаОплату,
		КассоваяСсылка,
		ПараметрыИнтеграции);
	
	// Прекратить вызов операции, в случае ошибок.
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Возврат ДанныеЗаказа;
	КонецЕсли;
	
	// Сохранить данные заказа для проверки
	// идемпотентности.
	РегистрыСведений.ДанныеОперацийСБП.СохранитьДанныеОплаты(
		Идентификатор,
		ЗаказНаОплату,
		КассоваяСсылка,
		ДокументОплаты,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	ДанныеЗаказа.ЗаказНаОплату = ЗаказНаОплату;
	ДанныеЗаказа.Идентификатор = Идентификатор;
	
	Возврат ДанныеЗаказа;
	
КонецФункции

// Формирует и заполняет новое описание заказа на оплату в платежной системе. При проведении оплаты
// проверяется заполнение всех свойств заказа.
//
// Возвращаемое значение:
//  Структура - содержит описание заказа на оплату в платежной системе:
//    * СуммаОплаты - Число - сумма оплаты в платежной системе. Сумма, которую необходимо
//      списать со счета или карты покупателя;
//    * ДатаОплаты - Дата - дата операции продажи;
//    * СрокЖизниQRКода - Число - содержит значение срока действия QR-кода в целых минутах.
//      Минимальное значение - 5 минут, максимальное значение - 129 600 минут
//      (90 дней в минутах). В случае передачи значения не входящего в выше
//      описанный диапазон возвращать ошибку "НеверныйФорматЗапроса".
//      Если значение не предано используется стандартный срок использования СБП.
//    * НазначениеПлатежа - Строка - информация о платеже, которая будет отображена пользователю
//      в момент сканирования QR-кода в мобильном приложении. Рекомендуется
//      делать строку не длинной и включать информацию об организации, которая
//      является получателем денежных средств, например:
//      Оплата СБП 524,00 RUB ООО Ромашка
//      Если строка не заполнена, будет передано стандартное представление
//      назначения: Оплата СБП 524,00 RUB;
//    * ОтложенноеПолучениеСтатуса - Булево - признак загрузки статуса оплаты регламентным заданием.
//
Функция СформироватьДанныеЗаказаНаОплатуПоДокументуОперации(
	ДокументОплаты,
	ТорговаяТочка,
	ДополнительныеПараметры,
	РезультатОперации) Экспорт
	
	ЗаказНаОплату = ОписаниеЗаказаНаОплату();
	ИнтеграцияПодсистемБИП.ПриФормированииЗаказаНаОплатуСБП(
		ДокументОплаты,
		ЗаказНаОплату,
		ТорговаяТочка,
		ДополнительныеПараметры);
	ИнтеграцияСПлатежнымиСистемамиПереопределяемый.ПриФормированииЗаказаНаОплатуСБП(
		ДокументОплаты,
		ЗаказНаОплату,
		ТорговаяТочка,
		ДополнительныеПараметры);
	
	// Валидация данных заказа на оплату и настроек.
	ПроверитьЗаказНаОплату(
		ЗаказНаОплату,
		РезультатОперации);
		
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Возврат ЗаказНаОплату;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЗаказНаОплату.НазначениеПлатежа) Тогда
		ЗаказНаОплату.НазначениеПлатежа = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оплата СБП %1'")
			+ " "
			+ ИдентификаторВалютыРоссийскийРубль(),
			ПреобразоватьЧислоВФорматJSON(ЗаказНаОплату.СуммаОплаты));
	ИначеЕсли СтрДлина(ЗаказНаОплату.НазначениеПлатежа) > 140 Тогда
		ЗаказНаОплату.НазначениеПлатежа = Лев(ЗаказНаОплату.НазначениеПлатежа, 140);
	КонецЕсли;
	
	Возврат ЗаказНаОплату;
	
КонецФункции

// Формирует новое описание заказа на оплату в платежной системе. При проведении оплаты
// проверяется заполнение всех свойств заказа.
//
// Возвращаемое значение:
//  Структура - содержит описание заказа на оплату в платежной системе:
//    * СуммаОплаты - Число - сумма оплаты в платежной системе. Сумма, которую необходимо
//      списать со счета или карты покупателя;
//    * ДатаОплаты - Дата - дата операции продажи;
//    * СрокЖизниQRКода - Число - содержит значение срока действия QR-кода в целых минутах.
//      Минимальное значение - 5 минут, максимальное значение - 129 600 минут
//      (90 дней в минутах). В случае передачи значения не входящего в выше
//      описанный диапазон возвращать ошибку "НеверныйФорматЗапроса".
//      Если значение не предано используется стандартный срок использования СБП.
//    * НазначениеПлатежа - Строка - информация о платеже, которая будет отображена пользователю
//      в момент сканирования QR-кода в мобильном приложении. Рекомендуется
//      делать строку не длинной и включать информацию об организации, которая
//      является получателем денежных средств, например:
//      Оплата СБП 524,00 RUB ООО Ромашка
//      Если строка не заполнена, будет передано стандартное представление
//      назначения: Оплата СБП 524,00 RUB;
//    * ОтложенноеПолучениеСтатуса - Булево - признак загрузки статуса оплаты регламентным заданием.
//
Функция ОписаниеЗаказаНаОплату()
	
	ЗаказаНаОплату = Новый Структура;
	ЗаказаНаОплату.Вставить("ДатаОплаты", Дата(1, 1, 1));
	ЗаказаНаОплату.Вставить("СуммаОплаты", 0);
	ЗаказаНаОплату.Вставить("СрокЖизниQRКода", Неопределено);
	ЗаказаНаОплату.Вставить("НазначениеПлатежа", "");
	ЗаказаНаОплату.Вставить("ОтложенноеПолучениеСтатуса", Ложь);
	
	Возврат ЗаказаНаОплату;
	
КонецФункции

// Формирует новый идентификатор заказа на оплату или получает существующий.
//
// Параметры:
//  РезультатОперации - Структура  - см. НовыйРезультатИдентификаторОплаты;
//  ДокументОплаты - ДокументСсылка - документ, который отражает
//                   оплату в информационной базе;
//  ЗаказНаОплату - Структура - описание заказа на оплату см. функцию ОписаниеЗаказаНаОплату;
//  КассоваяСсылка - Строка - идентификатор кассовой ссылки;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ПараметрыИнтеграции.
//
// Возвращаемое значение:
//  Строка - внешний идентификатор документа оплаты;
//
Функция ОпределитьИдентификаторЗаказаНаОплату(
		РезультатОперации,
		ДокументОплаты,
		ЗаказНаОплату,
		КассоваяСсылка,
		ПараметрыИнтеграции)
	
	Идентификатор = РегистрыСведений.ИдентификаторыОперацийСБП.ИдентификаторОперации(
		ДокументОплаты);
	
	// Первая итерация, дополнительных проверок не требуется.
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат РегистрыСведений.ИдентификаторыОперацийСБП.НовыйИдентификаторОперации(
			ДокументОплаты,
			ПараметрыИнтеграции.ТорговаяТочка,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			,
			Истина,
			ЗаказНаОплату.ОтложенноеПолучениеСтатуса);
	КонецЕсли;
	
	ПараметрыСтатуса = РегистрыСведений.ИдентификаторыОперацийСБП.ПараметрыОпределенияСтатусаОперации(
		ДокументОплаты);
	
	// Предыдущая операция генерации завершилась ошибкой,
	// необходимо запросить новый QR-код.
	Если Не ЗначениеЗаполнено(ПараметрыСтатуса.ИдентификаторОплаты) Тогда
		Возврат РегистрыСведений.ИдентификаторыОперацийСБП.НовыйИдентификаторОперации(
			ДокументОплаты,
			ПараметрыИнтеграции.ТорговаяТочка,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			,
			Ложь,
			ЗаказНаОплату.ОтложенноеПолучениеСтатуса);
	КонецЕсли;
	
	АнализИсторическихДанных = АнализИсторическихДанныхОплаты(
		ДокументОплаты,
		ЗаказНаОплату,
		КассоваяСсылка,
		Идентификатор,
		ПараметрыИнтеграции);
	
	// Если по документу ранее был сформирован возврат, делать по нему оплату запрещено.
	// Необходимо получить статус возврата, а для оплаты сформировать новый документ.
	Если Не АнализИсторическихДанных.Оплата И ЗначениеЗаполнено(ПараметрыСтатуса.ИдентификаторПлатежнойСистемы) Тогда
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Для документа ранее был сформирован возврат оплаты, создание новой оплаты запрещено.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Для документа ранее был сформирован возврат оплаты, создание новой оплаты запрещено.'");
		Возврат Неопределено;
	КонецЕсли;
	
	// Если параметры заказа на оплату не изменились
	// можно выполнить повторный запрос, т.к. операция
	// создания заказа на оплату поддерживает идемпотентность.
	Если АнализИсторическихДанных.ДанныеИдентичны Тогда
		
		Возврат Идентификатор;
		
	Иначе
		
		ИсторическиеПараметрыИнтеграции = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции(
			АнализИсторическихДанных.НастройкаИнтеграции);
		
		// Т.к. у СБП на текущий момент отсутствует операция
		// аннулирования QR-кода перед генерацией нового
		// следует проверить оплату предыдущего.
		ПараметрыСтатуса.ДатаЗапросаСтатуса = Дата(1, 1, 1);
		РезультатПроверки = ОпределитьСтатусОплаты(
			ПараметрыСтатуса,
			ИсторическиеПараметрыИнтеграции,
			ИдентификаторОперацииПроверкиОплаты());
		
		Если РезультатПроверки.СтатусОплаты = ИдентификаторСтатусаВыполнена() Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиУжеОплачен();
			РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Оплата по ранее сформированному QR-коду уже выполнена, создание нового запрещено.'");
			РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Оплата по ранее сформированному QR-коду уже выполнена, создание нового запрещено.'");
			Возврат Неопределено;
		КонецЕсли;
	
		Возврат РегистрыСведений.ИдентификаторыОперацийСБП.НовыйИдентификаторОперации(
			ДокументОплаты,
			ПараметрыИнтеграции.ТорговаяТочка,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			,
			Ложь,
			ЗаказНаОплату.ОтложенноеПолучениеСтатуса);
		
	КонецЕсли;
	
КонецФункции

// Формирует новый идентификатор заказа на возврат или получает существующий.
//
// Параметры:
//  РезультатОперации - Структура  - см. НовыйРезультатИдентификаторОплаты;
//  ДокументОплаты - ДокументСсылка - документ, который отражает
//                   оплату в информационной базе;
//  ЗаказНаВозврат - Структура - описание заказа на оплату см. функцию ОписаниеЗаказаНаВозврат;
//  ИдентификаторОплаты - Строка - идентификатор оплаты по которой производится возврат;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ПараметрыИнтеграции.
//
// Возвращаемое значение:
//  Строка - внешний идентификатор документа возврата;
//
Функция ОпределитьИдентификаторЗаказаНаВозврат(
		РезультатОперации,
		ДокументВозврата,
		ЗаказНаВозврат,
		ИдентификаторОплаты,
		ПараметрыИнтеграции)
	
	Идентификатор = РегистрыСведений.ИдентификаторыОперацийСБП.ИдентификаторОперации(
		ДокументВозврата);
	
	// Первая итерация, дополнительных проверок не требуется.
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат РегистрыСведений.ИдентификаторыОперацийСБП.НовыйИдентификаторОперации(
			ДокументВозврата,
			ПараметрыИнтеграции.ТорговаяТочка,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			ИдентификаторОплаты);
	КонецЕсли;
	
	ПараметрыСтатуса = РегистрыСведений.ИдентификаторыОперацийСБП.ПараметрыОпределенияСтатусаОперации(
		ДокументВозврата);
	
	// Если по возврату уже получен отрицательный терминальный статус
	// можно сгенерировать новый идентификатор операции.
	Если ПараметрыСтатуса.СтатусОперации = ИдентификаторСтатусаНеОплачен()
		Или ПараметрыСтатуса.СтатусОперации = ИдентификаторСтатусаНетИнформации()
		Или ПараметрыСтатуса.СтатусОперации = ИдентификаторСтатусаОтклонена()
		Или ПараметрыСтатуса.СтатусОперации = ИдентификаторСтатусаПревышение() Тогда
		Возврат РегистрыСведений.ИдентификаторыОперацийСБП.НовыйИдентификаторОперации(
			ДокументВозврата,
			ПараметрыИнтеграции.ТорговаяТочка,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			ИдентификаторОплаты);
	КонецЕсли;
	
	ИсторическиеДанные = РегистрыСведений.ДанныеОперацийСБП.ДанныеОперацииСБППоИдентификатору(
		Идентификатор);
	
	// Если по документу ранее был сформирован идентификатор оплаты, делать по нему возврат запрещено.
	// Необходимо получить статус оплаты, а для возврата сформировать новый документ.
	Если ИсторическиеДанные.Оплата И ЗначениеЗаполнено(ПараметрыСтатуса.ИдентификаторОплаты) Тогда
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Для документа ранее был сформирован идентификатор оплаты, создание возврата запрещено.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Для документа ранее был сформирован идентификатор оплаты, создание возврата запрещено.'");
		Возврат Неопределено;
	КонецЕсли;
	
	// Если параметры заказа на возврат не изменились
	// можно выполнить повторный запрос, т.к. операция
	// создания заказа на возврат поддерживает идемпотентность.
	Если ИсторическиеДанные <> Неопределено
		И ИсторическиеДанные.ДатаОперации = ЗаказНаВозврат.ДатаВозврата
		И ИсторическиеДанные.СуммаОперации = ЗаказНаВозврат.СуммаВозврата
		И ИсторическиеДанные.ТорговаяТочка = ПараметрыИнтеграции.ТорговаяТочка
		И ИсторическиеДанные.ДокументОперации = ДокументВозврата Тогда
		
		Возврат Идентификатор;
		
	Иначе
		
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Для документа уже сформирован возврат со значениями суммы и даты, отличающимися от текущего документа. Создание возврата запрещено.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Для документа уже сформирован возврат со значениями суммы и даты, отличающимися от текущего документа. Создание возврата запрещено.'");
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Определяет изменение заказа на оплату перед формированием платежной ссылки.
//
// Параметры:
//  ДокументОплаты - ОпределяемыйТип.ДокументОперацииБИП - документ, который отражает
//    оплату в информационной базе;
//  ЗаказНаОплату - Структура - см. ОписаниеЗаказаНаОплату;
//  КассоваяСсылка - Строка - идентификатор кассовой ссылки;
//  Идентификатор - Строка - внешний идентификатор заказа на оплату;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//    см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//
// Возвращаемое значение:
//  Структура - содержит результат подготовки заказа на оплату:
//    * Оплата - Булево - признак оплаты;
//    * НастройкаИнтеграции - СправочникСсылка.НастройкиИнтеграцииСПлатежнымиСистемами - сохраненная настройка;
//    * ДанныеИдентичны - Булево - признак идентичности заказов;
//
Функция АнализИсторическихДанныхОплаты(
		ДокументОплаты,
		ЗаказНаОплату,
		КассоваяСсылка,
		Идентификатор,
		ПараметрыИнтеграции) Экспорт
	
	ИсторическиеДанные = РегистрыСведений.ДанныеОперацийСБП.ДанныеОперацииСБППоИдентификатору(
		Идентификатор);
	
	ДанныеАнализаИсторическихДанныхОплаты = Новый Структура;
	ДанныеАнализаИсторическихДанныхОплаты.Вставить("Оплата",              ИсторическиеДанные.Оплата);
	ДанныеАнализаИсторическихДанныхОплаты.Вставить("НастройкаИнтеграции", ИсторическиеДанные.НастройкаИнтеграции);
	
	Если ИсторическиеДанные <> Неопределено
		И ИсторическиеДанные.ДатаОперации = ЗаказНаОплату.ДатаОплаты
		И ИсторическиеДанные.СуммаОперации = ЗаказНаОплату.СуммаОплаты
		И ИсторическиеДанные.НазначениеПлатежа = ЗаказНаОплату.НазначениеПлатежа
		И ИсторическиеДанные.ТорговаяТочка = ПараметрыИнтеграции.ТорговаяТочка
		И ИсторическиеДанные.ДокументОперации = ДокументОплаты
		И ИсторическиеДанные.КассоваяСсылка = КассоваяСсылка Тогда
		
		ДанныеАнализаИсторическихДанныхОплаты.Вставить("ДанныеИдентичны", Истина);
		
	Иначе
		
		ДанныеАнализаИсторическихДанныхОплаты.Вставить("ДанныеИдентичны", Ложь);
		
	КонецЕсли;
	
	Возврат ДанныеАнализаИсторическихДанныхОплаты;
	
КонецФункции

// Производит валидацию параметров заказа на оплату в платежной системе.
//
// Параметры:
//  ЗаказНаОплату - Структура - данные для оплаты в платежной системе.
//  РезультатОперации  - Структура - результат проверки заказа на оплату в платежной системе:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим методом:
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//
Процедура ПроверитьЗаказНаОплату(
		ЗаказНаОплату,
		РезультатОперации)
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(ЗаказНаОплату.СуммаОплаты)
		Или Не ЗначениеЗаполнено(ЗаказНаОплату.ДатаОплаты) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Некорректно сформирован заказ на оплату для платежной системы. Обратитесь к администратору.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'При проверке заказа на оплату в платежной системе возникли ошибки.'");
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
	КонецЕсли;
	
КонецПроцедуры

// Производит проверку идентификатора оплаты.
//
// Параметры:
//  ДанныеСтроки - Строка - идентификатор оплаты;
//  РезультатОперации - Структура - см. ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации.
//
Процедура ПроверитьИдентификаторОбъектаОплаты(ДанныеСтроки, РезультатОперации)
	
	Если ДанныеСтроки.Количество() <> 3 Тогда
		
		РезультатОперации.СтатусОперации  = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Идентификатор оплаты в платежной системе не соответствует формату.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Идентификатор оплаты в платежной системе не соответствует формату.'");
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
	ИначеЕсли НРег(ДанныеСтроки[0]) <> НРег(ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП()) Тогда
		
		РезультатОперации.СтатусОперации  = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Идентификатор системы отличается от СБП.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Идентификатор системы отличается от СБП.'");
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
	ИначеЕсли Не ЗначениеЗаполнено(ДанныеСтроки[1]) Тогда
		
		РезультатОперации.СтатусОперации  = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Идентификатор QR-кода не заполнен.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Идентификатор QR-кода не заполнен.'");
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
	ИначеЕсли Не ЗначениеЗаполнено(ДанныеСтроки[2]) Тогда
		
		РезультатОперации.СтатусОперации  = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Идентификатор оплаты не заполнен.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Идентификатор оплаты не заполнен.'");
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет статус заказа на оплату платежной системе.
//
// Параметры:
//  ПараметрыСтатуса - Структура - см. РегистрыСведений.ИдентификаторыОперацийСБП.ПараметрыОпределенияСтатусаОперации
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//    см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ИдентификаторОперацииПроверки - Строка - идентификатор операции проверки наличия статуса;
//  СвойствоИдентификаторОплаты - Строка - свойство в ПараметрыСтатуса, в котором хранится идентификатор.
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на оплату в платежной системе:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом;
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    * СтатусОплаты - Строка - состояние оплаты в платежной системе;
//    * ИдентификаторПлатежнойСистемы - Строка - идентификатор оплаты платежной системы;
//    * ДатаОперации - Дата - дата регистрации операции в платежной системе;
//    * СуммаОперации - Число - фактическая суммы оплаты по документу.
//
Функция ОпределитьСтатусОплаты(
		ПараметрыСтатуса,
		ПараметрыИнтеграции,
		ИдентификаторОперацииПроверки,
		ЗапросНаличияСтатуса = Истина,
		СвойствоИдентификаторОплаты = "ИдентификаторОплаты")
	
	// Параметры активации могут быть только у кассовых ссылок,
	// поэтому, если они не заполнены выполняется проверка статуса
	// динамической ссылки.
	Если Не ЗначениеЗаполнено(ПараметрыСтатуса.ПараметрыАктивации) Тогда
		Возврат ОперацияСтатусОплатыДинамическойСсылки(
			ПараметрыСтатуса[СвойствоИдентификаторОплаты],
			ПараметрыСтатуса.ДатаЗапросаСтатуса,
			ПараметрыИнтеграции,
			ИдентификаторОперацииПроверки,
			ЗапросНаличияСтатуса);
	Иначе
		Возврат ОперацияСтатусОплатыКассовойСсылки(
			ПараметрыСтатуса[СвойствоИдентификаторОплаты],
			ПараметрыСтатуса.ПараметрыАктивации,
			ПараметрыСтатуса.ДатаЗапросаСтатуса,
			ПараметрыИнтеграции,
			ИдентификаторОперацииПроверки,
			ЗапросНаличияСтатуса);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеОперацииВозвратов

// Формирует новое описание заказа на возврат в платежной системе. При проведении возврата
// проверяется заполнение всех свойств.
//
// Возвращаемое значение:
//  Структура - содержит описание заказа на оплату в платежной системе:
//    *СуммаВозврата - Число - сумма возврата в платежной системе. Сумма, которую необходимо
//                   списать со счета или карты покупателя;
//    *ДатаВозврата - Дата - дата операции возврата.
//
Функция ОписаниеЗаказаНаВозврат()
	
	ЗаказаНаОплату = Новый Структура;
	ЗаказаНаОплату.Вставить("ДатаВозврата",  Дата(1, 1, 1));
	ЗаказаНаОплату.Вставить("СуммаВозврата", 0);
	
	Возврат ЗаказаНаОплату;
	
КонецФункции

// Производит валидацию параметров заказа на возврат в платежной системе.
//
// Параметры:
//  ЗаказНаВозврат - Структура - данные для оплаты в платежной системе.
//  РезультатОперации  - Структура - результат создания возврата оплаты в платежной системе:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим методом:
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//
Процедура ПроверитьЗаказНаВозврат(ЗаказНаВозврат, РезультатОперации)
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(ЗаказНаВозврат.СуммаВозврата)
		Или Не ЗначениеЗаполнено(ЗаказНаВозврат.ДатаВозврата) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Некорректно сформирован заказ на возврат для платежной системы. Обратитесь к администратору.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'При проверке заказа на возврат в платежной системе возникли ошибки.'");
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет результат операции на основании статуса операции возврата.
//
// Параметры:
//  СтатусВозврата - Строка - статус возврата СБП;
//  РезультатОперации - Структура - см. ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации.
//
Процедура ОбработатьСтатусВозврата(РезультатОперации, СтатусВозврата)
	
	Если СтатусВозврата = ИдентификаторСтатусаПревышение() Тогда
		РезультатОперации.СтатусОперации     = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Превышена допустимая сумма возврата по заказу на оплату.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Превышена допустимая сумма возврата по заказу на оплату.'");
	ИначеЕсли СтатусВозврата = ИдентификаторСтатусаНеОплачен() Тогда
		РезультатОперации.СтатусОперации     = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Оплата в платежной системе не выполнена, возврат невозможен.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Оплата в платежной системе не выполнена, возврат невозможен.'");
	ИначеЕсли СтатусВозврата = ИдентификаторСтатусаНетИнформации() Тогда
		РезультатОперации.СтатусОперации     = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Оплата в платежной системе не обнаружена, возврат невозможен.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Оплата в платежной системе не обнаружена, возврат невозможен.'");
	ИначеЕсли СтатусВозврата = ИдентификаторСтатусаОтменена() Тогда
		РезультатОперации.СтатусОперации     = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОтменена();
		РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеизвестнаяОшибка();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Операция отменена по неизвестной причине, повторите операцию или обратитесь к администратору.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Операция отменена по неизвестной причине, дополнительная информация об отмене записана в журнал регистрации.'");
	ИначеЕсли СтатусВозврата = ИдентификаторСтатусаВыполнена() Тогда
		РезультатОперации.СтатусОперации  = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполнена();
	ИначеЕсли СтатусВозврата = ИдентификаторСтатусаВПроцессе() Тогда
		РезультатОперации.СтатусОперации  = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполняется();
	ИначеЕсли СтатусВозврата = ИдентификаторСтатусаТребуетсяПодтверждение() Тогда
		РезультатОперации.СтатусОперации  = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииТребуетсяПодтверждение();
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не известный статус возврата %1'"),
			СтатусВозврата);
	КонецЕсли;
	
	Если РезультатОперации.СтатусОперации  = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка() Тогда
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеОперацииСверкиВзаиморасчетов

// Заполняет результат операции на основании статуса отчета.
//
// Параметры:
//  РезультатОперации - Структура - см. ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации;
//  СтатусОтчета - Строка - статус формирования отчета;
//  ТипОшибки - Строка - вид ошибки формирования отчета.
//
Процедура ОбработатьСтатусОтчета(РезультатОперации, СтатусОтчета, ТипОшибки)
	
	Если СтатусОтчета = ИдентификаторСтатусаВыполнена() Тогда
		РезультатОперации.СтатусОперации  = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполнена();
	ИначеЕсли СтатусОтчета = ИдентификаторСтатусаОжидание() Тогда
		РезультатОперации.СтатусОперации  = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполняется();
	ИначеЕсли СтатусОтчета = ИдентификаторСтатусаОшибка() Тогда
		РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
		Если ТипОшибки = ИдентификаторСтатусаПревышенПериод() Тогда
			РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
			РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Превышен максимально допустимый период отчета. Измените даты отчета и повторите запрос.'");
			РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Превышен максимально допустимый период отчета. Измените даты отчета и повторите запрос.'");
		Иначе
			РезультатОперации.КодОшибки          = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеизвестнаяОшибка();
			РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Отчет по сверке оборотов не сформирован.'");
			РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Отчет по сверке оборотов не сформирован.'");
		КонецЕсли;
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не известный статус возврата %1'"),
			СтатусОтчета);
	КонецЕсли;
	
	Если РезультатОперации.СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка() Тогда
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеОперацииОтложенногоПолученияСтатусов

// Определяет операции по котором необходимо выполнить
// получение статуса в сервисе участника СБП.
//
// Параметры:
//  ОбработанныеОперации - Массив из ОпределяемыйТип.ДокументОперацииБИП - данные обработанных документов.
//
// Возвращаемое значение:
//  Массив - данные операция, по которым необходимо загрузить статус.
//
Функция ДанныеОперацийСТерминальнымСтатусом(ОбработанныеОперации)
	
	ОтложенныеОперации = РегистрыСведений.ИдентификаторыОперацийСБП.ОтложенныеОперации();
	
	ДанныеОпераций = Новый Массив;
	
	ИдентификаторыДинамическихСсылок = НовыйИдентификаторыПроверкиНаличияСтатуса();
	ИдентификаторыКассовыхСсылок = НовыйИдентификаторыПроверкиНаличияСтатусаКассовыхСсылок();
	ИдентификаторыВозвратов = НовыйИдентификаторыПроверкиНаличияСтатуса();
	
	Для Каждого КлючЗначение Из ОтложенныеОперации Цикл
		
		ОперацииДляОбработки = Новый Массив;
		ДатаЗапросаСтатуса = ТекущаяДатаСеанса();
		
		Для Каждого Операция Из КлючЗначение.Значение Цикл
			
			// Если операция уже в терминальном статусе, выполнять запрос
			// не имеет смысла, необходимо выполнить обработку в прикладной
			// логике и перейти к следующей.
			Если Операция.СтатусОперации <> ИдентификаторСтатусаВПроцессе() Тогда
				ПриЗагрузкеСтатусаОперации(
					Операция.ДокументОперации,
					Операция.ПараметрыОперации,
					Операция.СтатусОперации,
					"",
					ОбработанныеОперации);
				Продолжить;
			КонецЕсли;
			
			Если Операция.Оплата Тогда
				
				// Если заполнены параметры активации, это кассовая ссылка,
				// она обрабатывается особым способом.
				Если ЗначениеЗаполнено(Операция.ПараметрыАктивации) Тогда
					ДанныеОплаты = ИдентификаторыКассовыхСсылок.Добавить();
					ДанныеОплаты.ПараметрыАктивации = Операция.ПараметрыАктивации;
				Иначе
					ДанныеОплаты = ИдентификаторыДинамическихСсылок.Добавить();
				КонецЕсли;
				
				ДанныеОплаты.Идентификатор = Операция.ИдентификаторОплаты;
				
				ДанныеОплаты.ДатаЗапросаСтатуса = ?(Не ЗначениеЗаполнено(Операция.ДатаЗапросаСтатуса),
					ДатаЗапросаСтатуса,
					Операция.ДатаЗапросаСтатуса);
				
				Если ИдентификаторыДинамическихСсылок.Количество() > 19 Тогда
					
					РезультатПроверки = ОперацияПроверкаНаличияСтатусаОперацийОплат(
						ИдентификаторыДинамическихСсылок,
						КлючЗначение.Ключ,
						ИдентификаторОперацииПроверкиДлительнойОплаты());
					
					Если Не ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
						ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
							ОперацииДляОбработки,
							РезультатПроверки.Идентификаторы);
					КонецЕсли;
					
					ИдентификаторыДинамическихСсылок.Очистить();
					
				КонецЕсли;
				
				Если ИдентификаторыКассовыхСсылок.Количество() > 19 Тогда
					
					РезультатПроверки = ОперацияПроверкаНаличияСтатусаКассовыхСсылок(
						ИдентификаторыКассовыхСсылок,
						КлючЗначение.Ключ,
						ИдентификаторОперацииПроверкиДлительнойОплаты());
					
					Если Не ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
						ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
							ОперацииДляОбработки,
							РезультатПроверки.Идентификаторы);
					КонецЕсли;
					
					ИдентификаторыКассовыхСсылок.Очистить();
					
				КонецЕсли;
				
			Иначе
				
				ДанныеВозврата = ИдентификаторыВозвратов.Добавить();
				ДанныеВозврата.Идентификатор = Операция.ИдентификаторПлатежнойСистемы;
				
				ДанныеВозврата.ДатаЗапросаСтатуса = ?(Не ЗначениеЗаполнено(Операция.ДатаЗапросаСтатуса),
					ДатаЗапросаСтатуса,
					Операция.ДатаЗапросаСтатуса);
				
				Если ИдентификаторыВозвратов.Количество() > 19 Тогда
					
					РезультатПроверки = ОперацияПроверкаНаличияСтатусаОперацийВозвратов(
						ИдентификаторыВозвратов,
						КлючЗначение.Ключ,
						ИдентификаторОперацииПроверкиДлительногоВозврата());
					
					Если Не ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
						ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
							ОперацииДляОбработки,
							РезультатПроверки.Идентификаторы);
					КонецЕсли;
					
					ИдентификаторыВозвратов.Очистить();
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ИдентификаторыДинамическихСсылок.Количество() <> 0 Тогда
			
			РезультатПроверки = ОперацияПроверкаНаличияСтатусаОперацийОплат(
				ИдентификаторыДинамическихСсылок,
				КлючЗначение.Ключ,
				ИдентификаторОперацииПроверкиДлительнойОплаты());
			
			Если Не ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
					ОперацииДляОбработки,
					РезультатПроверки.Идентификаторы);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ИдентификаторыКассовыхСсылок.Количество() <> 0 Тогда
			
			РезультатПроверки = ОперацияПроверкаНаличияСтатусаКассовыхСсылок(
				ИдентификаторыКассовыхСсылок,
				КлючЗначение.Ключ,
				ИдентификаторОперацииПроверкиДлительнойОплаты());
			
			Если Не ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
					ОперацииДляОбработки,
					РезультатПроверки.Идентификаторы);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ИдентификаторыВозвратов.Количество() <> 0 Тогда
			
			РезультатПроверки = ОперацияПроверкаНаличияСтатусаОперацийВозвратов(
				ИдентификаторыВозвратов,
				КлючЗначение.Ключ,
				ИдентификаторОперацииПроверкиДлительногоВозврата());
			
			Если Не ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
					ОперацииДляОбработки,
					РезультатПроверки.Идентификаторы);
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого Операция Из КлючЗначение.Значение Цикл
			
			// Если операция уже в терминальном статусе, выполнять запрос
			// не имеет смысла.
			Если Операция.СтатусОперации <> ИдентификаторСтатусаВПроцессе() Тогда
				Продолжить;
			КонецЕсли;
			
			Если Операция.Оплата Тогда
				
				// Если заполнены параметры активации, это кассовая ссылка,
				// она обрабатывается особым способом.
				Если ЗначениеЗаполнено(Операция.ПараметрыАктивации) Тогда
					ЗапроситьСтатус = (ОперацииДляОбработки.Найти(Операция.ПараметрыАктивации) <> Неопределено);
				Иначе
					ЗапроситьСтатус = (ОперацииДляОбработки.Найти(Операция.ИдентификаторОплаты) <> Неопределено);
				КонецЕсли;
				
				ИдентификаторОперации = Операция.ИдентификаторОплаты;
				
			Иначе
				ЗапроситьСтатус = (ОперацииДляОбработки.Найти(Операция.ИдентификаторПлатежнойСистемы)  <> Неопределено);
				ИдентификаторОперации = Операция.ИдентификаторПлатежнойСистемы;
			КонецЕсли;
			
			Если ЗапроситьСтатус Тогда
				
				// Формирование данных для запроса статуса статуса.
				Операция.ДатаЗапросаСтатуса = ДатаЗапросаСтатуса;
				
				ОтложеннаяОперация = Новый Структура;
				ОтложеннаяОперация.Вставить("ДокументОперации", Операция.ДокументОперации);
				ОтложеннаяОперация.Вставить("НастройкаИнтеграции", Операция.НастройкаИнтеграции);
				ОтложеннаяОперация.Вставить("Идентификатор", ИдентификаторОперации);
				ОтложеннаяОперация.Вставить("ПериодИспользования", Операция.ПериодИспользования);
				ОтложеннаяОперация.Вставить("Оплата", Операция.Оплата);
				ОтложеннаяОперация.Вставить("ЗапроситьСтатус", ЗапроситьСтатус);
				ОтложеннаяОперация.Вставить("ДатаЗапросаСтатуса", ДатаЗапросаСтатуса);
				ОтложеннаяОперация.Вставить("КоличествоПопыток", Операция.КоличествоПопыток);
				ОтложеннаяОперация.Вставить("ПараметрыАктивации", Операция.ПараметрыАктивации);
				
				ДанныеОпераций.Добавить(ОтложеннаяОперация);
				
			ИначеЕсли Не ЗначениеЗаполнено(Операция.ДатаЗапросаСтатуса) Тогда
				
				// Для операций, по которым не требуется запрашивать статус,
				// обновляем дату запроса статуса.
				РегистрыСведений.ИдентификаторыОперацийСБП.ЗаписатьСтатусОперации(
					Операция.ДокументОперации,
					Операция.ИдентификаторПлатежнойСистемы,
					Дата(1, 1, 1),
					Операция.СтатусОперации,
					,
					,
					ДатаЗапросаСтатуса);
				
			ИначеЕсли Операция.ПериодИспользования < (ТекущаяДатаСеанса() + 600) Тогда
				
				ПриЗагрузкеСтатусаОперации(
					Операция.ДокументОперации,
					Операция.ПараметрыОперации,
					ИдентификаторСтатусаОтклонена(),
					"",
					ОбработанныеОперации);
				Продолжить;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ДанныеОпераций;
	
КонецФункции

// Загружает статусы оплат операций.
//
// Параметры:
//  ОтложенныеОперации  - Соответствие - см. СервисИнтеграцииССБП.ДанныеОперацийСТерминальнымСтатусом;
//  ОбработанныеОперации - Массив из ОпределяемыйТип.ДокументОперацииБИП - данные обработанных документов.
//
Процедура ОбработатьСтатусыСтатусыОплат(ДанныеОпераций, ОбработанныеОперации)
	
	НастройкиИнтеграции = Новый Массив;
	Для Каждого ОтложеннаяОперация Из ДанныеОпераций Цикл
		
		Если Не ОтложеннаяОперация.Оплата Тогда
			Продолжить;
		КонецЕсли;
		
		НастройкиИнтеграции.Добавить(ОтложеннаяОперация.НастройкаИнтеграции);
		
	КонецЦикла;
	
	ПараметрыНастроекИнтеграции = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыНастроекИнтеграции(
		НастройкиИнтеграции);
	
	Для Каждого ОтложеннаяОперация Из ДанныеОпераций Цикл
		
		Если Не ОтложеннаяОперация.Оплата Тогда
			Продолжить;
		КонецЕсли;
		
		РезультатПроверки = ОпределитьСтатусОплаты(
			ОтложеннаяОперация,
			ПараметрыНастроекИнтеграции.Получить(ОтложеннаяОперация.НастройкаИнтеграции),
			ИдентификаторОперацииПроверкиДлительнойОплаты(),
			Ложь,
			"Идентификатор");
		
		Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
			РегистрыСведений.ИдентификаторыОперацийСБП.УвеличитьКоличествоПопытокЗапросаСтатуса(
				ОтложеннаяОперация.ДокументОперации);
			Если ОтложеннаяОперация.КоличествоПопыток >= РегистрыСведений.ИдентификаторыОперацийСБП.МаксимальноеКоличествоПопытокЗапросаСтатуса(Истина) Тогда
				ПриЗагрузкеСтатусаОперации(
					ОтложеннаяОперация.ДокументОперации,
					ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйОписаниеПараметровОперации(),
					ИдентификаторСтатусаОшибка(),
					РезультатПроверки.СообщениеОбОшибке,
					ОбработанныеОперации);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Если РезультатПроверки.СтатусОплаты = ИдентификаторСтатусаВПроцессе()
			И (ОтложеннаяОперация.ПериодИспользования + 600) < ТекущаяДатаСеанса() Тогда
			РезультатПроверки.СтатусОплаты = ИдентификаторСтатусаОтклонена();
		КонецЕсли;
		
		РегистрыСведений.ИдентификаторыОперацийСБП.ЗаписатьСтатусОперации(
			ОтложеннаяОперация.ДокументОперации,
			РезультатПроверки.ИдентификаторПлатежнойСистемы,
			РезультатПроверки.ДатаОперации,
			РезультатПроверки.СтатусОплаты,
			РезультатПроверки.СуммаОперации,
			,
			ОтложеннаяОперация.ДатаЗапросаСтатуса);
		
		Если РезультатПроверки.СтатусОплаты = ИдентификаторСтатусаВыполнена()
			Или РезультатПроверки.СтатусОплаты = ИдентификаторСтатусаОтклонена() Тогда
			
			ПараметрыОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйОписаниеПараметровОперации();
			ЗаполнитьЗначенияСвойств(ПараметрыОперации, РезультатПроверки);
			ПараметрыОперации.ИдентификаторОперации = РезультатПроверки.ИдентификаторПлатежнойСистемы;
			
			ПриЗагрузкеСтатусаОперации(
				ОтложеннаяОперация.ДокументОперации,
				ПараметрыОперации,
				РезультатПроверки.СтатусОплаты,
				РезультатПроверки.СообщениеОбОшибке,
				ОбработанныеОперации);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Загружает статусы возвратов операций.
//
// Параметры:
//  ОтложенныеОперации  - Соответствие - см. СервисИнтеграцииССБП.ДанныеОперацийСТерминальнымСтатусом;
//  ОбработанныеОперации - Массив из ОпределяемыйТип.ДокументОперацииБИП - данные обработанных документов.
//
Процедура ОбработатьСтатусыСтатусыВозвратов(ДанныеОпераций, ОбработанныеОперации)
	
	НастройкиИнтеграции = Новый Массив;
	Для Каждого ОтложеннаяОперация Из ДанныеОпераций Цикл
		
		Если ОтложеннаяОперация.Оплата Тогда
			Продолжить;
		КонецЕсли;
		
		НастройкиИнтеграции.Добавить(ОтложеннаяОперация.НастройкаИнтеграции);
		
	КонецЦикла;
	
	ПараметрыНастроекИнтеграции = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыНастроекИнтеграции(
		НастройкиИнтеграции);
	
	Для Каждого ОтложеннаяОперация Из ДанныеОпераций Цикл
		
		Если ОтложеннаяОперация.Оплата Тогда
			Продолжить;
		КонецЕсли;
		
		РезультатПроверки = ОперацияСтатусВозврата(
			ОтложеннаяОперация.Идентификатор,
			ОтложеннаяОперация.ДатаЗапросаСтатуса,
			ПараметрыНастроекИнтеграции.Получить(ОтложеннаяОперация.НастройкаИнтеграции),
			ИдентификаторОперацииПроверкиДлительногоВозврата(),
			Ложь);
		
		Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
			РегистрыСведений.ИдентификаторыОперацийСБП.УвеличитьКоличествоПопытокЗапросаСтатуса(
				ОтложеннаяОперация.ДокументОперации);
			Если ОтложеннаяОперация.КоличествоПопыток >= РегистрыСведений.ИдентификаторыОперацийСБП.МаксимальноеКоличествоПопытокЗапросаСтатуса(Ложь) Тогда
					ПриЗагрузкеСтатусаОперации(
						ОтложеннаяОперация.ДокументОперации,
						ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйОписаниеПараметровОперации(),
						ИдентификаторСтатусаОшибка(),
						РезультатПроверки.СообщениеОбОшибке,
						ОбработанныеОперации);
				КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		РегистрыСведений.ИдентификаторыОперацийСБП.ЗаписатьСтатусОперации(
			ОтложеннаяОперация.ДокументОперации,
			ОтложеннаяОперация.Идентификатор,
			РезультатПроверки.ДатаОперации,
			РезультатПроверки.СтатусВозврата,
			РезультатПроверки.СуммаОперации,
			,
			ОтложеннаяОперация.ДатаЗапросаСтатуса);
		
		Если РезультатПроверки.СтатусВозврата = ИдентификаторСтатусаВыполнена()
			Или РезультатПроверки.СтатусВозврата = ИдентификаторСтатусаОтменена()
			Или РезультатПроверки.СтатусВозврата = ИдентификаторСтатусаНетИнформации()
			Или РезультатПроверки.СтатусВозврата = ИдентификаторСтатусаПревышение() Тогда
			
			ПараметрыОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйОписаниеПараметровОперации();
			ЗаполнитьЗначенияСвойств(ПараметрыОперации, РезультатПроверки);
			
			ПриЗагрузкеСтатусаОперации(
				ОтложеннаяОперация.ДокументОперации,
				ПараметрыОперации,
				РезультатПроверки.СтатусВозврата,
				РезультатПроверки.СообщениеОбОшибке,
				ОбработанныеОперации);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Определяет алгоритм обработки операций, статус которых был получен регламентным заданием.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииБИП - документ, который отражает
//    операцию в информационной базе;
//  ПараметрыОперации - Структура - дополнительные данные по оплате:
//    * ДатаОперации - Дата - фактическая дата оплаты в UTC;
//    * СуммаОперации - Число - фактическая суммы оплаты по документу;
//    * ИдентификаторПлатежнойСистемы - Строка - ключ контроля загрузки;
//  СтатусОперацииСервис - Строка - текущее состояние операции операции. Для проверки статуса
//      операции, необходимо функции программного интерфейса общего модуля
//      ИнтеграцияСПлатежнымиСистемамиКлиентСервер. Возможные значения:
//        - "Отменена" - по ранее сформированная операция отменена НСПК;
//        - "Выполнена" - участник СБП подтвердил выполнение операции;
//        - "Ошибка" - не удалось выполнить проверку статуса операции из-за ошибки
//           или участник СБП вернул ошибку;
//  СообщениеОбОшибке - Строка - сообщение пользователю. Заполняется в случае ошибки;
//  ОбработанныеОперации - Массив из ОпределяемыйТип.ДокументОперацииБИП - данные обработанных документов.
//
Процедура ПриЗагрузкеСтатусаОперации(
		ДокументОперации,
		ПараметрыОперации,
		СтатусОперацииСервис,
		СообщениеОбОшибке,
		ОбработанныеОперации) Экспорт
	
	СтатусОперации = "";
	
	Если СтатусОперацииСервис = ИдентификаторСтатусаВыполнена() Тогда
		СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииВыполнена();
	ИначеЕсли СтатусОперацииСервис = ИдентификаторСтатусаОтклонена()
		Или СтатусОперацииСервис = ИдентификаторСтатусаОтменена() Тогда
		СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОтменена();
	ИначеЕсли СтатусОперацииСервис = ИдентификаторСтатусаПревышение()
		Или СтатусОперацииСервис = ИдентификаторСтатусаНеОплачен()
		Или СтатусОперацииСервис = ИдентификаторСтатусаНетИнформации()
		Или СтатусОперацииСервис = ИдентификаторСтатусаОшибка() Тогда
		СтатусОперации = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.СтатусОперацииОшибка();
	Иначе
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Передан не валидный статус операции %1 в отложенную обработку.'"),
				СтатусОперацииСервис),
			Истина);
		РегистрыСведений.ИдентификаторыОперацийСБП.УстановитьОтложенноеПолучениеСтатуса(
			ДокументОперации,
			Ложь);
	КонецЕсли;
	
	Попытка
		
		РезультатОбработки = Новый Структура;
		РезультатОбработки.Вставить("СтатусОперации", СтатусОперации);
		РезультатОбработки.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
		РезультатОбработки.Вставить("ПараметрыОперации", ПараметрыОперации);
		
		Обработан = Ложь;
		
		ИнтеграцияПодсистемБИП.ПриЗагрузкеСтатусаОперации(
			ДокументОперации,
			РезультатОбработки,
			Обработан);
		ИнтеграцияСПлатежнымиСистемамиПереопределяемый.ПриЗагрузкеСтатусаОперации(
			ДокументОперации,
			РезультатОбработки,
			Обработан);
		
		Если Обработан = Истина Тогда
			РегистрыСведений.ИдентификаторыОперацийСБП.УстановитьОтложенноеПолучениеСтатуса(
				ДокументОперации,
				Ложь);
			ОбработанныеОперации.Добавить(ДокументОперации);
		КонецЕсли;
		
	Исключение
		
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
			ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				НСтр("ru = 'По завершении выполнения обработчика ПриЗагрузкеСтатусаОперации не была закрыта транзакция.'"),
				Истина);
		КонецЕсли;
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		
	КонецПопытки;
	
КонецПроцедуры

// Формирует новую таблицу идентификаторов, которую необходимо использовать
// для проверки статусов.
//
// Возвращаемое значение:
//  ТаблицаЗначений - данные для проверки статуса:
//    * Идентификатор - Строка - идентификатор QR-кода СБП;
//    * ДатаЗапросаСтатуса - Дата - дата последнего запроса статуса;
//
Функция НовыйИдентификаторыПроверкиНаличияСтатуса()
	
	Идентификаторы = Новый ТаблицаЗначений;
	Идентификаторы.Колонки.Добавить(
		"Идентификатор",
		ОбщегоНазначения.ОписаниеТипаСтрока(36));
	Идентификаторы.Колонки.Добавить(
		"ДатаЗапросаСтатуса",
		ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	
	Возврат Идентификаторы;
	
КонецФункции

// Формирует новую таблицу идентификаторов, которую необходимо использовать
// для проверки статусов.
//
// Возвращаемое значение:
//  ТаблицаЗначений - данные для проверки статуса:
//    * Идентификатор - Строка - идентификатор QR-кода СБП;
//    * ДатаЗапросаСтатуса - Дата - дата последнего запроса статуса;
//    * ПараметрыАктивации - Строка - параметры активации ссылки;
//
Функция НовыйИдентификаторыПроверкиНаличияСтатусаКассовыхСсылок()
	
	Идентификаторы = НовыйИдентификаторыПроверкиНаличияСтатуса();
	Идентификаторы.Колонки.Добавить("ПараметрыАктивации");
	
	Возврат Идентификаторы;
	
КонецФункции

#КонецОбласти

#Область Аутентификация

// Возвращает логин и пароль Интернет-поддержки или тикет аутентификации.
//
// Параметры:
//  URLОперации -Строка - URL операции, для которой получаются данные аутентификации.
//
// Возвращаемое значение:
//  Структура - структура, содержащая результаты определения параметров
//              аутентификации пользователя Интернет-поддержки:
//    *ДанныеАутентификации - Структура - параметры аутентификации пользователя Интернет-поддержки;
//    *ИнформацияОбОшибке   - Строка    - информация об ошибке для пользователя.
//    *Ошибка               - Строка    - признак наличия ошибки.
//
Функция ДанныеАутентификации(URLОперации)
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатИПП = ИнтеграцияСПлатежнымиСистемамиСлужебный.ДанныеАутентификации(
		URLОперации);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатИПП;
	
КонецФункции

// Формирует заголовок для способа аутентификации basic authentication.
//
// Параметры:
//  Логин - Строка - логин в платежной системе;
//  Пароль - Строка - пароль в платежной системе.
//
// Возвращаемое значение:
//  Строка - заголовок аутентификации.
//
Функция ЗаголовокБазовойСхемыАутентификации(Логин, Пароль)
	
	Заголовок = "Basic "
		+ ПолучитьBase64СтрокуИзДвоичныхДанных(
			ПолучитьДвоичныеДанныеИзСтроки(
				Логин + ":" + Пароль));
	
	Возврат СтрЗаменить(
		СтрЗаменить(
			Заголовок,
			Символы.ВК,
			""),
		Символы.ПС,
		"");
	
КонецФункции

// Формирует заголовок для способа аутентификации bearer authentication.
//
// Параметры:
//  Токен - Строка - токен в платежной системе.
//
// Возвращаемое значение:
//  Строка - заголовок аутентификации.
//
Функция ЗаголовокBearerАутентификации(Токен)
	
	Возврат "Bearer " + Токен;
	
КонецФункции

// Формирует заголовок для способа аутентификации hmac.
//
// Параметры:
//  СекретныйКлюч - ДвоичныеДанные - секретный ключ для выполнения аутентификации;
//  ДанныеДляПодписи - Массив из Строка  - данные шифрования;
//  ЗначениеХешФункция - Строка - способ вычисления хеш-суммы.
//
// Возвращаемое значение:
//  Строка - заголовок аутентификации.
//
Функция ЗаголовокHMACАутентификации(СекретныйКлюч, ДанныеДляПодписи, ЗначениеХешФункция)
	
	Данные = "";
	Для Каждого Значение Из ДанныеДляПодписи Цикл
		
		Если Не ЗначениеЗаполнено(Значение) И Значение <> 0 Тогда
			Данные = Данные + "-";
		ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
			ЗначениеДата = ЗаписатьДатуJSON(
				Значение,
				ФорматДатыJSON.ISO,
				ВариантЗаписиДатыJSON.УниверсальнаяДата);
			Данные = Данные + СтрДлина(ЗначениеДата) + ЗначениеДата;
		ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда
			ЗначениеЧисло = СтрЗаменить("" + Значение, ",", ".");
			ЗначениеЧисло = СтрЗаменить(ЗначениеЧисло, Символы.НПП, "");
			Данные = Данные + СтрДлина(ЗначениеЧисло) + ЗначениеЧисло;
		Иначе
			Данные = Данные + СтрДлина("" + Значение) + Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	СлучайноеЧислоHex = ДлинноеСлучайноеЧислоHEX();
	Данные = Данные + СтрДлина(СлучайноеЧислоHex) + СлучайноеЧислоHex;
	
	Хеш = РассчитатьHmac(
		СекретныйКлюч,
		ПолучитьДвоичныеДанныеИзСтроки(
			Данные,
			КодировкаТекста.UTF8),
		ЗначениеХешФункция);
		
	Префикс = ?(ЗначениеХешФункция = ХешФункция.SHA256, "HMACSHA256", "HMACSHA1");
	Возврат Префикс + " " + СлучайноеЧислоHex + "." + НРег(Хеш);
	
КонецФункции

// Формирует заголовок для способа аутентификации
// Resource Owner Password Credentials Grant (https://tools.ietf.org/html/rfc6749#section-4.3).
//
// Параметры:
//  ДанныеАутентификации - Структура - данные аутентификации для получения заголовка;
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежнаяСистема - платежная система,
//                     которая выступает в роли Resource Owner;
//  Результат - Структура - результат получения заголовка.
//
Процедура СформироватьЗаголовокАутентификацииPasswordCredentialsGrant(
		ДанныеАутентификации,
		ПлатежнаяСистема,
		Результат)
	
	Если ДанныеАутентификации.expiresDate = Неопределено
		Или ДанныеАутентификации.tokenType = Неопределено
		Или ДанныеАутентификации.accessToken = Неопределено
		Или ТекущаяУниверсальнаяДатаВМиллисекундах() > ДанныеАутентификации.expiresDate Тогда
		РезультатОперации = ОперацияПредоставлениеУчетныхДанных(
			ДанныеАутентификации.login,
			ДанныеАутентификации.password,
			ПлатежнаяСистема);
		Если Не ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			ДанныеАутентификации.accessToken = РезультатОперации.Токен;
			ДанныеАутентификации.tokenType   = РезультатОперации.ТипТокена;
			ДанныеАутентификации.expiresDate = ТекущаяУниверсальнаяДатаВМиллисекундах()
				+ РезультатОперации.ДатаИстеченияСрока * 1000;
		Иначе
			Результат.Ошибка             = Истина;
			Результат.ИнформацияОбОшибке = РезультатОперации.СообщениеОбОшибке;
		КонецЕсли;
	КонецЕсли;
	
	Результат.Аутентификация = ""
		+ ДанныеАутентификации.tokenType
		+ " "
		+ ДанныеАутентификации.accessToken;
	
КонецПроцедуры

// Добавляет в запись JSON данные аутентификации платежной системы.
//
// Параметры:
//  ЗаписьДанныхСообщения  - ЗаписьJSON - запись, в которую необходимо
//                           добавить данные аутентификации;
//  ДанныеАутентификации   - Структура - параметры аутентификации мерчанта в
//                           платежной системе. См. ДанныеАутентификацииВПлатежнойСистеме;
//  ТорговаяТочка          - идентификатор торговой точки.
//
Процедура ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		Аутентификация,
		ТорговаяТочка)
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("sbpMemberRequestHeaders");
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("authorization");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(Аутентификация);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("xmerchantId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ТорговаяТочка);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиИнтеграции

// См. ИнтеграцияСПлатежнымиСистемамиСлужебный.УстановитьИдентификаторТорговойТочки.
//
Процедура УстановитьИдентификаторТорговойТочки(
		ТекущийОбъект,
		ПлатежнаяСистема,
		НастройкиАутентификации) Экспорт
	
	ТекущийОбъект.ТорговаяТочка = ИдентификаторТорговойТочки(ПлатежнаяСистема, НастройкиАутентификации);
	
КонецПроцедуры

// Получает актуальный список участников Системы быстрых платежей.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - данные для выполнения запроса;
//  АдресРезультата - Строка - адрес хранилища результат обновления.
//
Процедура ПолучитьУчастниковСБПВФоне(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	РезультатОперации = ПолучитьУчастниковСБП(Ложь);
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Получает актуальный список участников Системы быстрых платежей.
//
// Параметры:
//  ИдентификаторКакКлюч - Булево - признак представления ключа структуры в виде идентификатора участника СБП.
//
Функция ПолучитьУчастниковСБП(ИдентификаторКакКлюч) Экспорт
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("ДанныеУчастников", Новый Соответствие);
	
	РезультатВызова = ОперацияПолучениеНастроекУчастниковСБП();
	
	Если ЗначениеЗаполнено(РезультатВызова.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатВызова,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатОперации;
	КонецЕсли;
	
	// Подготовка данных существующих платежных систем.
	ПлатежныеСистемыПрограммы = Новый Соответствие;
	Для Каждого ПлатежнаяСистема Из Перечисления.ПлатежныеСистемы Цикл
		Идентификатор = ИдентификаторУчастникаСБП(ПлатежнаяСистема, Ложь);
		Если ЗначениеЗаполнено(Идентификатор) Тогда
			ПлатежныеСистемыПрограммы.Вставить(Идентификатор, ПлатежнаяСистема);
		КонецЕсли;
	КонецЦикла;
	
	// Получение слияние настроек сервиса и программы.
	Для Каждого КлючЗначение Из ПлатежныеСистемыПрограммы Цикл
		НастройкаСервис = РезультатВызова.Настройки.Получить(КлючЗначение.Ключ);
		Если НастройкаСервис <> Неопределено И НастройкаСервис.Активен Тогда
			РезультатОперации.ДанныеУчастников.Вставить(
				?(ИдентификаторКакКлюч = Истина, КлючЗначение.Ключ, КлючЗначение.Значение),
				Новый Структура("АдресСтраницыПодключения, ТекстПредупреждения, БИК, Представление",
					НастройкаСервис.АдресСтраницыПодключения,
					НастройкаСервис.ТекстПредупреждения,
					НастройкаСервис.БИК,
					Строка(КлючЗначение.Значение)));
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатОперации;
	
КонецФункции

// Производит проверку настроек подключения к СБП для новой интеграции.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - данные для регистрации новой интеграции;
//  АдресРезультата - Строка - адрес хранилища результат обновления.
//
Процедура ПроверитьПараметрыПодключенияВФоне(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	РезультатОперации = ПроверитьПараметрыПодключения(
		ПараметрыПроцедуры.ПлатежнаяСистема,
		ПараметрыПроцедуры.ПараметрыАутентификации);
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Производит проверку настроек подключения к СБП для новой интеграции.
//
// Параметры:
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//    для которой выполняется операция.
//  ПараметрыАутентификации - Соответствие - данные аутентификации в платежной системе.
//
Функция ПроверитьПараметрыПодключения(ПлатежнаяСистема, ПараметрыАутентификации) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("КодОшибки",         "");
	Результат.Вставить("СообщениеОбОшибке", "");
	
	ИдентификаторТорговойТочки = ИдентификаторТорговойТочки(
		ПлатежнаяСистема,
		ПараметрыАутентификации);
	
	ДанныеАутентификации = ПреобразоватьНастройкиАутентификации(
		ПлатежнаяСистема,
		ПараметрыАутентификации);
	
	РезультатПроверки = ОперацияПроверитьПараметрыПодключения(
		ИдентификаторТорговойТочки,
		ДанныеАутентификации,
		ПлатежнаяСистема);
	
	ЗаполнитьЗначенияСвойств(Результат, РезультатПроверки);
	
	Возврат Результат;
	
КонецФункции

// Производит проверку настроек подключения к СБП для существующей интеграции.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - данные для регистрации новой интеграции;
//  АдресРезультата - Строка - адрес хранилища результат обновления.
//
Процедура ПроверитьПараметрыПодключенияОбъект(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КодОшибки",         "");
	Результат.Вставить("СообщениеОбОшибке", "");
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыАутентификации = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ПараметрыПроцедуры.ТорговаяТочкаСсылка);
	УстановитьПривилегированныйРежим(Ложь);
	
	РезультатПроверки = ОперацияПроверитьПараметрыПодключения(
		ПараметрыПроцедуры.ИдентификаторТорговойТочки,
		ПараметрыАутентификации,
		ПараметрыПроцедуры.ПлатежнаяСистема);
	
	ЗаполнитьЗначенияСвойств(Результат, РезультатПроверки);
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

// Определяет доступность операций для торговой точки. В сценариях оплаты
// различных платежных систем существуют отличия, поэтому ряд операций могут
// быть запрещены для выполнения. Метод следует использовать для настройки
// элементов форм оплаты и возвратов и получение общих настроек интеграции.
//
// Параметры:
//  ТорговаяТочка - СправочникСсылка.НастройкиИнтеграцииСПлатежнымиСистемами -
//    настройка выполнения операции платежной системы.
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//    для которой выполняется операция.
//
// Возвращаемое значение:
//  Структура - см. ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйНастройкиТорговойТочки.
//
Функция НастройкиТорговойТочки(ТорговаяТочка, ПлатежнаяСистема) Экспорт
	
	Настройки = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйНастройкиТорговойТочки();
	Настройки.ВыборПлатежнойСистемыВозврата = (
		ПлатежнаяСистема <> Перечисления.ПлатежныеСистемы.СБП_Webmoney
		И ПлатежнаяСистема <> Перечисления.ПлатежныеСистемы.SWiP
		И ПлатежнаяСистема <> Перечисления.ПлатежныеСистемы.СБП_PayMaster
		И ПлатежнаяСистема <> Перечисления.ПлатежныеСистемы.СБП_1ССБП);
	Настройки.СрокЖизниQRКода = (ПлатежнаяСистема <> Перечисления.ПлатежныеСистемы.СБП_ЮМани);
	Настройки.ПлатежнаяСсылка = Истина;
	Настройки.СинонимСистемы = НСтр("ru = 'Система быстрых платежей'");
	Настройки.Идентификатор = ИдентификаторУчастникаСБП(ПлатежнаяСистема);
	Настройки.Используется = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ТорговаяТочка,
		"Используется");
	Если ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_PayMaster
		Или ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_1ССБП
		Или ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮМани Тогда
		Настройки.ПлатежныйАгрегатор = Истина;
		Если ПлатежнаяСистема <> Перечисления.ПлатежныеСистемы.СБП_ЮМани Тогда
			Настройки.КассовыеСсылки = Истина;
		КонецЕсли;
	Иначе
		НастройкиУчастникаСБП = ИнтеграцияСПлатежнымиСистемамиСлужебный.НастройкиУчастникаСБП(
			Настройки.Идентификатор);
		Настройки.КассовыеСсылки = НастройкиУчастникаСБП.КассовыеСсылки;
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#Область ГенерацияQRКода

// См. ИнтеграцияСПлатежнымиСистемами.СоздатьQRКодОплаты.
//
Функция СоздатьQRКодОплаты(QRКод, Размер, ТипПечати) Экспорт
	
	Если ТипПечати = 0 Тогда
		Логотип = БиблиотекаКартинок.ЛоготипСБПЦветной;
	ИначеЕсли ТипПечати = 1 Тогда
		Логотип = БиблиотекаКартинок.ЛоготипСБПМонохромный;
	Иначе
		Логотип = БиблиотекаКартинок.ЛоготипСБПЧерный;
	КонецЕсли;
	
	Возврат ИнтеграцияСПлатежнымиСистемамиСлужебный.ИзображениеQRКодa(
		QRКод,
		Размер,
		Base64Строка(Логотип.ПолучитьДвоичныеДанные()));
	
КонецФункции

// См. ИнтеграцияСПлатежнымиСистемами.СоздатьQRКодВозврата.
//
Функция СоздатьQRКодВозврата(ДокументОплаты, Размер) Экспорт
	
	Возврат ИнтеграцияСПлатежнымиСистемамиСлужебный.ИзображениеQRКодa(
		ИдентификаторыОперацииОплаты(ДокументОплаты),
		Размер);
	
КонецФункции

#КонецОбласти

#Область ИдентификаторыОпераций

// См. ИнтеграцияСПлатежнымиСистемами.ИдентификаторыОперацииОплаты.
//
Функция ИдентификаторыОперацииОплаты(ДокументОплаты) Экспорт
	
	ИдентификаторыОплаты = РегистрыСведений.ИдентификаторыОперацийСБП.ИдентификаторыОперацииСБП(
		ДокументОплаты);
	
	Если ИдентификаторыОплаты = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Идентификатор оплаты не обнаружен.'");
	КонецЕсли;
	
	Возврат ""
		+ ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП()
		+ ":"
		+ ИдентификаторыОплаты.ИдентификаторОплаты
		+ ":"
		+ ИдентификаторыОплаты.ИдентификаторПлатежнойСистемы;
	
КонецФункции

// См. ИнтеграцияСПлатежнымиСистемами.ИдентификаторыОперацииОплаты.
//
Функция ИдентификаторФункциональнойСсылки(ДокументОплаты) Экспорт
	
	ИдентификаторыОплаты = РегистрыСведений.ИдентификаторыОперацийСБП.ИдентификаторыОперацииСБП(
		ДокументОплаты);
	
	Если ИдентификаторыОплаты = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Идентификатор оплаты не обнаружен.'");
	КонецЕсли;
	
	Возврат ИдентификаторыОплаты.ИдентификаторОплаты;
	
КонецФункции

// Формирует идентификатор операции наличия терминального статуса при длительной оплате.
//
// Возвращаемое значение:
//  Строка - идентификатор операции.
//
Функция ИдентификаторОперацииПроверкиДлительнойОплаты()
	
	Возврат "LONG_PAY";
	
КонецФункции

// Формирует идентификатор операции наличия терминального статуса при оплате.
//
// Возвращаемое значение:
//  Строка - идентификатор операции.
//
Функция ИдентификаторОперацииПроверкиОплаты()
	
	Возврат "PAY";
	
КонецФункции

// Формирует идентификатор операции наличия терминального статуса при длительном возврате.
//
// Возвращаемое значение:
//  Строка - идентификатор операции.
//
Функция ИдентификаторОперацииПроверкиДлительногоВозврата()
	
	Возврат "LONG_REFUND";
	
КонецФункции

// Формирует идентификатор операции наличия терминального статуса при возврате.
//
// Возвращаемое значение:
//  Строка - идентификатор операции.
//
Функция ИдентификаторОперацииПроверкиВозврата()
	
	Возврат "REFUND";
	
КонецФункции

#КонецОбласти

#Область НастройкиУчастниковСБП

// Определяет идентификатор торговой точки на основании данных
// аутентификации платежной системы.
//
// Параметры:
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//                     для которой производится настройка;
//  НастройкиАутентификации - Соответствие - данные аутентификации.
//
// Возвращаемое значение:
//  Строка - идентификатор торговой точки.
// Пример:
//
Функция ИдентификаторТорговойТочки(ПлатежнаяСистема, НастройкиАутентификации) Экспорт
	
	Если ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Открытие Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТорговойТочкиБанкОткрытие(
			НастройкиАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_СКБ Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТорговойТочкиСКБ(
			НастройкиАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Webmoney Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТорговойТочкиWebmoney(
			НастройкиАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_PayMaster Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТорговойТочкиPayMaster(
			НастройкиАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_1ССБП Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТорговойТочки1ССБП(
			НастройкиАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Промсвязьбанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТорговойТочкиПромсвязьбанк(
			НастройкиАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮМани Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТорговойТочкиЮМани(
			НастройкиАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮгИнвестбанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТорговойТочкиЮгИнвестбанк(
			НастройкиАутентификации);
	ИначеЕсли ПлатежнаяСистемаПодключаетсяЧерезTTP(ПлатежнаяСистема) Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТорговойТочкиTTP(
			НастройкиАутентификации);
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестная платежная система.'");
	КонецЕсли;
	
КонецФункции

// Формирует декларативное описание настроек аутентификации в платежной системе.
//
// Параметры:
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//                     для которой производится настройка;
//  Реквизиты - Массив из Структура - декларативное описание настройки;
//  Подсказка - Строка - инструкции по заполнению данных аутентификации.
//
Процедура НастройкиАутентификации(ПлатежнаяСистема, Реквизиты, Подсказка) Экспорт
	
	Если ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Открытие Тогда
		НастройкиУчастниковСБП.НастройкиАутентификацииБанкОткрытие(
			Реквизиты,
			Подсказка);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_СКБ Тогда
		НастройкиУчастниковСБП.НастройкиАутентификацииСКББанк(
			Реквизиты,
			Подсказка);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Webmoney Тогда
		НастройкиУчастниковСБП.НастройкиАутентификацииWebmoney(
			Реквизиты,
			Подсказка);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_PayMaster Тогда
		НастройкиУчастниковСБП.НастройкиАутентификацииPayMaster(
			Реквизиты,
			Подсказка);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_1ССБП Тогда
		НастройкиУчастниковСБП.НастройкиАутентификации1ССБП(
			Реквизиты,
			Подсказка);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Промсвязьбанк Тогда
		НастройкиУчастниковСБП.НастройкиАутентификацииПромсвязьбанк(
			Реквизиты,
			Подсказка);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮМани Тогда
		НастройкиУчастниковСБП.НастройкиАутентификацииЮМани(
			Реквизиты,
			Подсказка);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮгИнвестбанк Тогда
		НастройкиУчастниковСБП.НастройкиАутентификацииЮгИнвестбанк(
			Реквизиты,
			Подсказка);
	ИначеЕсли ПлатежнаяСистемаПодключаетсяЧерезTTP(ПлатежнаяСистема) Тогда
		НастройкиУчастниковСБП.НастройкиАутентификацииTTP(
			Реквизиты,
			Подсказка,
			ПлатежнаяСистема);
	КонецЕсли;
	
КонецПроцедуры

// См. ИнтеграцияСПлатежнымиСистемамиСлужебный.СохранитьНастройкиАутентификации.
//
Процедура СохранитьНастройкиАутентификации(Интеграция, ПараметрыАутентификации, ПлатежнаяСистема) Экспорт
	
	Если ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Открытие Тогда
		НастройкиУчастниковСБП.СохранитьНастройкиАутентификацииБанкОткрытие(
			Интеграция,
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_СКБ Тогда
		НастройкиУчастниковСБП.СохранитьНастройкиАутентификацииСКББанк(
			Интеграция,
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Webmoney Тогда
		НастройкиУчастниковСБП.СохранитьНастройкиАутентификацииWebmoney(
			Интеграция,
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_PayMaster Тогда
		НастройкиУчастниковСБП.СохранитьНастройкиАутентификацииPayMaster(
			Интеграция,
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_1ССБП Тогда
		НастройкиУчастниковСБП.СохранитьНастройкиАутентификации1ССБП(
			Интеграция,
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Промсвязьбанк Тогда
		НастройкиУчастниковСБП.СохранитьНастройкиАутентификацииПромсвязьбанк(
			Интеграция,
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮМани Тогда
		НастройкиУчастниковСБП.СохранитьНастройкиАутентификацииЮМани(
			Интеграция,
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮгИнвестбанк Тогда
		НастройкиУчастниковСБП.СохранитьНастройкиАутентификацииЮгИнвестбанк(
			Интеграция,
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистемаПодключаетсяЧерезTTP(ПлатежнаяСистема) Тогда
		НастройкиУчастниковСБП.СохранитьНастройкиАутентификацииTTP(
			Интеграция,
			ПараметрыАутентификации);
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестная платежная система.'");
	КонецЕсли;
	
КонецПроцедуры

// См. ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗначениеНастроекАутентификации.
//
Функция ДанныеАутентификацииНастройки(Интеграция, ПлатежнаяСистема) Экспорт
	
	Результат = Новый Соответствие;
	УстановитьПривилегированныйРежим(Истина);
	Данные = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		Интеграция);
	Если ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Открытие Тогда
		НастройкиУчастниковСБП.ДанныеАутентификацииНастройкиБанкОткрытие(
			Данные,
			Результат);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_СКБ Тогда
		НастройкиУчастниковСБП.ДанныеАутентификацииНастройкиСКББанк(
			Данные,
			Результат);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Webmoney Тогда
		НастройкиУчастниковСБП.ДанныеАутентификацииНастройкиWebmoney(
			Данные,
			Результат);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_PayMaster Тогда
		НастройкиУчастниковСБП.ДанныеАутентификацииНастройкиPayMaster(
			Данные,
			Результат);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_1ССБП Тогда
		НастройкиУчастниковСБП.ДанныеАутентификацииНастройки1ССБП(
			Данные,
			Результат);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Промсвязьбанк Тогда
		НастройкиУчастниковСБП.ДанныеАутентификацииНастройкиПромсвязьбанк(
			Данные,
			Результат);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮМани Тогда
		НастройкиУчастниковСБП.ДанныеАутентификацииНастройкиЮМани(
			Данные,
			Результат);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮгИнвестбанк Тогда
		НастройкиУчастниковСБП.ДанныеАутентификацииНастройкиЮгИнвестбанк(
			Данные,
			Результат);
	ИначеЕсли ПлатежнаяСистемаПодключаетсяЧерезTTP(ПлатежнаяСистема) Тогда
		НастройкиУчастниковСБП.ДанныеАутентификацииНастройкиTTP(
			Данные,
			Результат);
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестная платежная система.'");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Добавляет платежные системы СБП, которые поддерживают сверку взаиморасчетов.
//
// Параметры:
//  ПлатежныеСистемы - Массив из ПеречислениеСсылка.ПлатежныеСистемы - платежные системы,
//                     поддерживающие сверку взаиморасчетов.
//
Процедура ДобавитьПлатежныеСистемыСверкеВзаиморасчетов(ПлатежныеСистемы) Экспорт
	
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_PayMaster);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_Открытие);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_ЮгИнвестбанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_ЮМани);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_Совкомбанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БАНКУРАЛСИБ);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_Экспобанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_АбсолютБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкЛевобережный);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БланкБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_КБХлынов);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_ВЛАДБИЗНЕСБАНК);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_НОКССБАНК);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_ЗемскийБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_НСБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_Форштадт);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_СКБПримсоцбанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_КБРостФинанс);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_ЭсБиАйБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_АКИБАНК);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_АлефБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_НИКОБАНК);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_КБАГРОПРОМКРЕДИТ);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БАНКОРЕНБУРГ);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкСаратов);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_ХакасскийМуниципальныйБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_КБАРЕСБАНК);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_Автоградбанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкАкцепт);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_ТойотаБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_КБЭНЕРГОТРАНСБАНК);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкВенец);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкИТУРУП);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_СИБСОЦБАНК);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_МирПривилегий);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкИнтеза);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_МОРСКОЙБАНК);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкРазвитиеСтолица);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_ТаврическийБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_ПервыйИнвестиционныйБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_МОСКОМБАНК);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_КБДолинск);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_Нацинвестпромбанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкРЕСОКредит);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_РУСНАРБАНК);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_Кузнецкбизнесбанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкКремлевский);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_МеждународныйФинансовыйКлуб);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_Томскпромстройбанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкАЛЕКСАНДРОВСКИЙ);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_ДжейЭндТиБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_Новобанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкПриморье);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_СолидБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_РеалистБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_АКБДержава);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкНациональныйСтандарт);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_АКБЛантаБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_РНБАНК);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_АИКБЕнисейскийОбъединенныйБанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкБЖФ);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_КБДолинск);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_Алтайкапиталбанк);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_БанкСИАБ);
	ПлатежныеСистемы.Добавить(Перечисления.ПлатежныеСистемы.СБП_МТСБанк);
	
КонецПроцедуры

// Определяет идентификатор платежной системы в СБП.
//
// Параметры:
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//    для которой выполняется операция;
//  ПоказатьИсключение - Булево - если передано значение Истина, будет вызвано
//    исключение, в случае невозможности определения идентификатора.
//
// Возвращаемое значение:
//  Строка - идентификатор участника СБП.
//
Функция ИдентификаторУчастникаСБП(ПлатежнаяСистема, ПоказатьИсключение = Истина) Экспорт
	
	Если ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Открытие Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкОткрытие();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_СКБ Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторСКББанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Webmoney Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторWebmoney();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_PayMaster Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторPayMaster();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_1ССБП Тогда
		Возврат НастройкиУчастниковСБП.Идентификатор1ССБП();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Промсвязьбанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторПромсвязьбанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮМани Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторЮМани();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮгИнвестбанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторЮгИнвестбанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Совкомбанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторСовкомбанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Экспобанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторЭкспобанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_АбсолютБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторАбсолютБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкЛевобережный Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкЛевобережный();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БланкБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБланкБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_КБХлынов Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторКБХлынов();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ВЛАДБИЗНЕСБАНК Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторВЛАДБИЗНЕСБАНК();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_НОКССБАНК Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторНОКССБАНК();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЗемскийБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторЗемскийБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_НСБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторНСБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Форштадт Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторФорштадт();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_СКБПримсоцбанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторСКБПримсоцбанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_КБРостФинанс Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторКБРостФинанс();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЭсБиАйБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторЭсБиАйБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_АКИБАНК Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторАКИБАНК();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_АлефБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторАлефБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_НИКОБАНК Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторНИКОБАНК();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_КБАГРОПРОМКРЕДИТ Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторКБАГРОПРОМКРЕДИТ();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БАНКОРЕНБУРГ Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБАНКОРЕНБУРГ();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкСаратов Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкСаратов();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ХакасскийМуниципальныйБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторХакасскийМуниципальныйБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_КБАРЕСБАНК Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторКБАРЕСБАНК();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Автоградбанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторАвтоградбанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкАкцепт Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкАкцепт();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ТойотаБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТойотаБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_КБЭНЕРГОТРАНСБАНК Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторКБЭНЕРГОТРАНСБАНК();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкВенец Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкВенец();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкИТУРУП Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкИТУРУП();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_СИБСОЦБАНК Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторСИБСОЦБАНК();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_МирПривилегий Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторМирПривилегий();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкИнтеза Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкИнтеза();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_МОРСКОЙБАНК Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторМОРСКОЙБАНК();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкРазвитиеСтолица Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкРазвитиеСтолица();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ТаврическийБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТаврическийБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ПервыйИнвестиционныйБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторПервыйИнвестиционныйБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_МОСКОМБАНК Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторМОСКОМБАНК();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_КБДолинск Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторКБДолинск();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Нацинвестпромбанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторНацинвестпромбанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкРЕСОКредит Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкРЕСОКредит();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_РУСНАРБАНК Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторРУСНАРБАНК();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Кузнецкбизнесбанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторКузнецкбизнесбанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкКремлевский Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкКремлевский();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_МеждународныйФинансовыйКлуб Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторМеждународныйФинансовыйКлуб();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Томскпромстройбанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторТомскпромстройбанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкАЛЕКСАНДРОВСКИЙ Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкАЛЕКСАНДРОВСКИЙ();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ДжейЭндТиБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторДжейЭндТиБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Новобанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторНовобанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкПриморье Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкПриморье();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_СолидБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторСолидБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_РеалистБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторРеалистБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_АКБДержава Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторАКБДержава();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкНациональныйСтандарт Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкНациональныйСтандарт();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_АКБЛантаБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторАКБЛантаБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_РНБАНК Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторРНБАНК();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_АИКБЕнисейскийОбъединенныйБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторАИКБЕнисейскийОбъединенныйБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкБЖФ Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкБЖФ();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_КБДолинск Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторКБДолинск();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Алтайкапиталбанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторАлтайкапиталбанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БанкСИАБ Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкСИАБ();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_МТСБанк Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторМТСБанк();
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_БАНКУРАЛСИБ Тогда
		Возврат НастройкиУчастниковСБП.ИдентификаторБанкУРАЛСИБ();
	Иначе
		Если ПоказатьИсключение Тогда
			ВызватьИсключение НСтр("ru = 'Неизвестная платежная система.'");
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Определяет участника СБП по идентификатору.
//
// Параметры:
//  Идентификатор - Строка - идентификатор участника;
//  ПоказатьИсключение - Булево - если передано значение Истина, будет вызвано
//    исключение, в случае невозможности определения идентификатора.
//
// Возвращаемое значение:
//  Строка - идентификатор участника СБП.
//
Функция УчастникСБППоИдентификатору(Идентификатор, ПоказатьИсключение = Истина) Экспорт
	
	Если Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкОткрытие() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Открытие;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторСКББанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_СКБ;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторWebmoney() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Webmoney;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторPayMaster() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_PayMaster;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.Идентификатор1ССБП() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_1ССБП;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторПромсвязьбанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Промсвязьбанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторЮМани() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_ЮМани;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторЮгИнвестбанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_ЮгИнвестбанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторСовкомбанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Совкомбанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторЭкспобанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Экспобанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторАбсолютБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_АбсолютБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкЛевобережный() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкЛевобережный;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБланкБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БланкБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторКБХлынов() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_КБХлынов;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторВЛАДБИЗНЕСБАНК() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_ВЛАДБИЗНЕСБАНК;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторНОКССБАНК() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_НОКССБАНК;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторЗемскийБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_ЗемскийБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторНСБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_НСБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторФорштадт() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Форштадт;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторСКБПримсоцбанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_СКБПримсоцбанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторКБРостФинанс() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_КБРостФинанс;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторЭсБиАйБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_ЭсБиАйБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторАКИБАНК() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_АКИБАНК;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторАлефБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_АлефБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторНИКОБАНК() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_НИКОБАНК;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторКБАГРОПРОМКРЕДИТ() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_КБАГРОПРОМКРЕДИТ;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБАНКОРЕНБУРГ() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БАНКОРЕНБУРГ;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкСаратов() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкСаратов;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторКБАРЕСБАНК() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_КБАРЕСБАНК;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторХакасскийМуниципальныйБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_ХакасскийМуниципальныйБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторАвтоградбанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Автоградбанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкАкцепт() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкАкцепт;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторТойотаБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_ТойотаБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторКБЭНЕРГОТРАНСБАНК() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_КБЭНЕРГОТРАНСБАНК;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкВенец() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкВенец;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкИТУРУП() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкИТУРУП;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторСИБСОЦБАНК() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_СИБСОЦБАНК;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторМирПривилегий() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_МирПривилегий;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкИнтеза() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкИнтеза;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторМОРСКОЙБАНК() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_МОРСКОЙБАНК;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкРазвитиеСтолица() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкРазвитиеСтолица;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторТаврическийБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_ТаврическийБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторПервыйИнвестиционныйБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_ПервыйИнвестиционныйБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторМОСКОМБАНК() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_МОСКОМБАНК;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторГазтрансбанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Газтрансбанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторНацинвестпромбанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Нацинвестпромбанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкРЕСОКредит() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкРЕСОКредит;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторРУСНАРБАНК() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_РУСНАРБАНК;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторКузнецкбизнесбанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Кузнецкбизнесбанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкКремлевский() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкКремлевский;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторМеждународныйФинансовыйКлуб() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_МеждународныйФинансовыйКлуб;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторТомскпромстройбанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Томскпромстройбанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкАЛЕКСАНДРОВСКИЙ() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкАЛЕКСАНДРОВСКИЙ;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторДжейЭндТиБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_ДжейЭндТиБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторНовобанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Новобанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкПриморье() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкПриморье;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторСолидБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_СолидБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторРеалистБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_РеалистБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторАКБДержава() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_АКБДержава;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкНациональныйСтандарт() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкНациональныйСтандарт;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторАКБЛантаБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_АКБЛантаБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторРНБАНК() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_РНБАНК;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторАИКБЕнисейскийОбъединенныйБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_АИКБЕнисейскийОбъединенныйБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкБЖФ() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкБЖФ;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторКБДолинск() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_КБДолинск;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторАлтайкапиталбанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_Алтайкапиталбанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкСИАБ() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БанкСИАБ;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторМТСБанк() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_МТСБанк;
	ИначеЕсли Идентификатор = НастройкиУчастниковСБП.ИдентификаторБанкУРАЛСИБ() Тогда
		Возврат Перечисления.ПлатежныеСистемы.СБП_БАНКУРАЛСИБ;
	Иначе
		Если ПоказатьИсключение Тогда
			ВызватьИсключение НСтр("ru = 'Неизвестная платежная система.'");
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Возвращает данные аутентификации в платежной системе.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции.
//  ДанныеДляПодписи - Массив из Строка  - данные шифрования;
//  ТребуетсяОбновление - Булево - признак необходимости обновления устаревших данных.
//
// Возвращаемое значение:
//  Структура - структура, содержащая результаты определения параметров
//              аутентификации мерчанта в платежной системе:
//    *Аутентификация - Структура, Неопределено - параметры аутентификации в платежной системе.
//                            Если Неопределено, значение настроек не обнаружено;
//    *ИнформацияОбОшибке   - Строка    - информация об ошибке для пользователя.
//    *Ошибка               - Строка    - признак наличия ошибки.
//
Функция ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи,
		ТребуетсяОбновление = Ложь)
	
	Результат = Новый Структура;
	Результат.Вставить("Аутентификация",     Неопределено);
	Результат.Вставить("ИнформацияОбОшибке", "");
	Результат.Вставить("Ошибка",             Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеАутентификации = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ПараметрыИнтеграции.ТорговаяТочкаСсылка);
	УстановитьПривилегированныйРежим(Ложь);
	Если ДанныеАутентификации = Неопределено  Тогда
		Результат.Ошибка = Истина;
		Результат.ИнформацияОбОшибке =
			НСтр("ru = 'Для работы с сервисом интеграции с платежными системами
				|необходимо ввести данные аутентификации.'");
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Результат.ИнформацияОбОшибке);
		Возврат Результат;
	КонецЕсли;
	
	Если ПараметрыИнтеграции.ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Открытие Тогда
		Возврат НастройкиУчастниковСБП.ДанныеАутентификацииЗапросБанкОткрытие(
			ДанныеАутентификации,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			ПараметрыИнтеграции.ПлатежнаяСистема,
			ТребуетсяОбновление);
	ИначеЕсли ПараметрыИнтеграции.ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_СКБ Тогда
		Возврат НастройкиУчастниковСБП.ДанныеАутентификацииЗапросСКББанк(
			ДанныеАутентификации,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			ПараметрыИнтеграции.ПлатежнаяСистема);
	ИначеЕсли ПараметрыИнтеграции.ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Webmoney Тогда
		Возврат НастройкиУчастниковСБП.ДанныеАутентификацииЗапросWebmoney(
			ДанныеАутентификации,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			ПараметрыИнтеграции.ПлатежнаяСистема);
	ИначеЕсли ПараметрыИнтеграции.ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_PayMaster Тогда
		Возврат НастройкиУчастниковСБП.ДанныеАутентификацииЗапросPayMaster(
			ДанныеАутентификации,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			ПараметрыИнтеграции.ПлатежнаяСистема);
	ИначеЕсли ПараметрыИнтеграции.ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_1ССБП Тогда
		Возврат НастройкиУчастниковСБП.ДанныеАутентификацииЗапрос1ССБП(
			ДанныеАутентификации,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			ПараметрыИнтеграции.ПлатежнаяСистема);
	ИначеЕсли ПараметрыИнтеграции.ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Промсвязьбанк Тогда
		Возврат НастройкиУчастниковСБП.ДанныеАутентификацииЗапросПромсвязьбанк(
			ДанныеАутентификации,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			ПараметрыИнтеграции.ПлатежнаяСистема,
			ДанныеДляПодписи);
	ИначеЕсли ПараметрыИнтеграции.ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮМани Тогда
		Возврат НастройкиУчастниковСБП.ДанныеАутентификацииЗапросЮМани(
			ДанныеАутентификации,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			ПараметрыИнтеграции.ПлатежнаяСистема);
	ИначеЕсли ПараметрыИнтеграции.ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮгИнвестбанк Тогда
		Возврат НастройкиУчастниковСБП.ДанныеАутентификацииЗапросЮгИнвестбанк(
			ДанныеАутентификации,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			ПараметрыИнтеграции.ПлатежнаяСистема);
	ИначеЕсли ПлатежнаяСистемаПодключаетсяЧерезTTP(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		Возврат НастройкиУчастниковСБП.ДанныеАутентификацииЗапросTTP(
			ДанныеАутентификации,
			ПараметрыИнтеграции.ТорговаяТочкаСсылка,
			ПараметрыИнтеграции.ПлатежнаяСистема);
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестная платежная система.'");
	КонецЕсли;
	
КонецФункции

// Формирует заголовок аутентификации для запроса в платежную систему.
//
// Параметры:
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//                     для которой выполняется операция;
//  ДанныеАутентификации - Структура - настройки аутентификации платежной системы;
//  ДанныеДляПодписи - Массив из Строка  - данные шифрования;
//  ТорговаяТочка - СправочникСсылка.НастройкиИнтеграцииСПлатежнымиСистемами -
//                  настройка выполнения оплаты.
//
// Возвращаемое значение:
//  Строка - заголовок аутентификации.
//
Функция ЗаголовокАутентификацииПоДаннымАутентификации(
		ПлатежнаяСистема,
		ДанныеАутентификации,
		ДанныеДляПодписи,
		ТорговаяТочка) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Аутентификация",     Неопределено);
	Результат.Вставить("ИнформацияОбОшибке", "");
	Результат.Вставить("Ошибка",             Ложь);
	
	Если ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Открытие Тогда
		
		ДатаТокена = ДанныеАутентификации.expiresDate;
		
		СформироватьЗаголовокАутентификацииPasswordCredentialsGrant(
			ДанныеАутентификации,
			ПлатежнаяСистема,
			Результат);
		
		// Обновление данных аутентификации.
		Если ЗначениеЗаполнено(ТорговаяТочка)
			И ДатаТокена <> ДанныеАутентификации.expiresDate Тогда
			УстановитьПривилегированныйРежим(Истина);
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
				ТорговаяТочка,
				ДанныеАутентификации);
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
		
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_СКБ
		Или ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮМани Тогда
		Результат.Аутентификация = ЗаголовокБазовойСхемыАутентификации(
			ДанныеАутентификации.login,
			ДанныеАутентификации.password);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Промсвязьбанк Тогда
		Результат.Аутентификация = ЗаголовокHMACАутентификации(
			ДанныеАутентификации.key,
			ДанныеДляПодписи,
			ХешФункция.SHA256);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_PayMaster
		Или ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_1ССБП
		Или ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮгИнвестбанк
		Или ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Webmoney
		Или ПлатежнаяСистемаПодключаетсяЧерезTTP(ПлатежнаяСистема) Тогда
		Результат.Аутентификация = ЗаголовокBearerАутентификации(
			ДанныеАутентификации.token);
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестная платежная система.'");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Выполняет преобразование параметров аутентификации в формат
// хранения и выполнения запросов к сервису.
//
// Параметры:
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//                     для которой выполняется операция;
//  ПараметрыАутентификации - Соответствие - настройки аутентификации платежной системы.
//
// Возвращаемое значение:
//  Структура - преобразованные настройки аутентификации.
//
Функция ПреобразоватьНастройкиАутентификации(ПлатежнаяСистема, ПараметрыАутентификации)
	
	Если ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Открытие Тогда
		Возврат НастройкиУчастниковСБП.ПреобразоватьНастройкиАутентификацииБанкОткрытие(
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_СКБ Тогда
		Возврат НастройкиУчастниковСБП.ПреобразоватьНастройкиАутентификацииСКББанк(
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Webmoney Тогда
		Возврат НастройкиУчастниковСБП.ПреобразоватьНастройкиАутентификацииWebmoney(
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_PayMaster Тогда
		Возврат НастройкиУчастниковСБП.ПреобразоватьНастройкиАутентификацииPayMaster(
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_1ССБП Тогда
		Возврат НастройкиУчастниковСБП.ПреобразоватьНастройкиАутентификации1ССБП(
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Промсвязьбанк Тогда
		Возврат НастройкиУчастниковСБП.ПреобразоватьНастройкиАутентификацииПромсвязьбанк(
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮМани Тогда
		Возврат НастройкиУчастниковСБП.ПреобразоватьНастройкиАутентификацииЮМани(
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_ЮгИнвестбанк Тогда
		Возврат НастройкиУчастниковСБП.ПреобразоватьНастройкиАутентификацииЮгИнвестбанк(
			ПараметрыАутентификации);
	ИначеЕсли ПлатежнаяСистемаПодключаетсяЧерезTTP(ПлатежнаяСистема) Тогда
		Возврат НастройкиУчастниковСБП.ПреобразоватьНастройкиАутентификацииTTP(
			ПараметрыАутентификации);
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестная платежная система.'");
	КонецЕсли;
	
КонецФункции

// Определяет необходимость расчета HAMC для платежной системы.
//
// Параметры:
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//                     для которой выполняется операция;
//
// Возвращаемое значение:
//  Булево - если Истина, необходимо подготовить данные для расчета HMAC.
//
Функция ПлатежнаяСистемаИспользуетHMAC(ПлатежнаяСистема)
	
	Возврат (ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Промсвязьбанк);
	
КонецФункции

// Определяет необходимость получения токена аутентификации.
//
// Параметры:
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//                     для которой выполняется операция;
//
// Возвращаемое значение:
//  Булево - если Истина, необходимо получить токен аутентификации.
//
Функция ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПлатежнаяСистема)
	
	Возврат (ПлатежнаяСистема = Перечисления.ПлатежныеСистемы.СБП_Открытие);
	
КонецФункции

// Возвращает массив платежных агрегаторов участников СБП.
//
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ПлатежныеСистемы - список участников СБП относящихся к платежным агрегаторам.
//
Функция ПлатежныеАгрегаторы() Экспорт
	
	ПлатежныеАгрегаторы = Новый Массив;
	ПлатежныеАгрегаторы.Добавить(Перечисления.ПлатежныеСистемы.СБП_Webmoney);
	ПлатежныеАгрегаторы.Добавить(Перечисления.ПлатежныеСистемы.СБП_PayMaster);
	ПлатежныеАгрегаторы.Добавить(Перечисления.ПлатежныеСистемы.СБП_1ССБП);
	
	Возврат ПлатежныеАгрегаторы;
	
КонецФункции

// Возвращает массив участников СБП, которые поддерживают функционал кассовых ссылок.
//
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ПлатежныеСистемы - список участников СБП с поддержкой кассовых ссылок.
//
Функция ПоддержкаКассовыхСсылок() Экспорт
	
	УчастникиСБП = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИдентификаторыУчастниковСБП.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.ИдентификаторыУчастниковСБП КАК ИдентификаторыУчастниковСБП
		|ГДЕ
		|	ИдентификаторыУчастниковСБП.КассовыеСсылки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Участник = УчастникСБППоИдентификатору(
			ВыборкаДетальныеЗаписи.Идентификатор,
			Ложь);
		Если ЗначениеЗаполнено(Участник) Тогда
			УчастникиСБП.Добавить(Участник);
		КонецЕсли;
	КонецЦикла;
	
	Если УчастникиСБП.Найти(Перечисления.ПлатежныеСистемы.СБП_PayMaster) = Неопределено Тогда
		УчастникиСБП.Добавить(Перечисления.ПлатежныеСистемы.СБП_PayMaster);
	КонецЕсли;
	
	Если УчастникиСБП.Найти(Перечисления.ПлатежныеСистемы.СБП_1ССБП) = Неопределено Тогда
		УчастникиСБП.Добавить(Перечисления.ПлатежныеСистемы.СБП_1ССБП);
	КонецЕсли;
	
	Возврат УчастникиСБП;
	
КонецФункции

// Определяет вид подключения платежной системы.
//
// Параметры:
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//                     для которой выполняется операция;
//
// Возвращаемое значение:
//  Булево - если Истина, если платежная система подключается через процессор третьей стороны.
//
Функция ПлатежнаяСистемаПодключаетсяЧерезTTP(ПлатежнаяСистема)
	
	Возврат ИнтеграцияСПлатежнымиСистемамиПовтИсп.ПлатежнаяСистемаПодключаетсяЧерезTTP(
		ПлатежнаяСистема);
	
КонецФункции

#КонецОбласти

#Область ВызовОперацийПолучениеНастроекУчастников

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/members/get-info.

// Получает настройки участников СБП..
//
// Возвращаемое значение:
//  Структура - получения данных аутентификации:
//    *Настройки - Массив из Структура - настройки участников СБП;
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияПолучениеНастроекУчастниковСБП()
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Получение настроек участников СБП.'"),
		Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("Настройки", Новый Соответствие);
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		"/members/infos?c2bIntegrationSupported=true",
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "GET");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить настройки участников СБП.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При получении настроек участников СБП сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеНастроек = ПрочитатьДанные_get_info(РезультатОтправки.Содержимое);
	
	Для Каждого ОписаниеНастройки Из ДанныеНастроек Цикл
		
		Настройка = Новый Структура;
		Настройка.Вставить("Активен", ОписаниеНастройки.integrationEnabled);
		Настройка.Вставить("ТекстПредупреждения", ОписаниеНастройки.attentionText);
		Настройка.Вставить("БИК", ОписаниеНастройки.biks);
		
		Настройка.Вставить(
			"АдресСтраницыПодключения",
			?(ЗначениеЗаполнено(ОписаниеНастройки.personalAccountPageUrl),
			ОписаниеНастройки.personalAccountPageUrl,
			АдресСтраницыПодключенияКСБП(ОписаниеНастройки.memberId)));
		
		РезультатОперации.Настройки.Вставить(
			ОписаниеНастройки.memberId,
			Настройка);
		
	КонецЦикла;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Завершено получение настроек участников СБП.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Чтение ответа операции /sbp/members/get-info.
//
Функция ПрочитатьДанные_get_info(ТелоJSON)
	
	// Ответ сервиса:
	// [
	//  {
	//   "memberId": "string",
	//   "personalAccountPageUrl": "string",
	//   "integrationEnabled": true
	//  },
	//  {
	//   "memberId": "string",
	//   "personalAccountPageUrl": "string",
	//   "integrationEnabled": true
	//  }
	// ]
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ВызовОперацийАутентификации

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/auth/get-credentials-grant.

// Получает обновленный токен для схемы аутентификации Resource owner password
// credentials grant (https://tools.ietf.org/html/rfc6749#section-4.3).
//
// Параметры:
//  Логин - Строка - логин пользователя;
//  Пароль - Строка - пароль пользователя;
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система
//                     для которой выполняется получение токена.
//
// Возвращаемое значение:
//  Структура - получения данных аутентификации:
//    *Токен - Строка - идентификатор, по которому выполняется оплата;
//    *ТипТокена - Строка - идентификатор оплаты в СБП;
//    *ДатаИстеченияСрока - Дата - идентификатор оплаты в СБП;
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияПредоставлениеУчетныхДанных(Логин, Пароль, ПлатежнаяСистема)
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Обновление токена Resource owner password credentials grant.'"),
		Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("Токен",              Неопределено);
	РезультатОперации.Вставить("ТипТокена",          Неопределено);
	РезультатОперации.Вставить("ДатаИстеченияСрока", Неопределено);
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/auth/get-credentials-grant",
			ИдентификаторУчастникаСБП(ПлатежнаяСистема)),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ПараметрыЗапросаJSON = get_credentials_grant(
		Логин,
		Пароль,
		РезультатИПП.ДанныеАутентификации);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("X-Correlation-ID", Строка(Новый УникальныйИдентификатор));
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		
		// Для метода используется собственное переопределении ошибки
		// т.к. формат ошибок указанный в rfc 6749 отличается
		// от выбранного для других методов.
		РезультатОперации.СообщениеОбОшибке = ПереопределитьСообщениеПользователюPasswordCredentialsGrant(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось обновить токен аутентификации в платежной системе.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При получении токена сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеАутентификации = ПрочитатьДанные_get_credentials_grant(
		РезультатОтправки.Содержимое);
	
	РезультатОперации.Токен              = ДанныеАутентификации.sbpMemberResponse.access_token;
	РезультатОперации.ДатаИстеченияСрока = ДанныеАутентификации.sbpMemberResponse.expires_in;
	РезультатОперации.ТипТокена          = ДанныеАутентификации.sbpMemberResponse.token_type;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Завершено обновление токена.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/auth/get-credentials-grant.
//
Функция get_credentials_grant(
		Логин,
		Пароль,
		ДанныеАутентификации)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("password");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(Пароль);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("username");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(Логин);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции /sbp/v1/members/{memberId}/auth/get-credentials-grant.
//
Функция ПрочитатьДанные_get_credentials_grant(ТелоJSON)
	
	// Ответ сервиса:
	// {
	//  "accessToken": "string",
	//  "expiresIn": 0,
	//  "tokenType": "string"
	// }
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/merchants/{merchantId}/get-merchant-info

// Производит проверку данных аутентификации в платежной системе.
//
// Параметры:
//  ИдентификаторТорговойТочки - Строка - идентификатор торговой точки в платежной системе;
//  ПараметрыАутентификации - Структура - данные аутентификации в платежной системе;
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежнаяСистема - проверяемая платежная система.
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на оплату в платежной системе:
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим методом;
//    *СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияПроверитьПараметрыПодключения(
		ИдентификаторТорговойТочки,
		ПараметрыАутентификации,
		ПлатежнаяСистема)
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/merchants/%2/get-merchant-info",
			ИдентификаторУчастникаСБП(ПлатежнаяСистема),
			ИдентификаторТорговойТочки),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ИдентификаторТорговойТочки);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ЗаголовокАутентификацииПоДаннымАутентификации(
		ПлатежнаяСистема,
		ПараметрыАутентификации,
		ДанныеДляПодписи,
		Неопределено);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;

	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = get_merchant_info(
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		ИдентификаторТорговойТочки);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("X-Correlation-ID", Строка(Новый УникальныйИдентификатор));
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось проверить данные аутентификации в банке.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При проверке данных аутентификации сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ИдентификаторМерчатанаОтвет = ПрочитатьДанные_get_merchant_info(
		РезультатОтправки.Содержимое);
	
	Если ИдентификаторМерчатанаОтвет <> ИдентификаторТорговойТочки Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеизвестнаяОшибка();
		РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Идентификатор торговой точки отличается от исходного.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Идентификатор торговой точки отличается от исходного.'");
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/merchants/{merchantId}/get-merchant-info.
//
Функция get_merchant_info(
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции:
// /sbp/v1/members/{memberId}/merchants/{merchantId}/get-merchant-info.
//
Функция ПрочитатьДанные_get_merchant_info(ТелоJSON)
	
	// Ответ сервиса:
	//  merchantId - идентификатор мерчанта;
	// 
	// {
	//   "merchantId": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат.sbpMemberResponse.merchantId;
	
КонецФункции

#КонецОбласти

#Область ВызовОперацийПроверкиСостояний

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /**/test-check-needed

// Определяет наличие терминального статус операции.
//
// Параметры:
//  ИдентификаторОплаты - Строка - идентификатор оплаты в системе быстрых платежей;
//  ДатаЗапросаСтатуса - Дата - дата последнего запроса статуса.
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на оплату в платежной системе:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом;
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    * ЗапроситьСтатус - Булево - признак наличия терминального статуса оплаты.
//
Функция ОперацияПроверкаНаличияСтатусаОперации(URLОперации, ДатаЗапросаСтатуса)
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("ЗапроситьСтатус", Ложь);
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = test_check_needed(ДатаЗапросаСтатуса);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("X-Correlation-ID", Строка(Новый УникальныйИдентификатор));
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить информацию о статусе операции.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При проверке статуса операции сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатОперации.ЗапроситьСтатус = ПрочитатьДанные_test_check_needed(
		РезультатОтправки.Содержимое);
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для проверки наличия статуса операции.
//
Функция test_check_needed(ДатаЗапросаСтатуса)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("lastStatusCheckDate");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(
		ЗаписатьДатуJSON(
			ДатаЗапросаСтатуса,
			ФорматДатыJSON.ISO,
			ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением));
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа проверки наличия статуса операции.
//
Функция ПрочитатьДанные_test_check_needed(ТелоJSON)
	
	// Ответ сервиса:
	//  notificationReceived - статус операции получен;
	//  responseDate - дата получения;
	// 
	//  {
	//  "checkNeeded": "string",
	//  "qrId": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат.checkNeeded = Истина;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/status-manager/payments/statuses/test-check-needed

// Определяет статус заказа на оплату платежной системе.
//
// Параметры:
//  ИдентификаторыОплат - ТаблицаЗначений - данные для проверки статуса:
//    * Идентификатор - Строка - идентификатор QR-кода СБП;
//    * ДатаЗапросаСтатуса - Дата - дата последнего запроса статуса;
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система торговой точки;
//  ИдентификаторОперации - Строка - идентификатор операции проверки наличия статуса;
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на оплату в платежной системе:
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом;
//    *СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    *Идентификаторы - Массив Из Строка - идентификаторы оплат, по которым получен терминальный статус.
//
Функция ОперацияПроверкаНаличияСтатусаОперацийОплат(
		ИдентификаторыОплат,
		ПлатежнаяСистема,
		ИдентификаторОперации)
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("Идентификаторы", Новый Массив);
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/status-manager/payments/statuses/test-check-needed",
			ИдентификаторУчастникаСБП(ПлатежнаяСистема)),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = payments_test_check_needed(
		ИдентификаторыОплат,
		ИдентификаторОперации);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить информацию о статусах операций.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При проверке статусов операции сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатОперации.Идентификаторы = ПрочитатьДанные_payments_test_check_needed(
		РезультатОтправки.Содержимое);
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для проверки наличия статуса операций оплаты.
//
Функция payments_test_check_needed(
		ИдентификаторОплат,
		ИдентификаторОперации)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("operation");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторОперации);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("qrs");
	
	ЗаписьДанныхСообщения.ЗаписатьНачалоМассива();
	
	Для Каждого ДанныеОплаты Из ИдентификаторОплат Цикл
		
		ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
		
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("qrId");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеОплаты.Идентификатор);
		
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("lastStatusCheckDate");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(
			ЗаписатьДатуJSON(
				ДанныеОплаты.ДатаЗапросаСтатуса,
				ФорматДатыJSON.ISO,
				ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением));
		
		ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
		
	КонецЦикла;
	
	ЗаписьДанныхСообщения.ЗаписатьКонецМассива();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа проверки наличия статуса операции.
//
Функция ПрочитатьДанные_payments_test_check_needed(ТелоJSON)
	
	// Ответ сервиса:
	//
	// {
	//  "checkByObjectIds": [
	//    "string"
	//  ],
	//  "responseDate": "2021-11-17T13:41:39.511Z"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат.checkByObjectIds;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/status-manager/cash-register-payments/statuses/test-check-needed

// Определяет статус заказа на оплату платежной системе.
//
// Параметры:
//  ИдентификаторыОплат - ТаблицаЗначений - данные для проверки статуса:
//    * Идентификатор - Строка - идентификатор QR-кода СБП;
//    * ДатаЗапросаСтатуса - Дата - дата последнего запроса статуса;
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система торговой точки;
//  ИдентификаторОперации - Строка - идентификатор операции проверки наличия статуса;
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на оплату в платежной системе:
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом;
//    *СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    *Идентификаторы - Массив Из Строка - идентификаторы оплат, по которым получен терминальный статус.
//
Функция ОперацияПроверкаНаличияСтатусаКассовыхСсылок(
		ИдентификаторыОплат,
		ПлатежнаяСистема,
		ИдентификаторОперации)
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("Идентификаторы", Новый Массив);
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/status-manager/cash-register-payments/statuses/test-check-needed",
			ИдентификаторУчастникаСБП(ПлатежнаяСистема)),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = cash_payments_test_check_needed(
		ИдентификаторыОплат,
		ИдентификаторОперации);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить информацию о статусах операций.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При проверке статусов операции сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатОперации.Идентификаторы = ПрочитатьДанные_cash_payments_test_check_needed(
		РезультатОтправки.Содержимое);
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для проверки наличия статуса операций оплаты.
//
Функция cash_payments_test_check_needed(ИдентификаторыОплат, ИдентификаторОперации)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("operation");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторОперации);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("qrs");
	
	ЗаписьДанныхСообщения.ЗаписатьНачалоМассива();
	
	Для Каждого ДанныеОплаты Из ИдентификаторыОплат Цикл
		
		ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
		
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("qrId");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеОплаты.Идентификатор);
		
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("paramsId");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеОплаты.ПараметрыАктивации);
		
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("lastStatusCheckDate");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(
			ЗаписатьДатуJSON(
				ДанныеОплаты.ДатаЗапросаСтатуса,
				ФорматДатыJSON.ISO,
				ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением));
		
		ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
		
	КонецЦикла;
	
	ЗаписьДанныхСообщения.ЗаписатьКонецМассива();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа проверки наличия статуса операции.
//
Функция ПрочитатьДанные_cash_payments_test_check_needed(ТелоJSON)
	
	// Ответ сервиса:
	//
	// {
	//  "operation": "string",
	//  "qrs": [
	//    {
	//      "qrId": "string",
	//      "paramsId": "string",
	//      "lastStatusCheckDate": "2022-07-27T09:14:29.908Z"
	//    }
	//  ]
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	РезультатЧтения = ПрочитатьJSON(ЧтениеОтвета);
	
	Результат = Новый Массив;
	Для Каждого Идентификаторы Из РезультатЧтения.checkByObjectIds Цикл
		Результат.Добавить(Идентификаторы.paramsId);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/status-manager/refunds/statuses/test-check-needed

// Определяет статус возвратов платежной системе.
//
// Параметры:
//  ИдентификаторыВозвратов - ТаблицаЗначений - данные для проверки статуса:
//    * Идентификатор - Строка - идентификатор операции участника СБП;
//    * ДатаЗапросаСтатуса - Дата - дата последнего запроса статуса;
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система торговой точки;
//  ИдентификаторОперации - Строка - идентификатор операции проверки наличия статуса;
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на оплату в платежной системе:
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом;
//    *СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    *Идентификаторы - Массив Из Строка - идентификаторы возврата, по которым получен терминальный статус.
//
Функция ОперацияПроверкаНаличияСтатусаОперацийВозвратов(
		ИдентификаторыВозвратов,
		ПлатежнаяСистема,
		ИдентификаторОперации)
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("Идентификаторы", Новый Массив);
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/status-manager/refunds/statuses/test-check-needed",
			ИдентификаторУчастникаСБП(ПлатежнаяСистема)),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = refunds_test_check_needed(
		ИдентификаторыВозвратов,
		ИдентификаторОперации);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить информацию о статусах операций.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При проверке статуса операции сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатОперации.Идентификаторы = ПрочитатьДанные_refunds_test_check_needed(
		РезультатОтправки.Содержимое);
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для проверки наличия статуса операций оплаты.
//
Функция refunds_test_check_needed(ИдентификаторыВозвратов, ИдентификаторОперации)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("operation");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторОперации);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("refundRequests");
	
	ЗаписьДанныхСообщения.ЗаписатьНачалоМассива();
	
	Для Каждого ДанныеВозврата Из ИдентификаторыВозвратов Цикл
		
		ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
		
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("refundId");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеВозврата.Идентификатор);
		
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("lastStatusCheckDate");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(
			ЗаписатьДатуJSON(
				ДанныеВозврата.ДатаЗапросаСтатуса,
				ФорматДатыJSON.ISO,
				ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением));
		
		ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
		
	КонецЦикла;
	
	ЗаписьДанныхСообщения.ЗаписатьКонецМассива();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа проверки наличия статуса операции.
//
Функция ПрочитатьДанные_refunds_test_check_needed(ТелоJSON)
	
	// Ответ сервиса:
	//
	// {
	//  "checkByObjectIds": [
	//    "string"
	//  ],
	//  "responseDate": "2021-11-17T13:41:39.511Z"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат.checkByObjectIds;
	
КонецФункции

#КонецОбласти

#Область ВызовОперацийОплат

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/qrs/register-qr.

// Создает новый заказ на оплату в платежной системы.
//
// Параметры:
//  ЗаказНаОплату - Структура - описание заказа на оплату см. функцию ОписаниеЗаказаНаОплату;
//  Идентификатор - Строка - внешний идентификатор документа оплаты;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции.
//
// Возвращаемое значение:
//  Структура - результат создания заказа на оплату в платежной системе:
//    *QRКод - Строка - идентификатор, по которому выполняется оплата;
//    *ИдентификаторОплаты - Строка - идентификатор оплаты в СБП;
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияЗаказНаОплату(ЗаказОплату, Идентификатор, ПараметрыИнтеграции)
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Создание заказа на оплату в платежной системе.'"),
		Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатИдентификаторОплаты();
	РезультатОперации.Вставить("ИдентификаторОплаты", "");
	РезультатОперации.Вставить("ПериодИспользования", Неопределено);
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/qrs/register-qr",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема)),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ПараметрыИнтеграции.ТорговаяТочка);
		ДанныеДляПодписи.Добавить(ЗаказОплату.СуммаОплаты);
		ДанныеДляПодписи.Добавить(ИдентификаторВалютыРоссийскийРубль());
		ДанныеДляПодписи.Добавить(Идентификатор);
		ДанныеДляПодписи.Добавить(ЗаказОплату.НазначениеПлатежа);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовОперацииЗаказНаОплату(
		ЗаказОплату,
		Идентификатор,
		ПараметрыИнтеграции,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовОперацииЗаказНаОплату(
			ЗаказОплату,
			Идентификатор,
			ПараметрыИнтеграции,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось создать заказ на оплату в платежной системе.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При создании заказа на оплату сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеОплаты = ПрочитатьДанные_register_qr(РезультатОтправки.Содержимое);
	
	РезультатОперации.ИдентификаторОплаты = ДанныеОплаты.sbpMemberResponse.qrId;
	РезультатОперации.QRКод = ДанныеОплаты.sbpMemberResponse.payload;
	Если ЗначениеЗаполнено(ЗаказОплату.СрокЖизниQRКода) Тогда
		РезультатОперации.ПериодИспользования = ТекущаяДатаСеанса() + ЗаказОплату.СрокЖизниQRКода * 60;
	Иначе
		РезультатОперации.ПериодИспользования = ТекущаяДатаСеанса() + ДанныеОплаты.qrSettings.defaultQrTtl * 60;
	КонецЕсли;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Завершено создание заказа на оплату в платежной системе.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции создания нового заказа на оплату.
//
// Параметры:
//  ЗаказНаОплату - Структура - описание заказа на оплату см. функцию ОписаниеЗаказаНаОплату;
//  Идентификатор - Строка - внешний идентификатор документа оплаты;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат создания заказа на оплату в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовОперацииЗаказНаОплату(
		ЗаказОплату,
		Идентификатор,
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = register_qr(
		ЗаказОплату,
		Идентификатор,
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/qrs/register-qr.
//
Функция register_qr(
		ЗаказОплату,
		Идентификатор,
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные аутентификации платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	// Параметры оплаты.
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("sbpMemberRequest");
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("amount");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ЗаказОплату.СуммаОплаты);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("currency");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторВалютыРоссийскийРубль());
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("createDate");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(
		ЗаписатьДатуJSON(
			ЗаказОплату.ДатаОплаты,
			ФорматДатыJSON.ISO,
			ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("merchantId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыИнтеграции.ТорговаяТочка);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("orderId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(Строка(Идентификатор));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("qrTtl");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(
		?(Не ЗначениеЗаполнено(ЗаказОплату.СрокЖизниQRКода),
			Неопределено,
			ЗаказОплату.СрокЖизниQRКода));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("paymentPurpose");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ЗаказОплату.НазначениеПлатежа);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции /sbp/v1/members/{memberId}/qrs/register-qr.
//
Функция ПрочитатьДанные_register_qr(ТелоJSON)
	
	// Ответ сервиса:
	//  payload - идентификатор оплаты для покупателя;
	//  qrId - идентификатор оплаты в СБП;
	// 
	//  {
	//  "payload": "string",
	//  "qrId": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/payments/qrs/{qrId}/search-payment

// Определяет статус заказа на оплату платежной системе.
//
// Параметры:
//  ИдентификаторОплаты - Строка - идентификатор оплаты в системе быстрых платежей;
//  ДатаЗапросаСтатуса - Дата - дата последнего запроса статуса оплаты;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//    см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ИдентификаторОперации - Строка - идентификатор операции проверки наличия статуса;
//  ЗапросНаличияСтатуса - Булево - если Истина, будет выполнен запрос наличия статуса.
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на оплату в платежной системе:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом;
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    * СтатусОплаты - Строка - состояние оплаты в платежной системе;
//    * ИдентификаторПлатежнойСистемы - Строка - идентификатор оплаты платежной системы;
//    * ДатаОперации - Дата - дата регистрации операции в платежной системе;
//    * СуммаОперации - Число - фактическая суммы оплаты по документу.
//
Функция ОперацияСтатусОплатыДинамическойСсылки(
		ИдентификаторОплаты,
		ДатаЗапросаСтатуса,
		ПараметрыИнтеграции,
		ИдентификаторОперацииПроверки,
		ЗапросНаличияСтатуса = Истина)
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("СтатусОплаты", "");
	РезультатОперации.Вставить("ИдентификаторПлатежнойСистемы", "");
	РезультатОперации.Вставить("ДатаОперации", Неопределено);
	РезультатОперации.Вставить("СуммаОперации", Неопределено);
	РезультатОперации.Вставить("ОбновитьДатуЗапросаСтатуса", Ложь);
	
	Если ЗапросНаличияСтатуса Тогда
		
		Если Не ЗначениеЗаполнено(ИдентификаторОплаты) Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
			РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Не заполнен идентификатор операции.'");
			РезультатОперации.ИнформацияОбОшибке = РезультатОперации.СообщениеОбОшибке;
			
			ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				Строка(РезультатОперации.ИнформацияОбОшибке),
				Истина);
			
			Возврат РезультатОперации;
		КонецЕсли;
		
		ИдентификаторыОплат = НовыйИдентификаторыПроверкиНаличияСтатуса();
		
		ДанныеОплаты = ИдентификаторыОплат.Добавить();
		ДанныеОплаты.Идентификатор = ИдентификаторОплаты;
		ДанныеОплаты.ДатаЗапросаСтатуса = ДатаЗапросаСтатуса;
		
		РезультатПроверки = ОперацияПроверкаНаличияСтатусаОперацийОплат(
			ИдентификаторыОплат,
			ПараметрыИнтеграции.ПлатежнаяСистема,
			ИдентификаторОперацииПроверки);
		
		Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				РезультатПроверки,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
			Возврат РезультатОперации;
		КонецЕсли;
		
		Если РезультатПроверки.Идентификаторы.Найти(ИдентификаторОплаты) = Неопределено Тогда
			РезультатОперации.СтатусОплаты = ИдентификаторСтатусаВПроцессе();
			Возврат РезультатОперации;
		КонецЕсли;
		
	КонецЕсли;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Проверка статуса заказа на оплату в платежной системе.'"),
		Ложь);
	
	// Необходимо обновить дату запроса с статуса в базе данных.
	РезультатОперации.ОбновитьДатуЗапросаСтатуса = Истина;
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/payments/qrs/%2/search-payment",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема),
			ИдентификаторОплаты),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ИдентификаторОплаты);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовСтатусОплатыДинамическойСсылки(
		ПараметрыИнтеграции,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовСтатусОплатыДинамическойСсылки(
			ПараметрыИнтеграции,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить статус оплаты в платежной системе.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При получении статуса оплаты сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеСтатуса = ПрочитатьДанные_search_payment(РезультатОтправки.Содержимое);
	
	РезультатОперации.СтатусОплаты = ДанныеСтатуса.sbpMemberResponse.paymentStatus;
	РезультатОперации.ИдентификаторПлатежнойСистемы = ИдентификаторПлатежнойСистемыБезСлужебныхСимволов(
		ДанныеСтатуса.sbpMemberResponse.paymentId);
	РезультатОперации.ДатаОперации          = ДанныеСтатуса.sbpMemberResponse.operationDate;
	РезультатОперации.СуммаОперации         = ДанныеСтатуса.sbpMemberResponse.amount;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Проверка статуса заказа на оплату в платежной системе завершена.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции проверки статуса заказа на оплату.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат получения статуса заказа на оплату в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовСтатусОплатыДинамическойСсылки(
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = search_payment(
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/payments/qrs/{qrId}/search-payment.
//
Функция search_payment(
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции:
// /sbp/v1/members/{memberId}/payments/qrs/{qrId}/search-payment
//
Функция ПрочитатьДанные_search_payment(ТелоJSON)
	
	// Ответ сервиса:
	//  operationDate - дата операции в платежной системе;
	//  paymentId - идентификатор операции в платежной системе;
	//  paymentStatus - статус оплаты;
	//  qrId - идентификатор операции в СБП;
	// 
	//{
	//  "operationDate": "string",
	//  "paymentId": "string",
	//  "paymentStatus": "SUCCESS",
	//  "qrId": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ИменаСвойствСоЗначениямиДата = Новый Массив;
	ИменаСвойствСоЗначениямиДата.Добавить("operationDate");
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(
		ЧтениеОтвета,
		,
		ИменаСвойствСоЗначениямиДата,
		,
		"ВосстановитьДатуJSON",
		СервисИнтеграцииССБП,
		,
		ИменаСвойствСоЗначениямиДата);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ВызовОперацийКассовыхСсылок

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/cash-register-qrs/register-qr.

// Регистрирует новую кассовую ссылку в платежной системы.
//
// Параметры:
//  ЗаказНаОплату - Структура - описание заказа на оплату см. функцию ОписаниеЗаказаНаОплату;
//  Идентификатор - Строка - внешний идентификатор документа оплаты;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//   см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции.
//
// Возвращаемое значение:
//  Структура - результат создания заказа на оплату в платежной системе:
//    *КассоваяСсылка - Строка - идентификатор, по которому выполняется оплата;
//    *ИдентификаторОплаты - Строка - идентификатор оплаты в СБП;
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияРегистрацияКассовойСсылки(ПараметрыИнтеграции)
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Регистрация новой кассовой ссылки в платежной системе.'"),
		Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатИдентификаторОплаты();
	РезультатОперации.Вставить("ИдентификаторОплаты", "");
	РезультатОперации.Вставить("КассоваяСсылка", "");
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/cash-register-qrs/register-qr",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема)),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ПараметрыИнтеграции.ТорговаяТочка);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовОперацииРегистрацияКассовойСсылки(
		ПараметрыИнтеграции,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовОперацииРегистрацияКассовойСсылки(
			ПараметрыИнтеграции,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось создать зарегистрировать кассовую ссылку.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При регистрации ссылки сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеОплаты = ПрочитатьДанные_cash_register_qrs_register_qr(
		РезультатОтправки.Содержимое);
	
	РезультатОперации.ИдентификаторОплаты = ДанныеОплаты.sbpMemberResponse.qrId;
	РезультатОперации.КассоваяСсылка = ДанныеОплаты.sbpMemberResponse.payload;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Завершена регистрация кассовой ссылки в платежной системе.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции создания новой кассовой ссылки.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//   см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат создания заказа на оплату в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовОперацииРегистрацияКассовойСсылки(
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = cash_register_qrs_register_qr(
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/cash-register-qrs/register-qr.
//
Функция cash_register_qrs_register_qr(
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные аутентификации платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	// Параметры кассовой ссылки.
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("sbpMemberRequest");
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("merchantId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыИнтеграции.ТорговаяТочка);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции /sbp/v1/members/{memberId}/cash-register-qrs/register-qr.
//
Функция ПрочитатьДанные_cash_register_qrs_register_qr(ТелоJSON)
	
	// Ответ сервиса:
	//  payload - идентификатор оплаты для покупателя;
	//  qrId - идентификатор оплаты в СБП;
	// 
	//  {
	//  "payload": "string",
	//  "qrId": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/cash-register-qrs/{qrId}/activate.

// Активирует ранее зарегистрированную кассовую ссылку.
//
// Параметры:
//  ЗаказНаОплату - Структура - описание заказа на оплату см. функцию ОписаниеЗаказаНаОплату;
//  Идентификатор - Строка - внешний идентификатор документа оплаты;
//  ИдентификаторОплаты - Строка - идентификатор кассовой ссылки;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//   см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции.
//
// Возвращаемое значение:
//  Структура - результат создания заказа на оплату в платежной системе:
//    *QRКод - Строка - идентификатор, по которому выполняется оплата;
//    *ИдентификаторОплаты - Строка - идентификатор оплаты в СБП;
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияАктивацияКассовойСсылки(
		ЗаказОплату,
		Идентификатор,
		ИдентификаторОплаты,
		ПараметрыИнтеграции)
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Активация кассовой ссылки.'"),
		Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатИдентификаторОплаты();
	РезультатОперации.Вставить("ИдентификаторОплаты", "");
	РезультатОперации.Вставить("ПериодИспользования", Неопределено);
	РезультатОперации.Вставить("ПараметрыАктивации", "");
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/cash-register-qrs/%2/activate",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема),
			ИдентификаторОплаты),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ИдентификаторОплаты);
		ДанныеДляПодписи.Добавить(ЗаказОплату.СуммаОплаты);
		ДанныеДляПодписи.Добавить(ИдентификаторВалютыРоссийскийРубль());
		ДанныеДляПодписи.Добавить(Идентификатор);
		ДанныеДляПодписи.Добавить(ЗаказОплату.НазначениеПлатежа);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовОперацииАктивацияКассовойСсылки(
		ЗаказОплату,
		Идентификатор,
		ПараметрыИнтеграции,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовОперацииАктивацияКассовойСсылки(
			ЗаказОплату,
			Идентификатор,
			ПараметрыИнтеграции,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось активировать кассовую ссылку.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При активации кассовой ссылки сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеОплаты = ПрочитатьДанные_params(РезультатОтправки.Содержимое);
	
	РезультатОперации.ИдентификаторОплаты = ДанныеОплаты.sbpMemberResponse.qrId;
	РезультатОперации.ПараметрыАктивации = ДанныеОплаты.sbpMemberResponse.paramsId;
	Если ЗначениеЗаполнено(ЗаказОплату.СрокЖизниQRКода) Тогда
		РезультатОперации.ПериодИспользования = ТекущаяДатаСеанса() + ЗаказОплату.СрокЖизниQRКода * 60;
	Иначе
		РезультатОперации.ПериодИспользования = ТекущаяДатаСеанса() + ДанныеОплаты.qrSettings.defaultQrTtl * 60;
	КонецЕсли;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Завершена активация кассовой ссылки.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции создания активации кассовой ссылки.
//
// Параметры:
//  ЗаказНаОплату - Структура - описание заказа на оплату см. функцию ОписаниеЗаказаНаОплату;
//  Идентификатор - Строка - внешний идентификатор документа оплаты;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//    см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат создания заказа на оплату в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовОперацииАктивацияКассовойСсылки(
		ЗаказОплату,
		Идентификатор,
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = activate(
		ЗаказОплату,
		Идентификатор,
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/cash-register-qrs/{qrId}/activate.
//
Функция activate(
		ЗаказОплату,
		Идентификатор,
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные аутентификации платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	// Параметры оплаты.
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("sbpMemberRequest");
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("amount");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ЗаказОплату.СуммаОплаты);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("currency");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторВалютыРоссийскийРубль());
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("createDate");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(
		ЗаписатьДатуJSON(
			ЗаказОплату.ДатаОплаты,
			ФорматДатыJSON.ISO,
			ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("orderId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(Строка(Идентификатор));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("qrTtl");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(
		?(Не ЗначениеЗаполнено(ЗаказОплату.СрокЖизниQRКода),
			Неопределено,
			ЗаказОплату.СрокЖизниQRКода));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("paymentPurpose");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ЗаказОплату.НазначениеПлатежа);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции /sbp/v1/members/{memberId}/cash-register-qrs/{qrId}/activate.
//
Функция ПрочитатьДанные_params(ТелоJSON)
	
	// Ответ сервиса:
	//  payload - идентификатор оплаты для покупателя;
	//  qrId - идентификатор оплаты в СБП;
	// 
	//  {
	//  "payload": "string",
	//  "qrId": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/cash-register-payments/qrs/{qrId}/params/{paramsId}/search-payment

// Определяет статус заказа на оплату платежной системе.
//
// Параметры:
//  ИдентификаторОплаты - Строка - идентификатор оплаты в системе быстрых платежей;
//  ДатаЗапросаСтатуса - Дата - дата последнего запроса статуса оплаты;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//    см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ИдентификаторОперации - Строка - идентификатор операции проверки наличия статуса;
//  ЗапросНаличияСтатуса - Булево - если Истина, будет выполнен запрос наличия статуса.
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на оплату в платежной системе:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом;
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    * СтатусОплаты - Строка - состояние оплаты в платежной системе;
//    * ИдентификаторПлатежнойСистемы - Строка - идентификатор оплаты платежной системы;
//    * ДатаОперации - Дата - дата регистрации операции в платежной системе;
//    * СуммаОперации - Число - фактическая суммы оплаты по документу.
//
Функция ОперацияСтатусОплатыКассовойСсылки(
		ИдентификаторОплаты,
		ПараметрыАктивации,
		ДатаЗапросаСтатуса,
		ПараметрыИнтеграции,
		ИдентификаторОперацииПроверки,
		ЗапросНаличияСтатуса = Истина)
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("СтатусОплаты", "");
	РезультатОперации.Вставить("ИдентификаторПлатежнойСистемы", "");
	РезультатОперации.Вставить("ДатаОперации", Неопределено);
	РезультатОперации.Вставить("СуммаОперации", Неопределено);
	РезультатОперации.Вставить("ОбновитьДатуЗапросаСтатуса", Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("СтатусОплаты", "");
	РезультатОперации.Вставить("ИдентификаторПлатежнойСистемы", "");
	РезультатОперации.Вставить("ДатаОперации", Неопределено);
	РезультатОперации.Вставить("СуммаОперации", Неопределено);
	РезультатОперации.Вставить("ОбновитьДатуЗапросаСтатуса", Ложь);
	
	Если ЗапросНаличияСтатуса Тогда
		
		Если Не ЗначениеЗаполнено(ПараметрыАктивации) Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
			РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Не заполнены параметры активации операции.'");
			РезультатОперации.ИнформацияОбОшибке = РезультатОперации.СообщениеОбОшибке;
			
			ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				Строка(РезультатОперации.ИнформацияОбОшибке),
				Истина);
			
			Возврат РезультатОперации;
		КонецЕсли;
		
		ИдентификаторыОплат = НовыйИдентификаторыПроверкиНаличияСтатусаКассовыхСсылок();
		
		ДанныеОплаты = ИдентификаторыОплат.Добавить();
		ДанныеОплаты.Идентификатор = ИдентификаторОплаты;
		ДанныеОплаты.ДатаЗапросаСтатуса = ДатаЗапросаСтатуса;
		ДанныеОплаты.ПараметрыАктивации = ПараметрыАктивации;
		
		РезультатПроверки = ОперацияПроверкаНаличияСтатусаКассовыхСсылок(
			ИдентификаторыОплат,
			ПараметрыИнтеграции.ПлатежнаяСистема,
			ИдентификаторОперацииПроверки);
		
		Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				РезультатПроверки,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
			Возврат РезультатОперации;
		КонецЕсли;
		
		Если РезультатПроверки.Идентификаторы.Найти(ПараметрыАктивации) = Неопределено Тогда
			РезультатОперации.СтатусОплаты = ИдентификаторСтатусаВПроцессе();
			Возврат РезультатОперации;
		КонецЕсли;
		
	КонецЕсли;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Проверка статуса заказа на оплату кассовой ссылки в платежной системе.'"),
		Ложь);
	
	// Необходимо обновить дату запроса с статуса в базе данных.
	РезультатОперации.ОбновитьДатуЗапросаСтатуса = Истина;
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/cash-register-payments/qrs/%2/params/%3/search-payment",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема),
			ИдентификаторОплаты,
			ПараметрыАктивации),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ИдентификаторОплаты);
		ДанныеДляПодписи.Добавить(ПараметрыАктивации);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовСтатусОплатыКассовойСсылки(
		ПараметрыИнтеграции,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовСтатусОплатыКассовойСсылки(
			ПараметрыИнтеграции,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить статус оплаты кассовой ссылки в платежной системе.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При получении статуса оплаты сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеСтатуса = ПрочитатьДанные_cash_search_payment(РезультатОтправки.Содержимое);
	
	РезультатОперации.СтатусОплаты = ДанныеСтатуса.sbpMemberResponse.paymentStatus;
	РезультатОперации.ИдентификаторПлатежнойСистемы = ИдентификаторПлатежнойСистемыБезСлужебныхСимволов(
		ДанныеСтатуса.sbpMemberResponse.paymentId);
	РезультатОперации.ДатаОперации = ДанныеСтатуса.sbpMemberResponse.operationDate;
	РезультатОперации.СуммаОперации = ДанныеСтатуса.sbpMemberResponse.amount;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Проверка статуса заказа на оплату кассовой ссылки в платежной системе завершена.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции проверки статуса заказа на оплату.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//   см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат получения статуса заказа на оплату в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовСтатусОплатыКассовойСсылки(
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = cash_search_payment(
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/cash-register-payments/qrs/{qrId}/params/{paramsId}/search-payment.
//
Функция cash_search_payment(
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции:
// /sbp/v1/members/{memberId}/cash-register-payments/qrs/{qrId}/params/{paramsId}/search-payment
//
Функция ПрочитатьДанные_cash_search_payment(ТелоJSON)
	
	// Ответ сервиса:
	//  operationDate - дата операции в платежной системе;
	//  paymentId - идентификатор операции в платежной системе;
	//  paymentStatus - статус оплаты;
	//  qrId - идентификатор операции в СБП;
	// 
	//{
	//  "operationDate": "string",
	//  "paymentId": "string",
	//  "paymentStatus": "SUCCESS",
	//  "qrId": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ИменаСвойствСоЗначениямиДата = Новый Массив;
	ИменаСвойствСоЗначениямиДата.Добавить("operationDate");
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(
		ЧтениеОтвета,
		,
		ИменаСвойствСоЗначениямиДата,
		,
		"ВосстановитьДатуJSON",
		СервисИнтеграцииССБП,
		,
		ИменаСвойствСоЗначениямиДата);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /program1c/sbp/v1/members/{memberId}/cash-register-qrs/{qrId}/deactivate.

// Деактивирует кассовую ссылку в платежной системы.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//   см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции.
//  *ИдентификаторОплаты - Строка - идентификатор оплаты в СБП;
//
// Возвращаемое значение:
//  Структура - результат создания заказа на оплату в платежной системе:
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияДеактивироватьКассовуюСсылку(
		ПараметрыИнтеграции,
		ИдентификаторОплаты)
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Деактивация кассовой ссылки в платежной системе.'"),
		Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатИдентификаторОплаты();
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/cash-register-qrs/%2/deactivate",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема),
			ИдентификаторОплаты),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ИдентификаторОплаты);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовОперацииОперацияДеактивироватьКассовуюСсылку(
		ПараметрыИнтеграции,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовОперацииОперацияДеактивироватьКассовуюСсылку(
			ПараметрыИнтеграции,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось создать зарегистрировать кассовую ссылку.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При регистрации ссылки сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Завершена деактивация кассовой ссылки в платежной системе.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции деактивации кассовой ссылки.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//   см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат создания заказа на оплату в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовОперацииОперацияДеактивироватьКассовуюСсылку(
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = cash_register_qrs_register_deactivate(
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокПлатежнаяСистема);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/cash-register-qrs/register-qr.
//
Функция cash_register_qrs_register_deactivate(
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные аутентификации платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

#КонецОбласти

#Область ВызовОперацийВозвратов

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/refunds/create-refund

// Создает возврат оплаты в платежной системе.
//
// Параметры:
//  ПараметрыВозврата - Структура - данные для формирования возврата;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции.
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на оплату в платежной системе:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим методом;
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    * СтатусВозврата - Строка - состояние возврата в платежной системе.
//    * ИдентификаторВозврата - Строка - идентификатор возврата платежной системы;
//    * ДатаОперации - Дата - дата регистрации операции в платежной системе;
//    * СуммаОперации - Число - фактическая сумма возврата по документу.
//    * Подтверждение - Структура, Неопределено - данные для подтверждения возврата в платежной системе:
//        **ФИО - Строка - маскированное ФИО физического лица на расчетный счет, которого
//                переводятся денежные средства;
//        ** НомерСчета - Строка - маскированный номер счета, на который будут переведены денежные средства;
//        ** НомерТелефона - Строка - маскированный номер телефона, к которому привязан расчетный счет в платежной
//                           системе получателе платежа;
//        ** ИдентификаторСессии - Строка - идентификатор сессии возврата. Используется при подтверждении возврата
//                                 см. ИнтеграцияСПлатежнымиСистемами.ПодтвердитьВозврат;
//
Функция ОперацияВозвратОплаты(ПараметрыВозврата, ПараметрыИнтеграции)
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Создание заказа на возврат в платежной системе.'"),
		Ложь);
	
	Подтверждение = Новый Структура;
	Подтверждение.Вставить("ФИО", "");
	Подтверждение.Вставить("НомерСчета", "");
	Подтверждение.Вставить("НомерТелефона", "");
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатДлительнойОперации();
	РезультатОперации.Вставить("ИдентификаторОплаты", "");
	РезультатОперации.Вставить("СтатусВозврата", "");
	РезультатОперации.Вставить("ИдентификаторВозврата", "");
	РезультатОперации.Вставить("ДатаОперации", Неопределено);
	РезультатОперации.Вставить("СуммаОперации", Неопределено);
	РезультатОперации.Вставить("ИдентификаторСессии", "");
	РезультатОперации.Вставить("Подтверждение", Подтверждение);
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/refunds/create-refund",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема)),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ПараметрыВозврата.ИдентификаторСПБ);
		ДанныеДляПодписи.Добавить(ПараметрыВозврата.ИдентификаторПлатежнойСистемы);
		ДанныеДляПодписи.Добавить(ПараметрыВозврата.СуммаВозврата);
		ДанныеДляПодписи.Добавить(ИдентификаторВалютыРоссийскийРубль());
		ДанныеДляПодписи.Добавить(ПараметрыВозврата.ИдентификаторыОперации);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовОперацииВозвратОплаты(
		ПараметрыИнтеграции,
		ПараметрыВозврата,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовОперацииВозвратОплаты(
			ПараметрыИнтеграции,
			ПараметрыВозврата,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		
		// Для возвратов ошибка 400 обрабатывается по отдельному сценарию, т.к.
		// возможны ситуации, при которых счет получателя в банке будет не найден или заблокирован.
		// НСПК не возвращает код ошибки, по которому можно идентифицировать подобную ошибку
		// поэтому пользователю отображается общее сообщение.
		Если РезультатОтправки.КодСостояния = 400 Тогда
			РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Не удалось создать заказ на возврат. Возможные причины ошибки:
				| - у покупателя отсутствует счет в выбранном банке;
				| - неверно сформирован запрос на возврат;
				|Обратитесь к администратору.'");
		Иначе
			РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
				РезультатОперации.КодОшибки,
				РезультатОтправки.Содержимое);
		КонецЕсли;
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось создать заказ на возврат в платежной системе.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При создании заказа на возврат сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеВозврата = ПрочитатьДанные_refunds_create_refund(
		РезультатОтправки.Содержимое);
	
	РезультатОперации.СтатусВозврата = ДанныеВозврата.sbpMemberResponse.refundStatus;
	РезультатОперации.ИдентификаторВозврата = ИдентификаторПлатежнойСистемыБезСлужебныхСимволов(
		ДанныеВозврата.sbpMemberResponse.refundId);
	РезультатОперации.ДатаОперации  = ДанныеВозврата.sbpMemberResponse.operationDate;
	РезультатОперации.СуммаОперации = ДанныеВозврата.sbpMemberResponse.amount;
	Если ДанныеВозврата.sbpMemberResponse.confirmation <> Неопределено Тогда
		РезультатОперации.Подтверждение.НомерСчета    = ДанныеВозврата.sbpMemberResponse.confirmation.accountNumber;
		РезультатОперации.Подтверждение.ФИО           = ДанныеВозврата.sbpMemberResponse.confirmation.fullName;
		РезультатОперации.Подтверждение.НомерТелефона = ДанныеВозврата.sbpMemberResponse.confirmation.phone;
		РезультатОперации.ИдентификаторСессии         = ДанныеВозврата.sbpMemberResponse.confirmation.state;
	КонецЕсли;
	
	Если ДанныеВозврата.sbpMemberResponse.refundStatus = ИдентификаторСтатусаОтменена() Тогда
		
		Если ДанныеВозврата.sbpMemberResponse.Свойство("refundStatusDescription") Тогда
			ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось создать заказ на возврат в платежной системе.
					|
					|%1
					|
					|Техническая информация об ошибке:
					|Операция была отменена.'"),
				ДанныеВозврата.sbpMemberResponse.refundStatusDescription);
		Иначе
			ИнформацияОбОшибке = НСтр("ru = 'Не удалось создать заказ на возврат в платежной системе.
				|
				|Причина отмены операции не указана.
				|
				|Техническая информация об ошибке:
				|Операция была отменена.'");
		КонецЕсли;
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ИнформацияОбОшибке,
			Истина);
		
	Иначе
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			НСтр("ru = 'Завершено создание заказа на возврат в платежной системе.'"),
			Ложь);
		
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции возврата оплаты.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ПараметрыВозврата - Структура - данные для формирования возврата;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат создания возврата оплаты в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовОперацииВозвратОплаты(
		ПараметрыИнтеграции,
		ПараметрыВозврата,
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = refunds_create_refund(
		ПараметрыВозврата,
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	// Вызов операции выполняется в 6 итераций. Повторные вызовы операции требуются
	// для случаев, когда результат выполнения возврата не определен. Обычно операция возврата
	// не занимает много времени, но в ряде случаев время обработки может быть увеличено.
	// Между вызовами добавляется пауза, чтобы не нагружать сервис банка.
	//
	// Паузы между вызовами:
	// - между 2-м и 3-м вызовом делается пауза 3 секунды;
	// - между 3-м и 4-м вызовом делается пауза 10 секунд;
	// - между 4-м и 5-м вызовом делается пауза 15 секунд;
	// - между 5-м и 6-м вызовом делается пауза 15 секунд; 
	
	КоличествоОпераций = 0;
	Пока КоличествоОпераций <= 5 Цикл
		
		ПараметрыОтправки = Новый Структура;
		ПараметрыОтправки.Вставить("Метод"                   , "POST");
		ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
		ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
		ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
		ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
		ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
		ПараметрыОтправки.Вставить("Таймаут"                 , 30);
		
		РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
			URLОперации,
			,
			,
			ПараметрыОтправки);
		
		Если РезультатОтправки.КодСостояния < 500
			И РезультатОтправки.КодСостояния <> 0 Тогда
			Прервать;
		КонецЕсли;
		
		// Перед выполнением повторного вызова при работе в модели
		// сервиса необходимо получить новый тикет.
		Если ОбщегоНазначения.РазделениеВключено() Тогда
			
			РезультатИПП = ДанныеАутентификации(URLОперации);
			Если РезультатИПП.Ошибка Тогда
				Прервать;
			КонецЕсли;
			
			ПараметрыЗапросаJSON = refunds_create_refund(
				ПараметрыВозврата,
				РезультатИПП.ДанныеАутентификации,
				ЗаголовокАутентификации,
				ПараметрыИнтеграции.ТорговаяТочка);
			
		КонецЕсли;
		
		КоличествоОпераций = КоличествоОпераций + 1;
		
		Если КоличествоОпераций = 2 Тогда
			ИнтернетПоддержкаПользователей.Пауза(3);
		ИначеЕсли КоличествоОпераций = 3 Тогда
			ИнтернетПоддержкаПользователей.Пауза(5);
		ИначеЕсли КоличествоОпераций = 4 Тогда
			ИнтернетПоддержкаПользователей.Пауза(10);
		ИначеЕсли КоличествоОпераций = 5 Тогда
			ИнтернетПоддержкаПользователей.Пауза(15);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатОтправки;
	
КонецФункции

// Формирует параметры запроса для операции
// sbp/members/{memberId}/refunds/create-refund.
//
Функция refunds_create_refund(
		ПараметрыВозврата,
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	// Параметры возврата.
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("sbpMemberRequest");
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("amount");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыВозврата.СуммаВозврата);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("currency");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторВалютыРоссийскийРубль());
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("createDate");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(
		ЗаписатьДатуJSON(
			ПараметрыВозврата.ДатаВозврата,
			ФорматДатыJSON.ISO,
			ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("memberId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(
		?(ЗначениеЗаполнено(ПараметрыВозврата.ПлатежнаяСистема), 
			СтрЗаменить("" + ПараметрыВозврата.ПлатежнаяСистема, Символы.НПП, ""),
			Неопределено));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("qrId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыВозврата.ИдентификаторСПБ);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("refundOperationId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(Строка(ПараметрыВозврата.ИдентификаторыОперации));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("paymentId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыВозврата.ИдентификаторПлатежнойСистемы);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции:
// sbp/members/{memberId}/refunds/create-refund.
//
Функция ПрочитатьДанные_refunds_create_refund(ТелоJSON)
	
	// Ответ сервиса:
	//  operationDate - дата операции в платежной системе;
	//  refundId - идентификатор операции в платежной системе;
	//  refundStatus - статус возврата;
	//  confirmation - данные для подтверждения возврата;
	// 
	//{
	//  "confirmation": {
	//    "accountNumber": "string",
	//    "fullName": "string",
	//    "phone": "string",
	//    "state": "string"
	//  },
	//  "operationDate": "2020-09-08T09:47:19.258Z",
	//  "refundId": "string",
	//  "refundStatus": "SUCCESS",
	//  "refundStatusDescription": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ИменаСвойствСоЗначениямиДата = Новый Массив;
	ИменаСвойствСоЗначениямиДата.Добавить("operationDate");
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(
		ЧтениеОтвета,
		,
		ИменаСвойствСоЗначениямиДата,
		,
		"ВосстановитьДатуJSON",
		СервисИнтеграцииССБП,
		,
		ИменаСвойствСоЗначениямиДата);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/refunds/{refundId}/confirm-refund

// Отправляет подтверждение возврата и получает статус возврат оплаты в платежной системе.
//
// Параметры:
//  ИдентификаторПлатежнойСистемы - Строка - идентификатор возврата в платежной системе;
//  ИдентификаторСессии - Строка - сессия возврата в платежной системе;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции.
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на оплату в платежной системе:
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим методом;
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    * СтатусВозврата - Строка - состояние возврата в платежной системе;
//    * ДатаОперации - Дата - дата регистрации операции в платежной системе;
//    * СуммаОперации - Число - фактическая сумма возврата по документу.
//
Функция ОперацияПодтверждениеВозврата(
		ИдентификаторыОперации,
		ИдентификаторСессии,
		ПараметрыИнтеграции)
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Подтверждение заказа на возврат в платежной системе.'"),
		Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("СтатусВозврата", "");
	РезультатОперации.Вставить("ДатаОперации", Дата(1, 1, 1));
	РезультатОперации.Вставить("СуммаОперации", Неопределено);
	РезультатОперации.Вставить("ИдентификаторВозврата", "");
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/refunds/confirm-refund",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема),
			ИдентификаторыОперации.ИдентификаторПлатежнойСистемы),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ИдентификаторСессии);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовОперацииПодтверждениеВозврата(
		ПараметрыИнтеграции,
		ИдентификаторСессии,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовОперацииПодтверждениеВозврата(
			ПараметрыИнтеграции,
			ИдентификаторСессии,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось подтвердить заказ на возврат в платежной системе.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При подтверждении заказа на возврат сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеВозврата = ПрочитатьДанные_refunds_confirm_refund(
		РезультатОтправки.Содержимое);
	
	РезультатОперации.СтатусВозврата        = ДанныеВозврата.sbpMemberResponse.refundStatus;
	РезультатОперации.ДатаОперации          = ДанныеВозврата.sbpMemberResponse.operationDate;
	РезультатОперации.СуммаОперации         = ДанныеВозврата.sbpMemberResponse.amount;
	РезультатОперации.ИдентификаторВозврата = ИдентификаторПлатежнойСистемыБезСлужебныхСимволов(
		ДанныеВозврата.sbpMemberResponse.refundId);
	
	Если РезультатОперации.СтатусВозврата = ИдентификаторСтатусаОтменена() Тогда
		
		ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось подтвердить заказ на возврат в платежной системе.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|Операция была отменена.'"),
			ДанныеВозврата.sbpMemberResponse.refundStatusDescription);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ИнформацияОбОшибке,
			Истина);
		
	Иначе
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			НСтр("ru = 'Завершено подтверждение заказа на возврат в платежной системе.'"),
			Ложь);
		
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции подтверждения возврата оплаты.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ИдентификаторСессии - Строка - данные сессии возврата;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат подтверждения возврата в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовОперацииПодтверждениеВозврата(
		ПараметрыИнтеграции,
		ИдентификаторСессии,
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = refunds_confirm_refund(
		ИдентификаторСессии,
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	// Вызов операции выполняется в 6 итераций. Повторные вызовы операции требуются
	// для случаев, когда результат выполнения возврата не определен. Обычно операция возврата
	// не занимает много времени, но в ряде случаев время обработки может быть увеличено.
	// Между вызовами добавляется пауза, чтобы не нагружать сервис банка.
	//
	// Паузы между вызовами:
	// - между 2-м и 3-м вызовом делается пауза 3 секунды;
	// - между 3-м и 4-м вызовом делается пауза 10 секунд;
	// - между 4-м и 5-м вызовом делается пауза 15 секунд;
	// - между 5-м и 6-м вызовом делается пауза 15 секунд; 
	
	КоличествоОпераций = 0;
	Пока КоличествоОпераций <= 5 Цикл
		
		ПараметрыОтправки = Новый Структура;
		ПараметрыОтправки.Вставить("Метод"                   , "POST");
		ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
		ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
		ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
		ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
		ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
		ПараметрыОтправки.Вставить("Таймаут"                 , 30);
		
		РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
			URLОперации,
			,
			,
			ПараметрыОтправки);
		
		Если РезультатОтправки.КодСостояния < 500
			И РезультатОтправки.КодСостояния <> 0 Тогда
			Прервать;
		КонецЕсли;
		
		// Перед выполнением повторного вызова при работе в модели
		// сервиса необходимо получить новый тикет.
		Если ОбщегоНазначения.РазделениеВключено() Тогда
			
			РезультатИПП = ДанныеАутентификации(URLОперации);
			Если РезультатИПП.Ошибка Тогда
				Прервать;
			КонецЕсли;
			
			ПараметрыЗапросаJSON = refunds_confirm_refund(
				ИдентификаторСессии,
				ДанныеАутентификации,
				ЗаголовокАутентификации,
				ПараметрыИнтеграции.ТорговаяТочка);
			
		КонецЕсли;
		
		КоличествоОпераций = КоличествоОпераций + 1;
		
		Если КоличествоОпераций = 2 Тогда
			ИнтернетПоддержкаПользователей.Пауза(3);
		ИначеЕсли КоличествоОпераций = 3 Тогда
			ИнтернетПоддержкаПользователей.Пауза(5);
		ИначеЕсли КоличествоОпераций = 4 Тогда
			ИнтернетПоддержкаПользователей.Пауза(10);
		ИначеЕсли КоличествоОпераций = 5 Тогда
			ИнтернетПоддержкаПользователей.Пауза(15);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатОтправки;
	
КонецФункции

// Формирует параметры запроса для операции
// sbp/members/{memberId}/refunds/{refundId}/confirm-refund.
//
Функция refunds_confirm_refund(
		ИдентификаторСессии,
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	// Параметры возврата.
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("sbpMemberRequest");
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("state");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторСессии);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции:
// sbp/members/{memberId}/refunds/create-refund.
//
Функция ПрочитатьДанные_refunds_confirm_refund(ТелоJSON)
	
	// Ответ сервиса:
	//  operationDate - дата операции в платежной системе;
	//  refundStatus - статус возврата;
	//
	// {
	//  "operationDate": "2020-09-08T13:09:20.093Z",
	//  "refundStatus": "SUCCESS",
	//  "refundStatusDescription": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ИменаСвойствСоЗначениямиДата = Новый Массив;
	ИменаСвойствСоЗначениямиДата.Добавить("operationDate");
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(
		ЧтениеОтвета,
		,
		ИменаСвойствСоЗначениямиДата,
		,
		"ВосстановитьДатуJSON",
		СервисИнтеграцииССБП,
		,
		ИменаСвойствСоЗначениямиДата);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/refund/{refundId}/get-refund

// Получает статус возврат оплаты в платежной системе.
//
// Параметры:
//  ИдентификаторПлатежнойСистемы - Строка - идентификатор возврата в платежной системе;
//  ДатаЗапросаСтатуса - Дата - дата последнего запроса статуса оплаты;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//    см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ЗапросНаличияСтатуса - Булево - если Истина, будет выполнен запрос наличия статуса;
//  ИдентификаторОперации - Строка - идентификатор операции проверки наличия статуса;
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на оплату в платежной системе:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом;
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    * СтатусВозврата - Строка - состояние возврата в платежной системе.
//    * ИдентификаторОперации - Строка - идентификатор возврата платежной системы;
//    * ДатаОперации - Дата - дата регистрации операции в платежной системе;
//    * СуммаОперации - Дата - фактическая сумма возврата по документу.
//
Функция ОперацияСтатусВозврата(
		ИдентификаторПлатежнойСистемы,
		ДатаЗапросаСтатуса,
		ПараметрыИнтеграции,
		ИдентификаторОперацииПроверки,
		ЗапросНаличияСтатуса = Истина)
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("СтатусВозврата", "");
	РезультатОперации.Вставить("ДатаОперации", Дата(1, 1, 1));
	РезультатОперации.Вставить("СуммаОперации", Неопределено);
	РезультатОперации.Вставить("ИдентификаторОперации", "");
	РезультатОперации.Вставить("ОбновитьДатуЗапросаСтатуса", Ложь);
	
	Если ЗапросНаличияСтатуса Тогда
		
		Если Не ЗначениеЗаполнено(ИдентификаторПлатежнойСистемы) Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйФорматЗапроса();
			РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Не заполнен идентификатор оплаты.'");
			РезультатОперации.ИнформацияОбОшибке = РезультатОперации.СообщениеОбОшибке;
			
			ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				Строка(РезультатОперации.ИнформацияОбОшибке),
				Истина);
			
			Возврат РезультатОперации;
		КонецЕсли;
		
		ИдентификаторыВозвратов = НовыйИдентификаторыПроверкиНаличияСтатуса();
		
		ДанныеВозврата = ИдентификаторыВозвратов.Добавить();
		ДанныеВозврата.Идентификатор = ИдентификаторПлатежнойСистемы;
		ДанныеВозврата.ДатаЗапросаСтатуса = ДатаЗапросаСтатуса;
		
		РезультатПроверки = ОперацияПроверкаНаличияСтатусаОперацийВозвратов(
			ИдентификаторыВозвратов,
			ПараметрыИнтеграции.ПлатежнаяСистема,
			ИдентификаторОперацииПроверки);
		
		Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				РезультатПроверки,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
			Возврат РезультатОперации;
		КонецЕсли;
		
		Если РезультатПроверки.Идентификаторы.Найти(ИдентификаторПлатежнойСистемы) = Неопределено Тогда
			РезультатОперации.СтатусВозврата = ИдентификаторСтатусаВПроцессе();
			Возврат РезультатОперации;
		КонецЕсли;
		
	КонецЕсли;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Проверка статуса заказа на оплату в платежной системе.'"),
		Ложь);
	
	РезультатОперации.ОбновитьДатуЗапросаСтатуса = Истина;
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/refunds/%2/get-refund",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема),
			ИдентификаторПлатежнойСистемы),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ИдентификаторПлатежнойСистемы);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовОперацииСтатусВозврата(
		ПараметрыИнтеграции,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовОперацииСтатусВозврата(
			ПараметрыИнтеграции,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить статус оплаты в платежной системе.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При получении статуса оплаты сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеСтатуса = ПрочитатьДанные_get_refund(РезультатОтправки.Содержимое);
	
	РезультатОперации.СтатусВозврата = ДанныеСтатуса.sbpMemberResponse.refundStatus;
	РезультатОперации.ДатаОперации   = ДанныеСтатуса.sbpMemberResponse.operationDate;
	РезультатОперации.СуммаОперации  = ДанныеСтатуса.sbpMemberResponse.amount;
	РезультатОперации.ИдентификаторОперации = ДанныеСтатуса.sbpMemberResponse.paymentId;
	
	Если ДанныеСтатуса.sbpMemberResponse.refundStatus = ИдентификаторСтатусаОтменена() Тогда
		
		Если ДанныеСтатуса.sbpMemberResponse.Свойство("refundStatusDescription") Тогда
			ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось получить статус возврата в платежной системе.
					|
					|%1
					|
					|Техническая информация об ошибке:
					|Операция была отменена.'"),
				ДанныеСтатуса.sbpMemberResponse.refundStatusDescription);
		Иначе
			ИнформацияОбОшибке = НСтр("ru = 'Не удалось получить статус возврата в платежной системе.
				|
				|Причина отмены операции не указана.
				|
				|Техническая информация об ошибке:
				|Операция была отменена.'");
		КонецЕсли;
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ИнформацияОбОшибке,
			Истина);

	Иначе
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			НСтр("ru = 'Проверка статуса заказа на оплату в платежной системе завершена.'"),
			Ложь);
		
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции определения статуса возврата оплаты.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат статуса возврата в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовОперацииСтатусВозврата(
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = get_refund(
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/refund/{refundId}/get-refund.
//
Функция get_refund(
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции:
// /sbp/v1/members/{memberId}/refund/{refundId}/get-refund.
//
Функция ПрочитатьДанные_get_refund(ТелоJSON)
	
	// Ответ сервиса:
	//  operationDate - дата операции в платежной системе;
	//  refundStatus - статус возврата;
	// 
	// {
	//  "operationDate": "string",
	//  "refundStatus": "SUCCESS",
	//  "refundStatusDescription": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ИменаСвойствСоЗначениямиДата = Новый Массив;
	ИменаСвойствСоЗначениямиДата.Добавить("operationDate");
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(
		ЧтениеОтвета,
		,
		ИменаСвойствСоЗначениямиДата,
		,
		"ВосстановитьДатуJSON",
		СервисИнтеграцииССБП,
		,
		ИменаСвойствСоЗначениямиДата);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции GET /sbp/v1/members/infos

// Получает идентификаторы участников СБП из сервиса.
//
// Возвращаемое значение:
//  Массив из Структура - загруженные настройки.
//
Функция ОперацияНастройкиУчастниковСБП()
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Загрузка идентификаторов платежных систем СБП.'"),
		Ложь);
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		"/members/infos?c2bSupported=true",
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "GET");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить об идентификаторах платежных систем СБП.
				|
				|Техническая информация об ошибке:
				|При получении информации об идентификаторах платежных систем СБП возникли ошибки.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(ИнформацияОбОшибке),
			Истина);
		
		ВызватьИсключение ИнформацияОбОшибке;
		
	КонецЕсли;
	
	Идентификаторы = ПрочитатьДанные_sbp_members(РезультатОтправки.Содержимое);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Завершена  загрузка идентификаторов платежных систем СБП.'"),
		Ложь);
	
	Возврат Идентификаторы;
	
КонецФункции

// Чтение ответа операции:
// /attachments/sbp/sbp-members.json.
//
Функция ПрочитатьДанные_sbp_members(ТелоJSON)
	
	// Ответ сервиса:
	//  id - идентификатор платежной системы;
	//  name - наименование платежной системы.
	// 
	// [
	//  {
	//    "memberId": "string",
	//    "memberName": "string",
	//    "biks": [
	//      "string"
	//    ],
	//    "integrationEnabled": true,
	//    "personalAccountPageUrl": "string",
	//    "attentionText": "string",
	//    "c2bSupported": true,
	//    "c2bVerified": true,
	//    "c2bIntegrationSupported": true
	//  }
	// ]
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ВызовОперацийСверкиВзаиморасчетов

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/turnovers/request-report

// Получает данные отчета по сверке взаиморасчетов из сервиса платежной системы.
//
// Параметры:
//  НачалоПериода - Дата - дата начало выборки данных отчета;
//  КонецПериода - Дата - дата окончания выборки данных отчета;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции.
//
// Возвращаемое значение:
//  Структура - данные сверки взаиморасчетов:
//    *Идентификатор - Строка - идентификатор отчета;
//    *URLФайла - Строка - url файла для загрузки данных;
//    *СуммаВозвратов - Число - общая сумма возвратов за период по торговой точке;
//    *СуммаОплат - Число - общая сумма оплат за период по торговой точке;
//    *СуммаКомиссии - Число - рассчитанная сумма возвратов за период по торговой точке;
//    *Статус - Строка - текущее состояние формирования отчета;
//    *ТипОшибки - Строка - вид ошибки формирования отчета;
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                 может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияЗапросОтчетаПоОборотам(
		НачалоПериода,
		КонецПериода,
		ПараметрыИнтеграции)
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Запрос отчета сверки взаиморасчетов по операциям.'"),
		Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("Идентификатор", "");
	РезультатОперации.Вставить("URLФайла", "");
	РезультатОперации.Вставить("СуммаВозвратов", 0);
	РезультатОперации.Вставить("СуммаОплат", 0);
	РезультатОперации.Вставить("СуммаКомиссии", 0);
	РезультатОперации.Вставить("Статус", "");
	РезультатОперации.Вставить("ТипОшибки", "");
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/reports/reconciliation-of-settlements/turnovers/request-report",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема)),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ПараметрыИнтеграции.ТорговаяТочка);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовОперацииЗапросОтчетаПоОборотам(
		ПараметрыИнтеграции,
		НачалоПериода,
		КонецПериода,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовОперацииЗапросОтчетаПоОборотам(
			ПараметрыИнтеграции,
			НачалоПериода,
			КонецПериода,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить запрос отчета по сверке взаиморасчетов.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При запросе отчета по сверке взаиморасчетов сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеОтвета = ПрочитатьДанные_turnovers_request_report(
		РезультатОтправки.Содержимое);
	
	РезультатОперации.Идентификатор = ДанныеОтвета.sbpMemberResponse.reportId;
	РезультатОперации.URLФайла = ДанныеОтвета.sbpMemberResponse.resultURL;
	РезультатОперации.Статус = ДанныеОтвета.sbpMemberResponse.reportState.status;
	
	Если ДанныеОтвета.sbpMemberResponse.reportState.Свойство("errorType") Тогда
		РезультатОперации.ТипОшибки = ДанныеОтвета.sbpMemberResponse.reportState.errorType;
	КонецЕсли;
	
	Если ДанныеОтвета.sbpMemberResponse.result <> Неопределено Тогда
		РезультатОперации.СуммаВозвратов = ДанныеОтвета.sbpMemberResponse.result.refundAmountSum;
		РезультатОперации.СуммаОплат = ДанныеОтвета.sbpMemberResponse.result.paymentAmountSum;
		РезультатОперации.СуммаКомиссии = ДанныеОтвета.sbpMemberResponse.result.feeSum;
	КонецЕсли;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Запрос отчета по оборотам в платежной системе завершен.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции запроса отчета по оборотам.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  НачалоПериода - Дата - дата начало выборки данных отчета;
//  КонецПериода - Дата - дата окончания выборки данных отчета;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат запроса отчета по оборотам в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовОперацииЗапросОтчетаПоОборотам(
		ПараметрыИнтеграции,
		НачалоПериода,
		КонецПериода,
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = turnovers_request_report(
		НачалоПериода,
		КонецПериода,
		ПараметрыИнтеграции.ТорговаяТочка,
		ДанныеАутентификации,
		ЗаголовокАутентификации);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/turnovers/request-report.
//
Функция turnovers_request_report(
		НачалоПериода,
		КонецПериода,
		ТорговаяТочка,
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	// Параметры отчета.
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("sbpMemberRequest");
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("endDate");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(
		ЗаписатьДатуJSON(
			КонецПериода,
			ФорматДатыJSON.ISO,
			ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("beginDate");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(
		ЗаписатьДатуJSON(
			НачалоПериода,
			ФорматДатыJSON.ISO,
			ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("merchantId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ТорговаяТочка);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции:
// /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/turnovers/request-report.
//
Функция ПрочитатьДанные_turnovers_request_report(ТелоJSON)
	
	// Ответ сервиса:
	//  reportId - идентификатор отчета;
	//  status - статус формирования отчета;
	//  statusDate - дата отчета;
	//  feeSum - сумма комиссии;
	//  paymentAmountSum - сумма оплат;
	//  refundAmountSum - сумма возвратов.
	// 
	// {
	//  "reportId": "string",
	//  "reportState": {
	//    "errorType": "INTERNAL_ERROR",
	//    "status": "PENDING",
	//    "statusDate": "2020-09-11T07:02:18.409Z"
	//  },
	//  "result": {
	//    "feeSum": 0,
	//    "paymentAmountSum": 0,
	//    "refundAmountSum": 0
	//  },
	//  "resultURL": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/turnovers/{reportId}/get-report

// Получает статус отчета по сверке взаиморасчетов из сервиса платежной системы.
//
// Параметры:
//  Идентификатор - Строка - идентификатор отчета;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДатаЗапросаСтатуса - Дата - для определения периодичности получения статуса
//                       формирования отчета.
//
// Возвращаемое значение:
//  Структура - данные сверке взаиморасчетов:
//    *Идентификатор - Строка - идентификатор отчета;
//    *URLФайла - Строка - url файла для загрузки данных;
//    *СуммаВозвратов - Число - общая сумма возвратов за период по торговой точке;
//    *СуммаОплат - Число - общая сумма оплат за период по торговой точке;
//    *СуммаКомиссии - Число - рассчитанная сумма возвратов за период по торговой точке;
//    *Статус - Строка - текущее состояние формирования отчета;
//    *ТипОшибки - Строка - вид ошибки формирования отчета;
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                 может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияЗапросСтатусаОтчетаПоОборотам(
		Идентификатор,
		ПараметрыИнтеграции,
		ДатаЗапросаСтатуса)
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Запрос статуса отчета по сверке оборотов.'"),
		Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("URLФайла", "");
	РезультатОперации.Вставить("СуммаВозвратов", 0);
	РезультатОперации.Вставить("СуммаОплат", 0);
	РезультатОперации.Вставить("СуммаКомиссии", 0);
	РезультатОперации.Вставить("Статус", "");
	РезультатОперации.Вставить("ТипОшибки", "");
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/status-manager/reports/reconciliation-of-settlements/turnovers/%2/states/test-check-needed",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема),
			Новый УникальныйИдентификатор),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатПроверки = ОперацияПроверкаНаличияСтатусаОперации(
		URLОперации,
		ДатаЗапросаСтатуса);
	
	Если Не РезультатПроверки.ЗапроситьСтатус Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатПроверки,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Если Не ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			РезультатОперации.Статус = ИдентификаторСтатусаОжидание();
		КонецЕсли;
		Возврат РезультатОперации;
	КонецЕсли;
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/reports/reconciliation-of-settlements/turnovers/%2/get-report",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема),
			Идентификатор),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(Идентификатор);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовОперацииЗапросСтатусаОтчетаПоОборотам(
		ПараметрыИнтеграции,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовОперацииЗапросСтатусаОтчетаПоОборотам(
			ПараметрыИнтеграции,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить статус отчета по сверке взаиморасчетов.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При запросе отчета по сверке взаиморасчетов сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеОтвета = ПрочитатьДанные_turnovers_get_report(
		РезультатОтправки.Содержимое);
	
	РезультатОперации.URLФайла = ДанныеОтвета.sbpMemberResponse.resultURL;
	РезультатОперации.Статус = ДанныеОтвета.sbpMemberResponse.reportState.status;
	
	Если ДанныеОтвета.sbpMemberResponse.reportState.Свойство("errorType") Тогда
		РезультатОперации.ТипОшибки = ДанныеОтвета.sbpMemberResponse.reportState.errorType;
	КонецЕсли;
	
	Если ДанныеОтвета.sbpMemberResponse.result <> Неопределено Тогда
		РезультатОперации.СуммаВозвратов = ДанныеОтвета.sbpMemberResponse.result.refundAmountSum;
		РезультатОперации.СуммаОплат = ДанныеОтвета.sbpMemberResponse.result.paymentAmountSum;
		РезультатОперации.СуммаКомиссии = ДанныеОтвета.sbpMemberResponse.result.feeSum;
	КонецЕсли;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Запрос статуса отчета по оборотам в платежной системе завершен.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции получения статуса отчета по оборотам.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат статуса отчета по оборотам в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовОперацииЗапросСтатусаОтчетаПоОборотам(
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = turnovers_get_report(
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/turnovers/{reportId}/get-report
//
Функция turnovers_get_report(
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции:
// /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/turnovers/{reportId}/get-report.
//
Функция ПрочитатьДанные_turnovers_get_report(ТелоJSON)
	
	// Ответ сервиса:
	//  reportId - идентификатор отчета;
	//  status - статус формирования отчета;
	//  statusDate - дата отчета;
	//  feeSum - сумма комиссии;
	//  paymentAmountSum - сумма оплат;
	//  refundAmountSum - сумма возвратов.
	// 
	// {
	//  "reportId": "string",
	//  "reportState": {
	//    "errorType": "INTERNAL_ERROR",
	//    "status": "PENDING",
	//    "statusDate": "2020-09-11T07:02:18.409Z"
	//  },
	//  "result": {
	//    "feeSum": 0,
	//    "paymentAmountSum": 0,
	//    "refundAmountSum": 0
	//  },
	//  "resultURL": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции загрузки файла отчетов сверки оборотов.

// Выполняет загрузку файла отчета по переданному url.
//
// Параметры:
//  URLОперации - Строка - url файла отчета;
//  Идентификатор - Строка - идентификатор отчета;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//
// Возвращаемое значение:
//  Структура - данные сверке взаиморасчетов:
//    *СуммаВозвратов - Число - общая сумма возвратов за период по торговой точке;
//    *СуммаОплат - Число - общая сумма оплат за период по торговой точке;
//    *СуммаКомиссии - Число - рассчитанная сумма возвратов за период по торговой точке;
//    *СтатусОперации - Строка - текущее состояние операции оплаты:
//                  - "Выполняется" - подтверждение оплаты от платежной системы не получено;
//                  - "Отменена" - оплата по оплата по ранее сформированному QR-коду невозможна;
//                  - "Выполнена" - платежная система подтвердила оплату;
//                  - "ТребуетсяПодтверждение" - необходимо подтвердить действие;
//                  - "Ошибка" - не удалось выполнить проверку оплаты из-за ошибки.
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                 может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияЗагрузитьФайлОтчетаПоСверкеОборотов(URLОперации, Идентификатор, ПараметрыИнтеграции)
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("СуммаВозвратов", 0);
	РезультатОперации.Вставить("СуммаОплат", 0);
	РезультатОперации.Вставить("СуммаКомиссии", 0);
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(Идентификатор);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	РезультатОтправки = ВызовЗагрузитьФайлОтчетаПоСверкеОборотов(
		ПараметрыИнтеграции,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовЗагрузитьФайлОтчетаПоСверкеОборотов(
			ПараметрыИнтеграции,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить загрузку отчета по сверке взаиморасчетов.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При загрузке файла отчета по сверке взаиморасчетов сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеОтвета = ПрочитатьДанныеДанныеОтчетаПоСверкеОборотов(
		РезультатОтправки.Содержимое);
	Если ДанныеОтвета.result <> Неопределено Тогда
		РезультатОперации.СуммаВозвратов = ДанныеОтвета.result.refundAmountSum;
		РезультатОперации.СуммаОплат = ДанныеОтвета.result.paymentAmountSum;
		РезультатОперации.СуммаКомиссии = ДанныеОтвета.result.feeSum;
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет загрузку файла отчета по переданному url.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат статуса отчета по оборотам в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовЗагрузитьФайлОтчетаПоСверкеОборотов(
		ПараметрыИнтеграции,
		ЗаголовокАутентификации,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-Correlation-ID", Строка(Новый УникальныйИдентификатор));
	
	// Особенность обработки загрузки файлов PayMaster.
	Если ПараметрыИнтеграции.ПлатежнаяСистема <> Перечисления.ПлатежныеСистемы.СБП_PayMaster Тогда
		Заголовки.Вставить("Authorization", ЗаголовокАутентификации);
	КонецЕсли;
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "GET");
	ПараметрыОтправки.Вставить("Заголовки"            ,     Заголовки);
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 2);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Чтение файла сверке взаиморасчетов по оборотам.
//
Функция ПрочитатьДанныеДанныеОтчетаПоСверкеОборотов(ДанныеФайла)
	
	// Ответ сервиса:
	//  endDate - дата окончания формирования отчета;
	//  beginDate - дата начало формирования отчета;
	//  merchantId - идентификатор торговой точки;
	//  feeSum - сумма комиссии;
	//  paymentAmountSum - сумма оплат;
	//  refundAmountSum - сумма возвратов;
	//  reportDate - дата отчета;
	// 
	// {
	//   "request": {
	//      "endDate": "2020-06-24T09:14:38.107Z",
	//      "beginDate": "2020-06-25T09:14:38.107Z",
	//      "merchantId": "cb4c52a6-3032-40c2-a113-66c31f26cffb"
	//    }
	//  "result": {
	//      "refundAmountSum": 9103.01,
	//      "feeSum": 364.12,
	//      "paymentAmountSum": 9103.01,
	//    },
	// "reportDate": "2020-06-24T09:14:38.107Z"
	// } 
	
	ТекстЖурналРегистрации = НСтр("ru = 'Загружен файл отчета по сверке оборотов'");
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	Попытка
		Поток = ДанныеФайла.ОткрытьПотокДляЧтения();
		ЧтениеZip = Новый ЧтениеZipФайла(Поток);
		Элемент = ЧтениеZip.Элементы.Найти("report.json");
		Если Элемент = Неопределено Тогда
			ВызватьИсключение НСтр("ru = 'Архив отчета по оборотам не содержит файл report.json.'");
		КонецЕсли;
		Каталог = ФайловаяСистема.СоздатьВременныйКаталог("json");
		ЧтениеZip.Извлечь(Элемент, Каталог);
		ЧтениеZip.Закрыть();
	Исключение
		ФайловаяСистема.УдалитьВременныйКаталог(Каталог);
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		ВызватьИсключение НСтр("ru = 'Не удалось обработать архив отчета по сверке оборотов. Подробности в журнале регистрации.'");
	КонецПопытки;
	
	Попытка
		ФайлОтчета = Новый Файл(Каталог + "report.json");
		Если ФайлОтчета.ПолучитьТолькоЧтение() Тогда
			ФайлОтчета.УстановитьТолькоЧтение(Ложь);
		КонецЕсли;
	Исключение
		ФайловаяСистема.УдалитьВременныйКаталог(Каталог);
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		ВызватьИсключение НСтр("ru = 'Не удалось обработать файл отчета по сверке оборотов. Подробности в журнале регистрации.'");
	КонецПопытки;
	
	Попытка
		ЧтениеОтвета = Новый ЧтениеJSON;
		ЧтениеОтвета.ОткрытьФайл(Каталог + "report.json");
		Результат = ПрочитатьJSON(ЧтениеОтвета);
		ЧтениеОтвета.Закрыть();
	Исключение
		ФайловаяСистема.УдалитьВременныйКаталог(Каталог);
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		ВызватьИсключение НСтр("ru = 'Не удалось обработать файл отчета по сверке оборотов. Подробности в журнале регистрации.'");
	КонецПопытки;
	
	ФайловаяСистема.УдалитьВременныйКаталог(Каталог);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/operations/request-report

// Получает данные отчета по сверке взаиморасчетов по операциям из сервиса платежной системы.
//
// Параметры:
//  НачалоПериода - Дата - дата начало выборки данных отчета;
//  КонецПериода - Дата - дата окончания выборки данных отчета;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции.
//
// Возвращаемое значение:
//  Структура - данные сверке взаиморасчетов:
//    *Идентификатор - Строка - идентификатор отчета;
//    *URLФайла - Строка - url файла для загрузки данных;
//    *Статус - Строка - текущее состояние формирования отчета;
//    *ТипОшибки - Строка - вид ошибки формирования отчета;
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                 может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияЗапросОтчетаПоСверкеОпераций(
		НачалоПериода,
		КонецПериода,
		ПараметрыИнтеграции)
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Запрос отчета по сверке операций.'"),
		Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("Идентификатор", "");
	РезультатОперации.Вставить("URLФайла",      "");
	РезультатОперации.Вставить("Статус",        "");
	РезультатОперации.Вставить("ТипОшибки",     "");
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/reports/reconciliation-of-settlements/operations/request-report",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема)),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(ПараметрыИнтеграции.ТорговаяТочка);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовОперацииЗапросОтчетаПоСверкеОпераций(
		ПараметрыИнтеграции,
		НачалоПериода,
		КонецПериода,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовОперацииЗапросОтчетаПоСверкеОпераций(
			ПараметрыИнтеграции,
			НачалоПериода,
			КонецПериода,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить запрос отчета сверке взаиморасчетов по операциям.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При запросе отчета сверке взаиморасчетов по операциям сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеОтвета = ПрочитатьДанные_operations_request_report(
		РезультатОтправки.Содержимое);
	
	РезультатОперации.Идентификатор = ДанныеОтвета.sbpMemberResponse.reportId;
	РезультатОперации.URLФайла = ДанныеОтвета.sbpMemberResponse.resultURL;
	РезультатОперации.Статус = ДанныеОтвета.sbpMemberResponse.reportState.status;
	
	Если ДанныеОтвета.sbpMemberResponse.reportState.Свойство("errorType") Тогда
		РезультатОперации.ТипОшибки = ДанныеОтвета.sbpMemberResponse.reportState.errorType;
	КонецЕсли;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Запрос отчета по оборотам в платежной системе завершен.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции запроса отчета по операциям.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  НачалоПериода - Дата - дата начало выборки данных отчета;
//  КонецПериода - Дата - дата окончания выборки данных отчета;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат запроса отчета по операциям в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовОперацииЗапросОтчетаПоСверкеОпераций(
		ПараметрыИнтеграции,
		НачалоПериода,
		КонецПериода,
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = operations_request_report(
		НачалоПериода,
		КонецПериода,
		ПараметрыИнтеграции.ТорговаяТочка,
		ДанныеАутентификации,
		ЗаголовокАутентификации);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/operations/request-report.
//
Функция operations_request_report(
		НачалоПериода,
		КонецПериода,
		ТорговаяТочка,
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	// Параметры отчета.
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("sbpMemberRequest");
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("endDate");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(
		ЗаписатьДатуJSON(
			КонецПериода,
			ФорматДатыJSON.ISO,
			ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("beginDate");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(
		ЗаписатьДатуJSON(
			НачалоПериода,
			ФорматДатыJSON.ISO,
			ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением));
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("merchantId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ТорговаяТочка);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции:
// /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/operations/request-report.
//
Функция ПрочитатьДанные_operations_request_report(ТелоJSON)
	
	// Ответ сервиса:
	//  reportId - идентификатор отчета;
	//  status - статус формирования отчета;
	//  statusDate - дата отчета;
	//  resultURL - url размещения отчета для загрузки;
	// 
	// {
	//  "reportId": "string",
	//  "reportState": {
	//    "errorType": "INTERNAL_ERROR",
	//    "status": "PENDING",
	//    "statusDate": "2020-09-11T07:02:18.409Z"
	//  },
	//  "resultURL": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции POST /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/operations/{reportId}/get-report

// Получает статус отчета по сверке операций из сервиса платежной системы.
//
// Параметры:
//  Идентификатор - Строка - идентификатор отчета;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДатаЗапросаСтатуса - Дата - для определения периодичности получения статуса
//                       формирования отчета.
//
// Возвращаемое значение:
//  Структура - данные сверке взаиморасчетов:
//    *URLФайла - Строка - url файла для загрузки данных;
//    *Статус - Строка - текущее состояние формирования отчета;
//    *ТипОшибки - Строка - вид ошибки формирования отчета;
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                 может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияЗапросСтатусаОтчетаПоОперациям(
		Идентификатор,
		ПараметрыИнтеграции,
		ДатаЗапросаСтатуса)
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Запрос статуса отчета по сверке операций.'"),
		Ложь);
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("Статус",    "");
	РезультатОперации.Вставить("URLФайла",  "");
	РезультатОперации.Вставить("ТипОшибки", "");
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/status-manager/reports/reconciliation-of-settlements/operations/%2/states/test-check-needed",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема),
			Идентификатор),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатПроверки = ОперацияПроверкаНаличияСтатусаОперации(
		URLОперации,
		ДатаЗапросаСтатуса);
	
	Если Не РезультатПроверки.ЗапроситьСтатус Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатПроверки,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Если Не ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			РезультатОперации.Статус = ИдентификаторСтатусаОжидание();
		КонецЕсли;
		Возврат РезультатОперации;
	КонецЕсли;
	
	URLОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/members/%1/reports/reconciliation-of-settlements/operations/%2/get-report",
			ИдентификаторУчастникаСБП(ПараметрыИнтеграции.ПлатежнаяСистема),
			Идентификатор),
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП());
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(Идентификатор);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	Если РезультатПлатежнаяСистема.Ошибка Тогда
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОтправки = ВызовОперацииЗапросСтатусаОтчетаПоСверкеОпераций(
		ПараметрыИнтеграции,
		РезультатИПП.ДанныеАутентификации,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи,
			Истина);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовОперацииЗапросСтатусаОтчетаПоСверкеОпераций(
			ПараметрыИнтеграции,
			РезультатИПП.ДанныеАутентификации,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить статус отчета по сверке операций.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При запросе отчета по сверке операций сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеОтвета = ПрочитатьДанные_operations_get_report(
		РезультатОтправки.Содержимое);
	
	РезультатОперации.URLФайла = ДанныеОтвета.sbpMemberResponse.resultURL;
	РезультатОперации.Статус = ДанныеОтвета.sbpMemberResponse.reportState.status;
	
	Если ДанныеОтвета.sbpMemberResponse.reportState.Свойство("errorType") Тогда
		РезультатОперации.ТипОшибки = ДанныеОтвета.sbpMemberResponse.reportState.errorType;
	КонецЕсли;
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Запрос статуса отчета по операциям в платежной системе завершен.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции получения статуса отчета по операциям.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  Структура - результат статуса отчета по операциям в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовОперацииЗапросСтатусаОтчетаПоСверкеОпераций(
		ПараметрыИнтеграции,
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		URLОперации)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = operations_get_report(
		ДанныеАутентификации,
		ЗаголовокАутентификации,
		ПараметрыИнтеграции.ТорговаяТочка);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , НовыйЗаголовкиВызоваОперации());
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	Возврат ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
КонецФункции

// Формирует параметры запроса для операции
// /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/operations/{reportId}/get-report
//
Функция operations_get_report(
		ДанныеАутентификации,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	// Данные аутентификации.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьДанныеАутентификации(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации);
	
	// Данные платежная система.
	ЗаписатьДанныеАутентификацииПлатежнойСистемы(
		ЗаписьДанныхСообщения,
		АутентификацияПлатежнаяСистема,
		ТорговаяТочка);
	
	// Параметры клиента.
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьПараметрыКлиента(
		ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции:
// /sbp/v1/members/{memberId}/reports/reconciliation-of-settlements/operations/{reportId}/get-report.
//
Функция ПрочитатьДанные_operations_get_report(ТелоJSON)
	
	// Ответ сервиса:
	//  reportId - идентификатор отчета;
	//  status - статус формирования отчета;
	//  refundAmountSum - сумма возвратов.
	// 
	// {
	//  "reportId": "string",
	//  "reportState": {
	//    "errorType": "INTERNAL_ERROR",
	//    "status": "PENDING",
	//    "statusDate": "2020-09-11T07:02:18.409Z"
	//  },
	//  "resultURL": "string"
	// }
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ сервиса интеграции с платежными системами:
			|%1'"),
		ТелоJSON);
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(ЧтениеОтвета);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции загрузки файла отчетов сверки операций.

// Выполняет загрузку файла отчета по переданному url.
//
// Параметры:
//  URLОперации - Строка - url файла отчета;
//  Идентификатор - Строка - идентификатор отчета;
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//
// Возвращаемое значение:
//  Структура - данные сверке взаиморасчетов:
//    *ДанныеОпераций - ТаблицаЗначений - см. НовыйДанныеОтчетаПоСверкеОпераций;
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                 может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияЗагрузитьФайлОтчетаПоСверкеОпераций(
			URLОперации,
			Идентификатор,
			ПараметрыИнтеграции)
	
	РезультатОперации = ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("ДанныеОпераций", ИнтеграцияСПлатежнымиСистемамиСлужебный.НовыйДанныеОтчетаПоСверкеОпераций());
	
	ВременныйКаталог = ФайловаяСистема.СоздатьВременныйКаталог("zip");
	АдресФайла = ВременныйКаталог + "report.zip";
	
	ДанныеДляПодписи = Новый Массив;
	Если ПлатежнаяСистемаИспользуетHMAC(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		ДанныеДляПодписи.Добавить(Идентификатор);
	КонецЕсли;
	
	РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
		ПараметрыИнтеграции,
		ДанныеДляПодписи);
	
	РезультатОтправки = ВызовЗагрузитьФайлОтчетаПоСверкеОпераций(
		ПараметрыИнтеграции,
		РезультатПлатежнаяСистема.Аутентификация,
		URLОперации,
		АдресФайла);
	
	// Обработка случаев устаревания токена аутентификации для способа
	// Resource Owner Password Credentials Grant.
	Если РезультатОтправки.КодСостояния = 401
		И ПлатежнаяСистемаИспользуетPasswordCredentialsGrant(ПараметрыИнтеграции.ПлатежнаяСистема) Тогда
		
		// Могла загрузиться только часть файла перед повторной
		// попыткой необходимо заново сгенерировать файл для
		// загрузки отчета.
		ФайловаяСистема.УдалитьВременныйКаталог(ВременныйКаталог);
		ВременныйКаталог = ФайловаяСистема.СоздатьВременныйКаталог("zip");
		АдресФайла = ВременныйКаталог + "report.zip";
		
		РезультатПлатежнаяСистема = ДанныеАутентификацииВПлатежнойСистеме(
			ПараметрыИнтеграции,
			ДанныеДляПодписи);
		
		Если РезультатПлатежнаяСистема.Ошибка Тогда
			РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатПлатежнаяСистема.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		РезультатОтправки = ВызовЗагрузитьФайлОтчетаПоСверкеОпераций(
			ПараметрыИнтеграции,
			РезультатПлатежнаяСистема.Аутентификация,
			URLОперации,
			АдресФайла);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить загрузку отчета по сверке взаиморасчетов операций.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При загрузке файла отчета по сверке взаиморасчетов операций сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ФайловаяСистема.УдалитьВременныйКаталог(ВременныйКаталог);
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ОперацииСервис = ПрочитатьДанныеДанныеОтчетаПоСверкеОпераций(АдресФайла);
	Для Каждого СтрокаСервис Из ОперацииСервис.operations Цикл
		СтрокаДанные = РезультатОперации.ДанныеОпераций.Добавить();
		СтрокаДанные.ТипОперации = ?(СтрокаСервис.type = "PAYMENT",
			ИнтеграцияСПлатежнымиСистемами.ИдентификаторТипаОперацииОплата(),
			ИнтеграцияСПлатежнымиСистемами.ИдентификаторТипаОперацииВозврат());
		СтрокаДанные.ДатаОперации = СтрокаСервис.operationDate;
		СтрокаДанные.Сумма = СтрокаСервис.amount;
		СтрокаДанные.СуммаКомиссии = СтрокаСервис.fee;
		СтрокаДанные.Идентификатор = СтрокаСервис.qrId;
		СтрокаДанные.ИдентификаторОплаты = СтрокаСервис.id;
	КонецЦикла;
	
	ФайловаяСистема.УдалитьВременныйКаталог(ВременныйКаталог);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет загрузку файла отчета по переданному url.
//
// Параметры:
//  ПараметрыИнтеграции - Структура - параметры выполнения операции
//                        см. ИнтеграцияСПлатежнымиСистемамиСлужебный.ПараметрыИнтеграции;
//  ДанныеАутентификации - Структура - данные аутентификации на Портале 1С:ИТС;
//  ЗаголовокПлатежнаяСистема - Строка - данные аутентификации в платежной системе;
//  URLОперации - Строка - url для вызова операции;
//  АдресФайла - Строка - адрес файла результата.
//
// Возвращаемое значение:
//  Структура - результат статуса отчета по оборотам в платежной системе:
//   *КодСостояния - Число - http код результата операции;
//   *КодОшибки - Строка - идентификатор ошибки вызова операции;
//   *Содержимое - Строка - ответа сервиса;
//
Функция ВызовЗагрузитьФайлОтчетаПоСверкеОпераций(
		ПараметрыИнтеграции,
		ЗаголовокАутентификации,
		URLОперации,
		АдресФайла)
	
	ПараметрыПодключения = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИнициализироватьПараметрыПодключения();
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-Correlation-ID", Строка(Новый УникальныйИдентификатор));
	Заголовки.Вставить("X-Merchant-Id", ПараметрыИнтеграции.ТорговаяТочка);
	
	// Особенности обработки загрузки файлов PayMaster.
	Если ПараметрыИнтеграции.ПлатежнаяСистема <> Перечисления.ПлатежныеСистемы.СБП_PayMaster Тогда
		Заголовки.Вставить("Authorization", ЗаголовокАутентификации);
	КонецЕсли;
	
	// 1. Вызов операции получения размера отчета
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "HEAD");
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("ИмяФайлаОтвета",           АдресФайла);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 15);
	
	РезультатОперации = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОперации.КодОшибки) Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	ЕдиницаИзмерения = РезультатОперации.Заголовки.Получить("Accept-Ranges");
	РазмерФайла      = Число(РезультатОперации.Заголовки.Получить("Content-Length"));
	
	// 2. Проверка заголовка Accept-Ranges. Клиент поддерживает обработку
	// только единицы измерения bytes. В случае передачи другого значения
	// будет вызвано исключение.
	// https://tools.ietf.org/html/rfc7233#section-2.3
	Если НРег(ЕдиницаИзмерения) = "none" Или НРег(ЕдиницаИзмерения) <> "bytes" Тогда
		ВызватьИсключение НСтр("ru = 'Неверная единица измерения (Accept-Ranges) порции файла.'");
	КонецЕсли;
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "GET");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 2);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// 3. Загрузка данных файла порциями по 4 МБ.
	// https://tools.ietf.org/html/rfc7233#section-3.1
	НачальнаяПозиция = 0;
	ТекущаяПозиция   = 0;
	РазмерПорции     = 4096; // 4 МБ
	
	ЗаписьДанных = Новый ЗаписьДанных(АдресФайла);
	
	Пока Истина Цикл
		
		Если НачальнаяПозиция >= РазмерФайла Тогда
			Прервать;
		ИначеЕсли (НачальнаяПозиция + РазмерПорции) > РазмерФайла Тогда
			ТекущаяПозиция = РазмерФайла;
		Иначе
			ТекущаяПозиция = ТекущаяПозиция + РазмерПорции;
		КонецЕсли;
		
		Порция = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"bytes=%1-%2",
			ПреобразоватьЧислоВФорматJSON(НачальнаяПозиция),
			ПреобразоватьЧислоВФорматJSON(ТекущаяПозиция));
		
		Заголовки.Вставить("X-Correlation-ID", Строка(Новый УникальныйИдентификатор));
		Заголовки.Вставить("Range", Порция);
		
		ПараметрыОтправки.Вставить("Заголовки" , Заголовки);
		
		РезультатОперации = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
			URLОперации,
			,
			,
			ПараметрыОтправки);
		
		Если ПустаяСтрока(РезультатОперации.КодОшибки) Тогда
			ЗаписьДанных.Записать(РезультатОперации.Содержимое);
		Иначе
			Прервать;
		КонецЕсли;
		
		НачальнаяПозиция = ТекущаяПозиция + 1;
		
	КонецЦикла;
	
	ЗаписьДанных.Закрыть();
	
	Возврат РезультатОперации;
	
КонецФункции

// Чтение файла сверке взаиморасчетов по оборотам.
//
Функция ПрочитатьДанныеДанныеОтчетаПоСверкеОпераций(АдресФайла)
	
	// Ответ сервиса:
	//  endDate - дата окончания формирования отчета;
	//  beginDate - дата начало формирования отчета;
	//  merchantId - идентификатор торговой точки;
	//  reportDate - дата отчета;
	//  type - тип операции;
	//  qrId - сумма оплат;
	//  id - идентификатор операции в платежной систем;
	//  amount - сумма операции;
	//  fee - сумма комиссии операции;
	//  currency - валюта операции;
	//  operationDate - дата операции в платежной системе;
	// 
	// {
	//  "request": {
	//      "endDate": "2020-06-24T09:14:38.107Z",
	//      "beginDate": "2020-06-25T09:14:38.107Z",
	//      "merchantId": "cb4c52a6-3032-40c2-a113-66c31f26cffb"
	//    }
	//  "operations": [
	//    {
	//      "type": "PAYMENT",
	//      "qrId": "AS10002LMSG5QQGE8K2B6588FFVJP1DP",
	//      "id": "dc6f3bb2-ef3e-4bfa-8653-e20a6e77a7e7",
	//      "amount": 9103.01,
	//      "fee": 364.12,
	//      "currency": "RUB",
	//      "operationDate": "2020-06-24T09:14:38.107Z"
	//    },
	//    {
	//      "type": "PAYMENT",
	//      "qrId": "AS10002LMSG5QQGE8K2B6588FFVJP1DP",
	//      "id": "dc6f3bb2-ef3e-4bfa-8653-e20a6e77a7e7",
	//      "amount": 9103.01,
	//      "fee": 364.12,
	//      "currency": "RUB",
	//      "operationDate": "2020-06-24T09:14:38.107Z"
	//    }
	//  ],
	// "reportDate": "2020-06-24T09:14:38.107Z"
	// } 
	
	ТекстЖурналРегистрации = НСтр("ru = 'Загружен файл отчета по сверке операций.'");
	
	ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	Каталог = ФайловаяСистема.СоздатьВременныйКаталог("json");
	
	Попытка
		ЧтениеZip = Новый ЧтениеZipФайла(АдресФайла);
		Элемент = ЧтениеZip.Элементы.Найти("report.json");
		Если Элемент = Неопределено Тогда
			ВызватьИсключение НСтр("ru = 'Архив отчета по оборотам не содержит файл report.json.'");
		КонецЕсли;
		ЧтениеZip.Извлечь(Элемент, Каталог);
		ЧтениеZip.Закрыть();
	Исключение
		ФайловаяСистема.УдалитьВременныйКаталог(Каталог);
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		ВызватьИсключение НСтр("ru = 'Не удалось обработать архив отчета по сверке операций. Подробности в журнале регистрации.'");
	КонецПопытки;
	
	Попытка
		ФайлОтчета = Новый Файл(Каталог + "report.json");
		Если ФайлОтчета.ПолучитьТолькоЧтение() Тогда
			ФайлОтчета.УстановитьТолькоЧтение(Ложь);
		КонецЕсли;
	Исключение
		ФайловаяСистема.УдалитьВременныйКаталог(Каталог);
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		ВызватьИсключение НСтр("ru = 'Не удалось обработать файл отчета по сверке операций. Подробности в журнале регистрации.'");
	КонецПопытки;
	
	Попытка
		
		ИменаСвойствСоЗначениямиДата = Новый Массив;
		ИменаСвойствСоЗначениямиДата.Добавить("operationDate");
		
		ЧтениеОтвета = Новый ЧтениеJSON;
		ЧтениеОтвета.ОткрытьФайл(Каталог + "report.json");
		
		Результат = ПрочитатьJSON(
			ЧтениеОтвета,
			,
			ИменаСвойствСоЗначениямиДата,
			,
			"ВосстановитьДатуJSON",
			СервисИнтеграцииССБП,
			,
			ИменаСвойствСоЗначениямиДата);
		
		ЧтениеОтвета.Закрыть();
		
	Исключение
		ФайловаяСистема.УдалитьВременныйКаталог(Каталог);
		ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		ВызватьИсключение НСтр("ru = 'Не удалось обработать файл отчета по сверке операций. Подробности в журнале регистрации.'");
	КонецПопытки;
	
	ФайловаяСистема.УдалитьВременныйКаталог(Каталог);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область HMAC

// Формирование подписи для аутентификации (HMAC).
// См. RFC 2104 https://www.ietf.org/rfc/rfc2104.txt.
//
// Параметры:
//  СекретныйКлюч - ДвоичныеДанные - секретный ключ шифрования.
//  Данные - ДвоичныеДанные  - данные шифрования.
//  ХешФункция - ХешФункция  - хеш функция, например ХешФункция.SHA256.
// 
// Возвращаемое значение:
//  Строка - сформированный hmac.
//
Функция РассчитатьHmac(Знач СекретныйКлюч, Знач Данные, Знач ХешФункция)
	
	РазмерБлока = 64;
	
	Если СекретныйКлюч.Размер() > РазмерБлока Тогда
		СекретныйКлюч = Хешировать(СекретныйКлюч, ХешФункция);
	КонецЕсли;
	
	ПустыеДвоичныеДанные = ПолучитьДвоичныеДанныеИзСтроки("");
	СекретныйКлюч = ОбрезатьДвоичныеДанныеСлева(СекретныйКлюч, РазмерБлока);
	
	КлючВБлоке1 = ЗабитьДвоичныеДанныеСправа(СекретныйКлюч, РазмерБлока, "00");
	
	Блок0x36 = ЗабитьДвоичныеДанныеСправа(ПустыеДвоичныеДанные, РазмерБлока, "36");
	КлючВБлоке2 = ОбъединитьДвоичныеДанныеXOR(КлючВБлоке1, Блок0x36);
	
	Блок0x5C = ЗабитьДвоичныеДанныеСправа(ПустыеДвоичныеДанные, РазмерБлока, "5C");
	КлючВБлоке3 = ОбъединитьДвоичныеДанныеXOR(КлючВБлоке1, Блок0x5C);
	
	КлючИДанные = СоединитьДвоичныеДанныеПоЗначениям(КлючВБлоке2, Данные);
	ДанныеХеш = СоединитьДвоичныеДанныеПоЗначениям(КлючВБлоке3, Хешировать(КлючИДанные, ХешФункция));
	Результат = Хешировать(ДанныеХеш, ХешФункция);
	
	Возврат ПолучитьHexСтрокуИзДвоичныхДанных(Результат);
	
КонецФункции

// Применяет хеш функцию к переданным данным.
//
// Параметры:
//  Значение - ДвоичныеДанные  - данные хеширования;
//  ХешФункция - ХешФункция  - хеш функция, например ХешФункция.SHA256.
//
// Возвращаемое значение:
//  ДвоичныеДанные  - хеш сумма;
//
Функция Хешировать(Знач Значение, Знач ХешФункция)
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция);
	ХешированиеДанных.Добавить(Значение);
	Возврат ХешированиеДанных.ХешСумма;
	
КонецФункции

// Объединяет несколько объектов типа ДвоичныеДанные в один.
//
// Параметры:
//  ДвоичныеДанные1 - ДвоичныеДанные - первое значение;
//  ДвоичныеДанные2 - ДвоичныеДанные - второе значение.
//
// Возвращаемое значение:
//  ДвоичныеДанные - результат соединения.
//
Функция СоединитьДвоичныеДанныеПоЗначениям(Знач ДвоичныеДанные1, Знач ДвоичныеДанные2)
	
	Массив = Новый Массив;
	Массив.Добавить(ДвоичныеДанные1);
	Массив.Добавить(ДвоичныеДанные2);
	
	Возврат СоединитьДвоичныеДанные(Массив);
	
КонецФункции

// Дополняет двоичные данные переданным блоком.
//
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные - значение для обработки;
//  Длина - Число - длина дополнения;
//  Строка16 - Строка - строка дополнения;
//
// Возвращаемое значение:
//  ДвоичныеДанные - результат обработки.
//
Функция ЗабитьДвоичныеДанныеСправа(Знач ДвоичныеДанные, Знач Длина, Знач Строка16)
	
	Значение = ПолучитьДвоичныеДанныеИзHexСтроки(Строка16);
	РазмерБлока = Длина - ДвоичныеДанные.Размер();
	
	Блок = Значение;
	МассивБлока = Новый Массив;
	МассивБлока.Добавить(Значение);
	Пока Блок.Размер() < РазмерБлока Цикл
		МассивБлока.Добавить(Блок);
		Блок = СоединитьДвоичныеДанные(МассивБлока);
	КонецЦикла;
	
	МассивДанных = Новый Массив;
	МассивДанных.Добавить(ДвоичныеДанные);
	МассивБлоков = РазделитьДвоичныеДанные(Блок, РазмерБлока);
	Если МассивБлоков.Количество() > 0 Тогда
		МассивДанных.Добавить(МассивБлоков[0]);
	КонецЕсли;
	Результат = СоединитьДвоичныеДанные(МассивДанных);
	
	Возврат Результат;
	
КонецФункции

// Производит операцию исключающего ИЛИ для двоичных данных.
//
// Параметры:
//  ДвоичныеДанные1 - ДвоичныеДанные - первое значение;
//  ДвоичныеДанные2 - ДвоичныеДанные - второе значение.
//
// Возвращаемое значение:
//  ДвоичныеДанные - результат операции.
//
Функция ОбъединитьДвоичныеДанныеXOR(Знач ДвоичныеДанные1, Знач ДвоичныеДанные2)
	
	ПотокВПамяти = Новый ПотокВПамяти();
	ЗаписьДанных = Новый ЗаписьДанных(ПотокВПамяти);
	
	ЧтениеДанных1 = Новый ЧтениеДанных(ДвоичныеДанные1);
	ЧтениеДанных2 = Новый ЧтениеДанных(ДвоичныеДанные2);
	
	Буфер1 = ЧтениеДанных1.ПрочитатьВБуферДвоичныхДанных();
	Буфер2 = ЧтениеДанных2.ПрочитатьВБуферДвоичныхДанных();
	
	Если Буфер1.Размер > Буфер2.Размер Тогда
		Буфер1.ЗаписатьПобитовоеИсключительноеИли(0, Буфер2, Буфер2.Размер);
		ЗаписьДанных.ЗаписатьБуферДвоичныхДанных(Буфер1);
	Иначе 
		Буфер2.ЗаписатьПобитовоеИсключительноеИли(0, Буфер1, Буфер1.Размер);
		ЗаписьДанных.ЗаписатьБуферДвоичныхДанных(Буфер2);
	КонецЕсли;
	
	Результат = ПотокВПамяти.ЗакрытьИПолучитьДвоичныеДанные();
	
	Возврат Результат;
	
КонецФункции

// Выполняет удаление заданного количества байт из двоичных данных.
//
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные - значение для обработки;
//  КоличествоБайт - Число - количество байт для удаления.
//
// Возвращаемое значение:
//  ДвоичныеДанные - результат операции.
//
Функция ОбрезатьДвоичныеДанныеСлева(Знач ДвоичныеДанные, Знач КоличествоБайт)
	
	ЧтениеДанных = Новый ЧтениеДанных(ДвоичныеДанные);
	
	ПотокВПамяти = Новый ПотокВПамяти();
	ЗаписьДанных = Новый ЗаписьДанных(ПотокВПамяти);
	
	Буфер = ЧтениеДанных.ПрочитатьВБуферДвоичныхДанных(КоличествоБайт);
	ЗаписьДанных.ЗаписатьБуферДвоичныхДанных(Буфер);
	
	Возврат ПотокВПамяти.ЗакрытьИПолучитьДвоичныеДанные();
	
КонецФункции

#КонецОбласти

#Область ResourceOwnerPasswordCredentialsGrant

// Определяет по коду ошибки сообщение пользователю.
//
// Параметры:
//  КодОшибки - Строка - ошибка сервиса см. функцию
//              ИнтеграцияСПлатежнымиСистемамиСлужебный.ПереопределитьКодОшибкиСервиса.
//  ТелоJSON  - Строка - содержимое ответа сервиса.
//
// Возвращаемое значение:
//  Строка - сообщение пользователю.
//
Функция ПереопределитьСообщениеПользователюPasswordCredentialsGrant(
		КодОшибки,
		ТелоJSON = "")
	
	КодОшибкиСервиса = "";
	
	// Определение ошибки выполняется через попытку, т.к. в случае ошибки сервиса
	// есть вероятность получить не формализованное сообщение.
	Попытка
		ЧтениеОтвета = Новый ЧтениеJSON;
		ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
		Результат = ПрочитатьJSON(ЧтениеОтвета);
		КодОшибкиСервиса = ВРег(Результат.error);
	Исключение
		КодОшибкиСервиса = "";
	КонецПопытки;
	
	Если КодОшибкиСервиса = "BAD_REQUEST_1C" Тогда
		Возврат НСтр("ru = 'Неверный набор параметров или формат запроса к сервису Портала 1С:ИТС.'");
	ИначеЕсли КодОшибкиСервиса = "USER_AUTHENTICATION_EXCEPTION_1C" Тогда
		Возврат НСтр("ru = 'Ошибка аутентификации на Портале 1С:ИТС.'");
	ИначеЕсли КодОшибкиСервиса = "INVALID_REQUEST"
		Или КодОшибкиСервиса = "INVALID_CLIENT"
		Или КодОшибкиСервиса = "INVALID_GRANT"
		Или КодОшибкиСервиса = "UNAUTHORIZED_CLIENT"
		Или КодОшибкиСервиса = "UNSUPPORTED_GRANT_TYPE" Тогда
		Возврат НСтр("ru = 'Ошибка аутентификации в сервисе банка.'");
	КонецЕсли;
	
	Возврат НСтр("ru = 'Неизвестная ошибка при подключении к сервису.'");
	
КонецФункции

#КонецОбласти

#Область ИдентификаторыСтатусовСервиса

// Формирует идентификатор статуса в процессе.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаВПроцессе() Экспорт
	
	Возврат "IN_PROGRESS";
	
КонецФункции

// Формирует идентификатор статуса выполненной операции.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаВыполнена() Экспорт
	
	Возврат "SUCCESS";
	
КонецФункции

// Формирует идентификатор статуса отклоненной операции.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаОтклонена() Экспорт
	
	Возврат "DECLINED";
	
КонецФункции

// Формирует идентификатор статуса отмененной операции.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаОтменена() Экспорт
	
	Возврат "REJECTED";
	
КонецФункции

// Формирует идентификатор статуса превышения суммы.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаПревышение()
	
	Возврат "EXCEEDED";
	
КонецФункции

// Формирует идентификатор статуса нет информации.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаНетИнформации()
	
	Возврат "NO_INFO";
	
КонецФункции

// Формирует идентификатор статуса не оплачен.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаНеОплачен()
	
	Возврат "NOT_PAID";
	
КонецФункции

// Формирует идентификатор статуса требуется подтверждение.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаТребуетсяПодтверждение()
	
	Возврат "CONFIRMATION_REQUIRED";
	
КонецФункции

// Формирует идентификатор статуса ошибка.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаОшибка()
	
	Возврат "ERROR";
	
КонецФункции

// Формирует идентификатор статуса ожидание.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаОжидание()
	
	Возврат "PENDING";
	
КонецФункции

// Формирует идентификатор статуса превышен период.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаПревышенПериод()
	
	Возврат "REPORT_PERIOD_EXCEED";
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыФункции

// Формирует URL страницы подключения к Системе быстрых платежей.
//
// Параметры:
//  Идентификатор - Строка - идентификатор участника.
//
// Возвращаемое значение:
//  Строка - адрес страницы подключения к СБП.
//
Функция АдресСтраницыПодключенияКСБП(Идентификатор)
	
	Возврат "https://its.1c.ru/sbp?memberId=" + Идентификатор;
	
КонецФункции

// Формирует стандартные заголовки для вызова операции.
//
// Возвращаемое значение:
//  Соответствие - заголовки операции.
//
Функция НовыйЗаголовкиВызоваОперации()
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("X-Correlation-ID", Строка(Новый УникальныйИдентификатор));
	
	Возврат Заголовки;
	
КонецФункции

// Восстанавливает значение даты из JSON, если значение не равно Неопределено.
//
// Параметры:
//  Свойство - Строка - имя свойства;
//  Значение - Строка, Неопределено - значение свойства;
//  ДополнительныеПараметры - Структура, Неопределено - параметры восстановления.
//
// Возвращаемое значение:
//  Дата, Неопределено - результат восстановления.
//
Функция ВосстановитьДатуJSON(Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Если Значение <> Неопределено Тогда
		
		Результат = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
		
		// Свойство operationDate необходимо обрабатывать
		// и хранить в формате UTC, для того чтобы при
		// проведении сверки взаиморасчетов из разных
		// часовых поясов получать единые выборки операций.
		Если Свойство = "operationDate" Тогда
			Результат = ИнтеграцияСПлатежнымиСистемамиСлужебный.ДатаВUTC(Результат);
		КонецЕсли;
		
		Возврат Результат;
		
	Иначе
		Возврат Значение;
	КонецЕсли;
	
КонецФункции

// Производит преобразование число в формат JSON.
//
// Параметры
//  Значение - Число - значения для преобразования.
//
// Возвращаемое значение:
//   Строка - преобразованное число.
//
Функция ПреобразоватьЧислоВФорматJSON(Знач Значение)
	
	Возврат СтрЗаменить(
		СтрЗаменить(
			"" + Значение,
			",",
			"."),
		Символы.НПП,
		"");
	
КонецФункции

// Удаляет служебные символы из идентификатора платежной системы.
//
// Параметры
//  Идентификатор - Число - идентификатор платежной системы.
//
// Возвращаемое значение:
//   Строка - обработанный идентификатор.
//
Функция ИдентификаторПлатежнойСистемыБезСлужебныхСимволов(Идентификатор)
	
	Возврат СтрЗаменить("" + Идентификатор, Символы.НПП, "");
	
КонецФункции

// Определяет идентификатор валюты рубль РФ.
//
// Возвращаемое значение:
//  Строка - идентификатор валюты по ISO.
//
Функция ИдентификаторВалютыРоссийскийРубль()
	
	Возврат "RUB";
	
КонецФункции

// Создает случайное число длиной 16-32 разряда
// и переводит его в hex систему счисления.
//
// Возвращаемое значение:
//  Строка - случайное число в hex.
//
Функция ДлинноеСлучайноеЧислоHEX()
	
	ГенераторСлучайныхЧисел = Новый ГенераторСлучайныхЧисел();
	ДлинаЧисла = ГенераторСлучайныхЧисел.СлучайноеЧисло(16, 32);
	
	// Для инициализации первого разряда исключается 0.
	СлучайноеЧисло = "" + ГенераторСлучайныхЧисел.СлучайноеЧисло(1, 9);
	
	// Последовательное формирование случайного числа по разрядам.
	Сч = 1;
	Пока Сч < ДлинаЧисла Цикл
		СлучайноеЧисло = СлучайноеЧисло + ГенераторСлучайныхЧисел.СлучайноеЧисло(0, 9);
		Сч = Сч + 1;
	КонецЦикла;
	
	Возврат ПреобразоватьЧислоВHEX(Число(СлучайноеЧисло));
	
КонецФункции

// Переводит число в систему счисления hex.
//
// Параметры:
//  Значение - Число - исходное значения для преобразования;
//
// Возвращаемое значение:
//  Строка - результат преобразования
//
Функция ПреобразоватьЧислоВHEX(Знач Значение)
	
	Если Значение <= 0 Тогда
		ЧислоHEX = "0";
	Иначе
		Значение = Цел(Значение);
		ЧислоHEX = "";
		Пока Значение > 0 Цикл
			ЧислоHEX = Сред("0123456789ABCDEF", Значение % 16 + 1, 1) + ЧислоHEX;
			Значение = Цел(Значение / 16);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ЧислоHEX;
	
КонецФункции

#КонецОбласти

#КонецОбласти
