
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта" Тогда
		Если Параметры.Свойство("Ключ") И Параметры.Ключ = ЭтотУзел() Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаУзлаЭтойИнформационнойБазы";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС ДЛЯ ВЗАИМОДЕЙСТВИЯ С БСП
#Область СлужебныйПрограммныйИнтерфейс

// Представляет собой "точку входа", позволяющую механизмам
// подсистемы "Обмен данными" получить доступ ко всем свойствам и методам плана обмена,
// отвечающим за технологическую и прикладную специфику конкретного обмена.
//
// Параметры:
//  Настройки - Структура - ключ - имя настройки, значение - значение настройки.
//    * Алгоритмы - Структура - управление доступностью методов плана обмена (экспортных процедур и функций),
//                              поддерживаемых подсистемой "Обмен данными" для планов обмена.
//        ** Ключ     - Строка - имя метода (процедуры/функции), используется только для чтения.
//        ** Значение - Булево - признак использования метода.
//                               Истина - метод используется в данном плане обмена,
//                               Ложь - метод не используется.
//                               Значение по умолчанию для всех методов - Ложь.
//             Список методов:
//
//                ПриПолученииВариантовНастроекОбмена
//                ПриПолученииОписанияВариантаНастройки
//
//                ОбработчикПроверкиПараметровУчета
//                ОбработчикПроверкиОграниченийПередачиДанных
//                ОбработчикПроверкиЗначенийПоУмолчанию
//
//                ПриПодключенииККорреспонденту
//                ПриОтправкеДанныхОтправителя
//                ПриПолученииДанныхОтправителя
//
//                ПриСохраненииНастроекСинхронизацииДанных
//
//                ПриОпределенииПоддерживаемыхОбъектовФормата
//                ПриОпределенииПоддерживаемыхКорреспондентомОбъектовФормата
//
//                НастроитьИнтерактивнуюВыгрузку
//                НастроитьИнтерактивнуюВыгрузкуВМоделиСервиса
//                ПредставлениеОтбораИнтерактивнойВыгрузки
//
//        Для того чтобы использовать метод, необходимо установить значение Истина
//        для значения соответствующего ключа структуры,
//        а также объявить соответствующую экспортную процедуру/функцию в модуле менеджера плана обмена.
//    * НазначениеПланаОбмена - Строка - свойство, определяющее принадлежность плана обмена к одной из нескольких категорий,
//                                       влияет на его использование в различных механизмах подсистемы "Обмен данными":
//        ** РИБ - обмен РИБ без использования ограничений миграции данных.
//        ** РИБСФильтром - обмен РИБ с использованием ограничений миграции данных.
//        ** СинхронизацияСДругойПрограммой - обмен с другой программой.
//        В большинстве случаев достаточно использовать значения по умолчанию:
//        для планов обмена РИБ определяется как РИБ, для остальных - СинхронизацияСДругойПрограммой.
//    * ЭтоПланОбменаXDTO - Булево - признак того, что план обмена используется для ОУФ.
//                                   Значение по умолчанию - Ложь.
//    * ФорматОбмена - Строка - имя формата обмена, используемого для данного плана обмена.
//                              Используется только для ОУФ (см. свойство ЭтоПланОбменаXDTO).
//    * ВерсииФорматаОбмена - Структура - соответствие номеров поддерживаемых версий формата данных и ссылок на общие модули,
//                                        реализующих логику обмена через конкретную версию формата.
//                                        Используется только для ОУФ (см. свойство ЭтоПланОбменаXDTO).
//    * ПланОбменаИспользуетсяВМоделиСервиса - Булево - признак использования плана обмена для организации обмена в модели сервиса.
//                                                      Если признак установлен, то в сервисе можно включить обмен данными
//                                                      с использованием этого плана обмена, а также можно использовать этот план обмена
//                                                      для организации автономной работы в модели сервиса.
//                                                      Если признак не установлен, то план обмена будет использоваться только для обмена
//                                                      в локальном режиме работы конфигурации.
//                                                      Значение по умолчанию - Ложь.
//    * ИмяКонфигурацииИсточника - Строка - строковый идентификатор, отличающий эту конфигурацию от других при работе в модели сервиса.
//                                          Заполняется только для плана обмена, используемого в режиме сервиса
//                                          (см. свойство ПланОбменаИспользуетсяВМоделиСервиса).
//    * ПредупреждатьОНесоответствииВерсийПравилОбмена - Булево - признак необходимости проверки на расхождение версий в правилах конвертации.
//                                                                Проверка выполняется при загрузке комплекта правил, при отправке и получении данных.
//                                                                Используется только для УОП.
//    * ИмяПланаОбменаДляПереходаНаНовыйОбмен - Строка - при выполнении перехода от использования одного плана обмена на использование другого плана обмена
//                                                       (например, при переходе с УОП на аналогичный по функциональности ОУФ) определяет имя плана обмена-приемника.
//                                                       Если свойство установлено, в рабочих местах управления настройками не будет предлагаться настроить этот вид обмена.
//                                                       Существующие обмены этого вида будут продолжать отображаться в списке настроенных обменов.
//                                                       По умолчанию значение не заполнено.
//    * ВидГруппыДляВариантовНастроек - Строка - вид группы для группировки команд, предназначенных для создания разных вариантов настройки плана обмена.
//                                               Значение по умолчанию - Подменю.
//
Процедура ПриПолученииНастроек(Настройки) Экспорт
	
	Алгоритмы = Настройки.Алгоритмы;
	
	Алгоритмы.ПриПолученииВариантовНастроекОбмена   = Истина;
	Алгоритмы.ПриПолученииОписанияВариантаНастройки = Истина;
	
	Алгоритмы.НастроитьИнтерактивнуюВыгрузку           = Истина;
	Алгоритмы.ПредставлениеОтбораИнтерактивнойВыгрузки = Истина;
	
	Настройки.ЭтоПланОбменаXDTO   = Истина;
	Настройки.ФорматОбмена        = ОбменДаннымиБольничнаяАптека.ПространствоИменУниверсальногоФорматаОбмена();
	Настройки.ВерсииФорматаОбмена = ОбменДаннымиБольничнаяАптека.МенеджерыВерсийФорматаОбмена(
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПустаяСсылка().Метаданные()));
	
	Настройки.ПланОбменаИспользуетсяВМоделиСервиса = Истина;
	Настройки.ИмяКонфигурацииИсточника             = ОбменДаннымиБольничнаяАптека.ИмяКонфигурацииИсточника();
	
КонецПроцедуры

// Применяется, если для плана обмена необходимо определить перечень вариантов настроек,
// либо признаки их использования для обмена с корреспондентами работающими в локальном режиме и в режиме сервиса.
//
// Параметры:
//  ВариантыНастроекОбмена - ТаблицаЗначений - добавление варианта происходит путем добавления новой строки и заполнения следующих колонок:
//    * ИдентификаторНастройки        - Строка - строковый идентификатор варианта настройки, отличающий его от других вариантов настроек данного плана обмена.
//    * КорреспондентВМоделиСервиса   - Булево - признак того, что план обмена поддерживает обмен данными с корреспондентом, работающим в модели сервиса.
//    * КорреспондентВЛокальномРежиме - Булево - признак того, что план обмена поддерживает обмен данными с корреспондентом, работающем в локальном режиме.
//  ПараметрыКонтекста - Структура - определяет режим работы метода через контекст его выполнения. Ключ - параметр контекста, значение - его значение:
//    * ИмяКорреспондента - Строка, Неопределено - строковый идентификатор конфигурации или приложения, с которым настраивается обмен.
//                                                 (см. свойство ИмяКонфигурацииИсточника метода ПриПолученииНастроек).
//                                                 Незаполненное значение параметра означает отсутствие информации о корреспонденте.
//    * ВерсияКорреспондента        - Строка - номер версии корреспондента. Незаполненное значение параметра означает отсутствие информации о корреспонденте.
//    * КорреспондентВМоделиСервиса - Булево - признак того, что план корреспондент работает в модели сервиса.
//
Процедура ПриПолученииВариантовНастроекОбмена(ВариантыНастроекОбмена, ПараметрыКонтекста) Экспорт
	
	ВариантНастройки = ВариантыНастроекОбмена.Добавить();
	ВариантНастройки.ИдентификаторНастройки        = ИдентификаторНастройкиУниверсальный();
	ВариантНастройки.КорреспондентВМоделиСервиса   = Истина;
	ВариантНастройки.КорреспондентВЛокальномРежиме = Истина;
	
	Если ПолучитьФункциональнуюОпцию("НеБюджетноеУчреждение") Тогда
		ВариантНастройки = ВариантыНастроекОбмена.Добавить();
		ВариантНастройки.ИдентификаторНастройки        = ИдентификаторНастройкиБП30();
		ВариантНастройки.КорреспондентВМоделиСервиса   = Истина;
		ВариантНастройки.КорреспондентВЛокальномРежиме = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Применяется, если необходимо переопределить свойства варианта настройки.
//
// Параметры:
//  ОписаниеВарианта - Структура - содержит перечень доступных свойств варианта настройки.
//    * Ключ - Строка - имя свойства.
//    * Значение - Произвольный - значение свойства.
//        Список свойств:
//          ** ИспользоватьПомощникСозданияОбменаДанными - определяет, будет ли использоваться помощник для создания новых узлов плана обмена.
//                                                         Значение по умолчанию - Истина.
//          ** ЗаголовокКомандыДляСозданияНовогоОбменаДанными - представление команды, выводимое в пользовательском интерфейсе
//                                                              при создании новой настройки обмена данными.
//                                                              Значение по умолчанию - синоним плана обмена.
//          ** ЗаголовокПомощникаСозданияОбмена - представление заголовка формы помощника создания обмена данными, выводимое в пользовательском интерфейсе.
//                                                Значение по умолчанию - строка, образованная по шаблону
//                                                Синхронизация данных с [Название программы] (настройка),
//                                                где [Название программы] - синоним плана обмена.
//          ** ЗаголовокУзлаПланаОбмена - представление узла плана обмена, выводимое в пользовательском интерфейсе.
//                                        Значение по умолчанию - синоним плана обмена.
//          ** НаименованиеКонфигурацииКорреспондента - представление конфигурации корреспондента выводимое в пользовательском интерфейсе.
//                                                      Значение по умолчанию - синоним плана обмена.
//          ** КраткаяИнформацияПоОбмену - краткое описание обмена данными, которое выводится на первой странице "Помощника создания обмена данными".
//                                         По умолчанию свойство не заполнено.
//          ** ПодробнаяИнформацияПоОбмену - ссылка на веб-страницу или полный путь к форме внутри конфигурации строкой.
//                                           По умолчанию свойство не заполнено.
//          ** ПояснениеДляНастройкиПараметровУчетаБазыКорреспондента - текст пояснения для настройки параметров учета базы-корреспондента.
//                                                                      По умолчанию свойство не заполнено (имя файла необходимо будет указать вручную).
//          ** ИмяФайлаНастроекДляПриемника - имя файла настроек по умолчанию. В этот файл будут выгружены настройки обмена для приемника.
//                                            Свойство используется только для планов обмена ОУФ и УОП.
//                                            По умолчанию свойство не заполнено (имя файла необходимо будет указать вручную).
//          ** ПутьКФайлуКомплектаПравилНаПользовательскомСайте - Путь к файлу комплекта правил в виде архива на пользовательском сайте в разделе конфигурации.
//                                                                По умолчанию свойство не заполнено (путь необходимо будет указать вручную).
//          ** ПутьКФайлуКомплектаПравилВКаталогеШаблонов - относительный путь к файлу комплекта правил в каталоге шаблонов "1С:Предприятия".
//                                                          По умолчанию свойство не заполнено (путь необходимо будет указать вручную).
//          ** ИспользуемыеТранспортыСообщенийОбмена - перечень используемых транспортов сообщений для текущего плана обмена.
//                                                     Если не заполнен, будут доступны все допустимые настройки транспорта.
//                                                     Значение по умолчанию - пустой Массив.
//          ** ОтображатьНастройкуОтборовНаУзле - признак отображения настроек отборов для этого приложения в помощнике создания обмена.
//                                                Значение по умолчанию - Истина.
//          ** ОтображатьЗначенияПоУмолчаниюНаУзле - признак отображения значений по умолчанию для этого приложения в помощнике создания обмена.
//                                                   Значение по умолчанию - Истина.
//          ** ОтображатьНастройкуОтборовНаУзлеБазыКорреспондента - признак отображения настроек отборов для корреспондента в помощнике создания обмена.
//                                                                  Значение по умолчанию - Истина.
//          ** ОтображатьЗначенияПоУмолчаниюНаУзлеБазыКорреспондента - признак отображения значений по умолчанию для корреспондента в помощнике создания обмена.
//                                                                     Значение по умолчанию - Истина.
//          ** ИмяФормыОтборов - имя формы плана обмена, используемой для настройки отборов для этого приложения.
//                               Используется, если свойство ОтображатьНастройкуОтборовНаУзле имеет значение Истина.
//                               Значение по умолчанию - ФормаНастройкиУзла.
//          ** ИмяФормыЗначенийПоУмолчанию - имя формы плана обмена, используемой для настройки значений по умолчанию для этого приложения.
//                                           Используется, если свойство ОтображатьЗначенияПоУмолчаниюНаУзле имеет значение Истина.
//                                           Значение по умолчанию - ФормаНастройкиЗначенийПоУмолчанию.
//          ** ИмяФормыОтборовКорреспондента - имя формы плана обмена, используемой для настройки отборов для корреспондента.
//                                             Используется, если свойство ОтображатьНастройкуОтборовНаУзлеБазыКорреспондента имеет значение Истина.
//                                             Значение по умолчанию - ФормаНастройкиУзлаБазыКорреспондента.
//          ** ИмяФормыЗначенийПоУмолчаниюКорреспондента - имя формы плана обмена, используемой для настройки значений по умолчанию для корреспондента.
//                                                         Используется, если свойство ОтображатьЗначенияПоУмолчаниюНаУзлеБазыКорреспондента имеет значение Истина.
//                                                         Значение по умолчанию - ФормаНастройкиЗначенийПоУмолчаниюБазыКорреспондента.
//          ** ИмяФормыОбщиеДанныеУзлов - имя формы плана обмена, используемой для настройки отборов при обмене через Интернет или между приложениями в модели сервиса.
//                                        По умолчанию свойство не заполнено.
//          ** ИмяФормыСозданияНачальногоОбраза - имя формы плана обмена, используемой для создания начального образа.
//                                                Используется только для планов обмена РИБ.
//                                                По умолчанию свойство не заполнено (используется стандартная форма).
//          ** ОбщиеДанныеУзлов - имена реквизитов и табличных частей плана обмена, которые являются общими для пары обменивающихся конфигураций
//                                (см. раздел "Общие данные узлов"). Имена перечисляются через запятую.
//                                По умолчанию свойство не заполнено (нет общих данных).
//          ** Отборы - отборы для этого приложения, заполненные начальными данными.
//                      Если отборы не предусмотрены, не заполняется.
//                      Структура настроек должна повторять состав реквизитов шапки и табличных частей плана обмена, предназначенных для хранения отборов.
//                      Для реквизитов шапки используются аналогичные по ключу и значению элементы структуры, а для табличных частей используются структуры,
//                      содержащие массивы значений полей табличных частей плана обмена.
//                      Значение по умолчанию - пустая Структура.
//          ** ЗначенияПоУмолчанию - значения по умолчанию для этого приложения, заполненные начальными данными.
//                                   Если значения по умолчанию не предусмотрены, не заполняется.
//                                   Структура настроек должна повторять состав реквизитов шапки плана обмена, предназначенных для хранения значений по умолчанию.
//                                   Для реквизитов шапки используются аналогичные по ключу и значению элементы структуры.
//                                   Значение по умолчанию - пустая Структура.
//          ** ОтборыКорреспондента - отборы для корреспондента, заполненные начальными данными.
//                                    Если отборы не предусмотрены, не заполняется.
//                                    Структура настроек должна повторять состав реквизитов шапки и табличных частей плана обмена корреспондента, предназначенных для хранения отборов.
//                                    Для реквизитов шапки используются аналогичные по ключу и значению элементы структуры, а для табличных частей используются структуры,
//                                    содержащие массивы значений полей табличных частей плана обмена.
//                                    Для реквизитов шапки ссылочного типа в структуру дополнительно должны быть добавлены свойства,
//                                    предназначенные для хранения идентификаторов выбранных значений.
//                                    Имя свойства должно соответствовать шаблону [Имя реквизита]_Ключ.
//                                    Для реквизитов табличной части, имеющих ссылочный тип, аналогичные свойства должны быть добавлены в структуру,
//                                    описывающую данную табличную часть. Тип свойства должен быть Массив.
//                                    Значение по умолчанию - пустая Структура.
//          ** ЗначенияПоУмолчаниюКорреспондента - значения по умолчанию для корреспондента, заполненные начальными данными.
//                                                 Если значения по умолчанию не предусмотрены, не заполняется.
//                                                 Структура настроек должна повторять состав реквизитов шапки плана обмена, предназначенных для хранения значений по умолчанию.
//                                                 Для реквизитов шапки используются аналогичные по ключу и значению элементы структуры.
//                                                 Для реквизитов ссылочного типа в структуру дополнительно должны быть добавлены свойства,
//                                                 предназначенные для хранения идентификаторов выбранных значений.
//                                                 Имя свойства должно соответствовать шаблону [Имя реквизита]_Ключ.
//                                                 Значение по умолчанию - пустая Структура.
//          ** ПояснениеДляНастройкиПараметровУчета - пояснение о последовательности действий пользователя для настройки параметров учета в текущей информационной базе.
//                                                    По умолчанию свойство не заполнено.
//          ** ДополнительныеДанныеДляКорреспондента - дополнительные данные, которые будут использованы в приложении корреспондента при настройке обмена.
//                                                     В качестве значений структуры применимы только значения, поддерживающие XDTO-сериализацию.
//                                                     Дополнительные данные могут быть использованы при настройке обмена в приложении корреспондента,
//                                                     в обработчике ПриСозданииНаСервере в форме настройки узла плана обмена ФормаНастройкиЗначенийПоУмолчаниюБазыКорреспондента
//                                                     при настройке обмена данными с использованием помощника.
//  ИдентификаторНастройки - Строка - строковый идентификатор варианта настройки,
//                                    отличающий его от других вариантов настроек данного плана обмена.
//  ПараметрыКонтекста - Структура - определяет режим работы метода через контекст его выполнения.
//    * Ключ - параметр контекста.
//    * Значение - его значение.
//        Список свойств:
//          ** ИмяКорреспондента - строковый идентификатор конфигурации или приложения, с которым настраивается обмен
//                                 (см. свойство ИмяКонфигурацииИсточника метода ПриПолученииНастроек).
//          ** ВерсияКорреспондента - Номер версии корреспондента.
//
Процедура ПриПолученииОписанияВариантаНастройки(ОписаниеВарианта, ИдентификаторНастройки, ПараметрыКонтекста) Экспорт
	
	ОписаниеВарианта.ПодробнаяИнформацияПоОбмену           = ПустаяСсылка().Метаданные().Формы.ПодробнаяИнформация.ПолноеИмя();
	ОписаниеВарианта.ИмяФайлаНастроекДляПриемника          = НСтр("ru = 'Синхронизация данных через универсальный формат'");
	ОписаниеВарианта.ИспользуемыеТранспортыСообщенийОбмена = ОбменДаннымиСервер.ВсеТранспортыСообщенийОбменаКонфигурации();
	
	Если ИдентификаторНастройки = ИдентификаторНастройкиБП30() Тогда
		
		ОписаниеВарианта.ЗаголовокКомандыДляСозданияНовогоОбменаДанными = НСтр("ru = 'Бухгалтерия предприятия, редакция 3.0'");
		ОписаниеВарианта.ЗаголовокПомощникаСозданияОбмена               = НСтр("ru = 'Настройка синхронизации с программой ""Бухгалтерия предприятия"", редакция 3.0'");
		ОписаниеВарианта.ЗаголовокУзлаПланаОбмена                       = НСтр("ru = 'Синхронизация с программой ""Бухгалтерия предприятия"", редакция 3.0'");
		ОписаниеВарианта.НаименованиеКонфигурацииКорреспондента         = НСтр("ru = 'Бухгалтерия предприятия, редакция 3.0'");
		ОписаниеВарианта.КраткаяИнформацияПоОбмену                      = НСтр("ru = 'Данная настройка позволит синхронизировать данные между программами ""Больничная аптека"" и ""Бухгалтерия предприятия, редакция 3.0"".
			|В синхронизации участвуют документы и нормативно-справочная информация.'");
		ОписаниеВарианта.ИмяКонфигурацииКорреспондента                  = НСтр("ru = 'Бухгалтерия предприятия, редакция 3.0'");;
		
	КонецЕсли;
	
	Если ИдентификаторНастройки = ИдентификаторНастройкиУниверсальный() Тогда
		
		ОписаниеВарианта.ЗаголовокКомандыДляСозданияНовогоОбменаДанными = НСтр("ru = 'Другая программа'");
		ОписаниеВарианта.ЗаголовокПомощникаСозданияОбмена               = НСтр("ru = 'Настройка синхронизации данных через универсальный формат'");
		ОписаниеВарианта.ЗаголовокУзлаПланаОбмена                       = НСтр("ru = 'Синхронизация данных через универсальный формат'");
		ОписаниеВарианта.НаименованиеКонфигурацииКорреспондента         = НСтр("ru = 'Другая программа'");
		ОписаниеВарианта.КраткаяИнформацияПоОбмену                      = НСтр("ru = 'Позволяет синхронизировать данные между любыми программами, поддерживающими универсальный формат обмена ""Enterprise Data"".'");
		ОписаниеВарианта.ИмяКонфигурацииКорреспондента                  = "";
		
	КонецЕсли;
	
КонецПроцедуры

// Предназначена для настройки вариантов интерактивной настройки выгрузки по сценарию узла.
// Для настройки необходимо установить значения свойств параметров в необходимые значения.
//
// Используется для контроля режимов работы помощника интерактивного обмена данными.
//
// Параметры:
//  Получатель - ПланОбменаСсылка - Узел, для которого производится настройка.
//  Параметры  - Структура        - Параметры для изменения. Содержит поля:
//    * ВариантБезДополнения - Структура     - настройки типового варианта "Не добавлять". Содержит поля:
//        ** Использование - Булево - флаг разрешения использования варианта. По умолчанию Истина.
//        ** Порядок       - Число  - порядок размещения варианта на форме помощника, сверху вниз. По умолчанию 1.
//        ** Заголовок     - Строка - позволяет переопределить название типового варианта.
//        ** Пояснение     - Строка - позволяет переопределить текст пояснения варианта для пользователя.
//    * ВариантВсеДокументы - Структура      - настройки типового варианта "Добавить все документы за период". Содержит поля:
//        ** Использование - Булево - флаг разрешения использования варианта. По умолчанию Истина.
//        ** Порядок       - Число  - порядок размещения варианта на форме помощника, сверху вниз. По умолчанию 2.
//        ** Заголовок     - Строка - позволяет переопределить название типового варианта.
//        ** Пояснение     - Строка - позволяет переопределить текст пояснения варианта для пользователя.
//    * ВариантПроизвольныйОтбор - Структура - настройки типового варианта "Добавить данные с произвольным отбором". Содержит поля:
//        ** Использование - Булево - флаг разрешения использования варианта. По умолчанию Истина.
//        ** Порядок       - Число  - порядок размещения варианта на форме помощника, сверху вниз. По умолчанию 3.
//        ** Заголовок     - Строка - позволяет переопределить название типового варианта.
//        ** Пояснение     - Строка - позволяет переопределить текст пояснения варианта для пользователя.
//    * ВариантДополнительно - Структура     - настройки дополнительного варианта по сценарию узла. Содержит поля:
//        ** Использование            - Булево            - флаг разрешения использования варианта. По умолчанию Ложь.
//        ** Порядок                  - Число             - порядок размещения варианта на форме помощника, сверху вниз. По умолчанию 4.
//        ** Заголовок                - Строка            - название варианта для отображения на форме.
//        ** ИмяФормыОтбора           - Строка            - Имя формы, вызываемой для редактирования настроек.
//        ** ЗаголовокКомандыФормы    - Строка            - Заголовок для отрисовки на форме команды открытия формы настроек.
//        ** ИспользоватьПериодОтбора - Булево            - флаг того, что необходим общий отбор по периоду. По умолчанию Ложь.
//        ** ПериодОтбора             - СтандартныйПериод - значение периода общего отбора, предлагаемого по умолчанию.
//        ** Отбор                    - ТаблицаЗначений   - содержит строки с описанием подробных отборов по сценарию узла. Содержит колонки:
//             *** ПолноеИмяМетаданных - Строка                - полное имя метаданных регистрируемого объекта, отбор которого описывает строка.
//                                                             Например "Документ._ДемоПоступлениеТоваров". Можно
//                                                             использовать специальные  значения "ВсеДокументы" и
//                                                             "ВсеСправочники" для отбора соответственно всех
//                                                             документов и всех справочников, регистрирующихся на узле
//                                                             Получатель.
//             *** ВыборПериода        - Булево                - флаг того, что данная строка описывает отбор с общим периодом.
//             *** Период              - СтандартныйПериод     - значение периода общего отбора для метаданных строки, предлагаемого по умолчанию.
//             *** Отбор               - ОтборКомпоновкиДанных - отбор по умолчанию. Поля отбора формируются в соответствии с общим правилами
//                                                             формирования полей компоновки. Например, для указания
//                                                             отбора по реквизиту документа "Организация", необходимо
//                                                             использовать поле "Ссылка.Организация".
//
// Если для всех вариантов флаги разрешения использования установлены в Ложь, то страница дополнения выгрузки в
// помощнике интерактивного обмена данными будет пропущена и дополнительная регистрация объектов производится не будет.
// Например, инициализация вида:
//
//  Параметры.ВариантВсеДокументы.Использование      = Ложь;
//  Параметры.ВариантБезДополнения.Использование     = Ложь;
//  Параметры.ВариантПроизвольныйОтбор.Использование = Ложь;
//  Параметры.ВариантДополнительно.Использование     = Ложь;
//
//  Приведет к тому, что шаг дополнения выгрузки будет полностью пропущен.
//
Процедура НастроитьИнтерактивнуюВыгрузку(Получатель, Параметры) Экспорт
	
	СправочникиНеСинхронизируются = Получатель.ПравилаОтправкиСправочников = "НеСинхронизировать";
	ДокументыНеСинхронизируются   = Получатель.ПравилаОтправкиДокументов = "НеСинхронизировать";
	
	Если СправочникиНеСинхронизируются И ДокументыНеСинхронизируются Тогда
		
		Параметры.ВариантБезДополнения.Использование     = Ложь;
		Параметры.ВариантВсеДокументы.Использование      = Ложь;
		Параметры.ВариантПроизвольныйОтбор.Использование = Ложь;
		Параметры.ВариантДополнительно.Использование     = Ложь;
		
	Иначе
		
		// Отключаем вариант "ВариантВсеДокументы"
		Параметры.ВариантВсеДокументы.Использование = Ложь;
		
		// Настраиваем вариант "Без дополнения"
		Параметры.ВариантБезДополнения.Использование = Истина;
		Параметры.ВариантБезДополнения.Порядок       = 1;
		
		Если СправочникиНеСинхронизируются Тогда
			Параметры.ВариантБезДополнения.Заголовок = НСтр("ru = 'Не добавлять документы к отправке'");
		ИначеЕсли ДокументыНеСинхронизируются Тогда
			Параметры.ВариантБезДополнения.Заголовок = НСтр("ru = 'Не добавлять справочники к отправке'");
		Иначе
			Параметры.ВариантБезДополнения.Заголовок = НСтр("ru = 'Не добавлять документы и справочники к отправке'");
		КонецЕсли;
		
		// Настраиваем вариант "Дополнительно"
		Если ДокументыНеСинхронизируются Тогда
			Параметры.ВариантДополнительно.Использование = Ложь;
		Иначе
			Параметры.ВариантДополнительно.Использование            = Истина;
			Параметры.ВариантДополнительно.Порядок                  = 2;
			Параметры.ВариантДополнительно.Заголовок                = НСтр("ru = 'Добавлять все документы к отправке'");
			Параметры.ВариантДополнительно.ИмяФормыОтбора           = "ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.Форма.НастройкаВыгрузки";
			Параметры.ВариантДополнительно.ЗаголовокКомандыФормы    = НСтр("ru='Выбрать организации для отбора'");
			Параметры.ВариантДополнительно.ИспользоватьПериодОтбора = Истина;
			Параметры.ВариантДополнительно.ПериодОтбора             = Новый СтандартныйПериод(ВариантСтандартногоПериода.ПрошлыйМесяц);
			
			// Установка значений по-умолчанию.
			ДобавитьСтрокуОтбораОрганизацийИнтерактивнойВыгрузки(Параметры.ВариантДополнительно.Отбор);
			
		КонецЕсли;
		
		// Настраиваем вариант "Произвольный отбор"
		Параметры.ВариантПроизвольныйОтбор.Использование = Истина;
		Параметры.ВариантПроизвольныйОтбор.Порядок       = 3;
		
		Если СправочникиНеСинхронизируются Тогда
			Параметры.ВариантПроизвольныйОтбор.Заголовок = НСтр("ru = 'Добавлять произвольные документы к отправке'");
		ИначеЕсли ДокументыНеСинхронизируются Тогда
			Параметры.ВариантПроизвольныйОтбор.Заголовок = НСтр("ru = 'Добавлять произвольные справочники к отправке'");
		Иначе
			Параметры.ВариантПроизвольныйОтбор.Заголовок = НСтр("ru = 'Добавлять произвольные документы и справочники к отправке'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает представление отбора для варианта дополнения выгрузки по сценарию узла.
// См. описание "ВариантДополнительно" в процедуре "НастроитьИнтерактивнуюВыгрузку"
//
// Параметры:
//     Получатель - ПланОбменаСсылка - Узел, для которого определяется представление отбора
//     Параметры  - Структура        - Характеристики отбора. Содержит поля:
//         ИспользоватьПериодОтбора - Булево            - флаг того, что необходимо использовать общий отбор по периоду.
//         ПериодОтбора             - СтандартныйПериод - значение периода общего отбора.
//         Отбор                    - ДанныеФормыКоллекция   - содержит строки с описанием подробных отборов по сценарию узла.
//                                                        Содержит колонки:
//                 ПолноеИмяМетаданных - Строка                - полное имя метаданных регистрируемого объекта, отбор которого описывает строка.
//                                                               Например "Документ._ДемоПоступлениеТоваров". Могут быть использованы специальные 
//                                                               значения "ВсеДокументы" и "ВсеСправочники" для отбора соответственно всех 
//                                                               документов и всех справочников, регистрирующихся на узле Получатель.
//                 ВыборПериода        - Булево                - флаг того, что данная строка описывает отбор с общим периодом.
//                 Период              - СтандартныйПериод     - значение периода общего отбора для метаданных строки.
//                 Отбор               - ОтборКомпоновкиДанных - поля отбора. Поля отбора формируются в соответствии с общим правилами
//                                                               формирования полей компоновки. Например, для указания отбора по реквизиту
//                                                               документа "Организация", будет использовано поле "Ссылка.Организация"
//
// Возвращаемое значение: 
//     Строка - описание отбора
//
Функция ПредставлениеОтбораИнтерактивнойВыгрузки(Получатель, Параметры) Экспорт
	
	ПредставлениеОтбора = НСтр("ru = 'Дополнительно будут отправлены документы %1,
		|%2'");
	
	ОписаниеПериода = "";
	Если Параметры.ИспользоватьПериодОтбора Тогда
		Если ЗначениеЗаполнено(Параметры.ПериодОтбора) Тогда
			
			ПериодОтбора = Параметры.ПериодОтбора;
			
			ОписаниеПериода = НСтр("ru='за период: %1'");
			ПредставлениеПериода = ?(ПериодОтбора.Вариант <> ВариантСтандартногоПериода.ПроизвольныйПериод,
				ПериодОтбора.Вариант,
				ПредставлениеПериода(ПериодОтбора.ДатаНачала, ПериодОтбора.ДатаОкончания));
			
			ОписаниеПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеПериода, ПредставлениеПериода);
			
		Иначе
			
			ДатаНачалаВыгрузки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Получатель, "ДатаНачалаВыгрузкиДокументов");
			Если ЗначениеЗаполнено(ДатаНачалаВыгрузки) Тогда
				
				ОписаниеПериода = НСтр("ru='начиная с даты начала отправки документов: %1'");
				ПредставлениеПериода = Формат(ДатаНачалаВыгрузки, "ДЛФ=DD");
				ОписаниеПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеПериода, ПредставлениеПериода);
				
			Иначе
				ОписаниеПериода = НСтр("ru='за весь период учета'");
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	СписокОрганизаций = ОрганизацииИзОтбораИнтерактивнойВыгрузки(Параметры.Отбор);
	
	ОписаниеОтбораОрганизации = "";
	Если СписокОрганизаций.Количество() > 0 Тогда
		
		ОписаниеОтбораОрганизации = НСтр("ru='с отбором по организациям: %1'");
		ПредставлениеОрагнизаций = ОбщегоНазначенияБольничнаяАптекаКлиентСервер.СокращенноеПредставлениеКоллекцииЗначений(СписокОрганизаций);
		ОписаниеОтбораОрганизации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОтбораОрганизации, ПредставлениеОрагнизаций);
		
	Иначе
		ОписаниеОтбораОрганизации = НСтр("ru='по всем организациям'");
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеОтбора, ОписаниеПериода, ОписаниеОтбораОрганизации);
	
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

// Возвращает список организаций, установленных в качестве отбора,
// для варианта ВариантДополнительно интерактивной выгрузки данных.
// (см. процедуру ПредставлениеОтбораИнтерактивнойВыгрузки этого модуля)
// (см. процедуру ИнициализироватьСписокОрганизаций формы "СинхронизацияДанныхЧерезУниверсальныйФормат.Форма.НастройкаВыгрузки")
//
// Параметры:
//  Отбор - ДанныеФормыКоллекция - коллекция, содержащая строки с описанием подробных отборов по сценарию узла.
//
// Возвращаемое значение:
//  СписокЗначений - список организаций.
//
Функция ОрганизацииИзОтбораИнтерактивнойВыгрузки(Знач Отбор) Экспорт
	
	// Данные в коллекцию Отбор добавляются методом ДобавитьСтрокуОтбораОрганизацийИнтерактивнойВыгрузки этого модуля.
	
	Если Отбор.Количество() = 0 Или Отбор[0].Отбор.Элементы.Количество() = 0 Тогда
		// Нет данных отбора.
		Возврат Новый СписокЗначений;
	КонецЕсли;
	
	Возврат Отбор[0].Отбор.Элементы[0].ПравоеЗначение;
	
КонецФункции

// Добавляет строку в коллекцию Отбор, для варианта ВариантДополнительно интерактивной выгрузки данных.
// (см. процедуру НастроитьИнтерактивнуюВыгрузку этого модуля)
// (см. функцию РезультатВыбора формы "СинхронизацияДанныхЧерезУниверсальныйФормат.Форма.НастройкаВыгрузки")
//
// Параметры:
//  Отбор - ТаблицаЗначений, ДанныеФормыКоллекция - коллекция, содержащая строки с описанием подробных отборов по сценарию узла.
//            Заполняется следующими данными:
//              * ПолноеИмяМетаданных - Строка - полное имя метаданных регистрируемого объекта, отбор которого описывает строка.
//                Используется специальное значение "ВсеДокументы" для отбора всех
//                документов, регистрирующихся на узле Получатель.
//              * ВыборПериода - Булево - флаг того, что данная строка описывает отбор с общим периодом.
//              * Период       - СтандартныйПериод - значение периода общего отбора для метаданных строки.
//              * Отбор        - ОтборКомпоновкиДанных - заполняется отбором по переданному списку организаций.
//  ПериодОтбора - СтандартныйПериод - период устанавливаемый в строки коллекции Отбор.
//                                     Если значение не передается, устанавливается ВариантСтандартногоПериода.ПрошлыйМесяц.
//  СписокОрганизаций - СписокЗначений - список отбираемых организаций, которые устанавливаются в строки коллекции Отбор.
//                                       Если список не передается, считается, что будут переданы объекты без учета отбора по организациям.
//
Процедура ДобавитьСтрокуОтбораОрганизацийИнтерактивнойВыгрузки(Отбор, ПериодОтбора = Неопределено, СписокОрганизаций = Неопределено) Экспорт
	
	СтрокаОтбора = Отбор.Добавить();
	СтрокаОтбора.ПолноеИмяМетаданных = "ВсеДокументы";
	СтрокаОтбора.Период              = ?(ЗначениеЗаполнено(ПериодОтбора), ПериодОтбора, Новый СтандартныйПериод(ВариантСтандартногоПериода.ПрошлыйМесяц));
	СтрокаОтбора.ВыборПериода        = Истина;
	
	КомпоновщикОтбора = Новый КомпоновщикНастроекКомпоновкиДанных;
	СтрокаОтбора.Отбор = КомпоновщикОтбора.Настройки.Отбор;
	
	Если СписокОрганизаций <> Неопределено И СписокОрганизаций.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(СтрокаОтбора.Отбор,
			"Ссылка.Организация", ВидСравненияКомпоновкиДанных.ВСписке, СписокОрганизаций);
	КонецЕсли;
	
КонецПроцедуры

// Строковый идентификатор варианта настройки,
// отличающий его от других вариантов настроек данного плана обмена.
//
Функция ИдентификаторНастройкиУниверсальный()
	Возврат "ОбменУниверсальный";
КонецФункции

// Строковый идентификатор варианта настройки,
// отличающий его от других вариантов настроек данного плана обмена.
//
Функция ИдентификаторНастройкиБП30() Экспорт
	Возврат "ОбменБАБП30";
КонецФункции

#КонецОбласти

#КонецЕсли
