#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОтчетыБольничнаяАптека.АктуализироватьСебестоимостьТоваровДляОтчетов(КомпоновщикНастроек);
	
	ВалютаУпрУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	Для Каждого ЭлементВыбора Из НастройкиОтчета.Выбор.Элементы Цикл
		Если СтрНайти(ЭлементВыбора.Заголовок,"Сумма") > 0 Тогда
			ЭлементВыбора.Заголовок = НСтр("ru = 'Сумма'") + " (" + ВалютаУпрУчета + ")";
		КонецЕсли;
	КонецЦикла;
	
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("СтатусУчетСебестоимостиПоСериям" , ЗапасыКлиентСерверПовтИсп.СтатусыУказанияСерий().СтатусУчетСебестоимостиПоСериям);
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("СтатусУчетСебестоимостиПоПартиям", ЗапасыКлиентСерверПовтИсп.СтатусыУказанияПартий().СтатусУчетСебестоимостиПоПартиям);
	
	ТекстЗапроса = СхемаКомпоновкиДанных.НаборыДанных.Запрос.Запрос;
	
	СхемаКомпоновкиДанных.НаборыДанных.Запрос.Запрос = ТекстЗапроса;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	// Создадим и инициализируем процессор компоновки.
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	// Создадим и инициализируем процессор вывода результата.
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	// Обозначим начало вывода.
	ПроцессорВывода.НачатьВывод();
	ТаблицаЗафиксирована = Ложь;
	
	ДокументРезультат.ФиксацияСверху = 0;
	// Основной цикл вывода отчета.
	Пока Истина Цикл
		// Получим следующий элемент результата компоновки.
		ЭлементРезультата = ПроцессорКомпоновки.Следующий();
		
		Если ЭлементРезультата = Неопределено Тогда
			Прервать;
		Иначе
			// Зафиксируем шапку
			Если Не ТаблицаЗафиксирована
			   И ЭлементРезультата.ЗначенияПараметров.Количество() > 0
			   И ТипЗнч(КомпоновщикНастроек.Настройки.Структура[0]) <> Тип("ДиаграммаКомпоновкиДанных") Тогда
				
				ТаблицаЗафиксирована = Истина;
				ДокументРезультат.ФиксацияСверху = ДокументРезультат.ВысотаТаблицы;
				
			КонецЕсли;
			
			ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПроцессорВывода.ЗакончитьВывод();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли