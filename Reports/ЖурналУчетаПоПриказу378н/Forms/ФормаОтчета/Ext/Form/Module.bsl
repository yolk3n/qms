
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Отчет.ДатаНачала    = НачалоМесяца(ДобавитьМесяц(ТекущаяДатаСеанса(), -1));
	Отчет.ДатаОкончания = КонецМесяца(ДобавитьМесяц(ТекущаяДатаСеанса(), -1));
	
	ОтчетыБольничнаяАптека.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// ТехнологияСервиса.ИнформационныйЦент
	ИнформационныйЦентрСервер.ВывестиКонтекстныеСсылки(ЭтотОбъект, Элементы.ИнформационныеСсылки);
	// Конец ТехнологияСервиса.ИнформационныйЦент
	
	УстановитьВидимостьЭлементовФормы(Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОтчетыБольничнаяАптекаКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	
	ОтчетыБольничнаяАптека.ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	ОтчетыБольничнаяАптека.ПриСохраненииПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОтчетыБольничнаяАптекаКлиент.ПередЗакрытием(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНДЫ ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	Сформировать();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПредыдущийПериод(Команда)
	ИзменитьПериод(-1);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСледующийПериод(Команда)
	ИзменитьПериод(1);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	
	ПодключитьОбработчикОжидания("Подключаемый_ОткрытьНастройки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьНастройки(Команда)
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗакрытьНастройки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтандартныеНастройки(Команда)
	
	УстановитьСтандартныеНастройкиНаСервере();
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаСнятьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Отчет.Группировка Цикл
		СтрокаТаблицы.Использование = Ложь;
	КонецЦикла;
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаУстановитьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Отчет.Группировка Цикл
		СтрокаТаблицы.Использование = Истина;
	КонецЦикла;
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВыполнить(Команда)
	
	СписокВыбора = Новый СписокЗначений;
	Для Каждого ОбъектПечати Из Отчет.ОбъектыПечати Цикл
		СписокВыбора.Добавить(ОбъектПечати.Представление, Строка(ОбъектПечати.Значение));
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения("ПерейтиВыполнитьЗавершение", ЭтотОбъект);
	СписокВыбора.ПоказатьВыборЭлемента(Оповещение, НСтр("ru = 'Перейти к печатной форме'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочте(Команда)
	
	ОтображениеСостояния = Элементы.Результат.ОтображениеСостояния;
	Если ОтображениеСостояния.Видимость И ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность Тогда
		ТекстВопроса = НСтр("ru = 'Отчет не сформирован. Сформировать?'");
		Оповестить = Новый ОписаниеОповещения("ОтправитьПослеФормированияОтчета", ЭтотОбъект);
		ПоказатьВопрос(Оповестить, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	Иначе
		ПоказатьДиалогОтправкиПоЭлектроннойПочте();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// Шапка
#Область Шапка

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	ОтчетыБольничнаяАптекаКлиентСервер.ПриИзмененииРеквизитаОтчета(ЭтотОбъект, Элемент.Имя);
	
КонецПроцедуры

#КонецОбласти // Шапка

////////////////////////////////////////////////////////////////////////////////
// Табличный документ (Результат)
#Область ТабличныйДокументРезультат

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	ОтчетыБольничнаяАптекаКлиент.ОбработатьРасшифровкуОтчета(ЭтотОбъект, Элемент, Расшифровка, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	// Не будем обрабатывать нажатие на правую кнопку мыши.
	// Покажем стандартное контекстное меню ячейки табличного документа.
	Расшифровка = Неопределено;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// Группировка
#Область Группировка

&НаКлиенте
Процедура ГруппировкаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ИсключаемыеПоля = ПолучитьИсключаемыеПоля("Группировка");
	ОтчетыБольничнаяАптекаКлиент.ГруппировкаПередНачаломДобавления(ЭтотОбъект, Элемент, ИсключаемыеПоля, Отказ, Копирование, Родитель, Группа);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаПередНачаломИзменения(Элемент, Отказ)
	
	ИсключаемыеПоля = ПолучитьИсключаемыеПоля("Группировка");
	ОтчетыБольничнаяАптекаКлиент.ГруппировкаПередНачаломИзменения(ЭтотОбъект, Элемент, ИсключаемыеПоля, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти // Группировка

////////////////////////////////////////////////////////////////////////////////
// Отбор
#Область Отбор

&НаКлиенте
Процедура ГруппаПКУПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	ОтчетыБольничнаяАптекаКлиентСервер.ПриИзмененииРеквизитаОтчета(ЭтотОбъект, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	ОтчетыБольничнаяАптекаКлиентСервер.ПриИзмененииРеквизитаОтчета(ЭтотОбъект, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникФинансированияПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	ОтчетыБольничнаяАптекаКлиентСервер.ПриИзмененииРеквизитаОтчета(ЭтотОбъект, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ИсключаемыеПоля = ПолучитьИсключаемыеПоля("Отбор");
	ОтчетыБольничнаяАптекаКлиент.ОтборыПередНачаломДобавления(ЭтотОбъект, Элемент, ИсключаемыеПоля, Отказ, Копирование, Родитель, Группа);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломИзменения(Элемент, Отказ)
	
	ИсключаемыеПоля = ПолучитьИсключаемыеПоля("Отбор");
	ОтчетыБольничнаяАптекаКлиент.ОтборыПередНачаломИзменения(ЭтотОбъект, Элемент, ИсключаемыеПоля, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПриИзменении(Элемент)
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить("Организация");
	Реквизиты.Добавить("ГруппаПКУ");
	Реквизиты.Добавить("Склад");
	Реквизиты.Добавить("ИсточникФинансирования");
	ОтчетыБольничнаяАптекаКлиентСервер.ПриИзмененииОтбора(ЭтотОбъект, Реквизиты);
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить("Организация");
	Реквизиты.Добавить("ГруппаПКУ");
	Реквизиты.Добавить("Склад");
	Реквизиты.Добавить("ИсточникФинансирования");
	ОтчетыБольничнаяАптекаКлиентСервер.ПриИзмененииОтбора(ЭтотОбъект, Реквизиты);
	
КонецПроцедуры

#КонецОбласти // Отбор

////////////////////////////////////////////////////////////////////////////////
// Порядок
#Область Порядок

&НаКлиенте
Процедура ПорядокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ИсключаемыеПоля = ПолучитьИсключаемыеПоля("Порядок");
	ОтчетыБольничнаяАптекаКлиент.ПорядокПередНачаломДобавления(ЭтотОбъект, Элемент, ИсключаемыеПоля, Отказ, Копирование, Родитель, Группа);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокПередНачаломИзменения(Элемент, Отказ)
	
	ИсключаемыеПоля = ПолучитьИсключаемыеПоля("Порядок");
	ОтчетыБольничнаяАптекаКлиент.ПорядокПередНачаломИзменения(ЭтотОбъект, Элемент, ИсключаемыеПоля, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти // Порядок

////////////////////////////////////////////////////////////////////////////////
// Оформление
#Область Оформление

&НаКлиенте
Процедура УсловноеОформлениеПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьТитульныйЛистПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти // Оформление

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

#Область ФормированиеОтчета

&НаКлиенте
Процедура Сформировать()
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОткрытьНастройки", 0.1, Истина);
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = СформироватьОтчетНаСервере();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	Оповещение = Новый ОписаниеОповещения("СформироватьОтчетЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗакрытьНастройки", 0.1, Истина);
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере()
	
	ОтменитьВыполнениеЗадания();
	
	ИдентификаторЗадания = Неопределено;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	Отчет.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ВыводитьЗаголовок", Истина);
	Отчет.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ВыводитьПодвал"   , Ложь);
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчета();
	
	ОтчетыБольничнаяАптека.АктуализироватьСебестоимостьТоваровДляОтчетов(Отчет.КомпоновщикНастроек, ПараметрыОтчета);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ПолучитьНаименованиеЗаданияВыполненияОтчета(ЭтотОбъект);
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"ОтчетыБольничнаяАптека.СформироватьОтчет", ПараметрыОтчета, ПараметрыВыполнения);
	
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервере
Функция ПодготовитьПараметрыОтчета()
	
	Отчет.ОбъектыПечати.Очистить();
	ПараметрыОтчета = ОтчетыБольничнаяАптека.ПолучитьОсновныеПараметрыОтчета(ЭтотОбъект);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчетЗавершение(РезультатДлительнойОперации, НеИспользуется) Экспорт
	
	ИдентификаторЗадания = Неопределено;
	
	Если ТипЗнч(РезультатДлительнойОперации) = Тип("Структура") Тогда
		Если РезультатДлительнойОперации.Статус = "Выполнено" Тогда
			ЗагрузитьПодготовленныеДанные(РезультатДлительнойОперации.АдресРезультата);
			Если ОтправитьПослеФормирования Тогда
				ПоказатьДиалогОтправкиПоЭлектроннойПочте();
			КонецЕсли;
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
			Если РезультатДлительнойОперации.Статус = "Ошибка" Тогда
				ВызватьИсключение РезультатДлительнойОперации.КраткоеПредставлениеОшибки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОтправитьПослеФормирования = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(Знач АдресРезультата)
	
	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если РезультатВыполнения.Свойство("ОписаниеОшибки") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатВыполнения.ОписаниеОшибки);
	КонецЕсли;
	
	Результат = РезультатВыполнения.Результат;
	
	ИдентификаторЗадания = Неопределено;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	ДополнительныеПараметры = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатВыполнения, "ДополнительныеПараметры");
	Если ДополнительныеПараметры <> Неопределено Тогда
		Отчет.ОбъектыПечати = ДополнительныеПараметры.ОбъектыПечати;
	КонецЕсли;
	
	ОтчетыБольничнаяАптекаКлиент.ЗапуститьОжиданиеАктуализацииСебестоимостиТоваров(ЭтотОбъект, Результат);
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьВыполнениеЗадания()
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти // ФормированиеОтчета

#Область ОтправкаПоПочте

&НаКлиенте
Процедура ОтправитьПослеФормированияОтчета(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОтправитьПослеФормирования = Истина;
		Сформировать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДиалогОтправкиПоЭлектроннойПочте()
	
	ОтчетыБольничнаяАптекаКлиент.ПоказатьДиалогОтправкиПоЭлектроннойПочте(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // ОтправкаПоПочте

#Область Прочее

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы(Отказ, СтандартнаяОбработка)
	
	// Тесная интеграция с почтой.
	ДоступнаОтправкаПисем = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями") Тогда
		МодульРаботаСПочтовымиСообщениями = ОбщегоНазначения.ОбщийМодуль("РаботаСПочтовымиСообщениями");
		ДоступнаОтправкаПисем = МодульРаботаСПочтовымиСообщениями.ДоступнаОтправкаПисем();
	КонецЕсли;
	Если Не ДоступнаОтправкаПисем Тогда
		Элементы.ОтправитьПоЭлектроннойПочте.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПериод(Шаг)
	
	Отчет.ДатаНачала    = НачалоМесяца(ДобавитьМесяц(Отчет.ДатаНачала, Шаг));
	Отчет.ДатаОкончания = КонецМесяца(ДобавитьМесяц(Отчет.ДатаОкончания, Шаг));
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВыполнитьЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолеТабличногоДокумента = Элементы.Результат;
	ТабличныйДокумент = Результат;
	ОбластьВыбранногоДокумента = ТабличныйДокумент.Области.Найти(ВыбранныйЭлемент.Значение);
	
	ПолеТабличногоДокумента.ТекущаяОбласть = ТабличныйДокумент.Область("R1C1"); // переход к началу
	
	Если ОбластьВыбранногоДокумента <> Неопределено Тогда
		ПолеТабличногоДокумента.ТекущаяОбласть = ТабличныйДокумент.Область(ОбластьВыбранногоДокумента.Верх,, ОбластьВыбранногоДокумента.Низ,);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьНастройки()
	
	Элементы.ПрименитьНастройки.КнопкаПоУмолчанию = Истина;
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.НастройкиОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗакрытьНастройки()
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.Отчет;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтандартныеНастройкиНаСервере()
	
	ОтчетыБольничнаяАптека.УстановитьНастройкиПоУмолчанию(ЭтотОбъект);
	
	Схема = ПолучитьИзВременногоХранилища(СхемаКомпоновкиДанных);
	Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Схема.НастройкиПоУмолчанию);
	
	Отчет.ДатаНачала = НачалоМесяца(ТекущаяДатаСеанса());
	Отчет.ДатаОкончания = КонецМесяца(ТекущаяДатаСеанса());
	
	Отчет.Группировка.Очистить();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСостояниеОтображенияНеактуальность(ПолеТабличногоДокумента, ИдентификаторЗадания)
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ПолеТабличногоДокумента, "Неактуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИсключаемыеПоля(Режим = "")
	
	СписокПолей = Новый Массив;
	
	СписокПолей.Добавить("UserFields");
	СписокПолей.Добавить("DataParameters");
	СписокПолей.Добавить("SystemFields");
	
	Если Режим = "Группировка" Тогда
		Для Каждого ДоступноеПолеГруппировки Из Отчет.КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.Элементы Цикл
			Если ДоступноеПолеГруппировки.Ресурс Тогда
				СписокПолей.Добавить(Строка(ДоступноеПолеГруппировки.Поле));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(СписокПолей);
	
КонецФункции

#КонецОбласти // Прочее

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// ТехнологияСервиса.ИнформационныйЦентр

&НаКлиенте
Процедура Подключаемый_НажатиеНаИнформационнуюСсылку(Элемент)
	
	ИнформационныйЦентрКлиент.НажатиеНаИнформационнуюСсылку(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеНаСсылкуВсеИнформационныеСсылки(Элемент)
	
	ИнформационныйЦентрКлиент.НажатиеНаСсылкуВсеИнформационныеСсылки(ЭтотОбъект.ИмяФормы);
	
КонецПроцедуры

// Конец ТехнологияСервиса.ИнформационныйЦентр

#КонецОбласти // СтандартныеПодсистемы
