#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область СлужебныйПрограммныйИнтерфейс

// Вызывает модуль менеджера отчета для заполнения его настроек.
//   Для вызова из процедуры ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из процедуры НастроитьВариантыОтчетов.
//   ОтчетМетаданные - ОбъектМетаданных - Метаданные отчета.
//
// Важно:
//   Для использования в модуле менеджера отчета должна быть размещена экспортная процедура по шаблону:
//      // Настройки размещения в панели отчетов.
//      //
//      // Параметры:
//      //   Настройки - Коллекция - Передается "как есть" из ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//      //       Может использоваться для получения настроек варианта этого отчета при помощи функции ВариантыОтчетов.ОписаниеВарианта().
//      //   НастройкиОтчета - СтрокаДереваЗначений - Настройки этого отчета,
//      //       уже сформированные при помощи функции ВариантыОтчетов.ОписаниеОтчета() и готовые к изменению.
//      //       См. "Свойства для изменения" процедуры ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//      //
//      // Описание:
//      //   См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//      //
//      // Вспомогательные методы:
//      //	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//      //	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина/Ложь);
//      //
//      // Примеры:
//      //
//      //  1. Установка описания варианта.
//      //	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//      //	НастройкиВарианта.Описание = НСтр("ru = '<Описание>'");
//      //
//      //  2. Отключение варианта отчета.
//      //	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//      //	НастройкиВарианта.Включен = Ложь;
//      //
//      Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
//      	// Код процедуры.
//      КонецПроцедуры
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	// Эти варианты отчета по умолчанию видны в панели отчетов
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Основной");
	НастройкиВарианта.Описание = НСтр("ru = 'Отчет о приходе и расходе аптечных товаров в денежном выражении (Форма №11-МЗ)'");
	
КонецПроцедуры

// Возвращает признаки переопределения стандартного формирования отчета.
//
// Возвращаемое значение:
//  ПараметрыИсполнения - Структура - признаки использования методов формирования отчета.
//                      Каждый метод описывается в модуле менеджера отчета с именем,
//                      образованным от ключа структуры без приставки Использовать.
//    * ИспользоватьВнешниеНаборыДанных            - Булево - признак использования внешних наборов данных,
//                                                            полученных в результате выполнения указанного метода.
//    * ИспользоватьПриВыводеЗаголовка             - Булево - признак переопределения стандартного метода формирования заголовка отчета.
//                                                            Имеет смысл, если ВыводитьЗаголовок = Истина.
//    * ИспользоватьПриВыводеПодвала               - Булево - признак переопределения стандартного метода формирования подвала отчета.
//                                                            Имеет смысл, если ВыводитьПодвал = Истина.
//    * ИспользоватьПередКомпоновкойМакета         - Булево - признак выполнения дополнительных действий перед компоновкой макета.
//                                                            Имеет смысл, если необходимо изменить настройки отчета перед получением макета компоновки данных.
//    * ИспользоватьПослеКомпоновкиМакета          - Булево - признак выполнения дополнительных действий после компоновки макета.
//                                                            Имеет смысл, если необходимо изменить полученный макет компоновки данных.
//    * ИспользоватьПередВыводомЭлементаРезультата - Булево - признак выполнения дополнительных действий перед выводом элемента результата компоновки.
//                                                            Имеет смысл, если необходимо обработать элемент результата компоновки перед выводом.
//    * ИспользоватьПослеВыводаРезультата          - Булево - признак переопределения стандартного метода ОтчетыБольничнаяАптека.ПослеВыводаРезультата.
//    * ИспользоватьДанныеРасшифровки              - Булево - признак использования данных расшифровки отчета.
//                                                            Не имеет отдельного метода переопределения данных расшифровки.
//    * ИспользоватьПривилегированныйРежим         - Булево - признак использования привилегированного режима при выводе элементов результата компоновки.
//                                                            Не имеет отдельного метода.
//
// см. процедуру ОтчетыБольничнаяАптека.СформироватьОтчет
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	ПараметрыИсполнения = Новый Структура;
	ПараметрыИсполнения.Вставить("ИспользоватьПриВыводеЗаголовка"             , Истина);
	ПараметрыИсполнения.Вставить("ИспользоватьПередКомпоновкойМакета"         , Истина);
	ПараметрыИсполнения.Вставить("ИспользоватьПослеКомпоновкиМакета"          , Истина);
	ПараметрыИсполнения.Вставить("ИспользоватьДанныеРасшифровки"              , Истина);
	ПараметрыИсполнения.Вставить("ИспользоватьПриВыводеПодвала"               , Истина);
	
	Возврат ПараметрыИсполнения;
	
КонецФункции

// Обработчик исполнения отчета.
// см. функцию ПолучитьПараметрыИсполненияОтчета.
//
// Параметры:
//  ПараметрыОтчета     - Структура - параметры, влияющие на результат отчета.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - компоновщик для редактирования настроек отчета.
//  Результат           - ТабличныйДокумент - результат выполнения отчета.
//
// см. процедуру ОтчетыБольничнаяАптека.СформироватьОтчет
//
Процедура ПриВыводеЗаголовка(ПараметрыОтчета, КомпоновщикНастроек, Результат) Экспорт
	
	ДатаНачала    = ПараметрыОтчета.ДатаНачала;
	ДатаОкончания = ПараметрыОтчета.ДатаОкончания;
	Организация   = ПараметрыОтчета.Организация;
	МакетыОтчета  = ПараметрыОтчета.МакетыОтчета;
	
	Если Не ЗначениеЗаполнено(Организация) И ПолучитьФункциональнуюОпцию("НеИспользоватьНесколькоОрганизаций") Тогда
		Организация = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОрганизациюПоУмолчанию();
	КонецЕсли;
	ПодразделениеОрганизации = ПараметрыОтчета.ПодразделениеОрганизации;
	ИсточникФинансирования = ПараметрыОтчета.ИсточникФинансирования;
	
	ВестиУчетПоИсточникамФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьИсточникиФинансирования");
	
	ОбластьШапки = МакетыОтчета.ПФ_MXL_11МЗ.ПолучитьОбласть("Шапка");
	
	// Выведем заголовок.
	СведенияОПокупателе = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Организация, ДатаОкончания);
	
	ДатаСрезаОтветственных = ?(ЗначениеЗаполнено(ДатаОкончания), ДатаОкончания, ТекущаяДатаСеанса());
	ОтветственныеЛица      = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(Организация, ДатаСрезаОтветственных);
	
	ОбластьШапки.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОПокупателе);
	ОбластьШапки.Параметры.Организация              = Организация;
	ОбластьШапки.Параметры.Подразделение            = ПодразделениеОрганизации;
	ОбластьШапки.Параметры.РуководительФИО          = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(СокрЛП(ОтветственныеЛица.Руководитель));;
	ОбластьШапки.Параметры.Руководитель             = ОтветственныеЛица.Руководитель;
	ОбластьШапки.Параметры.Период                   = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'С %1 по %2'"), Формат(ДатаНачала, "ДЛФ=D"), Формат(ДатаОкончания, "ДЛФ=D"));
	
	ОтчетыБольничнаяАптека.ВывестиОбластьВТабличныйДокумент(Результат, ОбластьШапки);
	
	ТекстОтбора = "";
	Если ВестиУчетПоИсточникамФинансирования И ЗначениеЗаполнено(ИсточникФинансирования) Тогда
		ТекстОтбораИсточника = НСтр("ru = 'Источник финансирования Равно'") + " """ + ИсточникФинансирования + """";
		Если СтрНайти(Строка(КомпоновщикНастроек.Настройки.Отбор), ТекстОтбораИсточника) = 0 Тогда
			ТекстОтбора = ТекстОтбораИсточника;
		КонецЕсли;
	КонецЕсли;
	
	ОтчетыБольничнаяАптека.ВывестиОтборВТабличныйДокумент(Результат, КомпоновщикНастроек, ТекстОтбора);
	ОтчетыБольничнаяАптека.ВывестиГруппировкуВТабличныйДокумент(Результат, ПараметрыОтчета.Группировка);
	
КонецПроцедуры

// Обработчик исполнения отчета.
// см. функцию ПолучитьПараметрыИсполненияОтчета.
//
// Параметры:
//  ПараметрыОтчета     - Структура - параметры, влияющие на результат отчета.
//  Схема               - СхемаКомпоновкиДанных - схема компоновки, на основании которой будет выполняться отчет.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - компоновщик для редактирования настроек отчета.
//
// см. процедуру ОтчетыБольничнаяАптека.СформироватьОтчет
//
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	ПараметрыДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных;
	Если ЗначениеЗаполнено(ПараметрыОтчета.ДатаНачала) Тогда
		ПараметрыДанных.УстановитьЗначениеПараметра("НачалоПериода", НачалоДня(ПараметрыОтчета.ДатаНачала));
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыОтчета.ДатаОкончания) Тогда
		ПараметрыДанных.УстановитьЗначениеПараметра("КонецПериода", КонецДня(ПараметрыОтчета.ДатаОкончания));
	КонецЕсли;
	
	СкладыАптеки = ПолучитьОтборПоСкладам(КомпоновщикНастроек);
	ПараметрыДанных.УстановитьЗначениеПараметра("СкладыАптеки", СкладыАптеки);
	
	ВестиУчетПоИсточникамФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьИсточникиФинансирования");
	
	Отбор = КомпоновщикНастроек.Настройки.Отбор;
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, "Организация", ВидСравненияКомпоновкиДанных.Равно, ПараметрыОтчета.Организация, "###ОтборПоОрганизации###", ЗначениеЗаполнено(ПараметрыОтчета.Организация));
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, "ИсточникФинансирования", ВидСравненияКомпоновкиДанных.Равно, ПараметрыОтчета.ИсточникФинансирования, "###ОтборПоИсточникуФинансирования###", ВестиУчетПоИсточникамФинансирования И ЗначениеЗаполнено(ПараметрыОтчета.ИсточникФинансирования));
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, "Склад", ВидСравненияКомпоновкиДанных.ВСписке, СкладыАптеки, "###ОтборПоСкладу###", Истина);
	
	Если Не ПараметрыОтчета.ПоказыватьВсеГруппыБухгалтерскогоУчета Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, "ГруппаБухгалтерскогоУчета.ВыводитьВОтчетах", ВидСравненияКомпоновкиДанных.Равно, Истина, "###ГруппыБухгалтерскогоУчетаДляОтчетов###", Истина);
	КонецЕсли;
	
	// Расчет показателей и заполнение параметров подвала отчета.
	ВыводитьПодвал = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(КомпоновщикНастроек.Настройки.ДополнительныеСвойства, "ВыводитьПодвал");
	Если ВыводитьПодвал = Ложь Тогда
		Для Каждого СтруктураКомпоновкиНастроек Из КомпоновщикНастроек.Настройки.Структура Цикл
			Если СтруктураКомпоновкиНастроек.Использование И СтруктураКомпоновкиНастроек.Имя = "Подвал" Тогда
				СтруктураКомпоновкиНастроек.Использование = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

// Обработчик исполнения отчета.
// см. функцию ПолучитьПараметрыИсполненияОтчета.
//
// Параметры:
//  ПараметрыОтчета  - Структура - параметры, влияющие на результат отчета.
//  МакетКомпоновки  - МакетКомпоновкиДанных - макет компоновки данных отчета.
//
// см. процедуру ОтчетыБольничнаяАптека.СформироватьОтчет
//
Процедура ПослеКомпоновкиМакета(ПараметрыОтчета, МакетКомпоновки) Экспорт
	
	// Обеспечение вывода групп показателей, по которым не было движений в отчетном периоде.
	// Это достигается за счет наборов ПустыеПриходы, ПустыеРасходы и ВыдачаАмбулаторнымБольным,
	// в которых получаются все Группы бухгалтерского учета по всем необходимым Показателям (ХозяйственнаяОперация),
	// но с пустыми значениями остальной аналитикой (Организация, Склад, Номенклатура и т.д.)
	// (см. соответствующие наборы в схеме компоновки данных).
	// Дополнительно нужно удалить отборы в результирующих текстах запросов этих наборов по пустой аналитике,
	// т.к. пустые значения должны всегда попадать в результат.
	
	НаборДанных = МакетКомпоновки.НаборыДанных.ДанныеОтчета.Элементы.Приход.Элементы.ПустыеПриходы;
	УдалитьНесовместимыеОтборыНабораМакетаКомпоновки(НаборДанных);
	
	НаборДанных = МакетКомпоновки.НаборыДанных.ДанныеОтчета.Элементы.Расход.Элементы.ПустыеРасходы;
	УдалитьНесовместимыеОтборыНабораМакетаКомпоновки(НаборДанных);
	
	НаборДанных = МакетКомпоновки.НаборыДанных.ДанныеОтчета.Элементы.ВыдачаАмбулаторнымБольным;
	УдалитьНесовместимыеОтборыНабораМакетаКомпоновки(НаборДанных);
	
	// Изменение текста полей показателей.
	
	МакетНачальныйОстаток = ОтчетыБольничнаяАптека.ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, "НачальныйОстаток")[0];
	МакетНачальныйОстаток.Макет[0].Ячейки[0].Элементы[0].Значение = НСтр("ru = 'Остаток на начало месяца'");
	
	ОтпущеноОтделениям = ОтчетыБольничнаяАптека.ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, "ОтпущеноОтделениям")[0];
	ОтпущеноОтделениям.Макет[0].Ячейки[0].Элементы[0].Значение = НСтр("ru = 'Отпущено отделениям (кабинетам) согласно протаксированным требованиям'");
	
	ОтпущеноОтделениям = ОтчетыБольничнаяАптека.ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, "ОтпущеноОтделениям",, "Подвал")[0];
	ОтпущеноОтделениям.Макет[0].Ячейки[0].Элементы[0].Значение = НСтр("ru = 'Итого отпущено'");
	
	Списано = ОтчетыБольничнаяАптека.ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, "СписаноПоАктамИДругимДокументам")[0];
	Списано.Макет[0].Ячейки[0].Элементы[0].Значение = НСтр("ru = 'Списано по актам и другим документам (название документа, причина списания)'");
	
	Списано = ОтчетыБольничнаяАптека.ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, "СписаноПоАктамИДругимДокументам",, "Подвал")[0];
	Списано.Макет[0].Ячейки[0].Элементы[0].Значение = НСтр("ru = 'Итого списано'");
	
	Списано = ОтчетыБольничнаяАптека.ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, "Расход",, "Подвал")[0];
	Списано.Макет[0].Ячейки[0].Элементы[0].Значение = НСтр("ru = 'Всего израсходовано'");
	
	Выдано = ОтчетыБольничнаяАптека.ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, "Выдача")[0];
	Выдано.Макет[0].Ячейки[0].Элементы[0].Значение = НСтр("ru = 'в том числе для бесплатной выдачи амбулаторным больным'");
	
	МакетКонечныйОстаток = ОтчетыБольничнаяАптека.ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, "КонечныйОстаток")[0];
	МакетКонечныйОстаток.Макет[0].Ячейки[0].Элементы[0].Значение = НСтр("ru = 'Остаток на конец месяца'");
	
КонецПроцедуры

// Обработчик исполнения отчета.
// см. функцию ПолучитьПараметрыИсполненияОтчета.
//
// Параметры:
//  ПараметрыОтчета - Структура - параметры, влияющие на результат отчета.
//  Результат       - ТабличныйДокумент - результат выполнения отчета.
//
// см. процедуру ОтчетыБольничнаяАптека.СформироватьОтчет
//
Процедура ПриВыводеПодвала(ПараметрыОтчета, Результат) Экспорт
	
	// Расчет показателей и заполнение параметров подвала отчета.
	ВыводитьПодвал = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета.НастройкиКомпоновкиДанных.ДополнительныеСвойства, "ВыводитьПодвал");
	Если ВыводитьПодвал = Истина Тогда
		Область = ПараметрыОтчета.МакетыОтчета.ПФ_MXL_11МЗ.ПолучитьОбласть("Подвал");
		
		ОписаниеРуководителя = Новый Структура("ЗаведующийДолжность, ЗаведующийФИО", "ТекущаяДолжностьРуководителя", "ТекущийРуководитель");
		ДанныеРуководителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыОтчета.ПодразделениеОрганизации, ОписаниеРуководителя);
		ДанныеРуководителя.ЗаведующийФИО = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(СокрЛП(ДанныеРуководителя.ЗаведующийФИО));
		Если Не ЗначениеЗаполнено(ДанныеРуководителя.ЗаведующийДолжность) Тогда
			ДанныеРуководителя.ЗаведующийДолжность = НСтр("ru = 'Заведующий аптекой'");
		КонецЕсли;
		
		Область.Параметры.ЗаведующийДолжность = ДанныеРуководителя.ЗаведующийДолжность;
		Область.Параметры.ЗаведующийФИО = ДанныеРуководителя.ЗаведующийФИО;
		
		ОтчетыБольничнаяАптека.ВывестиОбластьВТабличныйДокумент(Результат, Область);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Функция ПолучитьОтборПоСкладам(КомпоновщикНастроек)
	
	СхемаКД = Новый СхемаКомпоновкиДанных;
	ИсточникДанных = СхемаКД.ИсточникиДанных.Добавить();
	ИсточникДанных.Имя = "ИсточникДанных1";
	ИсточникДанных.ТипИсточникаДанных = "Local";
	
	НаборДанных = СхемаКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Имя = "НаборДанных1";
	НаборДанных.ИсточникДанных = ИсточникДанных.Имя;
	
	НаборДанных.Запрос = "
	|ВЫБРАТЬ
	|	Ссылка КАК Склад
	|ИЗ
	|	Справочник.Склады
	|ГДЕ
	|	ТипСклада В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.БольничнаяАптека),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.РозничныйМагазин))
	|";
	
	ПолеНабораДанных = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	ПолеНабораДанных.Поле = "Склад";
	
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКД);
	Компоновщик.Инициализировать(ИсточникНастроек);
	Компоновщик.ЗагрузитьНастройки(КомпоновщикНастроек.Настройки);
	Компоновщик.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.Полное);
	
	Компоновщик.Настройки.Структура.Очистить();
	
	Группировка = Компоновщик.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	Поле = Группировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	Поле.Поле = Новый ПолеКомпоновкиДанных("Склад");
	
	Группировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКД, Компоновщик.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	ТаблицаРезультата = Новый ТаблицаЗначений;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаРезультата);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Возврат ТаблицаРезультата.ВыгрузитьКолонку("Склад");
	
КонецФункции

Процедура УдалитьНесовместимыеОтборыНабораМакетаКомпоновки(НаборДанных)
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(НаборДанных.Запрос);
	ПакетЗапросов = СхемаЗапроса.ПакетЗапросов;
	
	Для Каждого ЗапросОбъект Из ПакетЗапросов Цикл
		Для Каждого Оператор Из ЗапросОбъект.Операторы Цикл
			Граница = Оператор.Отбор.Количество() - 1;
			Для Индекс = 0 По Граница Цикл
				Отбор = Оператор.Отбор[Граница - Индекс];
				Если СтрНайти(Отбор, "ГруппыБухгалтерскогоУчета") <> 0 Тогда
					Продолжить;
				КонецЕсли;
				Оператор.Отбор.Удалить(Граница - Индекс);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	НаборДанных.Запрос = СхемаЗапроса.ПолучитьТекстЗапроса();
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли
