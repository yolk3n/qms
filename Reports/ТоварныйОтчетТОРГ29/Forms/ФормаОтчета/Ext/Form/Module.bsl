
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		Отчет.Организация = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОрганизациюПоУмолчанию();
	КонецЕсли;
	
	Отчет.ДатаНачала    = НачалоГода(ТекущаяДатаСеанса());
	Отчет.ДатаОкончания = ТекущаяДатаСеанса();
	
	ОтчетыБольничнаяАптека.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// ТехнологияСервиса.ИнформационныйЦент
	ИнформационныйЦентрСервер.ВывестиКонтекстныеСсылки(ЭтотОбъект, Элементы.ИнформационныеСсылки);
	// Конец ТехнологияСервиса.ИнформационныйЦент
	
	УстановитьВидимостьЭлементовФормы(Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если Не ЗначениеЗаполнено(Настройки.Получить("Отчет.Организация")) Тогда
		
		Настройки.Удалить("Отчет.Организация");
		Отчет.Организация = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОрганизациюПоУмолчанию(Отчет.Организация);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Настройки.Получить("Отчет.ДатаНачала")) Тогда
		Настройки.Удалить("Отчет.ДатаНачала");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Настройки.Получить("Отчет.ДатаОкончания")) Тогда
		Настройки.Удалить("Отчет.ДатаОкончания");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОтчетыБольничнаяАптекаКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	
	ОтчетыБольничнаяАптека.ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	ОтчетыБольничнаяАптека.ПриСохраненииПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Отчет.Склад) Тогда
		
		ОписаниеДанныхСклада = Новый Структура("ВидЦены, ВалютаСклада", "РозничныйВидЦены", "РозничныйВидЦены.ВалютаЦены");
		ДанныеСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Отчет.Склад, ОписаниеДанныхСклада);
		
		Если Не ЗначениеЗаполнено(ДанныеСклада.ВидЦены) Тогда
			ДеталиОшибки = НСтр("ru = 'Для склада не установлен розничный вид цены.'");
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", Элементы.Склад.Имя,,, ДеталиОшибки);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "Склад", "Отчет", Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ДанныеСклада.ВалютаСклада) Тогда
			ДеталиОшибки = НСтр("ru = 'Не установлена валюта розничного вида цены для склада.'");
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", Элементы.Склад.Имя,,, ДеталиОшибки);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "Склад", "Отчет", Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ОтчетыБольничнаяАптекаКлиент.ОбработатьСобытиеЗаписиМакета(ЭтотОбъект, ИмяСобытия, Параметр, Источник) Тогда
		УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОтчетыБольничнаяАптекаКлиент.ПередЗакрытием(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// Шапка
#Область Шапка

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	ОтчетыБольничнаяАптекаКлиентСервер.ПриИзмененииРеквизитаОтчета(ЭтотОбъект, "Склад");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	ОтчетыБольничнаяАптекаКлиентСервер.ПриИзмененииРеквизитаОтчета(ЭтотОбъект, "Организация");
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерОтчетаПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти // Шапка

////////////////////////////////////////////////////////////////////////////////
// Отбор
#Область Отбор

&НаКлиенте
Процедура ОтборыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ИсключаемыеПоля = ПолучитьИсключаемыеПоля("Отбор");
	ОтчетыБольничнаяАптекаКлиент.ОтборыПередНачаломДобавления(ЭтотОбъект, Элемент, ИсключаемыеПоля, Отказ, Копирование, Родитель, Группа);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломИзменения(Элемент, Отказ)
	
	ИсключаемыеПоля = ПолучитьИсключаемыеПоля("Отбор");
	ОтчетыБольничнаяАптекаКлиент.ОтборыПередНачаломИзменения(ЭтотОбъект, Элемент, ИсключаемыеПоля, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПриИзменении(Элемент)
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить("Организация");
	Реквизиты.Добавить("Склад");
	ОтчетыБольничнаяАптекаКлиентСервер.ПриИзмененииОтбора(ЭтотОбъект, Реквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить("Организация");
	Реквизиты.Добавить("Склад");
	ОтчетыБольничнаяАптекаКлиентСервер.ПриИзмененииОтбора(ЭтотОбъект, Реквизиты);
	
КонецПроцедуры

#КонецОбласти // Отбор

////////////////////////////////////////////////////////////////////////////////
// Порядок
#Область Порядок

&НаКлиенте
Процедура ПорядокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ИсключаемыеПоля = ПолучитьИсключаемыеПоля("Порядок");
	ОтчетыБольничнаяАптекаКлиент.ПорядокПередНачаломДобавления(ЭтотОбъект, Элемент, ИсключаемыеПоля, Отказ, Копирование, Родитель, Группа);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокПередНачаломИзменения(Элемент, Отказ)
	
	ИсключаемыеПоля = ПолучитьИсключаемыеПоля("Порядок");
	ОтчетыБольничнаяАптекаКлиент.ПорядокПередНачаломИзменения(ЭтотОбъект, Элемент, ИсключаемыеПоля, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти // Порядок

////////////////////////////////////////////////////////////////////////////////
// Оформление
#Область Оформление

&НаКлиенте
Процедура УсловноеОформлениеПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьЗаголовокПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьПодвалПриИзменении(Элемент)
	
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти // Оформление

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	Сформировать();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = Новый СтандартныйПериод(Отчет.ДатаНачала, Отчет.ДатаОкончания);
	Диалог.Показать(Новый ОписаниеОповещения("УстановитьИнтервалЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	
	ПодключитьОбработчикОжидания("Подключаемый_ОткрытьНастройки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьНастройки(Команда)
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗакрытьНастройки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтандартныеНастройки(Команда)
	
	УстановитьСтандартныеНастройкиНаСервере();
	УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочте(Команда)
	
	ОтображениеСостояния = Элементы.Результат.ОтображениеСостояния;
	Если ОтображениеСостояния.Видимость И ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность Тогда
		ТекстВопроса = НСтр("ru = 'Отчет не сформирован. Сформировать?'");
		Оповестить = Новый ОписаниеОповещения("ОтправитьПослеФормированияОтчета", ЭтотОбъект);
		ПоказатьВопрос(Оповестить, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	Иначе
		ПоказатьДиалогОтправкиПоЭлектроннойПочте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьМакет(Команда)
	
	ОтчетыБольничнаяАптекаКлиент.ИзменитьМакет(ЭтотОбъект, МакетыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКУправлениюМакетами(Команда)
	
	ОткрытьФорму("РегистрСведений.ПользовательскиеМакетыПечати.Форма.МакетыПечатныхФорм");
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

#Область ФормированиеОтчета

&НаКлиенте
Процедура Сформировать()
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОткрытьНастройки", 0.1, Истина);
		Возврат;
	КонецЕсли;
	
	Задание = СформироватьОтчетНаСервере();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	Оповещение = Новый ОписаниеОповещения("СформироватьОтчетЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Задание, Оповещение, ПараметрыОжидания);
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗакрытьНастройки", 0.1, Истина);
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере()
	
	ОтменитьВыполнениеЗадания();
	ИдентификаторЗадания = Неопределено;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчета();
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ПолучитьНаименованиеЗаданияВыполненияОтчета(ЭтотОбъект);
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне("ОтчетыБольничнаяАптека.СформироватьОтчет", ПараметрыОтчета, ПараметрыВыполнения);
	
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервере
Функция ПодготовитьПараметрыОтчета()
	
	Отчет.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ВыводитьЗаголовок", ВыводитьЗаголовок);
	Отчет.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ВыводитьПодвал"   , ВыводитьПодвал);
	
	Отчет.НомерОтчета = Отчет.НомерОтчета + 1;
	
	ПараметрыОтчета = ОтчетыБольничнаяАптека.ПолучитьОсновныеПараметрыОтчета(ЭтотОбъект);
	ПараметрыОтчета.СхемаКомпоновкиДанных = Отчеты.ВедомостьПоТоварамНаСкладахВЦенахНоменклатуры.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчетЗавершение(РезультатДлительнойОперации, НеИспользуется) Экспорт
	
	ИдентификаторЗадания = Неопределено;
	
	Если ТипЗнч(РезультатДлительнойОперации) = Тип("Структура") Тогда
		Если РезультатДлительнойОперации.Статус = "Выполнено" Тогда
			ЗагрузитьПодготовленныеДанные(РезультатДлительнойОперации.АдресРезультата);
			Если ОтправитьПослеФормирования Тогда
				ПоказатьДиалогОтправкиПоЭлектроннойПочте();
			КонецЕсли;
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
			Если РезультатДлительнойОперации.Статус = "Ошибка" Тогда
				ВызватьИсключение РезультатДлительнойОперации.КраткоеПредставлениеОшибки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОтправитьПослеФормирования = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанные(Знач АдресРезультата)
	
	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если РезультатВыполнения.Свойство("ОписаниеОшибки") Тогда
		ОбщегоНазначения.СообщитьПользователю(РезультатВыполнения.ОписаниеОшибки);
	КонецЕсли;
	
	Результат = РезультатВыполнения.Результат;
	
	ИдентификаторЗадания = Неопределено;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьВыполнениеЗадания()
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти // ФормированиеОтчета

#Область ОтправкаПоПочте

&НаКлиенте
Процедура ОтправитьПослеФормированияОтчета(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОтправитьПослеФормирования = Истина;
		Сформировать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДиалогОтправкиПоЭлектроннойПочте()
	
	ОтчетыБольничнаяАптекаКлиент.ПоказатьДиалогОтправкиПоЭлектроннойПочте(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // ОтправкаПоПочте

#Область Прочее

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы(Отказ, СтандартнаяОбработка)
	
	// Тесная интеграция с почтой.
	ДоступнаОтправкаПисем = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями") Тогда
		МодульРаботаСПочтовымиСообщениями = ОбщегоНазначения.ОбщийМодуль("РаботаСПочтовымиСообщениями");
		ДоступнаОтправкаПисем = МодульРаботаСПочтовымиСообщениями.ДоступнаОтправкаПисем();
	КонецЕсли;
	Если Не ДоступнаОтправкаПисем Тогда
		Элементы.ОтправитьПоЭлектроннойПочте.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалЗавершение(Период, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Период) Тогда
		Отчет.ДатаНачала    = Период.ДатаНачала;
		Отчет.ДатаОкончания = Период.ДатаОкончания;
		УстановитьСостояниеОтображенияНеактуальность(Элементы.Результат, ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьНастройки()
	
	Элементы.ПрименитьНастройки.КнопкаПоУмолчанию = Истина;
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.НастройкиОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗакрытьНастройки()
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.Отчет;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтандартныеНастройкиНаСервере()
	
	ОтчетыБольничнаяАптека.УстановитьНастройкиПоУмолчанию(ЭтотОбъект);
	
	Схема = ПолучитьИзВременногоХранилища(СхемаКомпоновкиДанных);
	Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Схема.НастройкиПоУмолчанию);
	
	ПериодОтчета = ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ПолучитьПараметр(Отчет.КомпоновщикНастроек, "Период").Значение;
	ЗаполнитьЗначенияСвойств(Отчет, ПериодОтчета);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСостояниеОтображенияНеактуальность(ПолеТабличногоДокумента, ИдентификаторЗадания)
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ПолеТабличногоДокумента, "Неактуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИсключаемыеПоля(Режим = "")
	
	СписокПолей = Новый Массив;
	
	СписокПолей.Добавить("UserFields");
	СписокПолей.Добавить("DataParameters");
	СписокПолей.Добавить("SystemFields");
	
	СписокПолей.Добавить("СуммаНачальныйОстаток");
	СписокПолей.Добавить("СуммаПриход");
	СписокПолей.Добавить("СуммаРасход");
	СписокПолей.Добавить("УвеличениеЦены");
	СписокПолей.Добавить("УменьшениеЦены");
	СписокПолей.Добавить("СуммаКонечныйОстаток");
	
	Если Режим = "Отбор" Тогда
		
		СписокПолей.Добавить("Организация");
		СписокПолей.Добавить("Склад");
		
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(СписокПолей);
	
КонецФункции

#КонецОбласти // Прочее

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// ТехнологияСервиса.ИнформационныйЦентр

&НаКлиенте
Процедура Подключаемый_НажатиеНаИнформационнуюСсылку(Элемент)
	
	ИнформационныйЦентрКлиент.НажатиеНаИнформационнуюСсылку(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеНаСсылкуВсеИнформационныеСсылки(Элемент)
	
	ИнформационныйЦентрКлиент.НажатиеНаСсылкуВсеИнформационныеСсылки(ЭтотОбъект.ИмяФормы);
	
КонецПроцедуры

// Конец ТехнологияСервиса.ИнформационныйЦентр

#КонецОбласти // СтандартныеПодсистемы
