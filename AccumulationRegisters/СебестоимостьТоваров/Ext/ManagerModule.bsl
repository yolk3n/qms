#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Процедура сообщает пользователю об ошибках проведения по регистру СебестоимостьТоваров
//
// Параметры
//	Объект - проводимый документ
//	Отказ - признак отказа от проведения документа
//	РезультатЗапроса - информация об ошибках проведения по регистру
//
Процедура СообщитьОбОшибкахПроведения(Объект, Отказ, РезультатЗапроса) Экспорт
	
	ШаблонОперативный = НСтр("ru = 'Номенклатура %Номенклатура% 
		|Превышен оперативный остаток товара по организации %Организация% на складе %Склад%%ИсточникФинансирования% на %Количество% (%Единица%)'");
	ШаблонКонтрольный = НСтр("ru = 'Номенклатура %Номенклатура% 
		|Превышен остаток товара на %ДатаОстатка% по организации %Организация% на складе %Склад%%ИсточникФинансирования% на %Количество% (%Единица%)'");
	
	ПредыдущаяЗапись = Новый Структура("АналитикаВидаУчета, АналитикаУчетаНоменклатуры, РазделУчета");
	
	ПроводитьБезКонтроляОстатков = ПараметрыСеанса.ПроводитьБезКонтроляОстатковТоваровПоРегиструСебестоимости;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ПредыдущаяЗапись.АналитикаВидаУчета <> Выборка.АналитикаВидаУчета
		 Или ПредыдущаяЗапись.АналитикаУчетаНоменклатуры <> Выборка.АналитикаУчетаНоменклатуры
		 Или ПредыдущаяЗапись.РазделУчета <> Выборка.РазделУчета Тогда
			ЗаполнитьЗначенияСвойств(ПредыдущаяЗапись, Выборка);
		Иначе
			Продолжить;
		КонецЕсли;
		ШаблонСообщения = ?(Выборка.ДатаОстатка = ПроведениеБольничнаяАптека.ДатаАктуальныхОстатков(), ШаблонОперативный, ШаблонКонтрольный);
		
		ТекстСообщения = СтрЗаменить(
			ШаблонСообщения, "%Номенклатура%",
			ОбщегоНазначенияБольничнаяАптека.ПолучитьПредставлениеНоменклатуры(Выборка.Номенклатура, Выборка.СерияНоменклатуры, Выборка.Партия));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДатаОстатка%", Формат(Выборка.ДатаОстатка, "ДЛФ=D"));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Организация%", Строка(Выборка.Организация));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Склад%",Строка(Выборка.Склад));
		ТекстСообщения = СтрЗаменить(
			ТекстСообщения,
			"%ИсточникФинансирования%",
			?(ЗначениеЗаполнено(Выборка.ИсточникФинансирования), "" + Символы.ПС + НСтр("ru='по источнику финансирования'") + " " + Строка(Выборка.ИсточникФинансирования) + " ", ""));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Количество%", Строка(-Выборка.Количество));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Единица%", Строка(Выборка.Упаковка));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект);
		
		Если Не ПроводитьБезКонтроляОстатков Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПроводитьБезКонтроляОстатков И Не РезультатЗапроса.Пустой() Тогда
		
		ТекстСообщения = НСтр( "ru = 'Документ %Ссылка% проведен с выключенным контролем остатков товаров по регистру себестоимости.'", ОбщегоНазначения.КодОсновногоЯзыка());
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Объект.Ссылка);
		
		ЗаписьЖурналаРегистрации(ЗапасыСервер.ИмяСобытияВыключенКонтрольОстатков(),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			Объект.Ссылка,
			ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаВидаУчета КАК Т1 
	|	ПО Т.АналитикаВидаУчета = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)
	|	И ЗначениеРазрешено(Т1.Склад)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти // ПрограммныйИнтерфейс

#КонецЕсли