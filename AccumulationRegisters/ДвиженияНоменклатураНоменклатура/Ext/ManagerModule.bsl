#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаВидаУчета КАК Т1 
	|	ПО Т.АналитикаВидаУчета = Т1.КлючАналитики 
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаВидаУчета КАК Т2 
	|	ПО Т.КорАналитикаВидаУчета = Т2.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	( ЗначениеРазрешено(Т1.Организация)
	|	И ЗначениеРазрешено(Т1.Склад)
	|	)ИЛИ( ЗначениеРазрешено(Т2.Организация)
	|	И ЗначениеРазрешено(Т2.Склад)
	|	) ";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область СлужебныйПрограммныйИнтерфейс

#Область РасчетСебестоимостиТоваров

Функция ХозяйственныеОперацииКорректировки() Экспорт
	
	Операции = Новый Массив;
	Операции.Добавить(Перечисления.ХозяйственныеОперации.ВозвратТоваровИзОтделения);
	Операции.Добавить(Перечисления.ХозяйственныеОперации.Изготовление);
	Операции.Добавить(Перечисления.ХозяйственныеОперации.ПересортицаТоваров);
	Операции.Добавить(Перечисления.ХозяйственныеОперации.ОтпускТоваровВОтделение);
	Операции.Добавить(Перечисления.ХозяйственныеОперации.ПеремещениеТоваров);
	Операции.Добавить(Перечисления.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами);
	
	Возврат Операции;
	
КонецФункции

Процедура СформироватьДвижениеКорректировки(ПараметрыРасчета, Выборка) Экспорт
	
	Если Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтпускТоваровВОтделение
	   И Не ЗначениеЗаполнено(Выборка.КорАналитикаВидаУчета) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРегистра = СоздатьНаборЗаписей().Метаданные().Имя;
	ЗаполняемыеПоля = Новый Структура;
	ЗаполняемыеПоля.Вставить("Период");
	ЗаполняемыеПоля.Вставить("ХозяйственнаяОперация");
	ЗаполняемыеПоля.Вставить("АналитикаВидаУчета");
	ЗаполняемыеПоля.Вставить("АналитикаУчетаНоменклатуры");
	ЗаполняемыеПоля.Вставить("КорАналитикаВидаУчета");
	ЗаполняемыеПоля.Вставить("КорАналитикаУчетаНоменклатуры");
	ЗаполняемыеПоля.Вставить("ДокументДвижения");
	ЗаполняемыеПоля = ОбщегоНазначенияКлиентСервер.КлючиСтруктурыВСтроку(ЗаполняемыеПоля);
	
	Запись = Документы.РасчетСебестоимостиТоваров.ДобавитьЗаписьВТаблицуДвижений(ПараметрыРасчета, ИмяРегистра, Выборка, ЗаполняемыеПоля);
	Запись.Стоимость       = Выборка.СтоимостьКорректировка + Выборка.СуммаДопРасходовКорректировка;
	Запись.СтоимостьБезНДС = Выборка.СтоимостьБезНДСКорректировка + Выборка.СуммаДопРасходовБезНДСКорректировка;
	Запись.СтоимостьРегл   = Выборка.СтоимостьРеглКорректировка;
	
	Если Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами Тогда
		Запись = Документы.РасчетСебестоимостиТоваров.ДобавитьЗаписьВТаблицуДвижений(ПараметрыРасчета, ИмяРегистра, Выборка, ЗаполняемыеПоля);
		Запись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутреннееПоступлениеТоваров;
		Запись.Стоимость             = Выборка.СтоимостьКорректировка + Выборка.СуммаДопРасходовКорректировка;
		Запись.СтоимостьБезНДС       = Выборка.СтоимостьБезНДСКорректировка + Выборка.СуммаДопРасходовБезНДСКорректировка;
		Запись.СтоимостьРегл         = Выборка.СтоимостьРеглКорректировка;
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти // РасчетСебестоимостиТоваров

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#КонецЕсли