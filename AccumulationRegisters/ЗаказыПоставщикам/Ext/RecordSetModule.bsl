#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеБольничнаяАптека.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу "ДвиженияЗаказыПоставщикамПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый", ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ПроведениеБольничнаяАптека.ПолучитьМенеджерВременныхТаблицДляКонтроляПроведения(ДополнительныеСвойства);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.ЗаказПоставщику                         КАК ЗаказПоставщику,
	|	Таблица.Спецификация                            КАК Спецификация,
	|	Таблица.Номенклатура                            КАК Номенклатура,
	|	Таблица.КодСтроки                               КАК КодСтроки,
	|	Таблица.КодСтрокиСпецификации                   КАК КодСтрокиСпецификации,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.ПоСпецификации
	|		ИНАЧЕ
	|			-Таблица.ПоСпецификации
	|	КОНЕЦ                                           КАК ПоСпецификацииПередЗаписью,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.КОформлению
	|		ИНАЧЕ
	|			-Таблица.КОформлению
	|	КОНЕЦ                                           КАК КОформлениюПередЗаписью
	|ПОМЕСТИТЬ ДвиженияЗаказыПоставщикамПередЗаписью
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеБольничнаяАптека.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ПроведениеБольничнаяАптека.ПолучитьМенеджерВременныхТаблицДляКонтроляПроведения(ДополнительныеСвойства);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.ЗаказПоставщику                  КАК ЗаказПоставщику,
	|	Таблица.Спецификация                     КАК Спецификация,
	|	Таблица.Номенклатура                     КАК Номенклатура,
	|	Таблица.КодСтроки                        КАК КодСтроки,
	|	Таблица.КодСтрокиСпецификации            КАК КодСтрокиСпецификации,
	|	Таблица.ПоСпецификацииПередЗаписью       КАК ПоСпецификацииИзменение,
	|	Таблица.КОформлениюПередЗаписью          КАК КОформлениюИзменение
	|ПОМЕСТИТЬ ТаблицаИзменений
	|ИЗ
	|	ДвиженияЗаказыПоставщикамПередЗаписью КАК Таблица
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.ЗаказПоставщику                     КАК ЗаказПоставщику,
	|	Таблица.Спецификация                        КАК Спецификация,
	|	Таблица.Номенклатура                        КАК Номенклатура,
	|	Таблица.КодСтроки                           КАК КодСтроки,
	|	Таблица.КодСтрокиСпецификации               КАК КодСтрокиСпецификации,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			-Таблица.ПоСпецификации
	|		ИНАЧЕ
	|			Таблица.ПоСпецификации
	|	КОНЕЦ                                       КАК ПоСпецификацииИзменение,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			-Таблица.КОформлению
	|		ИНАЧЕ
	|			Таблица.КОформлению
	|	КОНЕЦ                                       КАК КОформлениюИзменение
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИзменений.ЗаказПоставщику                     КАК ЗаказПоставщику,
	|	ТаблицаИзменений.Спецификация                        КАК Спецификация,
	|	ТаблицаИзменений.Номенклатура                        КАК Номенклатура,
	|	ТаблицаИзменений.КодСтроки                           КАК КодСтроки,
	|	ТаблицаИзменений.КодСтрокиСпецификации               КАК КодСтрокиСпецификации,
	|	СУММА(ТаблицаИзменений.ПоСпецификацииИзменение)      КАК ПоСпецификацииИзменение,
	|	СУММА(ТаблицаИзменений.КОформлениюИзменение)         КАК КОформлениюИзменение
	|ПОМЕСТИТЬ ДвиженияЗаказыПоставщикамИзменение
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ЗаказПоставщику,
	|	ТаблицаИзменений.Спецификация,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.КодСтроки,
	|	ТаблицаИзменений.КодСтрокиСпецификации
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.ПоСпецификацииИзменение) > 0
	|	ИЛИ СУММА(ТаблицаИзменений.КОформлениюИзменение) > 0
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияЗаказыПоставщикамПередЗаписью
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаИзменений
	|";
	
	Выборка = Запрос.ВыполнитьПакет()[1].Выбрать();
	Выборка.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияЗаказыПоставщикамИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	Если Выборка.Количество > 0 Тогда
		ПроведениеБольничнаяАптека.ДобавитьПараметрыКонтроля(
			ДополнительныеСвойства,
			ТекстЗапросаПроверки(),
			РегистрыНакопления.ЗаказыПоставщикам);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПроверки()
	
	ТекстЗапроса ="
	|ВЫБРАТЬ
	|	ТаблицаОстатков.ЗаказПоставщику                     КАК Заказ,
	|	ТаблицаОстатков.Спецификация                        КАК Спецификация,
	|	ТаблицаОстатков.Номенклатура                        КАК Номенклатура,
	|	ТаблицаОстатков.КодСтроки                           КАК КодСтроки,
	|	ТаблицаОстатков.КодСтрокиСпецификации               КАК КодСтрокиСпецификации,
	|	ТаблицаОстатков.ПоСпецификацииОстаток               КАК ПоСпецификации,
	|	ТаблицаОстатков.КОформлениюОстаток                  КАК КОформлению,
	|	ЕСТЬNULL(СпецификацияТовары.ЕдиницаИзмерения, ЗаказТовары.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(СпецификацияТовары.Коэффициент, ЗаказТовары.Коэффициент) КАК Коэффициент
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам.Остатки(,
	|			(ЗаказПоставщику, Спецификация, Номенклатура, КодСтроки, КодСтрокиСпецификации) В
	|				(ВЫБРАТЬ
	|					Таблица.ЗаказПоставщику,
	|					Таблица.Спецификация,
	|					Таблица.Номенклатура,
	|					Таблица.КодСтроки,
	|					Таблица.КодСтрокиСпецификации
	|				ИЗ
	|					ДвиженияЗаказыПоставщикамИзменение КАК Таблица)
	|	) КАК ТаблицаОстатков
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.СпецификацияКДоговору.Товары КАК СпецификацияТовары
	|		ПО
	|			СпецификацияТовары.Ссылка = ТаблицаОстатков.Спецификация
	|			И СпецификацияТовары.КодСтроки = ТаблицаОстатков.КодСтрокиСпецификации
	|			И СпецификацияТовары.Номенклатура = ТаблицаОстатков.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ЗаказПоставщику.Товары КАК ЗаказТовары
	|		ПО
	|			ЗаказТовары.Ссылка = ТаблицаОстатков.ЗаказПоставщику
	|			И ЗаказТовары.КодСтроки = ТаблицаОстатков.КодСтроки
	|			И ЗаказТовары.Номенклатура = ТаблицаОстатков.Номенклатура
	|
	|ГДЕ
	|	ТаблицаОстатков.ПоСпецификацииОстаток < 0
	|	ИЛИ ТаблицаОстатков.КОформлениюОстаток < 0
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли