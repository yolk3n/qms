#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Процедура сообщает пользователю об ошибках проведения по регистру ТоварыКОформлениюИзлишковНедостач
//
// Параметры
//	Объект - проводимый документ
//	Отказ - признак отказа от проведения документа
//	РезультатЗапроса - информация об ошибках проведения по регистру
//
Процедура СообщитьОбОшибкахПроведения(Объект, Отказ, РезультатЗапроса) Экспорт
	
	ШаблонСообщенияНедостача = НСтр("ru = 'Номенклатура: %Номенклатура%
		|Списание по документу %Основание% превышает недостачу на %Количество%'");
	
	ШаблонСообщенияИзлишек = НСтр("ru = 'Номенклатура: %Номенклатура%
		|Оприходование по документу %Основание% превышает излишек на %Количество%'");
		
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КОформлениюОприходования < 0 Тогда
			ШаблонСообщения = ШаблонСообщенияИзлишек;
		Иначе
			ШаблонСообщения = ШаблонСообщенияНедостача;
		КонецЕсли;
		
		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%Номенклатура%", ОбщегоНазначенияБольничнаяАптека.ПолучитьПредставлениеНоменклатуры(Выборка.Номенклатура, Выборка.СерияНоменклатуры, Выборка.Партия));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Основание%", Строка(Выборка.ДокументОснование));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Количество%", Строка(?(Выборка.КОформлениюОприходования < 0, -Выборка.КОформлениюОприходования, -Выборка.КОформлениюСписания)) + " (" + Строка(Выборка.Упаковка) + ")");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект,,, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Склад)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти // ПрограммныйИнтерфейс

#КонецЕсли