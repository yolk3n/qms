#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция КодОперации(Операция) Экспорт
	
	ИмяЗначения = ОбщегоНазначения.ИмяЗначенияПеречисления(Операция);
	КодСтрокой = Операция.Метаданные().ЗначенияПеречисления[ИмяЗначения].Комментарий;
	Возврат ОбщегоНазначения.ОписаниеТипаЧисло(5).ПривестиЗначение(КодСтрокой);
	
КонецФункции

Функция ВозможнаОтменаОперации(Операция, ВерсияСхемОбмена = Неопределено) Экспорт
	
	ОтменяемыеОперации = ИнтеграцияМДЛП.ТипФабрикаXDTO("recall_type_enum", ИнтеграцияМДЛП.ПространствоИмен(ВерсияСхемОбмена));
	
	КодОперации = Строка(КодОперации(Операция));
	
	Для Каждого ОперацияОтмены Из ОтменяемыеОперации.Фасеты Цикл
		Если ОперацияОтмены.Значение = КодОперации Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли