
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииНовогоПриЧтенииНаСервере();
	КонецЕсли;
	
	ТолькоПросмотр = Истина;
	
	ЗаполнитьДоступныеИсточники();
	ЗаполнитьВыборДокументов();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииНовогоПриЧтенииНаСервере();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПредставлениеИсточникаДанныхНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Элемент.СписокВыбора;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеИсточникаДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		СтандартнаяОбработка = Ложь;
		Объект.ИсточникДанных = ВыбранноеЗначение;
		ВыбранныйИсточник = Элемент.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
		Объект.ПредставлениеИсточникаДанных = ВыбранныйИсточник.Представление;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНовогоПриЧтенииНаСервере()
	
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоступныеИсточники()
	
	СписокВыбора = Элементы.ПредставлениеИсточникаДанных.СписокВыбора;
	СписокВыбора.Очистить();
	
	Для Каждого Макет Из Метаданные.Справочники.НастройкиХозяйственныхОпераций.Макеты Цикл
		Если Макет.ТипМакета = Метаданные.СвойстваОбъектов.ТипМакета.СхемаКомпоновкиДанных Тогда
			СписокВыбора.Добавить(Макет.Имя, Макет.Синоним);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВыборДокументов()
	
	Список = Элементы.ДокументыДокумент.СписокВыбора;
	Список.Очистить();
	
	Для Каждого Документ Из Метаданные.Документы Цикл
		Список.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Документ));
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
