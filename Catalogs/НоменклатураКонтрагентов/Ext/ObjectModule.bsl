#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	// БольничнаяАптека
	Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) И ЗначениеЗаполнено(Упаковка) Тогда
		ЕдиницаИзмерения = Упаковка;
	ИначеЕсли Не ЗначениеЗаполнено(Упаковка) И ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		Упаковка = ЕдиницаИзмерения;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Владелец) И ЗначениеЗаполнено(ВладелецНоменклатуры) Тогда
		Владелец = ВладелецНоменклатуры;
	ИначеЕсли Не ЗначениеЗаполнено(ВладелецНоменклатуры) И ЗначениеЗаполнено(Владелец) Тогда
		ВладелецНоменклатуры = Владелец;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Если Владелец = Константы.КонтрагентДляПроектаОперативныйМониторингЛС.Получить() Тогда
		ДополнительныеСвойства.Вставить("Синхронизация");
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ЗначениеЗаполнено(ВладелецНоменклатуры) Тогда
		ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Владелец номенклатуры'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка, "ВладелецНоменклатуры", "Объект", Отказ);
	КонецЕсли;
	// Конец БольничнаяАптека
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ЭтоГруппа И Не ИспользоватьХарактеристики Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Характеристика");
	
	КонецЕсли;
	
	// БольничнаяАптека
	// Владелец заполняется в событии ПередЗаписью по данным реквизита ВладелецНоменклатуры.
	МассивНепроверяемыхРеквизитов.Добавить("Владелец");
	// Конец БольничнаяАптека
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли