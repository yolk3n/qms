
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Элементы.ФормаПодобратьИзКлассификатора.Видимость = ПравоДоступа("Добавление", Метаданные.Справочники.ЕдиницыИзмерения);
	
	НастроитьСписокВыбора();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьИзКлассификатора(Команда)
	
	ОткрытьФорму("Справочник.ЕдиницыИзмерения.Форма.КлассификаторЕдиницИзмерения",, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьСписокВыбора()
	
	Если Параметры.Свойство("ВыборБазовыхЕдиницИзмеренияПрепаратов") Тогда
		
		Отбор = ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ОбластьОтбораДинамическогоСписка(Список);
		ГруппаОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(Отбор.Элементы, "ЕдиницыПрепаратов", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		ТипыЕдиницИзмерения = Новый Массив;
		ТипыЕдиницИзмерения.Добавить(Перечисления.ТипыЕдиницИзмерения.Вес);
		ТипыЕдиницИзмерения.Добавить(Перечисления.ТипыЕдиницИзмерения.Объем);
		Единицы = Новый Массив;
		Единицы.Добавить(ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьЕдиницуИзмеренияШтук());
		Единицы.Добавить(ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьЕдиницуИзмеренияДоз());
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбора, "ТипЕдиницы", ТипыЕдиницИзмерения, ВидСравненияКомпоновкиДанных.ВСписке);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбора, "Ссылка", Единицы, ВидСравненияКомпоновкиДанных.ВСписке);
		
	Иначе
		
		Если Не Параметры.Отбор.Свойство("ТипЕдиницы") Тогда
			
			ОтборПоТипу = Новый Массив;
			ОтборПоТипу.Добавить(Перечисления.ТипыЕдиницИзмерения.Вес);
			ОтборПоТипу.Добавить(Перечисления.ТипыЕдиницИзмерения.Объем);
			ОтборПоТипу.Добавить(Перечисления.ТипыЕдиницИзмерения.Площадь);
			ОтборПоТипу.Добавить(Перечисления.ТипыЕдиницИзмерения.Длина);
			ОтборПоТипу.Добавить(Перечисления.ТипыЕдиницИзмерения.КоличествоШтук);
			ОтборПоТипу.Добавить(Перечисления.ТипыЕдиницИзмерения.ПустаяСсылка());
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ТипЕдиницы", ОтборПоТипу, ВидСравненияКомпоновкиДанных.ВСписке);
			
		ИначеЕсли Параметры.Отбор.Свойство("Ссылка") Тогда
			
			Отбор = ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ОбластьОтбораДинамическогоСписка(Список);
			ГруппаОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(Отбор.Элементы, "ЕдиницыПрепаратов", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбора, "ТипЕдиницы", Параметры.Отбор.ТипЕдиницы, ВидСравненияКомпоновкиДанных.ВСписке);
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбора, "Ссылка", Параметры.Отбор.Ссылка, ВидСравненияКомпоновкиДанных.ВСписке);
			
			Параметры.Отбор.Удалить("ТипЕдиницы");
			Параметры.Отбор.Удалить("Ссылка");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
