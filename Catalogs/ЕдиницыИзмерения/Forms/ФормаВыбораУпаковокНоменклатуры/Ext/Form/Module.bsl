
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеФормы();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НедоступныеДляВыбораЕдиницы = Новый Массив;
	ТекущаяУпаковка = Неопределено;
	Если Параметры.Отбор.Свойство("ТекущаяУпаковка", ТекущаяУпаковка) Тогда
		Если ЗначениеЗаполнено(ТекущаяУпаковка) Тогда
			Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	Единицы.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ЕдиницыИзмерения КАК Единицы
			|ГДЕ
			|	Единицы.Ссылка В ИЕРАРХИИ (&ТекущаяУпаковка)
			|");
			Запрос.УстановитьПараметр("ТекущаяУпаковка", ТекущаяУпаковка);
			НедоступныеДляВыбораЕдиницы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		КонецЕсли;
		Параметры.Отбор.Удалить("ТекущаяУпаковка");
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Номенклатура", Номенклатура) Тогда
		РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "Упаковка");
		Упаковка = РеквизитыНоменклатуры.Упаковка;
		Пока ЗначениеЗаполнено(Упаковка) Цикл
			Упаковка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Упаковка, "Родитель");
			Если ЗначениеЗаполнено(Упаковка) Тогда
				НедоступныеДляВыбораЕдиницы.Добавить(Упаковка);
			КонецЕсли;
		КонецЦикла;
		Параметры.Отбор.Удалить("Номенклатура");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Номенклатура", Номенклатура);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "НедоступныеДляВыбораЕдиницы", НедоступныеДляВыбораЕдиницы);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные.НедоступнаяДляВыбораЕдиница Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	Элемент = Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы.Добавить();
	Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НедоступнаяДляВыбораЕдиница");
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
