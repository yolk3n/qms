
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	Если Объект.Ссылка.Пустая() Тогда
		Если Не ЗначениеЗаполнено(Объект.Номенклатура) Тогда
			ВызватьИсключение НСтр("ru = 'Создание упаковок доступно только из списка упаковок номенклатуры.'");
		КонецЕсли;
		ПриСозданииНовогоПриЧтенииНаСервере();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Номенклатура) Тогда
		КлючНазначенияИспользования = "УпаковкаПрепарата";
		ТолькоПросмотр = Истина;
		Элементы.Номенклатура.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииНовогоПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовый", Объект.Ссылка.Пустая());
	ПараметрыЗаписи.Вставить("Номенклатура", Объект.Номенклатура);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	ОбновитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_УпаковкаНоменклатуры", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СостоитИзДругихУпаковокПриИзменении(Элемент)
	
	Объект.СостоитИзДругихУпаковок = (СостоитИзДругихУпаковок = 1);
	УстановитьТекущуюСтраницуКоэффициентов(ЭтотОбъект);
	ОбновитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаПриИзменении(Элемент)
	
	ОбновитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоВУпаковкеПриИзменении(Элемент)
	
	ОбновитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоУпаковокПриИзменении(Элемент)
	
	ОбновитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	ОбновитьНаименование();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНовогоПриЧтенииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Номенклатура) Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Номенклатура, "Упаковка");
		Элементы.СостоитИзДругихУпаковокПереключатель.Доступность = Не ЗначениеЗаполнено(Реквизиты.Упаковка);
	КонецЕсли;
	
	ЭтоУпаковкаЛекарственногоСредства = Не ЗначениеЗаполнено(Объект.Номенклатура)
		Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Номенклатура, "ЭтоЛекарственноеСредство");
	Элементы.Наименование.Доступность            = Не ЭтоУпаковкаЛекарственногоСредства;
	Элементы.НаименованиеПолное.Доступность      = Не ЭтоУпаковкаЛекарственногоСредства;
	Элементы.НаименованиеСокращенное.Доступность = Не ЭтоУпаковкаЛекарственногоСредства;
	
	СостоитИзДругихУпаковок = ?(Объект.СостоитИзДругихУпаковок, 1, 0);
	УстановитьТекущуюСтраницуКоэффициентов(ЭтотОбъект);
	ОбновитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущуюСтраницуКоэффициентов(Форма)
	
	Если Форма.Объект.СостоитИзДругихУпаковок Тогда
		Форма.Элементы.ГруппаСтраницыКоэффициенты.ТекущаяСтраница = Форма.Элементы.ГруппаКоличествоУпаковокУпаковка;
	Иначе
		Форма.Элементы.ГруппаСтраницыКоэффициенты.ТекущаяСтраница = Форма.Элементы.ГруппаКоэффициентЕдиницаИзмерения;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокФормы()
	
	ПредставлениеТипа = НСтр("ru='Упаковка'");
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1 (создание)'"),
			ПредставлениеТипа);
	Иначе
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1 (%2)'"),
			Объект.Наименование,
			ПредставлениеТипа);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаименование()
	
	Справочники.ЕдиницыИзмерения.ОтработатьЛогикуСвязиРеквизитов(Объект);
	Справочники.ЕдиницыИзмерения.УстановитьНаименованиеУпаковки(Объект);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовБольничнаяАптекаКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти // СтандартныеПодсистемы
