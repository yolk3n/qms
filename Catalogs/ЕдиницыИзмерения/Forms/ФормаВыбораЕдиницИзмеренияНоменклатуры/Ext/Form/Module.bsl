
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НастроитьСписокВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ЕдиницыИзмерения" Тогда
		Если Параметр.ЭтоНовый
		   И (Параметр.ТипЕдиницы = ПредопределенноеЗначение("Перечисление.ТипыЕдиницИзмерения.Вес")
		     Или Параметр.ТипЕдиницы = ПредопределенноеЗначение("Перечисление.ТипыЕдиницИзмерения.Объем")) Тогда
			УстановитьОтборПоОтборуНоменклатуры();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Запись_УпаковкаНоменклатуры" Тогда
		Если Параметр.ЭтоНовый И Параметр.Номенклатура= Номенклатура Тогда
			УстановитьОтборПоОтборуНоменклатуры();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьСписокВыбора()
	
	Отбор = Параметры.Отбор;
	Если Отбор.Свойство("Номенклатура", Номенклатура) Тогда
		
		Отбор.Удалить("Номенклатура");
		
		Если Отбор.Свойство("ТорговоеНаименование", ТорговоеНаименование) Тогда
			Отбор.Удалить("ТорговоеНаименование");
		КонецЕсли;
		Если Отбор.Свойство("ДействующиеВеществаМНН", ДействующиеВеществаМНН) Тогда
			Отбор.Удалить("ДействующиеВеществаМНН");
		КонецЕсли;
		Если Отбор.Свойство("ФормаВыпуска", ФормаВыпуска) Тогда
			Отбор.Удалить("ФормаВыпуска");
		КонецЕсли;
		
		УстановитьОтборПоОтборуНоменклатуры();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоОтборуНоменклатуры()
	
	ПараметрыОтбора = Новый Структура("Номенклатура", Номенклатура);
	ПараметрыОтбора.Вставить("ТорговоеНаименование", ?(ТорговоеНаименование.Пустая(), Неопределено, ТорговоеНаименование));
	ПараметрыОтбора.Вставить("ДействующиеВеществаМНН", ?(ДействующиеВеществаМНН.Пустая(), Неопределено, ДействующиеВеществаМНН));
	ПараметрыОтбора.Вставить("ФормаВыпуска", ФормаВыпуска);
	
	СписокЕдиниц = ОтборНоменклатуры.ЕдиницыИзмеренияПоОтбору(ПараметрыОтбора);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Ссылка",
		СписокЕдиниц,
		ВидСравненияКомпоновкиДанных.ВСписке,
		,
		Истина);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
