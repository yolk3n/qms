
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииНовогоПриЧтенииНаСервере();
	Иначе
		Элементы.ТипЕдиницы.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.Отображать;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииНовогоПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовый", Объект.Ссылка.Пустая());
	ПараметрыЗаписи.Вставить("ТипЕдиницы", Объект.ТипЕдиницы);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ЕдиницыИзмерения", ПараметрыЗаписи, Объект.Ссылка);
	
	Если ПараметрыЗаписи.ЭтоНовый Тогда
		Элементы.ТипЕдиницы.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.Отображать;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипЕдиницыПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура БазоваяЕдиницаИзмеренияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = Объект.Ссылка Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНовогоПриЧтенииНаСервере()
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	Если Объект.ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Вес Тогда
		
		Элементы.Коэффициент.Заголовок = НСтр("ru='Количество, г'");
		Элементы.Коэффициент.Видимость = Истина;
		Элементы.БазоваяЕдиницаИзмерения.Видимость = Ложь;
		
	ИначеЕсли Объект.ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Объем Тогда
		
		Элементы.Коэффициент.Заголовок = НСтр("ru='Количество, л (дм3)'");
		Элементы.Коэффициент.Видимость = Истина;
		Элементы.БазоваяЕдиницаИзмерения.Видимость = Ложь;
		
	ИначеЕсли Объект.ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Длина Тогда
		
		Элементы.Коэффициент.Заголовок = НСтр("ru='Количество, м'");
		Элементы.Коэффициент.Видимость = Истина;
		Элементы.БазоваяЕдиницаИзмерения.Видимость = Ложь;
		
	ИначеЕсли Объект.ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Площадь Тогда
		
		Элементы.Коэффициент.Заголовок = НСтр("ru='Количество, м2'");
		Элементы.Коэффициент.Видимость = Истина;
		Элементы.БазоваяЕдиницаИзмерения.Видимость = Ложь;
		
	ИначеЕсли Объект.ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Концентрация Тогда
		
		Элементы.Коэффициент.Заголовок = НСтр("ru='Количество'");
		Элементы.Коэффициент.Видимость = Истина;
		Элементы.БазоваяЕдиницаИзмерения.Видимость = Истина;
		
	Иначе
		
		Элементы.Коэффициент.Видимость = Ложь;
		Элементы.БазоваяЕдиницаИзмерения.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовБольничнаяАптекаКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти // СтандартныеПодсистемы
