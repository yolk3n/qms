#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипЗначения = ТипЗнч(ДанныеЗаполнения);
	Если ТипЗначения = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("ТипЕдиницы") И ДанныеЗаполнения.ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Упаковка Тогда
			Если ДанныеЗаполнения.Свойство("Родитель", Родитель) Тогда
				Если ЗначениеЗаполнено(Родитель) Тогда
					ТипЕдиницыРодителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Родитель, "ТипЕдиницы");
					Если ТипЕдиницыРодителя = Перечисления.ТипыЕдиницИзмерения.Упаковка Тогда
						СостоитИзДругихУпаковок = Истина;
					Иначе
						СостоитИзДругихУпаковок = Ложь;
						Родитель = Неопределено;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Если ДанныеЗаполнения.Свойство("Номенклатура", Номенклатура) И ЗначениеЗаполнено(Номенклатура) Тогда
				Если Не ЗначениеЗаполнено(Родитель) Тогда
					РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "Упаковка, ЕдиницаИзмерения");
					Родитель = РеквизитыНоменклатуры.Упаковка;
					Если ЗначениеЗаполнено(Родитель) И Родитель <> РеквизитыНоменклатуры.ЕдиницаИзмерения Тогда
						СостоитИзДругихУпаковок = Истина;
						БазоваяЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Родитель, "БазоваяЕдиницаИзмерения");
					Иначе
						БазоваяЕдиницаИзмерения = РеквизитыНоменклатуры.ЕдиницаИзмерения;
						СостоитИзДругихУпаковок = Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Упаковка Тогда
		
		НепроверяемыеРеквизиты.Добавить("Наименование");
		Если СостоитИзДругихУпаковок Тогда
			НепроверяемыеРеквизиты.Добавить("БазоваяЕдиницаИзмерения");
			НепроверяемыеРеквизиты.Добавить("Коэффициент");
		Иначе
			НепроверяемыеРеквизиты.Добавить("Родитель");
			НепроверяемыеРеквизиты.Добавить("КоличествоВУпаковке");
			Если ЗначениеЗаполнено(Номенклатура) Тогда
				НепроверяемыеРеквизиты.Добавить("БазоваяЕдиницаИзмерения");
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Справочники.ЕдиницыИзмерения.МерныеТипыЕдиницИзмерений().Найти(ТипЕдиницы) <> Неопределено Тогда
		
		НепроверяемыеРеквизиты.Добавить("Номенклатура");
		НепроверяемыеРеквизиты.Добавить("БазоваяЕдиницаИзмерения");
		НепроверяемыеРеквизиты.Добавить("Родитель");
		НепроверяемыеРеквизиты.Добавить("Упаковка");
		НепроверяемыеРеквизиты.Добавить("КоличествоВУпаковке");
		
	Иначе
		
		НепроверяемыеРеквизиты.Добавить("Номенклатура");
		НепроверяемыеРеквизиты.Добавить("Родитель");
		НепроверяемыеРеквизиты.Добавить("Упаковка");
		НепроверяемыеРеквизиты.Добавить("КоличествоВУпаковке");
		
		Если ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Концентрация Тогда
			Если Не ЗначениеЗаполнено(Коэффициент) И Не ЗначениеЗаполнено(БазоваяЕдиницаИзмерения) Тогда
				НепроверяемыеРеквизиты.Добавить("БазоваяЕдиницаИзмерения");
				НепроверяемыеРеквизиты.Добавить("Коэффициент");
			КонецЕсли;
		Иначе
			НепроверяемыеРеквизиты.Добавить("БазоваяЕдиницаИзмерения");
			НепроверяемыеРеквизиты.Добавить("Коэффициент");
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("ИзменилсяКоэффициент", Ложь);
	
	Справочники.ЕдиницыИзмерения.ОтработатьЛогикуСвязиРеквизитов(ЭтотОбъект);
	
	Если Не ЭтоНовый() Тогда
		
		Если ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Упаковка Тогда
			
			ДанныеУпаковки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Коэффициент, БазоваяЕдиницаИзмерения, Родитель, КоличествоВПотребительскойУпаковке");
			Если Коэффициент <> ДанныеУпаковки.Коэффициент
			 Или БазоваяЕдиницаИзмерения <> ДанныеУпаковки.БазоваяЕдиницаИзмерения
			 Или КоличествоВПотребительскойУпаковке <> ДанныеУпаковки.КоличествоВПотребительскойУпаковке
			 Или Родитель <> ДанныеУпаковки.Родитель Тогда
				ДополнительныеСвойства.Вставить("ОбновитьЗависимыеУпаковки");
				ДополнительныеСвойства.ИзменилсяКоэффициент = (Коэффициент <> ДанныеУпаковки.Коэффициент);
				ДополнительныеСвойства.Вставить("ИзмениласьБазоваяЕдиницаИзмерения", БазоваяЕдиницаИзмерения <> ДанныеУпаковки.БазоваяЕдиницаИзмерения);
				ДополнительныеСвойства.Вставить("ИзменилосьКоличествоВПотребительскойУпаковке", КоличествоВПотребительскойУпаковке <> ДанныеУпаковки.КоличествоВПотребительскойУпаковке);
				ДополнительныеСвойства.Вставить("ИзменилсяРодитель", Родитель <> ДанныеУпаковки.Родитель);
			КонецЕсли;
			
		ИначеЕсли Справочники.ЕдиницыИзмерения.МерныеТипыЕдиницИзмерений().Найти(ТипЕдиницы) <> Неопределено Тогда
			
			ДанныеЕдиницы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Коэффициент");
			Если Коэффициент <> ДанныеЕдиницы.Коэффициент Тогда
				ДополнительныеСвойства.ИзменилсяКоэффициент = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ОбновитьЗависимыеУпаковки") Тогда
		ОбновитьЗависимыеУпаковки();
	КонецЕсли;
	
	Если ДополнительныеСвойства.ИзменилсяКоэффициент Тогда
		РегистрыСведений.ЕдиницыИзмеренияНоменклатуры.ПересчитатьКоэффициентЕдиницыИзмеренияНоменклатуры(Ссылка, ТипЕдиницы);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ЭтоНовый Тогда
		РегистрыСведений.ЕдиницыИзмеренияНоменклатуры.ДобавитьЕдиницуИзмеренияНоменклатуры(Ссылка, ТипЕдиницы, Номенклатура);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьЗависимыеУпаковки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	УпаковкиНоменклатуры.Ссылка
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК УпаковкиНоменклатуры
	|ГДЕ
	|	УпаковкиНоменклатуры.Родитель = &Родитель
	|";
	Запрос.УстановитьПараметр("Родитель", Ссылка);
	
	ШаблонСообщения = НСтр("ru = 'Была изменена упаковка ""%Упаковка%"". Проверьте корректность заполнения.'");
	
	РезультатЗапрос = Запрос.Выполнить();
	Если Не РезультатЗапрос.Пустой() Тогда
		
		Выборка = РезультатЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ЗависимаяУпаковка = Выборка.Ссылка.ПолучитьОбъект();
			ЗависимаяУпаковка.Записать();
			Если ДополнительныеСвойства.ИзменилсяКоэффициент Тогда
				ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%Упаковка%", ЗависимаяУпаковка.Наименование);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЗависимаяУпаковка.Ссылка);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.ИзменилсяРодитель Тогда
		ОбновитьУпаковкиПрепаратов()
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьУпаковкиПрепаратов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КАТ КАК Препарат
	|ИЗ
	|	РегистрСведений.УпаковкиЛекарственныхСредств КАК Упаковки
	|ГДЕ
	|	Упаковки.Упаковка = &Упаковка
	|";
	Запрос.УстановитьПараметр("Упаковка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ТипЗнч(Выборка.Препарат) = Тип("СправочникСсылка.КЛП") Тогда
			Справочники.КЛП.ЗаполнитьУпаковкиПрепарата(Выборка.Препарат);
		ИначеЕсли ТипЗнч(Выборка.Препарат) = Тип("СправочникСсылка.РегистрЛекарственныхСредств") Тогда
			Справочники.РегистрЛекарственныхСредств.ЗаполнитьУпаковкиПрепарата(Выборка.Препарат);
		КонецЕсли;
	КонецЦикла;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Номенклатура
	|ИЗ
	|	РегистрСведений.ЕдиницыИзмеренияНоменклатуры
	|ГДЕ
	|	ЕдиницаИзмерения = &Упаковка
	|";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		РегистрыСведений.ЕдиницыИзмеренияНоменклатуры.ЗаполнитьЕдиницыИзмерения(Результат.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли