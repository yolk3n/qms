#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьПравоРедактирования = ПравоДоступа("Изменение",
		Метаданные.Справочники.ВидыВложенийДляАктированияЕИС);
	Элементы.ФормаОбновитьИзМакета.Доступность = ЕстьПравоРедактирования;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьИзМакета(Команда)
	
	ОбновитьИзМакетаНаСервере();
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьИзМакетаНаСервере()
	
	Макет = Справочники.ВидыВложенийДляАктированияЕИС.ПолучитьМакет("ВидыВложенийЕИС_ru");
	
	СтроковыйБуфер = ПолучитьСтрокуИзДвоичныхДанных(Макет, "UTF-8");
	
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(СтроковыйБуфер);
	ДанныеФайла = ФабрикаXDTO.ПрочитатьXML(Чтение);
	Чтение.Закрыть();
	
	Для каждого ТипДокумента Из ДанныеФайла.nsiEADocTypes.nsiEADocType Цикл
		Код = ТипДокумента.code;
		Имя = ТипДокумента.name;
		Идентификатор = ТипДокумента.id;
		Актуален = XMLЗначение(Тип("Булево"), ТипДокумента.isActual);
		Элемент = Справочники.ВидыВложенийДляАктированияЕИС.НайтиПоКоду(Код);
		Если ЗначениеЗаполнено(Элемент) Тогда
			Если НЕ Элемент.Наименование = Имя
				ИЛИ НЕ Элемент.Актуален = Актуален Тогда
				ОбъектЭлемента = Элемент.ПолучитьОбъект();
				ОбъектЭлемента.Наименование = Имя;
				ОбъектЭлемента.Актуален = Актуален;
				ОбъектЭлемента.Записать();
				Шаблон = НСтр("ru = 'Изменен вид вложения ""%1.""'");
				ТекстСообщения = СтрШаблон(Шаблон, Имя);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		Иначе
			Если Актуален Тогда
				ОбъектЭлемента = Справочники.ВидыВложенийДляАктированияЕИС.СоздатьЭлемент();
				ОбъектЭлемента.Код = Код;
				ОбъектЭлемента.Идентификатор = Идентификатор;
				ОбъектЭлемента.Наименование = Имя;
				ОбъектЭлемента.Актуален = Актуален;
				ОбъектЭлемента.Записать();
				Шаблон = НСтр("ru = 'Создан вид вложения ""%1.""'");
				ТекстСообщения = СтрШаблон(Шаблон, Имя);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ТекстСообщения = НСтр("ru = 'Виды вложений были успешно обновлены.'");
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти
