
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой.ОблачныеКлассификаторы
	ОблачныеКлассификаторы.ПриСозданииНаСервереФормаСпискаОКПД2(ЭтотОбъект, КоманднаяПанель);
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой.ОблачныеКлассификаторы
	
	Элементы.ПодобратьКлассификатор.Видимость =
		Элементы.Найти("ПодобратьИзСервисаЭлементыОКПД2") = Неопределено
		И ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.КлассификаторОКПД2);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗавершениеЗагрузкиДанныхОКПД2" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьКлассификатор(Команда)
	
	НачатьПодоборКлассификатора();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьПодоборКлассификатора()
	
	Оповестить = Новый ОписаниеОповещения("ПодобратьКлассификаторАльтернативныйСпособ", ЭтотОбъект);
	
	Если Не Элементы.ПодобратьКлассификатор.Видимость Тогда
		НастройкиРежима = Новый Структура("АльтернативныйСпособПодбора", Оповестить);
		ОблачныеКлассификаторыКлиент.ПодобратьИзСервисаЭлементыОКПД2(НастройкиРежима);
	Иначе
		ВыполнитьОбработкуОповещения(Оповестить);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьКлассификаторАльтернативныйСпособ(Результат, ДополнительныеПараметры) Экспорт
	
	ОткрытьФорму("Справочник.КлассификаторОКПД2.Форма.ДобавлениеЭлементовВКлассификатор",, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// РаботаСНоменклатурой.ОблачныеКлассификаторы

&НаКлиенте
Процедура Подключаемый_ПодобратьИзСервисаЭлементыОКПД2(Команда)
	
	НачатьПодоборКлассификатора();
	
КонецПроцедуры

// Конец РаботаСНоменклатурой.ОблачныеКлассификаторы

#КонецОбласти // СтандартныеПодсистемы
