#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьЛекарственныеСредства(ТорговоеНаименование, Исключения = Неопределено) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Препарат.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.РегистрЛекарственныхСредств КАК Препарат
	|ГДЕ
	|	Препарат.ТорговоеНаименование = &ТорговоеНаименование
	|	И Препарат.ДействующиеВеществаМНН <> &ДействующиеВеществаМНН
	|	И НЕ Препарат.Ссылка В (&Исключения)
	|");
	Запрос.УстановитьПараметр("ТорговоеНаименование", ТорговоеНаименование);
	ДействующиеВеществаМНН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТорговоеНаименование, "ДействующиеВеществаМНН");
	Запрос.УстановитьПараметр("ДействующиеВеществаМНН", ДействующиеВеществаМНН);
	Запрос.УстановитьПараметр("Исключения", Исключения);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Если ТранзакцияАктивна() Тогда
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.РегистрЛекарственныхСредств");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		БлокировкаДанных.Заблокировать();
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Препарат = Выборка.Ссылка.ПолучитьОбъект();
		Препарат.ДействующиеВеществаМНН = ДействующиеВеществаМНН;
		Препарат.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМ
#Область СтандартныеПодсистемы

// Возвращает описание блокируемых реквизитов
//
// Возвращаемое значение:
//  Массив - имена блокируемых реквизитов
//   Элемент массива - Строка в формате:
//     ИмяРеквизита[;ИмяЭлементаФормы,...]
//      где
//       ИмяРеквизита     - имя реквизита объекта
//       ИмяЭлементаФормы - имя элемента формы, связанного с реквизитом
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("ДействующиеВеществаМНН");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

#КонецОбласти // СтандартныеПодсистемы

#КонецЕсли