
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеФормы();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Для Каждого МДБизнесПроцесс Из Метаданные.БизнесПроцессы Цикл
		МДРеквизит = МДБизнесПроцесс.Реквизиты.Найти("Шаблон");
		Если МДРеквизит <> Неопределено Тогда
			Если МДРеквизит.Тип.СодержитТип(Тип("СправочникСсылка.ШаблоныСоставныхБизнесПроцессов")) Тогда
				ТипыШаблонов.Добавить(МДБизнесПроцесс.Имя, МДБизнесПроцесс.Синоним);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Параметры.Свойство("ТекущаяСтрока") И ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда
		НайденныйЭлемент = ТипыШаблонов.НайтиПоЗначению(Параметры.ТекущаяСтрока.ТипШаблона);
		Если НайденныйЭлемент <> Неопределено Тогда
			Элементы.ТипыШаблонов.ТекущаяСтрока = НайденныйЭлемент.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипыШаблоновПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = Элементы.ТипыШаблонов.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"ТипШаблона",
		ТипыШаблонов.НайтиПоИдентификатору(ТекущаяСтрока).Значение);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	УсловноеОформлениеСписка = ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ПолучитьУсловноеОформлениеДинамическогоСписка(Список);
	
	// Цвет текста в строках списка Список
	Элемент = УсловноеОформлениеСписка.Элементы.Добавить();
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"ПерсональныйШаблон", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветПерсональногоШаблонаБП);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции