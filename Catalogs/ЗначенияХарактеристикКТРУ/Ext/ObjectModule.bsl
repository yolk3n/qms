#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ТипЗначения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ТипЗначений");
	Если ТипЗначения = Перечисления.ТипыЗначенийХарактеристикиКТРУ.ФиксированноеЗначение Тогда
		МинимальноеЗначение = Неопределено;
		МаксимальноеЗначение = Неопределено;
		Наименование = Строка(Значение);
	Иначе
		Значение = Неопределено;
		Если МинимальноеЗначение = Неопределено Тогда
			Наименование = ?(ВключаяМаксимальноеЗначение, "≤ ", "< ") + Формат(МаксимальноеЗначение, "ЧН=; ЧГ=");
		ИначеЕсли МаксимальноеЗначение = Неопределено Тогда
			Наименование = ?(ВключаяМинимальноеЗначение, "≥ ", "> ") + Формат(МинимальноеЗначение, "ЧН=; ЧГ=");
		Иначе
			Наименование = ?(ВключаяМинимальноеЗначение, "[", "(")
				+ Формат(МинимальноеЗначение, "ЧН=; ЧГ=")
				+ "; "
				+ Формат(МаксимальноеЗначение, "ЧН=; ЧГ=")
				+ ?(ВключаяМаксимальноеЗначение, "]", ")");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли