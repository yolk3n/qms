
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Если Объект.Владелец.Пустая() Тогда
			ОписаниеОшибки = НСтр("ru = 'Не указан владелец.'");
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Объект.Реквизит = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "Реквизит");
		
		ПриСозданииНовогоПриЧтенииНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииНовогоПриЧтенииНаСервере();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЕстьНижняяГраницаПриИзменении(Элемент)
	
	Если Не ЕстьНижняяГраница Тогда
		Объект.МинимальноеЗначение = Неопределено;
	Иначе
		Объект.МинимальноеЗначение = 0;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.МинимальноеЗначение.Имя, "Доступность", ЕстьНижняяГраница);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.ВключаяМинимальноеЗначение.Имя, "Доступность", ЕстьНижняяГраница);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьВерхняяГраницаПриИзменении(Элемент)
	
	Если Не ЕстьВерхняяГраница Тогда
		Объект.МаксимальноеЗначение = Неопределено;
	Иначе
		Объект.МаксимальноеЗначение = 0;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.МаксимальноеЗначение.Имя, "Доступность", ЕстьВерхняяГраница);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.ВключаяМаксимальноеЗначение.Имя, "Доступность", ЕстьВерхняяГраница);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНовогоПриЧтенииНаСервере()
	
	ТипЗначения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ТипЗначений");
	ЗначениеДиапазон = ТипЗначения = Перечисления.ТипыЗначенийХарактеристикиКТРУ.Диапазон;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.Значение.Имя, "Видимость", Не ЗначениеДиапазон);
	
	ЭлементыДиапазона = Новый Массив;
	ЭлементыДиапазона.Добавить(Элементы.ЕстьНижняяГраница.Имя);
	ЭлементыДиапазона.Добавить(Элементы.МинимальноеЗначение.Имя);
	ЭлементыДиапазона.Добавить(Элементы.ВключаяМинимальноеЗначение.Имя);
	ЭлементыДиапазона.Добавить(Элементы.ЕстьВерхняяГраница.Имя);
	ЭлементыДиапазона.Добавить(Элементы.МаксимальноеЗначение.Имя);
	ЭлементыДиапазона.Добавить(Элементы.ВключаяМаксимальноеЗначение.Имя);
	
	Для Каждого Элемент Из ЭлементыДиапазона Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, Элемент, "Видимость", ЗначениеДиапазон);
	КонецЦикла;
	
	Если ЗначениеДиапазон Тогда
		
		ЕстьНижняяГраница = Объект.МинимальноеЗначение <> Неопределено;
		ЕстьВерхняяГраница = Объект.МаксимальноеЗначение <> Неопределено;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, Элементы.МинимальноеЗначение.Имя, "Доступность", ЕстьНижняяГраница);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, Элементы.ВключаяМинимальноеЗначение.Имя, "Доступность", ЕстьНижняяГраница);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, Элементы.МаксимальноеЗначение.Имя, "Доступность", ЕстьВерхняяГраница);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, Элементы.ВключаяМаксимальноеЗначение.Имя, "Доступность", ЕстьВерхняяГраница);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции