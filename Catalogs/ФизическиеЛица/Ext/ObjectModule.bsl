#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Перем ТекстСообщения;
	
	Если Не ЭтоНовый() Тогда
		ЗначенияРеквизитовВИнформационнойБазе = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ИНН");
	КонецЕсли;
	
	Если Не ПустаяСтрока(ИНН) Тогда 
		Если (ЭтоНовый() Или ЗначенияРеквизитовВИнформационнойБазе.ИНН <> ИНН)
		   И Не РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Ложь, ТекстСообщения) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ИНН",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ЭтоГруппа Тогда
		Наименование = СокрЛП(Наименование);
		Пока Найти(Наименование,"  ") > 0 Цикл
			Наименование = СтрЗаменить(Наименование, "  ", " ");
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#КонецЕсли