
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьСправочник(Команда)
	ТекущиеДанные_ = Элементы.Список.ТекущиеДанные;
	Если
		ТекущиеДанные_ <> Неопределено И
		ЗначениеЗаполнено(ТекущиеДанные_.ИмяОбъектаМетаданных)
	Тогда
		Префикс_ = "Справочник." + ТекущиеДанные_.ИмяОбъектаМетаданных;
		Если ТекущиеДанные_.ИмяОбъектаМетаданных = "ВидыПоказателейЗдоровья" Тогда
			Префикс_ = "ПланВидовХарактеристик." + ТекущиеДанные_.ИмяОбъектаМетаданных;
		ИначеЕсли ТекущиеДанные_.ИмяОбъектаМетаданных = "ЗначенияПоказателейЗдоровья" Тогда
			Префикс_ = "ПланВидовХарактеристик.ВидыПоказателейЗдоровья";
		КонецЕсли;
		
		Если ОбъектМетаданныхСуществует(ТекущиеДанные_.ИмяОбъектаМетаданных) Тогда
			ОткрытьФорму(Префикс_ + ".ФормаСписка");
		Иначе
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ПослеОтветаНаВопросОбУдаленииЗаписи", ЭтаФорма, Новый Структура("Ссылка", Элементы.Список.ТекущаяСтрока)),
				Стршаблон(
					"Невозможно определить менеджер объекта '%1' (%2).
					|Возможно он был модифицирован или удален из дерева конфигурации.",
					ТекущиеДанные_.НаименоваениеКлассификатора,
					ТекущиеДанные_.ОсновнойОИД
				),
				РежимДиалогаВопрос.ДаНет,,
				КодВозвратаДиалога.Нет,
				"Удалить запись о классификаторе?"
			)
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКарточкуСправочника(Команда)
	ТекущиеДанные_ = Элементы.Список.ТекущиеДанные;
	Если
		ТекущиеДанные_ <> Неопределено И
		ЗначениеЗаполнено(ТекущиеДанные_.ИмяОбъектаМетаданных) И
		ЗначениеЗаполнено(ТекущиеДанные_.ОсновнойОИД)
	Тогда
		Если ОбъектМетаданныхСуществует(ТекущиеДанные_.ИмяОбъектаМетаданных) Тогда
			ОбновитьКарточкуСправочникаНаСервере(ТекущиеДанные_.ИмяОбъектаМетаданных, ТекущиеДанные_.ОсновнойОИД);
			Элементы.Список.Обновить();
		Иначе
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ПослеОтветаНаВопросОбУдаленииЗаписи", ЭтаФорма, Новый Структура("Ссылка", Элементы.Список.ТекущаяСтрока)),
				Стршаблон(
					"Невозможно определить менеджер объекта '%1' (%2).
					|Возможно он был модифицирован или удален из дерева конфигурации.",
					ТекущиеДанные_.НаименоваениеКлассификатора,
					ТекущиеДанные_.ОсновнойОИД
				),
				РежимДиалогаВопрос.ДаНет,,
				КодВозвратаДиалога.Нет,
				"Удалить запись о классификаторе?"
			)
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИзМакета(Команда)
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ЗаполнитьИзМакетаЗавершение", ЭтотОбъект),
		ТекстВопросаЗамещать(),
		РежимДиалогаВопрос.ДаНетОтмена,
		,
		КодВозвратаДиалога.Нет
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ОбъектМетаданныхСуществует(ИмяОбъекта)
	Если
		Метаданные.Справочники.Найти(ИмяОбъекта) <> Неопределено Или
		Метаданные.ПланыВидовХарактеристик.Найти(ИмяОбъекта) <> Неопределено
	Тогда
		Возврат Истина;
	КонецЕсли;
	Возврат Ложь;
КонецФункции

&НаКлиенте
Процедура ПослеОтветаНаВопросОбУдаленииЗаписи(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УдалитьЗаписьНаСервере(ДополнительныеПараметры.Ссылка);
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьЗаписьНаСервере(Ссылка)
	Ссылка.ПолучитьОбъект().Удалить();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьКарточкуСправочникаНаСервере(ИмяОбъекта, ОИД)
	
	Менеджер_ = Неопределено;
	
	Если Метаданные.Справочники.Найти(ИмяОбъекта) <> Неопределено Тогда
		Менеджер_ = Справочники[ИмяОбъекта];
	ИначеЕсли Метаданные.ПланыВидовХарактеристик.Найти(ИмяОбъекта) <> Неопределено Тогда
		Менеджер_ = ПланыВидовХарактеристик[ИмяОбъекта];
	Иначе
		ВызватьИсключение("Невозможно определить менеджер объекта");
	КонецЕсли;
	
	Паспорт_ = НСИРосМинздрав.ЗапроситьПаспортКлассификатора(ОИД);
	Если ТипЗнч(Паспорт_) = Тип("ОбъектXDTO") Тогда
		Справочники.СправочникиФНСИ.СоздатьОбновитьКарточкуСправочникаФНСИ(Паспорт_, Менеджер_);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИзМакетаЗавершение(Ответ, Контекст) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Замещать_ = ?(Ответ = КодВозвратаДиалога.Да, Истина, Ложь);
	ЗаполнитьИзМакетаСервер(Замещать_);
	ОповеститьОбИзменении(Тип("СправочникСсылка.СправочникиФНСИ"));
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьИзМакетаСервер(Замещать)
	Справочники.СправочникиФНСИ.ПервоначальноеЗаполнение(Замещать);
КонецПроцедуры

&НаКлиенте
Функция ТекстВопросаЗамещать()
	
	Возврат
		НСтр("ru = 'Замещать записи?
		           |
		           |Записи чья дата публикации старше данных макета будут обновлены.
		           |Ели нет, то будут добавлены только новые записи.'");
		
	КонецФункции

#КонецОбласти

