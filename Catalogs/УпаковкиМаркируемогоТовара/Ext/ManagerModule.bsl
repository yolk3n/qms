
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

Процедура ЗарегистрироватьУпаковкиМаркируемогоТовара(ДанныеУпаковок) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЕстьПартия = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеУпаковок.Колонки, "Партия");
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеУпаковок.Номенклатура         КАК Номенклатура,
	|	ДанныеУпаковок.Серия                КАК Серия,
	|	ДанныеУпаковок.Партия               КАК Партия,
	|	ДанныеУпаковок.ГоденДо              КАК ГоденДо,
	|	ДанныеУпаковок.НомерУпаковки        КАК НомерУпаковки
	|ПОМЕСТИТЬ ДанныеУпаковок
	|ИЗ
	|	&ДанныеУпаковок КАК ДанныеУпаковок
	|ГДЕ
	|	НЕ ДанныеУпаковок.ГрупповаяУпаковка
	|";
	
	Если Не ЕстьПартия Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
		ЗапросОбъект = СхемаЗапроса.ПакетЗапросов[0];
		Колонки = ЗапросОбъект.Колонки;
		Колонки.Удалить(Колонки.Индекс(Колонки.Найти("Партия")));
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДанныеУпаковок", ДанныеУпаковок);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
	ИнтеграцияМДЛП.ДобавитьКлючиУпаковок(Запрос.МенеджерВременныхТаблиц, "ДанныеУпаковок");
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеУпаковок.Номенклатура       КАК Номенклатура,
	|	ДанныеУпаковок.Серия              КАК СерияНоменклатуры,
	|	ДанныеУпаковок.Партия             КАК Партия,
	|	ДанныеУпаковок.НомерУпаковки      КАК НомерУпаковки,
	|	ДанныеУпаковок.ГоденДо            КАК ГоденДо,
	|	УпаковкиТовара.Ссылка             КАК УпаковкаТовара
	|ИЗ
	|	ДанныеУпаковок КАК ДанныеУпаковок
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Справочник.УпаковкиМаркируемогоТовара КАК УпаковкиТовара
	|		ПО
	|			УпаковкиТовара.НомерУпаковки = ДанныеУпаковок.НомерУпаковки
	|			И УпаковкиТовара.КлючУпаковки  = ДанныеУпаковок.КлючУпаковки
	|			И УпаковкиТовара.ГоденДо = ДанныеУпаковок.ГоденДо
	|ГДЕ
	|	УпаковкиТовара.Ссылка ЕСТЬ NULL
	|		ИЛИ УпаковкиТовара.Номенклатура <> ДанныеУпаковок.Номенклатура
	|		ИЛИ УпаковкиТовара.СерияНоменклатуры <> ДанныеУпаковок.Серия
	|		ИЛИ УпаковкиТовара.Партия <> ДанныеУпаковок.Партия
	|";
	
	Если Не ЕстьПартия Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
		ЗапросОбъект = СхемаЗапроса.ПакетЗапросов[0];
		Колонки = ЗапросОбъект.Колонки;
		Колонки.Удалить(Колонки.Индекс(Колонки.Найти("Партия")));
		Отбор = ЗапросОбъект.Операторы[0].Отбор;
		Отбор.Удалить(Отбор.Индекс(Отбор.Найти("УпаковкиТовара.Ссылка ЕСТЬ NULL
		|ИЛИ УпаковкиТовара.Номенклатура <> ДанныеУпаковок.Номенклатура
		|ИЛИ УпаковкиТовара.СерияНоменклатуры <> ДанныеУпаковок.Серия
		|ИЛИ УпаковкиТовара.Партия <> ДанныеУпаковок.Партия")));
		Отбор.Добавить("УпаковкиТовара.Ссылка ЕСТЬ NULL
		|ИЛИ УпаковкиТовара.Номенклатура <> ДанныеУпаковок.Номенклатура
		|ИЛИ УпаковкиТовара.СерияНоменклатуры <> ДанныеУпаковок.Серия");
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Если ЗначениеЗаполнено(Выборка.УпаковкаТовара) Тогда
				УпаковкаТовара = Выборка.УпаковкаТовара.ПолучитьОбъект();
				ОбщегоНазначенияБольничнаяАптека.ЗаблокироватьОбъект(УпаковкаТовара);
			Иначе
				УпаковкаТовара = Справочники.УпаковкиМаркируемогоТовара.СоздатьЭлемент();
				УпаковкаТовара.НомерУпаковки = Выборка.НомерУпаковки;
				УпаковкаТовара.ГоденДо = Выборка.ГоденДо;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(УпаковкаТовара, Выборка, "Номенклатура, СерияНоменклатуры");
			Если ЕстьПартия Тогда
				УпаковкаТовара.Партия = Выборка.Партия;
			КонецЕсли;
			
			УпаковкаТовара.Записать();
			УпаковкаТовара.Разблокировать();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ИмяСобытия = НСтр("ru = 'Регистрация упаковки маркируемого товара.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			МетаданныеСправочника = ПустаяСсылка().Метаданные();
			
			Текст = НСтр("ru = 'Не удалось записать элемент справочника %1 по причине:'");
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, МетаданныеСправочника.Представление());
			
			ТекстЖурналаРегистрации = Текст + Символы.ПС + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, МетаданныеСправочника, УпаковкаТовара, ТекстЖурналаРегистрации);
			
			ТекстПользователю = Текст + Символы.ПС + ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбщегоНазначения.СообщитьПользователю(ТекстПользователю);
			
			Возврат;
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#КонецЕсли