#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКИ СОБЫТИЙ
#Область ОбработкиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	КодЕСКЛП = "";
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьСтатусЗабраковки();
	
	Если Не ЭтоНовый() Тогда
		ТекущиеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Статус, ПометкаУдаления");
		Если Статус <> ТекущиеРеквизиты.Статус Тогда
			ДополнительныеСвойства.Вставить("ПометитьНеобработанным");
			ДополнительныеСвойства.Вставить("ОбновитьИнформациюОЗабраковкеСерий")
		ИначеЕсли Статус = Перечисления.СтатусыЗабраковкиСерий.Действует И ПометкаУдаления <> ТекущиеРеквизиты.ПометкаУдаления Тогда
			ДополнительныеСвойства.Вставить("ОбновитьИнформациюОЗабраковкеСерий")
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодЕСКЛП) И Номенклатура.Пустая() Тогда
		АптечныйТовар = Справочники.КЛП.НайтиПоРеквизиту("КодЕСКЛП", КодЕСКЛП);
		Если ЗначениеЗаполнено(АптечныйТовар) Тогда
			Номенклатура = АптечныеТовары.ПолучитьНоменклатуруПоЭлементуКАТ(АптечныйТовар);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПометитьНеобработанным") Тогда
		РаботаСИнформациейОбОбъектах.УстановитьСвойствоОбработан(Ссылка, Ложь);
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ОбновитьИнформациюОЗабраковкеСерий") Тогда
		Справочники.ЗабракованныеСерии.ОбновитьИнформациюОЗабраковкеСерий(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработкиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

// Обновляет статус забракованной серии лекарственных средств.
//
Процедура ОбновитьСтатусЗабраковки() Экспорт
	
	Если Приказы.Количество() Тогда
		
		Приказы.Сортировать("ДатаДокумента УБЫВ, ОтменаЗабраковки УБЫВ");
		
		Статус = ?(Приказы[0].ОтменаЗабраковки, Перечисления.СтатусыЗабраковкиСерий.Отменена, Перечисления.СтатусыЗабраковкиСерий.Действует);
		ДатаПоследнегоПриказа = Приказы[0].ДатаДокумента;
		НомерПоследнегоПриказа = Приказы[0].НомерДокумента;
		
	Иначе
		
		Статус = Перечисления.СтатусыЗабраковкиСерий.Действует;
		ДатаПоследнегоПриказа = Дата(1, 1, 1);
		НомерПоследнегоПриказа = "";
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли