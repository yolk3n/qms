#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не Справочники.ГруппыДоступаНоменклатуры.ИспользуютсяГруппыДоступа() Тогда
		НепроверяемыеРеквизиты.Добавить("ГруппаДоступа");
	КонецЕсли;
	
	Если ИспользоватьСерии Тогда
		
		Если Не ИспользоватьСрокГодностиСерии Тогда
			НепроверяемыеРеквизиты.Добавить("ТочностьУказанияСрокаГодностиСерии");
		КонецЕсли;
		
		Если Не НастройкиСерийБерутсяИзДругихНастроекКатегорийНоменклатуры Тогда
			НепроверяемыеРеквизиты.Добавить("ВладелецСерий");
		КонецЕсли;
		
	Иначе
		НепроверяемыеРеквизиты.Добавить("ТочностьУказанияСрокаГодностиСерии");
		НепроверяемыеРеквизиты.Добавить("ПолитикаУчетаСерий");
		НепроверяемыеРеквизиты.Добавить("ПолитикаУчетаСерийВОтделениях");
		НепроверяемыеРеквизиты.Добавить("ВладелецСерий");
	КонецЕсли;
	
	Если ИспользоватьПартии Тогда
		
		Если Не НастройкиПартийБерутсяИзДругихНастроекКатегорийНоменклатуры Тогда
			НепроверяемыеРеквизиты.Добавить("ВладелецПартий");
		КонецЕсли;
		
	Иначе
		НепроверяемыеРеквизиты.Добавить("ПолитикаУчетаПартий");
		НепроверяемыеРеквизиты.Добавить("ПолитикаУчетаПартийВОтделениях");
		НепроверяемыеРеквизиты.Добавить("ВладелецПартий");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ШаблонНаименованияДляПечатиНоменклатуры = Метаданные().Реквизиты.ШаблонНаименованияДляПечатиНоменклатуры.ЗначениеЗаполнения;
	ШаблонРабочегоНаименованияНоменклатуры = "";
	ЗапретРедактированияРабочегоНаименованияНоменклатуры = Ложь;
	ЗапретРедактированияНаименованияДляПечатиНоменклатуры = Ложь;
	
	ШаблонРабочегоНаименованияСерии = "";
	
	Если ОбъектКопирования.ИспользоватьСерии Тогда
		Если Не ОбъектКопирования.НастройкиСерийБерутсяИзДругихНастроекКатегорийНоменклатуры Тогда
			НастройкиСерийБерутсяИзДругихНастроекКатегорийНоменклатуры = Истина;
			ВладелецСерий = ОбъектКопирования.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбъектКопирования.ИспользоватьПартии Тогда
		Если Не ОбъектКопирования.НастройкиПартийБерутсяИзДругихНастроекКатегорийНоменклатуры Тогда
			НастройкиПартийБерутсяИзДругихНастроекКатегорийНоменклатуры = Истина;
			ВладелецПартий = ОбъектКопирования.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Товар Тогда
		ИспользоватьСерии = Ложь;
		ИспользоватьПартии = Ложь;
	КонецЕсли;
	
	Если Не ИспользоватьСерии Тогда
		ИспользоватьНомерСерии = Ложь;
		ИспользоватьСрокГодностиСерии = Ложь;
		ИспользоватьКоличествоСерии = Ложь;
		ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.ПустаяСсылка();
		НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.НеИспользовать;
		ПолитикаУчетаСерий = Перечисления.ТипыПолитикУчетаСерий.НеУчитывать;
		ПолитикаУчетаСерийВОтделениях = Перечисления.ТипыПолитикУчетаСерий.НеУчитывать;
		НастройкиСерийБерутсяИзДругихНастроекКатегорийНоменклатуры = Ложь;
		ВладелецСерий = Справочники.НастройкиКатегорийНоменклатуры.ПустаяСсылка();
	Иначе
		Если НастройкиСерийБерутсяИзДругихНастроекКатегорийНоменклатуры
		   И Не ДополнительныеСвойства.Свойство("ПропуститьЗаполнениеПоДругимНастройкамКатегорийНоменклатуры") Тогда
			
			Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	НастройкиКатегорийНоменклатуры.НастройкаИспользованияСерий         КАК НастройкаИспользованияСерий,
			|	НастройкиКатегорийНоменклатуры.ТочностьУказанияСрокаГодностиСерии  КАК ТочностьУказанияСрокаГодностиСерии,
			|	НастройкиКатегорийНоменклатуры.ПолитикаУчетаСерий                  КАК ПолитикаУчетаСерий,
			|	НастройкиКатегорийНоменклатуры.ПолитикаУчетаСерийВОтделениях       КАК ПолитикаУчетаСерийВОтделениях,
			|	НастройкиКатегорийНоменклатуры.ИспользоватьНомерСерии              КАК ИспользоватьНомерСерии,
			|	НастройкиКатегорийНоменклатуры.ИспользоватьСрокГодностиСерии       КАК ИспользоватьСрокГодностиСерии
			|ИЗ
			|	Справочник.НастройкиКатегорийНоменклатуры КАК НастройкиКатегорийНоменклатуры
			|ГДЕ
			|	НастройкиКатегорийНоменклатуры.Ссылка = &ВладелецСерий
			|");
			
			Запрос.УстановитьПараметр("ВладелецСерий", ВладелецСерий);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
			КонецЕсли;
			
		КонецЕсли;
		
		ИспользоватьКоличествоСерии = (НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.ПартияТоваров);
		
		Если Не ИспользоватьСрокГодностиСерии Тогда
			ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не НастройкиСерийБерутсяИзДругихНастроекКатегорийНоменклатуры Тогда
		ВладелецСерий = Справочники.НастройкиКатегорийНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	Если Не ИспользоватьПартии Тогда
		ПолитикаУчетаПартий = Перечисления.ТипыПолитикУчетаПартий.НеУчитывать;
		ПолитикаУчетаПартийВОтделениях = Перечисления.ТипыПолитикУчетаПартий.НеУчитывать;
		НастройкиПартийБерутсяИзДругихНастроекКатегорийНоменклатуры = Ложь;
		ВладелецПартий = Справочники.НастройкиКатегорийНоменклатуры.ПустаяСсылка();
	Иначе
		Если НастройкиПартийБерутсяИзДругихНастроекКатегорийНоменклатуры
		   И Не ДополнительныеСвойства.Свойство("ПропуститьЗаполнениеПоДругимНастройкамКатегорийНоменклатуры") Тогда
			
			Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	НастройкиКатегорийНоменклатуры.ПолитикаУчетаПартий            КАК ПолитикаУчетаПартий,
			|	НастройкиКатегорийНоменклатуры.ПолитикаУчетаПартийВОтделениях КАК ПолитикаУчетаПартийВОтделениях
			|ИЗ
			|	Справочник.НастройкиКатегорийНоменклатуры КАК НастройкиКатегорийНоменклатуры
			|ГДЕ
			|	НастройкиКатегорийНоменклатуры.Ссылка = &ВладелецПартий
			|");
			
			Запрос.УстановитьПараметр("ВладелецПартий", ВладелецПартий);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если Не НастройкиПартийБерутсяИзДругихНастроекКатегорийНоменклатуры Тогда
		ВладелецПартий = Справочники.НастройкиКатегорийНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый"                                       , ЭтоНовый());
	ДополнительныеСвойства.Вставить("ИспользоватьСерии"                              , Ложь);
	ДополнительныеСвойства.Вставить("НастройкиСерийБерутсяИзДругихНастроекКатегорийНоменклатуры" , Ложь);
	ДополнительныеСвойства.Вставить("ИспользоватьПартии"                             , Ложь);
	ДополнительныеСвойства.Вставить("НастройкиПартийБерутсяИзДругихНастроекКатегорийНоменклатуры", Ложь);
	
	ДополнительныеСвойства.Вставить("ОтменаПометкиУдаления", Ложь);
	
	Если Не ЭтоНовый() Тогда
		
		ЗапрашиваемыеРеквизиты = Новый Структура;
		ЗапрашиваемыеРеквизиты.Вставить("ИспользоватьСерии");
		ЗапрашиваемыеРеквизиты.Вставить("НастройкиСерийБерутсяИзДругихНастроекКатегорийНоменклатуры");
		ЗапрашиваемыеРеквизиты.Вставить("ИспользоватьПартии");
		ЗапрашиваемыеРеквизиты.Вставить("НастройкиПартийБерутсяИзДругихНастроекКатегорийНоменклатуры");
		ЗапрашиваемыеРеквизиты.Вставить("ПометкаУдаления");
		
		СтарыеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ЗапрашиваемыеРеквизиты);
		ЗаполнитьЗначенияСвойств(ДополнительныеСвойства, СтарыеРеквизиты);
		
		Если Не ПометкаУдаления И СтарыеРеквизиты.ПометкаУдаления Тогда
			ДополнительныеСвойства.ОтменаПометкиУдаления = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьСвязанныеНастройкиКатегорийНоменклатуры();
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

//////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьСвязанныеНастройкиКатегорийНоменклатуры()
	
	Если ДополнительныеСвойства.ЭтоНовый Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.ИспользоватьСерии
	   И Не ДополнительныеСвойства.НастройкиСерийБерутсяИзДругихНастроекКатегорийНоменклатуры Тогда
		ОбновитьНастройкиСерийВСвязанныхНастройкахКатегорийНоменклатуры();
	КонецЕсли;
	
	Если ДополнительныеСвойства.ИспользоватьПартии
	   И Не ДополнительныеСвойства.НастройкиПартийБерутсяИзДругихНастроекКатегорийНоменклатуры Тогда
		ОбновитьНастройкиПартийВСвязанныхНастройкахКатегорийНоменклатуры();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьНастройкиСерийВСвязанныхНастройкахКатегорийНоменклатуры()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	НастройкиКатегорийНоменклатуры.Ссылка КАК НастройкаКатегорийНоменклатуры
	|ИЗ
	|	Справочник.НастройкиКатегорийНоменклатуры КАК НастройкиКатегорийНоменклатуры
	|ГДЕ
	|	НастройкиКатегорийНоменклатуры.ВладелецСерий = &ВладелецСерий
	|");
	
	Запрос.УстановитьПараметр("ВладелецСерий", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ИзменяемыйОбъект = Выборка.НастройкаКатегорийНоменклатуры.ПолучитьОбъект();
		
		ИзменяемыйОбъект.ИспользоватьСерии = ИспользоватьСерии;
		Если Не ИспользоватьСерии Тогда
			ИзменяемыйОбъект.НастройкиСерийБерутсяИзДругихНастроекКатегорийНоменклатуры = Ложь;
			// Остальные поля очистятся при записи
			ИзменяемыйОбъект.Записать();
			Продолжить;
		КонецЕсли;
		
		ИзменяемыйОбъект.НастройкаИспользованияСерий = НастройкаИспользованияСерий;
		ИзменяемыйОбъект.ТочностьУказанияСрокаГодностиСерии = ТочностьУказанияСрокаГодностиСерии;
		ИзменяемыйОбъект.ИспользоватьНомерСерии             = ИспользоватьНомерСерии;
		ИзменяемыйОбъект.ИспользоватьСрокГодностиСерии      = ИспользоватьСрокГодностиСерии;
		ИзменяемыйОбъект.ПолитикаУчетаСерий = ПолитикаУчетаСерий;
		ИзменяемыйОбъект.ПолитикаУчетаСерийВОтделениях = ПолитикаУчетаСерийВОтделениях;
		
		// Остальные поля заполнятся при записи
		ИзменяемыйОбъект.ДополнительныеСвойства.Вставить("ПропуститьЗаполнениеПоДругимНастройкамКатегорийНоменклатуры");
		
		ИзменяемыйОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьНастройкиПартийВСвязанныхНастройкахКатегорийНоменклатуры()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	НастройкиКатегорийНоменклатуры.Ссылка КАК НастройкаКатегорийНоменклатуры
	|ИЗ
	|	Справочник.НастройкиКатегорийНоменклатуры КАК НастройкиКатегорийНоменклатуры
	|ГДЕ
	|	НастройкиКатегорийНоменклатуры.ВладелецПартий = &ВладелецПартий
	|");
	
	Запрос.УстановитьПараметр("ВладелецПартий", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ИзменяемыйОбъект = Выборка.НастройкаКатегорийНоменклатуры.ПолучитьОбъект();
		
		ИзменяемыйОбъект.ИспользоватьПартии = ИспользоватьПартии;
		Если Не ИспользоватьПартии Тогда
			ИзменяемыйОбъект.НастройкиПартийБерутсяИзДругихНастроекКатегорийНоменклатуры = Ложь;
			// Остальные поля очистятся при записи
			ИзменяемыйОбъект.Записать();
			Продолжить;
		КонецЕсли;
		
		ИзменяемыйОбъект.ПолитикаУчетаПартий = ПолитикаУчетаПартий;
		ИзменяемыйОбъект.ПолитикаУчетаПартийВОтделениях = ПолитикаУчетаПартийВОтделениях;
		
		// Остальные поля заполнятся при записи
		ИзменяемыйОбъект.ДополнительныеСвойства.Вставить("ПропуститьЗаполнениеПоДругимНастройкамКатегорийНоменклатуры");
		
		ИзменяемыйОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли