#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКИ СОБЫТИЙ
#Область ОбработкиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	КодЕСКЛП                        = "";
	УникальныйИдентификаторЕСКЛП    = Неопределено;
	ДатаСозданияЗаписиСМНН          = Дата(1, 1, 1);
	ДатаОкончанияДействияЗаписиСМНН = Дата(1, 1, 1);
	ДатаПоследнегоИзменения         = Дата(1, 1, 1);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ЭтоНовый() И Не ДополнительныеСвойства.Свойство("ПропуститьОбновлениеЛекарственныхСредств") Тогда
		
		ИменаРеквизитов = Справочники.КЛП.РеквизитыНаследуемыеИзСМНН();
		
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, СтрСоединить(ИменаРеквизитов, ","));
		Для Каждого КлючИЗначение Из Реквизиты Цикл
			Если ЭтотОбъект[КлючИЗначение.Ключ] <> КлючИЗначение.Значение Тогда
				ДополнительныеСвойства.Вставить("ОбновитьЛекарственныеСредства");
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДополнительныеСвойства.Свойство("ОбновитьЛекарственныеСредства") Тогда
		ОбновитьЛекарственныеСредства(Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработкиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

// Процедура обновляет данные по данным СМНН
//
Процедура ОбновитьЛекарственныеСредства(Отказ)
	
	ИменаРеквизитов = Справочники.КЛП.РеквизитыНаследуемыеИзСМНН();
	
	Отбор = Новый Структура("СМНН", Ссылка);
	ДанныеОбновления = Новый Структура(СтрСоединить(ИменаРеквизитов, ","));
	ЗаполнитьЗначенияСвойств(ДанныеОбновления, ЭтотОбъект);
	Справочники.КЛП.ОбновитьЛекарственныеПрепараты(Отбор, ДанныеОбновления);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли