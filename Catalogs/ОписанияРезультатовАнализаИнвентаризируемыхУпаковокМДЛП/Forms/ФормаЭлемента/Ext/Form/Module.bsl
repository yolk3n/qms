
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииЧтенииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииЧтенииНаСервере();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РекомендуемоеДействиеПоСверкеОстатковНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораРекомендуемогоДействия(Объект.ДействияПоСверкеОстатков, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РекомендуемоеДействиеПоОформлениюУведомленийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораРекомендуемогоДействия(Объект.ДействияПоОформлениюУведомлений, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияПоСверкеОстатковПередУдалением(Элемент, Отказ)
	
	ПередУдалениемРекомендуемогоДействия(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияПоОформлениюУведомленийПередУдалением(Элемент, Отказ)
	
	ПередУдалениемРекомендуемогоДействия(Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	Если Параметры.РежимПросмотраОписания Тогда
		
		ТолькоПросмотр = Истина;
		
		ИменаЭлементовНеВидимость = Новый Массив;
		ИменаЭлементовНеВидимость.Добавить(Элементы.ФактическоеНаличие.Имя);
		ИменаЭлементовНеВидимость.Добавить(Элементы.СостояниеУпаковкиОтличается.Имя);
		ИменаЭлементовНеВидимость.Добавить(Элементы.МожноВыполнитьСверкуМДЛП.Имя);
		ИменаЭлементовНеВидимость.Добавить(Элементы.МожноВыполнитьСверкуИБ.Имя);
		ИменаЭлементовНеВидимость.Добавить(Элементы.МожноОформитьОприходованиеМДЛП.Имя);
		ИменаЭлементовНеВидимость.Добавить(Элементы.МожноОформитьПовторныйВводВОборотМДЛП.Имя);
		ИменаЭлементовНеВидимость.Добавить(Элементы.МожноОформитьВыводИзОборотаМДЛП.Имя);
		ИменаЭлементовНеВидимость.Добавить(Элементы.МожноПереместитьВАрхив.Имя);
		ИменаЭлементовНеВидимость.Добавить(Элементы.ИнвертированноеЗначениеПоФакту.Имя);
		ИменаЭлементовНеВидимость.Добавить(Элементы.ДействияПоСверкеОстатков.Имя);
		ИменаЭлементовНеВидимость.Добавить(Элементы.ДействияПоОформлениюУведомлений.Имя);
		
		Для Каждого ИмяЭлемента Из ИменаЭлементовНеВидимость Цикл
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяЭлемента, "Видимость", Ложь);
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	РедактированиеФормыНедоступно = Не ОбщегоНазначенияКлиентСервер.РежимОтладки();
	
	ЕстьПравоИзменение = ПравоДоступа("Изменение", Объект.Ссылка.Метаданные());
	Если Не ЕстьПравоИзменение И РедактированиеФормыНедоступно Тогда
		ТолькоПросмотр = Истина;
		Возврат;
	КонецЕсли;
	
	ИменаЭлементовТолькоПросмотр = Новый Массив;
	ИменаЭлементовТолькоПросмотр.Добавить(Элементы.ДействияПоСверкеОстатков.Имя);
	ИменаЭлементовТолькоПросмотр.Добавить(Элементы.ДействияПоОформлениюУведомлений.Имя);
	
	Для Каждого ИмяЭлемента Из ИменаЭлементовТолькоПросмотр Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяЭлемента, "ТолькоПросмотр", РедактированиеФормыНедоступно);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораРекомендуемогоДействия(ТаблицаДоступныхДействий, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Для Каждого СтрокаДействия Из ТаблицаДоступныхДействий Цикл
		ДанныеВыбора.Добавить(СтрокаДействия.ДоступноеДействие);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередУдалениемРекомендуемогоДействия(Элемент, Отказ)
	
	ИдентификаторыСтрок = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элемент.ВыделенныеСтроки Цикл
		ИдентификаторыСтрок.Добавить(ВыделеннаяСтрока);
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ИдентификаторыСтрок) Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.Имя = Элементы.ДействияПоСверкеОстатков.Имя Тогда
		ИмяТаблицыДоступныхДействий       = Элемент.Имя;
		ИмяРеквизитаРекомендуемоеДействие = "РекомендуемоеДействиеПоСверкеОстатков";
	ИначеЕсли Элемент.Имя = Элементы.ДействияПоОформлениюУведомлений.Имя Тогда
		ИмяТаблицыДоступныхДействий       = Элемент.Имя;
		ИмяРеквизитаРекомендуемоеДействие = "РекомендуемоеДействиеПоОформлениюУведомлений";
	Иначе
		Возврат;
	КонецЕсли;
	
	Для Каждого ВыделеннаяСтрока Из ИдентификаторыСтрок Цикл
		
		ТекущиеДанные = Объект[ИмяТаблицыДоступныхДействий].НайтиПоИдентификатору(ВыделеннаяСтрока);
		Если Объект[ИмяРеквизитаРекомендуемоеДействие] = ТекущиеДанные.ДоступноеДействие Тогда
			ТекстСообщения = НСтр("ru = 'Удаляемое действие установлено как рекомендуемое.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, ИмяРеквизитаРекомендуемоеДействие, "Объект", Отказ);
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
