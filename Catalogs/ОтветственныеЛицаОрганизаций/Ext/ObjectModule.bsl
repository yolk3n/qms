#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	ЗаполнитьДатыНачалаОкончания();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ТипЗнч(ДанныеЗаполнения) = Тип("Структура") ИЛИ НЕ ДанныеЗаполнения.Свойство("Владелец") Тогда
		Владелец = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОрганизациюПоУмолчанию(Владелец);
	КонецЕсли;
	
	ЗаполнитьДатыНачалаОкончания();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверитьПересечениеПериодаДействия(Отказ);
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не ПравоПодписиПоДоверенности Тогда
		НепроверяемыеРеквизиты.Добавить("ОснованиеПраваПодписи");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если НЕ ПравоПодписиПоДоверенности И ЗначениеЗаполнено(ОснованиеПраваПодписи) Тогда
		ОснованиеПраваПодписи = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПересечениеПериодаДействия(Отказ)
	
	Если Не ЗначениеЗаполнено(ДатаНачала)
	 Или Не ЗначениеЗаполнено(ОтветственноеЛицо)
	 Или Не ЗначениеЗаполнено(Владелец) Тогда
		Возврат; // не заполнены поля поиска
	КонецЕсли;
	
	Если ПравоПодписиПоДоверенности Тогда
		Возврат; // пересечения периодов допустимы
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаОкончания) И ДатаНачала > ДатаОкончания Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Дата окончания периода не может быть меньше даты начала'"),
			Ссылка,
			"Объект.ДатаОкончания",,
			Отказ);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийЭлемент"    , Ссылка);
	Запрос.УстановитьПараметр("Владелец"          , Владелец);
	Запрос.УстановитьПараметр("ОтветственноеЛицо" , ОтветственноеЛицо);
	Запрос.УстановитьПараметр("ДатаНачала"        , ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания"     , ДатаОкончания);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Спр.Наименование,
	|	Спр.ДатаНачала КАК ДатаНачала,
	|	Спр.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	Справочник.ОтветственныеЛицаОрганизаций КАК Спр
	|ГДЕ
	|	Спр.Ссылка <> &ТекущийЭлемент
	|	И Спр.Владелец = &Владелец
	|	И Спр.ОтветственноеЛицо = &ОтветственноеЛицо
	|	И НЕ Спр.ПравоПодписиПоДоверенности
	|	И (Спр.ДатаНачала <= &ДатаНачала
	|				И (Спр.ДатаОкончания >= &ДатаНачала
	|					ИЛИ Спр.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1))
	|			ИЛИ Спр.ДатаНачала <= &ДатаОкончания
	|				И (Спр.ДатаОкончания >= &ДатаОкончания
	|					ИЛИ Спр.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1))
	|			ИЛИ Спр.ДатаНачала >= &ДатаНачала
	|				И (Спр.ДатаНачала <= &ДатаОкончания
	|					ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНачала
	|";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='За период %1
		|уже введена информация по ответственным лицам:'"), ПредставлениеПериода(ДатаНачала, ДатаОкончания));
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = ТекстСообщения + "
		|" + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1'"), Выборка.Наименование);
	КонецЦикла;
	
	ОбщегоНазначения.СообщитьПользователю(
		ТекстСообщения,
		Ссылка,
		,
		,
		Отказ);
	
КонецПроцедуры

Процедура ЗаполнитьДатыНачалаОкончания()
	
	ДатаНачала = ТекущаяДатаСеанса();
	Если ДатаОкончания < ДатаНачала Тогда
		ДатаОкончания = Дата(1,1,1);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
