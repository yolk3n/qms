
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииЧтенииНаСервере();
	КонецЕсли;
	
	Элементы.Владелец.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриСозданииЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ФизическиеЛица",, Объект.ФизическоеЛицо);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидОтветственногоЛицаПриИзменении(Элемент)
	
	Объект.ПравоПодписиПоДоверенности = Булево(ВидОтветственногоЛица);
	
	УстановитьОснованиеПраваПодписи();
	
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)
	
	НаименованиеЗаполнитьСписокВыбора(Элементы.Наименование.СписокВыбора, Объект);
	
	УстановитьДолжностьФизЛицаПоФРМРНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственноеЛицоПриИзменении(Элемент)
	
	Если Не ИспользоватьНачислениеЗарплаты Тогда
		ДолжностиЗаполнитьСписокВыбора();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьСсылкаПриИзменении(Элемент)
	
	Если Объект.ПравоПодписиПоДоверенности Тогда
		НаименованиеЗаполнитьСписокВыбора(Элементы.Наименование.СписокВыбора, Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПриИзменении(Элемент)
	
	Если Объект.ПравоПодписиПоДоверенности Тогда
		НаименованиеЗаполнитьСписокВыбора(Элементы.Наименование.СписокВыбора, Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументПраваПодписиПриИзменении(Элемент)
	
	УстановитьОснованиеПраваПодписи();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерДокументаПраваПодписиПриИзменении(Элемент)
	
	УстановитьОснованиеПраваПодписи();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаДокументаПраваПодписиПриИзменении(Элемент)
	
	УстановитьОснованиеПраваПодписи();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	ВидОтветственногоЛица = Число(Объект.ПравоПодписиПоДоверенности);
	
	ИспользоватьНачислениеЗарплаты = Ложь;
	
	Если ИспользоватьНачислениеЗарплаты Тогда
		Элементы.ДолжностьСтрока.КнопкаВыпадающегоСписка = Ложь;
	Иначе
		Элементы.ДолжностьСтрока.Заголовок = НСтр("ru = 'Должность'");
		ДолжностиЗаполнитьСписокВыбора();
	КонецЕсли;
	
	НаименованиеЗаполнитьСписокВыбора(Элементы.Наименование.СписокВыбора, Объект, Ложь);
	
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиФормы(Форма)
	
	Форма.Элементы.ГруппаДокументПраваПодписи.Доступность = (Форма.ВидОтветственногоЛица = 1);
	
КонецПроцедуры

#Область ЗаполнениеСписковВыбора

&НаСервере
Процедура ДолжностиЗаполнитьСписокВыбора()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ОтветственныеЛицаОрганизаций.Должность КАК Должность
	|ИЗ
	|	Справочник.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
	|ГДЕ
	|	ОтветственныеЛицаОрганизаций.Должность <> """"
	|	И ОтветственныеЛицаОрганизаций.ОтветственноеЛицо = &ОтветственноеЛицо
	|	И НЕ ОтветственныеЛицаОрганизаций.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Должность
	|";
	
	Запрос.УстановитьПараметр("ОтветственноеЛицо", Объект.ОтветственноеЛицо);
	
	МассивДолжностей = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Должность");
	Элементы.ДолжностьСтрока.СписокВыбора.ЗагрузитьЗначения(МассивДолжностей);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НаименованиеЗаполнитьСписокВыбора(СписокВыбора, Объект, ОчищатьПоле = Истина)
	
	Если ОчищатьПоле Тогда
		Объект.Наименование = "";
	КонецЕсли;
	СписокВыбора.Очистить();
	
	Если ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
		
		ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(СокрЛП(Объект.ФизическоеЛицо));
		ФИО = ?(ЗначениеЗаполнено(ФИО.Фамилия), 
			ФИО.Фамилия 
			+ ?(ЗначениеЗаполнено(ФИО.Имя), " " + Лев(ФИО.Имя,1) + "." 
			+ ?(ЗначениеЗаполнено(ФИО.Отчество), Лев(ФИО.Отчество, 1) + ".", ""), ""),
			"");
		
		Если Объект.ПравоПодписиПоДоверенности Тогда
			
			Если ЗначениеЗаполнено(Объект.ОснованиеПраваПодписи) Тогда
				СписокВыбора.Добавить(ФИО + ", " + Объект.ОснованиеПраваПодписи);
				Если ЗначениеЗаполнено(Объект.Должность) Тогда
					СписокВыбора.Добавить(ФИО + ", " + Объект.Должность + ", " + Объект.ОснованиеПраваПодписи);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		СписокВыбора.Добавить(ФИО);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ЗаполнениеСписковВыбора

&НаКлиенте
Процедура УстановитьОснованиеПраваПодписи()
	
	Если Объект.ПравоПодписиПоДоверенности Тогда
		ОснованиеПраваПодписи = НСтр("ru = '[Документ] № [Номер] от [Дата]г.'");
		Поля = Новый Структура;
		Поля.Вставить("Документ", Объект.ДокументПраваПодписи);
		Поля.Вставить("Номер", Объект.НомерДокументаПраваПодписи);
		Поля.Вставить("Дата", Формат(Объект.ДатаДокументаПраваПодписи, "ДЛФ=D"));
		Объект.ОснованиеПраваПодписи = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ОснованиеПраваПодписи, Поля);
	Иначе
		Объект.ОснованиеПраваПодписи = "";
	КонецЕсли;
	
	НаименованиеЗаполнитьСписокВыбора(Элементы.Наименование.СписокВыбора, Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДолжностьФизЛицаПоФРМРНаСервере()
	
	Объект.Должность = Справочники.ФизическиеЛица.ДолжностьФизЛицаПоФРМР(Объект.ФизическоеЛицо);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
