#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет настройки начального заполнения элементов
//
// Параметры:
//  ПолноеИмяОбъекта - Строка - имя объекта, для которого вызывается заполнение.
//  Настройки - Структура:
//    * ПриНачальномЗаполненииЭлемента - Булево - если Истина, то для каждого элемента будет
//      вызвана процедура индивидуального заполнения ПриНачальномЗаполненииЭлемента.
//    * ПредопределенныеДанные - ТаблицаЗначений - данные заполненные в процедуре ПриНачальномЗаполненииЭлементов.
//
Процедура ПриНастройкеНачальногоЗаполненияЭлементов(ПолноеИмяОбъекта, Настройки) Экспорт
	Настройки.ПриНачальномЗаполненииЭлемента = Ложь;
КонецПроцедуры

// Процедура заполняет предопределенные элементы в справочнике Шаблоны этикеток и ценников.
//
Процедура ПриНачальномЗаполненииЭлементов() Экспорт
	
	ОписанияМакетов = ОписанияМакетов();
	
	НачатьТранзакцию();
	Попытка
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ШаблоныЭтикетокИЦенниковБПО.Ссылка КАК Ссылка,
			|	ШаблоныЭтикетокИЦенниковБПО.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
			|ИЗ
			|	Справочник.ШаблоныЭтикетокИЦенниковБПО КАК ШаблоныЭтикетокИЦенниковБПО
			|ГДЕ
			|	ШаблоныЭтикетокИЦенниковБПО.Предопределенный
			|	И (ВЫРАЗИТЬ(ШаблоныЭтикетокИЦенниковБПО.XMLОписаниеМакета КАК СТРОКА(1))) = """"";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить("Справочник.ШаблоныЭтикетокИЦенниковБПО");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		Блокировка.Заблокировать();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ШаблонОбъект = Выборка.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ШаблоныЭтикетокИЦенниковБПО
			ШаблонОбъект.XMLОписаниеМакета = ОписанияМакетов[Выборка.ИмяПредопределенныхДанных];
			ШаблонОбъект.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		МенеджерОборудования.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru = 'Заполнение предопределенных элементов'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ОписанияМакетов()
	
	ОписанияМакетов = Новый Соответствие();
	Макет = Справочники.ШаблоныЭтикетокИЦенниковБПО.ПолучитьМакет("ШаблоныЭтикеток");
	
	Для НомерСтроки = 1 По Макет.ВысотаТаблицы Цикл
		
		ИмяПредопределенного = Макет.Область(НомерСтроки, 1, НомерСтроки, 1).Текст;
		XMLОписаниеМакета = Макет.Область(НомерСтроки, 2, НомерСтроки, 2).Текст;
		
		Если Не ПустаяСтрока(ИмяПредопределенного) = "" Тогда
			ОписанияМакетов.Вставить(ИмяПредопределенного, XMLОписаниеМакета);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОписанияМакетов;
	
КонецФункции

#КонецОбласти 

#КонецЕсли
 
