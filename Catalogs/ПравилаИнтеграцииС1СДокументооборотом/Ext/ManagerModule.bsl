#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает реквизиты объекта ДО указанного типа, возможно, с уточнением до вида документа.
//
// Параметры:
//   ТипОбъектаДО - Строка - имя типа объекта XDTO Документооборота.
//   Параметры - Структура - уточняющие параметры со свойством:
//     * documentType - Структура - вид объекта Документооборота, со свойствами:
//         ** ID - Строка - идентификатор вида документа.
//         ** type - Строка - тип XDTO вида документа.
//         ** name - Строка - наименование вида документа.
//   НастройкиВида - ОбъектXDTO - неявно возвращаемое значение, содержащее настройки вида документа.
//   ПолучитьВсе - Булево - позволяет вернуть все возможные реквизиты, вне зависимости от вида объекта Документооборота.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * Имя - Строка
//     * Представление - Строка
//     * Тип - СписокЗначений
//     * ДопРеквизит - Булево
//     * ДопРеквизитID - Строка
//     * ДопРеквизитТип - Строка
//     * ОбязательноеЗаполнение - Булево
//     * ЭтоТаблица - Булево
//     * Таблица - Строка
//
Функция ПолучитьРеквизитыОбъектаДО(ТипОбъектаДО, Параметры = Неопределено, НастройкиВида = Неопределено,
		ПолучитьВсе = Ложь) Экспорт
	
	Реквизиты = Новый ТаблицаЗначений;
	Реквизиты.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("Тип", Новый ОписаниеТипов("СписокЗначений"));
	Реквизиты.Колонки.Добавить("ДопРеквизит", Новый ОписаниеТипов("Булево"));
	Реквизиты.Колонки.Добавить("ДопРеквизитID", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ДопРеквизитТип", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ОбязательноеЗаполнение", Новый ОписаниеТипов("Булево"));
	Реквизиты.Колонки.Добавить("ЭтоТаблица", Новый ОписаниеТипов("Булево"));
	Реквизиты.Колонки.Добавить("Таблица", Новый ОписаниеТипов("Строка"));
	
	// Общие настройки.
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetNewObjectRequest");
	ПолучаемыеПоля = Запрос.columnSet; // СписокXDTO
	
	Запрос.type = ТипОбъектаДО;
	ПолучаемыеПоля.Добавить("additionalProperties");
	ПолучаемыеПоля.Добавить("documentTypeEnabled");
	ПолучаемыеПоля.Добавить("accessLevelEnabled");
	ПолучаемыеПоля.Добавить("activityMatterEnabled");
	ПолучаемыеПоля.Добавить("organizationEnabled");
	ПолучаемыеПоля.Добавить("statusEnabled");
	ПолучаемыеПоля.Добавить("statusChangeEnabled");
	
	// Проекты.
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP") Тогда
		ПолучаемыеПоля.Добавить("projectsEnabled");
	КонецЕсли;
	
	ДоступенПакетныйЗапрос = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3");
	Если ДоступенПакетныйЗапрос Тогда
		ЗапросыПакета = Новый Массив;
		ЗапросыПакета.Добавить(Запрос);
	Иначе // только последовательные вызовы
		РезультатДокумент = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	КонецЕсли;
	
	Если Параметры <> Неопределено
			И Параметры.Свойство("documentType")
			И Параметры.documentType <> Неопределено Тогда
		
		// Настройки по виду документа.
		ОбъектДляПолученияИзДО = Новый Структура("ID, Тип", Параметры.documentType.ID, Параметры.documentType.type);
		Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъектыЗапрос(
			Прокси,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектДляПолученияИзДО));
		
		Если ДоступенПакетныйЗапрос Тогда
			
			ЗапросыПакета.Добавить(Запрос);
			
			ОтветНаЗапросыПакета = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьПакетныйЗапрос(
				Прокси,
				ЗапросыПакета);
			
			РезультатДокумент = ОтветНаЗапросыПакета.responses[0];
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, РезультатДокумент);
			НастройкиТипа = РезультатДокумент;
			ДопРеквизиты = РезультатДокумент.additionalProperties;
			
			РезультатПоНастройкам = ОтветНаЗапросыПакета.responses[1];
			Попытка
				ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, РезультатПоНастройкам);
				НужноПолучитьНастройкиТипа = Ложь;
			Исключение // Возможно, этого вида документа уже нет в ДО.
				НужноПолучитьНастройкиТипа = Истина;
				ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ЗаписатьПредупреждение(
					СтрШаблон(НСтр("ru = 'Не найден в 1С:Документообороте ранее существовавший вид документа %1'"),
					Параметры.documentType.ID));
			КонецПопытки;
			
		Иначе
			РезультатПоНастройкам = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
			НужноПолучитьНастройкиТипа = Ложь;
			
		КонецЕсли;
		
		Если Не НужноПолучитьНастройкиТипа Тогда
			
			НастройкиВида = РезультатПоНастройкам.objects[0];
			
			РезультатДокумент.documentType = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
				Прокси,
				ТипОбъектаДО + "Type");
			РезультатДокумент.documentType.name = Параметры.documentType.name;
			РезультатДокумент.documentType.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				Параметры.documentType.ID,
				Параметры.documentType.type);
			
			Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
				Прокси,
				"DMGetObjectAdditionalPropertiesRequest");
			Запрос.object = РезультатДокумент;
			НастройкиТипа = РезультатДокумент;
			
			Результат = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
			
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Результат);
			ДопРеквизиты = Результат.additionalProperties;
			
		КонецЕсли;
		
	Иначе // вид документа неизвестен
		
		НужноПолучитьНастройкиТипа = Истина;
		
	КонецЕсли;
	
	Если НужноПолучитьНастройкиТипа Тогда
		
		Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetNewObjectRequest");
		ПолучаемыеПоля = Запрос.columnSet; // СписокXDTO
		
		Запрос.type = ТипОбъектаДО;
		ПолучаемыеПоля.Добавить("additionalProperties");
		ПолучаемыеПоля.Добавить("documentTypeEnabled");
		ПолучаемыеПоля.Добавить("accessLevelEnabled");
		ПолучаемыеПоля.Добавить("activityMatterEnabled");
		ПолучаемыеПоля.Добавить("organizationEnabled");
		ПолучаемыеПоля.Добавить("statusEnabled");
		ПолучаемыеПоля.Добавить("statusChangeEnabled");
		
		РезультатДокумент = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
		
	КонецЕсли;
	
	Если Не ДоступенПакетныйЗапрос Или НужноПолучитьНастройкиТипа Тогда
		
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, РезультатДокумент);
		НастройкиТипа = РезультатДокумент;
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоСуществует(РезультатДокумент, "additionalProperties") Тогда
			ДопРеквизиты = РезультатДокумент.additionalProperties;
		Иначе
			ДопРеквизиты = Новый Массив;
		КонецЕсли;
		
		НастройкиВида = Неопределено;
		
	КонецЕсли;
	
	// Входящий документ.
	Если ТипОбъектаДО = "DMIncomingDocument" Тогда
		
		Если ПолучитьВсе Или НастройкиТипа.documentTypeEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "documentType", НСтр("ru = 'Вид документа'"), "DMIncomingDocumentType", Истина);
		КонецЕсли;
		
		ДобавитьРеквизит(Реквизиты, "correspondent", НСтр("ru = 'Отправитель'"), "DMCorrespondent", Истина);
		ДобавитьРеквизит(Реквизиты, "externalNumber", НСтр("ru = '№ отправителя'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "externalDate", НСтр("ru = 'Дата отправителя'"), "Дата");
		
		Если ПолучитьВсе Или НастройкиТипа.organizationEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "organization", НСтр("ru = 'Организация'"), "DMOrganization", Истина);
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиВида = Неопределено Или НастройкиВида.sumEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "sum", НСтр("ru = 'Сумма'"), "Число");
			ДобавитьРеквизит(Реквизиты, "currency", НСтр("ru = 'Валюта'"), "DMCurrency");
		КонецЕсли;
		
		ДобавитьРеквизит(Реквизиты, "title", НСтр("ru = 'Наименование'"), "Строка", Истина);
		ДобавитьРеквизит(Реквизиты, "summary", НСтр("ru = 'Содержание'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "regNumber", НСтр("ru = 'Регистрационный номер'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "regDate", НСтр("ru = 'Дата регистрации'"), "Дата");
		
		ДобавитьРеквизит(Реквизиты, "signer", НСтр("ru = 'Подписал'"), "DMContactPerson");
		ДобавитьРеквизит(Реквизиты, "addressee", НСтр("ru = 'Адресат'"), "DMUser");
		ДобавитьРеквизит(Реквизиты, "subdivision", НСтр("ru = 'Подразделение'"), "DMSubdivision");
		
		Если ПолучитьВсе Или НастройкиВида = Неопределено Или НастройкиВида.performanceDateEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "performanceDate", НСтр("ru = 'Срок исполнения'"), "Дата");
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиТипа.accessLevelEnabled  = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "accessLevel", НСтр("ru = 'Гриф доступа'"), "DMAccessLevel", Истина);
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиТипа.activityMatterEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "activityMatter", НСтр("ru = 'Вопрос деятельности'"), "DMActivityMatter", Истина);
		КонецЕсли;
		
		// Проекты.
		Если ПолучитьВсе Или (ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP")
				И НастройкиТипа.projectsEnabled = Истина) Тогда
			ДобавитьРеквизит(Реквизиты, "project", НСтр("ru = 'Проект'"), "DMProject");
		КонецЕсли;
		
		ДобавитьРеквизит(Реквизиты, "deliveryMethod",  НСтр("ru = 'Способ получения'"), "DMDeliveryMethod");
		
		ДобавитьРеквизит(Реквизиты, "comment", НСтр("ru = 'Комментарий'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "responsible", НСтр("ru = 'Ответственный'"), "DMUser");
		
	// Исходящий документ.
	ИначеЕсли ТипОбъектаДО = "DMOutgoingDocument" Тогда
		
		Если ПолучитьВсе Или НастройкиТипа.documentTypeEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "documentType", НСтр("ru = 'Вид документа'"), "DMOutgoingDocumentType", Истина);
		КонецЕсли;
			
		ДобавитьРеквизит(Реквизиты, "correspondent", НСтр("ru = 'Получатель'"), "DMCorrespondent", Истина);
		
		Если ПолучитьВсе Или НастройкиВида = Неопределено Или НастройкиВида.externalNumberEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "externalNumber", НСтр("ru = '№ получателя'"), "Строка");
			ДобавитьРеквизит(Реквизиты, "externalDate", НСтр("ru = 'Дата получателя'"), "Дата");
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиТипа.OrganizationEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "organization", НСтр("ru = 'Организация'"), "DMOrganization", Истина);
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиВида = Неопределено Или НастройкиВида.sumEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "sum", НСтр("ru = 'Сумма'"), "Число");
			ДобавитьРеквизит(Реквизиты, "currency", НСтр("ru = 'Валюта'"), "DMCurrency");
		КонецЕсли;
		
		ДобавитьРеквизит(Реквизиты, "title", НСтр("ru = 'Заголовок'"), "Строка", Истина);
		ДобавитьРеквизит(Реквизиты, "summary", НСтр("ru = 'Содержание'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "regNumber", НСтр("ru = 'Регистрационный номер'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "regDate", НСтр("ru = 'Дата регистрации'"), "Дата");
		
		ДобавитьРеквизит(Реквизиты, "addressee", НСтр("ru = 'Адресат'"), "DMContactPerson");
		ДобавитьРеквизит(Реквизиты, "sent", НСтр("ru = 'Отправлен'"), "Булево");
		ДобавитьРеквизит(Реквизиты, "sendDate", НСтр("ru = 'Дата отправки'"), "Дата");
		
		ДобавитьРеквизит(Реквизиты, "signer", НСтр("ru = 'Подписал'"), "DMUser");
		ДобавитьРеквизит(Реквизиты, "author", НСтр("ru = 'Подготовил'"), "DMUser");
		ДобавитьРеквизит(Реквизиты, "subdivision", НСтр("ru = 'Подразделение'"), "DMSubdivision");
		
		Если ПолучитьВсе Или НастройкиВида = Неопределено Или НастройкиВида.performanceDateEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "performanceDate", НСтр("ru = 'Срок исполнения'"), "Дата");
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиТипа.accessLevelEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "accessLevel", НСтр("ru = 'Гриф доступа'"), "DMAccessLevel", Истина);
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиТипа.activityMatterEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "activityMatter", НСтр("ru = 'Вопрос деятельности'"), "DMActivityMatter", Истина);
		КонецЕсли;
		
		// Проекты.
		Если ПолучитьВсе Или (ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP")
				И НастройкиТипа.projectsEnabled = Истина) Тогда
			ДобавитьРеквизит(Реквизиты, "project", НСтр("ru = 'Проект'"), "DMProject");
		КонецЕсли;
		
		ДобавитьРеквизит(Реквизиты, "deliveryMethod", НСтр("ru = 'Способ отправки'"), "DMDeliveryMethod");
		ДобавитьРеквизит(Реквизиты, "comment", НСтр("ru = 'Комментарий'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "responsible", НСтр("ru = 'Ответственный'"), "DMUser");
		
	// Внутренний документ.
	ИначеЕсли ТипОбъектаДО = "DMInternalDocument" Тогда
		
		ЯвляетсяЗаявкойНаОплату = Ложь;
		Если НастройкиВида <> Неопределено
				И ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоСуществует(НастройкиВида, "isAPaymentRequest")
				И НастройкиВида.isAPaymentRequest = Истина Тогда
			ЯвляетсяЗаявкойНаОплату = Истина;
		КонецЕсли;
		
		ВестиУчетСторон = Ложь;
		Если НастройкиВида <> Неопределено
				И ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(НастройкиВида, "partiesEnabled")
				И НастройкиВида.partiesEnabled = Истина Тогда
			ВестиУчетСторон = Истина;
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиТипа.documentTypeEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "documentType", НСтр("ru = 'Вид документа'"), "DMInternalDocumentType", Истина);
		КонецЕсли;
		
		ДобавитьРеквизит(Реквизиты, "folder", НСтр("ru = 'Папка'"), "DMInternalDocumentFolder", Истина);
		
		Если ПолучитьВсе Или (НастройкиВида = Неопределено
				Или (НастройкиВида.Свойства().Получить("addresseeEnabled") <> Неопределено
					И НастройкиВида.addresseeEnabled = Истина)) Тогда
			ДобавитьРеквизит(Реквизиты, "addressee", НСтр("ru = 'Адресат'"), "DMUser", Истина);
		КонецЕсли;
		
		Если ПолучитьВсе
				Или (НастройкиТипа.organizationEnabled = Истина
					И (НастройкиВида = Неопределено
						Или НастройкиВида.Свойства().Получить("organizationEnabled") = Неопределено
						Или НастройкиВида.organizationEnabled = Истина)) Тогда
			Если ЯвляетсяЗаявкойНаОплату Тогда
				ЗаголовокОрганизация = НСтр("ru = 'Плательщик'");
			Иначе
				ЗаголовокОрганизация = НСтр("ru = 'Организация'");
			КонецЕсли;
			ДобавитьРеквизит(Реквизиты, "organization", ЗаголовокОрганизация, "DMOrganization",
				ЯвляетсяЗаявкойНаОплату Или Не ВестиУчетСторон);
		КонецЕсли;
		
		Если ПолучитьВсе Или ЯвляетсяЗаявкойНаОплату Тогда
			ДобавитьРеквизит(Реквизиты, "recipient", НСтр("ru = 'Получатель'"), "DMParty", Истина);
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиВида = Неопределено Или НастройкиВида.sumEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "sum", НСтр("ru = 'Сумма'"), "Число");
			Если ПолучитьВсе
					Или ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.8.1.CORP") Тогда
				ДобавитьРеквизит(Реквизиты, "VAT", НСтр("ru = 'В т.ч. НДС'"), "Число");
			КонецЕсли;
			ДобавитьРеквизит(Реквизиты, "currency", НСтр("ru = 'Валюта'"), "DMCurrency");
		КонецЕсли;
		
		ДобавитьРеквизит(Реквизиты, "title", НСтр("ru = 'Заголовок'"), "Строка", Истина);
		ДобавитьРеквизит(Реквизиты, "summary", НСтр("ru = 'Содержание'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "regNumber", НСтр("ru = 'Регистрационный номер'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "regDate", НСтр("ru = 'Дата регистрации'"), "Дата");
		
		ДобавитьРеквизит(Реквизиты, "signer", НСтр("ru = 'Утвердил'"), "DMUser");
		ДобавитьРеквизит(Реквизиты, "author", НСтр("ru = 'Подготовил'"), "DMUser");
		ДобавитьРеквизит(Реквизиты, "subdivision", НСтр("ru = 'Подразделение'"), "DMSubdivision");
		
		Если ПолучитьВсе Или (ВестиУчетСторон И Не ЯвляетсяЗаявкойНаОплату)
				Или НастройкиВида = Неопределено
				Или НастройкиВида.correspondentEnabled = Истина Тогда
			
			Если ИнтеграцияС1СДокументооборотПовтИсп.ИспользоватьТерминКорреспонденты() Тогда
				ТерминКонтрагент = НСтр("ru = 'Корреспондент'");
			Иначе
				ТерминКонтрагент = НСтр("ru = 'Контрагент'");
			КонецЕсли;
			ДобавитьРеквизит(
				Реквизиты,
				"correspondent",
				ТерминКонтрагент,
				"DMCorrespondent",
				?(ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.28.12.CORP"), Ложь, Истина));
			ДобавитьРеквизит(Реквизиты, "contactPerson", НСтр("ru = 'Контактное лицо'"), "DMContactPerson");
			
		КонецЕсли;
		
		Если ПолучитьВсе
				Или (ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.28.12.CORP")
					И (НастройкиВида = Неопределено Или НастройкиВида.correspondentEnabled = Истина)) Тогда
			
			// Табличная часть Контрагенты.
			ДобавитьТаблицу(Реквизиты, "correspondentRows.rows", НСтр("ru = 'Контрагенты'"));
			ДобавитьРеквизитТаблицы(Реквизиты,
				"correspondentRows.rows", "correspondent", НСтр("ru = 'Контрагент'"), "DMCorrespondent", Истина);
			ДобавитьРеквизитТаблицы(Реквизиты,
				"correspondentRows.rows", "contact", НСтр("ru = 'Контактное лицо'"), "DMContactPerson");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"correspondentRows.rows",
				"signedFromTheCorrespondent", НСтр("ru = 'Подписал от контрагента'"), "DMContactPerson");
			
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиВида = Неопределено Или НастройкиВида.durationEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты,
				"beginDate", НСтр("ru = 'Дата начала действия'"), "Дата");
			ДобавитьРеквизит(Реквизиты,
				"endDate", НСтр("ru = 'Дата окончания действия'"), "Дата");
			ДобавитьРеквизит(Реквизиты,
				"openEnded", НСтр("ru = 'Бессрочный'"), "Булево");
			ДобавитьРеквизит(Реквизиты,
				"prolongationProcedure", НСтр("ru = 'Порядок продления'"), "DMProlongationProcedure");
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиТипа.accessLevelEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "accessLevel", НСтр("ru = 'Гриф доступа'"), "DMAccessLevel", Истина);
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиТипа.activityMatterEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "activityMatter", НСтр("ru = 'Вопрос деятельности'"), "DMActivityMatter", Истина);
		КонецЕсли;
		
		// Проекты.
		Если ПолучитьВсе Или (ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP")
				И НастройкиТипа.projectsEnabled = Истина) Тогда
			ДобавитьРеквизит(Реквизиты, "project", НСтр("ru = 'Проект'"), "DMProject");
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиВида = Неопределено Или НастройкиВида.performanceDateEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "performanceDate", НСтр("ru = 'Срок исполнения'"), "Дата");
		КонецЕсли;
		
		ДобавитьРеквизит(Реквизиты, "comment", НСтр("ru = 'Комментарий'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "responsible", НСтр("ru = 'Ответственный'"), "DMUser");
		
		// Суммы по статьям ДДС.
		Если ПолучитьВсе Или (НастройкиВида <> Неопределено
				И ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(НастройкиВида, "cashFlowDetailsEnabled")
				И НастройкиВида.cashFlowDetailsEnabled = Истина) Тогда
			
			ДобавитьТаблицу(Реквизиты, "cashFlowRows.rows", НСтр("ru = 'Статьи движения денежных средств'"));
			ДобавитьРеквизитТаблицы(Реквизиты, "cashFlowRows.rows", "item", НСтр("ru = 'Статья'"), "DMCashFlowItem", Истина);
			ДобавитьРеквизитТаблицы(Реквизиты, "cashFlowRows.rows", "total", НСтр("ru = 'Сумма'"), "Число");
			ДобавитьРеквизитТаблицы(Реквизиты, "cashFlowRows.rows", "VAT", НСтр("ru = 'Сумма НДС'"), "Число");
			
		КонецЕсли;
		
		// Табличная часть Товары.
		Если ПолучитьВсе Или (НастройкиВида <> Неопределено
				И ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(НастройкиВида, "productRowsEnabled")
				И НастройкиВида.productRowsEnabled = Истина) Тогда
			
			ДобавитьТаблицу(Реквизиты, "productRows.rows", НСтр("ru = 'Товары и услуги'"));
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "product", НСтр("ru = 'Товар, услуга'"), "DMProduct");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "price", НСтр("ru = 'Цена'"), "Число");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "quantity", НСтр("ru = 'Количество'"), "Число");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "measurementUnit", НСтр("ru = 'Единица измерения'"), "DMMeasurementUnit");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "VATRate", НСтр("ru = 'Ставка НДС'"), "DMVATRate");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "VAT", НСтр("ru = 'Сумма НДС'"), "Число");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "total", НСтр("ru = 'Сумма'"), "Число");
			
		КонецЕсли;
		
		// Табличная часть Стороны.
		Если ПолучитьВсе Или (ЯвляетсяЗаявкойНаОплату = Ложь И ВестиУчетСторон) Тогда
			
			ДобавитьТаблицу(Реквизиты, "partyRows.rows", НСтр("ru = 'Стороны'"));
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "comment", НСтр("ru = 'Комментарий'"), "Строка");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "contact", НСтр("ru = 'Контактное лицо'"), "DMPartyRowContact");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "description", НСтр("ru = 'Наименование стороны'"), "DMPartyName");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "party", НСтр("ru = 'Сторона'"), "DMParty", Истина);
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "setBy", НСтр("ru = 'Установил'"), "DMUser");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "signatureDate", НСтр("ru = 'Дата подписи'"), "Дата");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "signed", НСтр("ru = 'Подписан'"), "Булево");
			
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "signedBy", НСтр("ru = 'Подписал'"), "DMPartyRowContact");
			
		КонецЕсли;
		
	// Контрагент.
	ИначеЕсли ТипОбъектаДО = "DMCorrespondent" Тогда
		
		ДобавитьРеквизит(Реквизиты, "name", НСтр("ru = 'Наименование'"), "Строка", Истина);
		Если ИнтеграцияС1СДокументооборотПовтИсп.ИспользоватьТерминКорреспонденты() Тогда
			ДобавитьРеквизит(Реквизиты,
				"legalPrivatePerson",
				НСтр("ru = 'Вид корреспондента'"),
				"DMLegalPrivatePerson",
				Истина);
		Иначе
			ДобавитьРеквизит(Реквизиты,
				"legalPrivatePerson",
				НСтр("ru = 'Вид контрагента'"),
				"DMLegalPrivatePerson",
				Истина);
		КонецЕсли;
		ДобавитьРеквизит(Реквизиты, "inn", НСтр("ru = 'ИНН'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "kpp", НСтр("ru = 'КПП'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "okpo", НСтр("ru = 'Код по ОКПО'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "comment", НСтр("ru = 'Комментарий'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "fullName", НСтр("ru = 'Полное наименование'"), "Строка");
		ДобавитьРеквизит(Реквизиты, "privatePerson", НСтр("ru = 'Физическое лицо'"), "DMPrivatePerson");
		ДобавитьРеквизит(Реквизиты, "responsible", НСтр("ru = 'Ответственный'"), "DMUser");
		
	КонецЕсли;
	
	// Состояния.
	Если ТипОбъектаДО = "DMIncomingDocument"
			Или ТипОбъектаДО = "DMOutgoingDocument" Или ТипОбъектаДО = "DMInternalDocument" Тогда
		
		Если ПолучитьВсе Или (НастройкиТипа.statusEnabled = Истина И НастройкиТипа.statusChangeEnabled = Истина) Тогда
			ДобавитьРеквизит(Реквизиты, "statusApproval", НСтр("ru = 'Состояние согласования'"), "DMDocumentStatus");
			ДобавитьРеквизит(Реквизиты, "statusConfirmation", НСтр("ru = 'Состояние утверждения'"), "DMDocumentStatus");
			ДобавитьРеквизит(Реквизиты, "statusConsideration", НСтр("ru = 'Состояние рассмотрения'"), "DMDocumentStatus");
			ДобавитьРеквизит(Реквизиты, "statusPerformance", НСтр("ru = 'Состояние исполнения'"), "DMDocumentStatus");
			ДобавитьРеквизит(Реквизиты, "statusRegistration", НСтр("ru = 'Состояние регистрации'"), "DMDocumentStatus");
		КонецЕсли;
		
		Если ПолучитьВсе Или (ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP")
				И ТипОбъектаДО = "DMInternalDocument"
					И НастройкиТипа.statusEnabled = Истина И НастройкиТипа.statusChangeEnabled = Истина) Тогда
			ДобавитьРеквизит(Реквизиты, "statusSigning", НСтр("ru = 'Состояние подписания'"), "DMDocumentStatus");
		КонецЕсли;
		
	КонецЕсли;
	
	// Дополнительные свойства.
	Для Каждого ДопСвойство Из ДопРеквизиты Цикл
		
		Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ДопСвойство, "propertyValueTypes") Тогда
			Продолжить;
		КонецЕсли;
		
		Типы = Новый СписокЗначений;
		Для Каждого ОписаниеТипа Из ДопСвойство.propertyValueTypes Цикл
			Если ОписаниеТипа.xdtoClassName = "integer" Тогда
				ТипЗначения = "Число";
			ИначеЕсли ОписаниеТипа.xdtoClassName = "boolean" Тогда
				ТипЗначения = "Булево";
			ИначеЕсли ОписаниеТипа.xdtoClassName = "string" Тогда
				ТипЗначения = "Строка";
			ИначеЕсли ОписаниеТипа.xdtoClassName = "date" Тогда
				ТипЗначения = "Дата";
			Иначе	
				ТипЗначения = ОписаниеТипа.xdtoClassName;
			КонецЕсли;
			Типы.Добавить(ТипЗначения, ОписаниеТипа.presentation);
		КонецЦикла;
		
		ДобавитьДополнительныйРеквизит(Реквизиты,
			ДопСвойство.name,
			Типы,
			ДопСвойство.objectID.ID,
			ДопСвойство.objectID.type);
		
	КонецЦикла;
	
	Возврат Реквизиты;
	
КонецФункции

// Возвращает таблицу возможных состояний документа Документооборота.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * Имя - Строка
//     * Представление - Строка
//     * Тип - СписокЗначений
//     * ДопРеквизит - Булево
//     * ДопРеквизитID - Строка
//     * ДопРеквизитТип - Строка
//     * ОбязательноеЗаполнение - Булево
//
Функция ВозможныеСостоянияОбъектаДокументооборота() Экспорт
	
	Реквизиты = Новый ТаблицаЗначений;
	Реквизиты.Колонки.Добавить("Имя");
	Реквизиты.Колонки.Добавить("Представление");
	Реквизиты.Колонки.Добавить("Тип");
	Реквизиты.Колонки.Добавить("ДопРеквизит");
	Реквизиты.Колонки.Добавить("ДопРеквизитID");
	Реквизиты.Колонки.Добавить("ДопРеквизитТип");
	Реквизиты.Колонки.Добавить("ОбязательноеЗаполнение");
	
	ДобавитьРеквизит(Реквизиты, "statusRegistration", НСтр("ru = 'Состояние регистрации'"), "DMDocumentStatus");
	ДобавитьРеквизит(Реквизиты, "statusConsideration", НСтр("ru = 'Состояние рассмотрения'"), "DMDocumentStatus");
	ДобавитьРеквизит(Реквизиты, "statusApproval", НСтр("ru = 'Состояние согласования'"), "DMDocumentStatus");
	ДобавитьРеквизит(Реквизиты, "statusConfirmation", НСтр("ru = 'Состояние утверждения'"), "DMDocumentStatus");
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP") Тогда
		ДобавитьРеквизит(Реквизиты, "statusSigning", НСтр("ru = 'Состояние подписания'"), "DMDocumentStatus");
	КонецЕсли;
	ДобавитьРеквизит(Реквизиты, "statusPerformance", НСтр("ru = 'Состояние исполнения'"), "DMDocumentStatus");
	
	Возврат Реквизиты;
	
КонецФункции

// Возвращает реквизиты шаблона документа.
//
// Параметры:
//   ШаблонТип - Строка - имя типа объекта XDTO Документооборота (DM<..>DocumentType).
//   ШаблонID - Строка - имя типа объекта XDTO Документооборота (DM<..>DocumentType).
//   ШаблонЗапрещаетИзменение - Булево - неявно возвращаемое значение, Истина, если изменение
//     созданных по шаблону документов запрещено.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * ИмяРеквизитаОбъектаДО - Строка - имя реквизита в ДО.
//     * ШаблонЗначение - Произвольный - значение реквизита шаблона.
//     * ШаблонID - Строка - идентификатор ссылочного значения реквизита шаблона.
//     * ШаблонТип - Строка - тип значения реквизита шаблона.
//     * ШаблонТип - Строка - тип значения реквизита шаблона.
//     * ЭтоДополнительныйРеквизитДО - Булево - Истина, если это дополнительный реквизит.
//     * ДополнительныйРеквизитДОID - Строка - идентификатор доп. свойства ДО.
//     * ДополнительныйРеквизитДОТип - Строка - тип доп. свойства ДО.
//
Функция ПолучитьРеквизитыШаблонаДокумента(ШаблонТип, ШаблонID, ШаблонЗапрещаетИзменение) Экспорт
	
	Реквизиты = Новый ТаблицаЗначений;
	Реквизиты.Колонки.Добавить("ИмяРеквизитаОбъектаДО", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ШаблонЗначение", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ШаблонID", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ШаблонТип", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ЭтоДополнительныйРеквизитДО", Новый ОписаниеТипов("Булево"));
	Реквизиты.Колонки.Добавить("ДополнительныйРеквизитДОID", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ДополнительныйРеквизитДОТип", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("Таблица", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ЭтоТаблица", Новый ОписаниеТипов("Булево"));
	
	Если Не ЗначениеЗаполнено(ШаблонID) Тогда
		ШаблонЗапрещаетИзменение = Ложь;
		Возврат Реквизиты;
	КонецЕсли;
	
	// Общие настройки.
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	ОбъектID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(Прокси, ШаблонID, ШаблонТип);
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	СписокОбъектов = Запрос.objectIDs; // СписокXDTO
	
	СписокОбъектов.Добавить(ОбъектID);
	
	Результат = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	Попытка
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Результат);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	ШаблонXDTO = Результат.objects[0];
	
	Если ШаблонXDTO.Установлено("additionalProperties") Тогда
		ДопРеквизиты = ШаблонXDTO.additionalProperties;
	Иначе
		ДопРеквизиты = Новый Массив;
	КонецЕсли;
	
	Если ШаблонXDTO.Установлено("blockDerivedDocuments") Тогда
		ШаблонЗапрещаетИзменение = ШаблонXDTO.blockDerivedDocuments;
	Иначе
		ШаблонЗапрещаетИзменение = Ложь;
	КонецЕсли;
	
	// Общие реквизиты шаблонов.
	Если ШаблонТип = "DMIncomingDocumentTemplate"
		Или ШаблонТип = "DMOutgoingDocumentTemplate"
		Или ШаблонТип = "DMInternalDocumentTemplate" Тогда
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "accessLevel", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "comment", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "performanceDate", ШаблонXDTO, "performanceTerm");
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "activityMatter", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "responsible", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "organization", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "subdivision", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "summary", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "title", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "project", ШаблонXDTO);
	КонецЕсли;
	
	// Шаблон входящего документа.
	Если ШаблонТип = "DMIncomingDocumentTemplate" Тогда 
		
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "documentType", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "addressee", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "signer", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "correspondent", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "deliveryMethod", ШаблонXDTO);
		
	// Шаблон исходящего документа.
	ИначеЕсли ШаблонТип = "DMOutgoingDocumentTemplate" Тогда 
		
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "documentType", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "addressee", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "signer", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "correspondent", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "deliveryMethod", ШаблонXDTO);
		
	// Шаблон внутреннего документа.
	ИначеЕсли ШаблонТип = "DMInternalDocumentTemplate" Тогда 
		
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "documentType", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "folder", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "correspondent", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "contactPerson", ШаблонXDTO);
		ДобавитьРеквизитШаблона(Прокси, Реквизиты, "cashFlowItem", ШаблонXDTO);
		
	КонецЕсли;
	
	// Дополнительные реквизиты.
	Для Каждого ДопРеквизит Из ДопРеквизиты Цикл
		
		НоваяСтрока = Реквизиты.Добавить();
		НоваяСтрока.ИмяРеквизитаОбъектаДО = "";
		НоваяСтрока.ЭтоДополнительныйРеквизитДО = Истина;
		НоваяСтрока.ДополнительныйРеквизитДОID = ДопРеквизит.objectID.ID;
		НоваяСтрока.ДополнительныйРеквизитДОТип = ДопРеквизит.objectID.type;
		
		Если ДопРеквизит.Установлено("propertySimpleValue") Тогда
			НоваяСтрока.ШаблонЗначение = ДопРеквизит.propertySimpleValue;
		ИначеЕсли ДопРеквизит.Установлено("propertyObjectValue") Тогда
			НоваяСтрока.ШаблонЗначение = ДопРеквизит.propertyObjectValue.name;
			НоваяСтрока.ШаблонID = ДопРеквизит.propertyObjectValue.objectID.ID;
			НоваяСтрока.ШаблонТип = ДопРеквизит.propertyObjectValue.objectID.type;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Реквизиты;
	
КонецФункции

// Возвращает соответствие свойств XDTO и реквизитов формы объекта.
//
// Параметры:
//   ТипОбъекта - Строка - тип объекта XDTO.
//
// Возвращаемое значение:
//   Структура - где ключ - имя свойства XDTO, а значение - имя реквизита формы документа.
//
Функция СоответствиеСвойствXDTOиРеквизитовФормыОбъектаДО(ТипОбъекта) Экспорт
	
	Результат = Новый Структура;
	
	// Общие реквизиты документов.
	Если Найти(ТипОбъекта, "Document") <> 0 Тогда
		
		Результат.Вставить("title", "Наименование");
		Результат.Вставить("summary", "Описание");
		Результат.Вставить("comment", "Комментарий");
		Результат.Вставить("regNumber", "РегистрационныйНомер");
		Результат.Вставить("regDate", "ДатаРегистрации");
		Результат.Вставить("performanceDate", "СрокИсполнения");
		Результат.Вставить("sum", "Сумма");
		Результат.Вставить("currency", "Валюта");
		Результат.Вставить("status", "Состояние");
		Результат.Вставить("organization", "Организация");
		Результат.Вставить("subdivision", "Подразделение");
		Результат.Вставить("activityMatter", "ВопросДеятельности");
		Результат.Вставить("responsible", "Ответственный");
		Результат.Вставить("accessLevel", "ГрифДоступа");
		Результат.Вставить("documentType", "ВидДокумента");
		
		Результат.Вставить("statusRegistration", "СостояниеРегистрация");
		Результат.Вставить("statusConfirmation", "СостояниеУтверждение");
		Если ТипОбъекта = "DMInternalDocument"
				И ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP") Тогда
			Результат.Вставить("statusSigning", "СостояниеПодписание");
		КонецЕсли;
		Результат.Вставить("statusApproval", "СостояниеСогласование");
		Результат.Вставить("statusConsideration", "СостояниеРассмотрение");
		Результат.Вставить("statusPerformance", "СостояниеИсполнение");
		
		// Шаблоны.
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.0.8.1") Тогда
			Результат.Вставить("template", "Шаблон");
		КонецЕсли;
		
		// Проекты.
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP") Тогда
			Результат.Вставить("project", "Проект");
		КонецЕсли;
		
		// Состав документа
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1") Тогда
			Результат.Вставить("numberOfSheets", "КоличествоЛистов");
			Результат.Вставить("numberOfAnnexes", "КоличествоПриложений");
			Результат.Вставить("sheetsInAnnexes", "ЛистовВПриложениях");
			Результат.Вставить("numberOfCopies", "КоличествоЭкземпляров");
		КонецЕсли;
		
		// Хранения документа
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1") Тогда
			Результат.Вставить("caseFilesCatalog", "НоменклатураДел");
			Результат.Вставить("caseFileDossier", "Дело");
		КонецЕсли;
		
	КонецЕсли;
	
	// Входящий документ.
	Если ТипОбъекта = "DMIncomingDocument" Тогда
		
		Результат.Вставить("externalNumber", "НомерОтправителя");
		Результат.Вставить("externalDate", "ДатаОтправителя");
		Результат.Вставить("correspondent", "Отправитель");
		Результат.Вставить("signer", "Подписал");
		Результат.Вставить("addressee", "Адресат");
		Результат.Вставить("deliveryMethod", "СпособПолучения");
		
	// Исходящий документ.
	ИначеЕсли ТипОбъекта = "DMOutgoingDocument" Тогда
		
		Результат.Вставить("externalNumber", "НомерПолучателя");
		Результат.Вставить("externalDate", "ДатаПолучателя");
		Результат.Вставить("sent", "Отправлен");
		Результат.Вставить("sendDate", "ДатаОтправки");
		
		Результат.Вставить("correspondent", "Получатель");
		Результат.Вставить("addressee", "Адресат");
		Результат.Вставить("signer", "Подписал");
		Результат.Вставить("author", "Подготовил");
		Результат.Вставить("deliveryMethod", "СпособОтправки");
		
	// Внутренний документ.
	ИначеЕсли ТипОбъекта = "DMInternalDocument" Тогда
		
		Результат.Вставить("beginDate", "ДатаНачалаДействия");
		Результат.Вставить("endDate", "ДатаОкончанияДействия");
		Результат.Вставить("openEnded", "Бессрочный");
		
		Результат.Вставить("folder", "Папка");
		Результат.Вставить("author", "Подготовил");
		
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.13.1") Тогда
			Результат.Вставить("addressee", "Адресат");
		КонецЕсли;
		
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.0.0") Тогда
			Результат.Вставить("VAT", "СуммаНДС");
			
			СтрокаТовары = Новый Структура;
			СтрокаТовары.Вставить("product", "Номенклатура");
			СтрокаТовары.Вставить("quantity", "Количество");
			СтрокаТовары.Вставить("measurementUnit", "ЕдиницаИзмерения");
			СтрокаТовары.Вставить("price", "Цена");
			СтрокаТовары.Вставить("VATRate", "СтавкаНДС");
			СтрокаТовары.Вставить("VAT", "СуммаНДС");
			СтрокаТовары.Вставить("total", "Сумма");
			
			СтрокиТовары = Новый Структура;
			СтрокиТовары.Вставить("rows", Новый Массив);
			СтрокиТовары.rows.Добавить("Товары");
			СтрокиТовары.rows.Добавить(СтрокаТовары);
			
			Результат.Вставить("productRows", СтрокиТовары);
		КонецЕсли;
		
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.28.12.CORP") Тогда
			СтрокаСтороны = Новый Структура;
			СтрокаСтороны.Вставить("comment", "Комментарий");
			СтрокаСтороны.Вставить("contact", "КонтактноеЛицо");
			СтрокаСтороны.Вставить("description", "Наименование");
			СтрокаСтороны.Вставить("party", "Сторона");
			СтрокаСтороны.Вставить("setBy", "Установил");
			СтрокаСтороны.Вставить("signatureDate", "ДатаПодписи");
			СтрокаСтороны.Вставить("signed", "Подписан");
			СтрокаСтороны.Вставить("signedBy", "Подписал");
			
			СтрокиСтороны = Новый Структура;
			СтрокиСтороны.Вставить("rows", Новый Массив);
			СтрокиСтороны.rows.Добавить("Стороны");
			СтрокиСтороны.rows.Добавить(СтрокаСтороны);
			
			Результат.Вставить("partyRows", СтрокиСтороны);
			
			СтрокаКонтрагенты = Новый Структура;
			СтрокаКонтрагенты.Вставить("contact", "КонтактноеЛицо");
			СтрокаКонтрагенты.Вставить("correspondent", "Контрагент");
			СтрокаКонтрагенты.Вставить("signedFromTheCorrespondent", "ПодписалОтКонтрагента");
			
			СтрокиКонтрагенты = Новый Структура;
			СтрокиКонтрагенты.Вставить("rows", Новый Массив);
			СтрокиКонтрагенты.rows.Добавить("Контрагенты");
			СтрокиКонтрагенты.rows.Добавить(СтрокаКонтрагенты);
			
			Результат.Вставить("correspondentRows", СтрокиКонтрагенты);
		КонецЕсли;
		
		Результат.Вставить("signer", "Подписал");
		Результат.Вставить("correspondent", "Контрагент");
		Результат.Вставить("contactPerson", "КонтактноеЛицо");
		Результат.Вставить("prolongationProcedure", "ПорядокПродления");
		
		СтрокаСтатейДДС = Новый Структура;
		СтрокаСтатейДДС.Вставить("item", "СтатьяДДС");
		СтрокаСтатейДДС.Вставить("total", "Сумма");
		СтрокаСтатейДДС.Вставить("VAT", "СуммаНДС");
		
		СтрокиСтатейДДС = Новый Структура;
		СтрокиСтатейДДС.Вставить("rows", Новый Массив);
		СтрокиСтатейДДС.rows.Добавить("СтатьиДДС");
		СтрокиСтатейДДС.rows.Добавить(СтрокаСтатейДДС);
		
		Результат.Вставить("cashFlowRows", СтрокиСтатейДДС);
		
	// Контрагент (корреспондент).
	ИначеЕсли ТипОбъекта = "DMCorrespondent" Тогда
		
		Результат.Вставить("name", "Наименование");
		Результат.Вставить("inn", "ИНН");
		Результат.Вставить("kpp", "КПП");
		Результат.Вставить("okpo", "КодПоОКПО");
		Результат.Вставить("fullName", "ПолноеНаименование");
		Результат.Вставить("comment", "Комментарий");
		Результат.Вставить("legalPrivatePerson", "ЮрФизЛицо");
		Результат.Вставить("privatePerson", "ФизЛицо");
		Результат.Вставить("responsible", "Ответственный");
		
	КонецЕсли;
	
	// Пометка удаления.
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.0.1") Тогда
		Результат.Вставить("deletionMark", "ПометкаУдаления");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает соответствие свойств XDTO реквизитам формы документа, отвечающих за состояния.
//
// Параметры:
//   ТипДокумента - Строка - тип объекта XDTO.
//
// Возвращаемое значение:
//   Структура:
//     * statusRegistration - Строка - "СостояниеРегистрация"
//     * statusConsideration - Строка - "СостояниеРассмотрение"
//     * statusApproval - Строка - "СостояниеСогласование"
//     * statusConfirmation - Строка - "СостояниеУтверждение"
//     * statusSigning - Строка - "СостояниеПодписание"
//     * statusPerformance - Строка - "СостояниеИсполнение"
//
Функция СоответствиеСвойствXDTOиСостоянийДокумента(ТипДокумента) Экспорт
	
	СоответствиеРеквизитов = Новый Структура;
	
	СоответствиеРеквизитов.Вставить("statusRegistration",	"СостояниеРегистрация");
	СоответствиеРеквизитов.Вставить("statusConsideration",	"СостояниеРассмотрение");
	СоответствиеРеквизитов.Вставить("statusApproval",		"СостояниеСогласование");
	СоответствиеРеквизитов.Вставить("statusConfirmation",	"СостояниеУтверждение");
	Если ТипДокумента = "DMInternalDocument"
			И ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP") Тогда
		СоответствиеРеквизитов.Вставить("statusSigning",	"СостояниеПодписание");
	КонецЕсли;
	СоответствиеРеквизитов.Вставить("statusPerformance",	"СостояниеИсполнение");
	
	Возврат СоответствиеРеквизитов;
	
КонецФункции

// Заполняет объект ИС по объекту XDTO.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   Объект - Произвольный - заполняемый объект.
//   ОбъектXDTO - ОбъектXDTO - объект ДО, источник данных заполнения.
//   Правило - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом - правила заполнения.
//   Обновление - Булево - заполнять только реквизиты с Обновлять = Истина.
//   ТребуетсяПерепроведение - Булево - Истина, если изменение требует перепроведения.
//
// Возвращаемое значение:
//   Булево - Истина, если объект был изменен, и Ложь в противном случае.
//
Функция ЗаполнитьОбъектПоОбъектуXDTO(Прокси, Объект, ОбъектXDTO, Правило,
		Обновление = Ложь, ТребуетсяПерепроведение = Ложь) Экспорт
	
	ОбъектИзменен = Ложь;
	ОбновленныеТабличныеЧасти = Новый Соответствие;
	
	МетаданныеОбъекта = Объект.Метаданные();
	Реквизиты = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьРеквизитыОбъектаИС(
		МетаданныеОбъекта.ПолноеИмя());
	
	ПроверятьПерепроведение = Обновление
		И ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъекта)
		И МетаданныеОбъекта.Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить
		И Объект.Проведен;
	Запрещено =
		Перечисления.РежимИзмененияПроведенногоДокументаДанными1СДокументооборота.Запрещено;
	РазрешеноБезПерепроведения =
		Перечисления.РежимИзмененияПроведенногоДокументаДанными1СДокументооборота.РазрешеноБезПерепроведения;
	РазрешеноСПерепроведением =
		Перечисления.РежимИзмененияПроведенногоДокументаДанными1СДокументооборота.РазрешеноСПерепроведением;
	
	ЯвляетсяЗаявкойНаОплату = Ложь;
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ОбъектXDTO, "documentType")
			И ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоСуществует(ОбъектXDTO.documentType, "isAPaymentRequest")
			И ОбъектXDTO.documentType.isAPaymentRequest = Истина Тогда
		ЯвляетсяЗаявкойНаОплату = Истина;
	КонецЕсли;
	
	// Переопределяемый модуль может перекрыть настройки правил, разрешив изменение без перепроведения.
	ОбновляемыеРеквизиты = Новый Массив;
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриОпределенииОбновляемыхРеквизитовПроведенногоДокумента(
		МетаданныеОбъекта.ПолноеИмя(),
		ОбновляемыеРеквизиты);
	
	Для Каждого Реквизит Из Реквизиты Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ИмяРеквизитаОбъектаИС", Реквизит.Имя);
		ПараметрыОтбора.Вставить("ЭтоТаблица", Реквизит.ЭтоТаблица);
		ПараметрыОтбора.Вставить("Таблица", Реквизит.Таблица);
		Если Обновление Тогда
			ПараметрыОтбора.Вставить("Обновлять", Истина);
		КонецЕсли;
		
		НайденныеПравилаРеквизитов = Правило.ПравилаЗаполненияРеквизитовИС.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеПравилаРеквизитов.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		ПравилоРеквизита = НайденныеПравилаРеквизитов[0];
		
		Если ПроверятьПерепроведение Тогда
			Если ПравилоРеквизита.РежимИзмененияДанныхПроведенногоДокумента = РазрешеноСПерепроведением Тогда
				РежимИзменения = РазрешеноСПерепроведением;
			ИначеЕсли ПравилоРеквизита.РежимИзмененияДанныхПроведенногоДокумента = РазрешеноБезПерепроведения
				Или ОбновляемыеРеквизиты.Найти(Реквизит.Имя) <> Неопределено Тогда
				РежимИзменения = РазрешеноБезПерепроведения;
			Иначе
				РежимИзменения = Запрещено;
			КонецЕсли;
		Иначе
			РежимИзменения = РазрешеноБезПерепроведения;
		КонецЕсли;
		
		Если Реквизит.ЭтоТаблица Тогда
			
			ОбновленныеТабличныеЧасти.Вставить(Реквизит.Имя, Объект[Реквизит.Имя].Выгрузить());
			Объект[Реквизит.Имя].Очистить();
			
			Если ПравилоРеквизита.Вариант = ВариантРеквизит() Тогда // наполним пустыми строками
				
				РазделеннаяСтрока = СтрРазделить(ПравилоРеквизита.ИмяРеквизитаОбъектаДО, ".", Ложь);
				Если РазделеннаяСтрока.Количество() = 1 Тогда
					СписокXDTO = ОбъектXDTO[РазделеннаяСтрока[0]];
				Иначе
					СписокXDTO = ОбъектXDTO[РазделеннаяСтрока[0]][РазделеннаяСтрока[1]];
				КонецЕсли;
				
				Для Каждого ЭлементСпискаXDTO Из СписокXDTO Цикл
					Объект[Реквизит.Имя].Добавить();
				КонецЦикла;
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантРеквизитТаблицы() Тогда // добавим единственную строку
				
				Объект[Реквизит.Имя].Добавить();
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантВыражение() Тогда // загрузим из результата-таблицы
				
				Параметры = Новый Структура;
				Параметры.Вставить("Источник", ОбъектXDTO);
				Параметры.Вставить("Приемник", Объект);
				Параметры.Вставить("Результат", Неопределено);
				
				ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ПравилоРеквизита.ВычисляемоеВыражение, Параметры);
				
				Если ТипЗнч(Параметры.Результат) = Тип("ТаблицаЗначений") Тогда
					Объект[Реквизит.Имя].Загрузить(Параметры.Результат);
				КонецЕсли;
				
			Иначе // значениями, указанными для реквизитов: добавим единственную строку
				
				Объект[Реквизит.Имя].Добавить();
				
			КонецЕсли;
			
			Продолжить;
			
		КонецЕсли;
		
		Если Не Реквизит.ЭтоДополнительныйРеквизитИС Тогда
			
			// Из реквизита объекта ДО.
			Если ПравилоРеквизита.Вариант = ВариантРеквизит() Тогда
				
				ЗначенияСвойства = Новый Массив;
				
				Если Не ПравилоРеквизита.ЭтоДополнительныйРеквизитДО Тогда
					
					РазделеннаяСтрока = СтрРазделить(ПравилоРеквизита.ИмяРеквизитаОбъектаДО, ".", Ложь);
					Если РазделеннаяСтрока.Количество() = 1 Тогда // "свойство"
						ИмяСвойства = ПравилоРеквизита.ИмяРеквизитаОбъектаДО;
						Если ЯвляетсяЗаявкойНаОплату И ИмяСвойства = "recipient" Тогда
							ЗначенияСвойства.Добавить(СторонаЗаявкиНаОплатуXDTO(ОбъектXDTO, "Получатель")); //@NON-NLS-1
						ИначеЕсли ТипЗнч(ОбъектXDTO[ИмяСвойства]) = Тип("СписокXDTO") Тогда
							Для Каждого ЭлементСпискаXDTO Из ОбъектXDTO[ИмяСвойства] Цикл
								ЗначенияСвойства.Добавить(ЭлементСпискаXDTO);
							КонецЦикла;
						Иначе
							ЗначенияСвойства.Добавить(ОбъектXDTO[ИмяСвойства]);
						КонецЕсли;
					ИначеЕсли РазделеннаяСтрока.Количество() = 2 Тогда // "список.свойство"
						СписокXDTO = ОбъектXDTO[РазделеннаяСтрока[0]];
						ИмяСвойства = РазделеннаяСтрока[1];
						Для Каждого ЭлементСпискаXDTO Из СписокXDTO Цикл
							ЗначенияСвойства.Добавить(ЭлементСпискаXDTO[ИмяСвойства]);
						КонецЦикла;
					ИначеЕсли РазделеннаяСтрока.Количество() = 3 Тогда // "список.список.свойство"
						СписокXDTO = ОбъектXDTO[РазделеннаяСтрока[0]][РазделеннаяСтрока[1]];
						ИмяСвойства = РазделеннаяСтрока[2];
						Для Каждого ЭлементСпискаXDTO Из СписокXDTO Цикл
							ЗначенияСвойства.Добавить(ЭлементСпискаXDTO[ИмяСвойства]);
						КонецЦикла;
					Иначе
						ВызватьИсключение СтрШаблон(
							НСтр("ru = 'В правилах интеграции указано недопустимое имя реквизита-источника данных заполнения: %1'"), 
							ПравилоРеквизита.ИмяРеквизитаОбъектаДО);
					КонецЕсли;
					
				Иначе // дополнительный реквизит ДО
					
					Для Каждого Строка Из ОбъектXDTO.additionalProperties Цикл
						Если Строка.objectID.ID = ПравилоРеквизита.ДополнительныйРеквизитДОID 
							И Строка.objectID.type = ПравилоРеквизита.ДополнительныйРеквизитДОТип Тогда
							Если Строка.propertySimpleValue <> Неопределено Тогда
								ЗначенияСвойства.Добавить(Строка.propertySimpleValue);
							ИначеЕсли Строка.propertyObjectValue <> Неопределено Тогда
								ЗначенияСвойства.Добавить(Строка.propertyObjectValue);
							Иначе
								ЗначениеСвойства = Неопределено;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					
				КонецЕсли;
				
				Для НомерЗначения = 0 По ЗначенияСвойства.Количество() - 1 Цикл
					
					ЗначениеСвойства = ЗначенияСвойства[НомерЗначения];
					ЗначениеРеквизита = Неопределено;
					
					Если ТипЗнч(ЗначениеСвойства) = Тип("ОбъектXDTO") Тогда
						ТипРеквизита = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПодходящийТип(
							ЗначениеСвойства.objectID.type,
							Реквизит.Тип.Типы());
					Иначе
						ТипРеквизита = Реквизит.Тип.Типы()[0];
					КонецЕсли;
					
					ЭтоПримитивныйТип = ТипРеквизита = Тип("Строка")
						Или ТипРеквизита = Тип("Дата")
						Или ТипРеквизита = Тип("Число")
						Или ТипРеквизита = Тип("Булево");
					ЭтоОбъектноеЗначение =
						ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЭтоОбъектноеЗначение(Прокси, ЗначениеСвойства);
					
					Если ЭтоПримитивныйТип И Не ЭтоОбъектноеЗначение Тогда
						
						ЗначениеРеквизита = ЗначениеСвойства;
						
					ИначеЕсли ЗначениеСвойства <> Неопределено И ЭтоОбъектноеЗначение Тогда
						
						ПараметрыВыбора = Новый Соответствие;
						Для Каждого СвязьПараметровВыбора Из Реквизит.СвязиПараметровВыбора Цикл
							РазделенныйПуть = СтрРазделить(СвязьПараметровВыбора.ПутьКДанным, ".");
							Если РазделенныйПуть.Количество() = 2
								И РазделенныйПуть[0] = Реквизит.Таблица Тогда
								Пока Объект[Реквизит.Таблица].Количество() - 1 < НомерЗначения Цикл
									Объект[Реквизит.Таблица].Добавить();
								КонецЦикла;
								Строка = Объект[Реквизит.Таблица][НомерЗначения];
								ПараметрыВыбора[СвязьПараметровВыбора.Имя] = Строка[РазделенныйПуть[1]];
							ИначеЕсли РазделенныйПуть.Количество() = 1 Тогда
								ПараметрыВыбора[СвязьПараметровВыбора.Имя] = Объект[РазделенныйПуть[0]];
							Иначе
								Продолжить; // прочие варианты пути к данным не поддерживаются
							КонецЕсли;
						КонецЦикла;
						
						ЗначениеРеквизита = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗначениеРеквизитаПоОбъектуXDTO(
							ЗначениеСвойства,
							ТипРеквизита,
							Реквизит.Имя,
							ТипЗнч(Объект),
							Объект,,,
							ПараметрыВыбора);
						
					КонецЕсли;
					
					Если ЗначениеЗаполнено(Реквизит.Таблица) Тогда // реквизит табличной части
						
						Пока Объект[Реквизит.Таблица].Количество() - 1 < НомерЗначения Цикл
							Объект[Реквизит.Таблица].Добавить();
						КонецЦикла;
						Строка = Объект[Реквизит.Таблица][НомерЗначения];
						СтараяТаблица = ОбновленныеТабличныеЧасти[Реквизит.Таблица];
						Если НомерЗначения < СтараяТаблица.Количество() Тогда
							ПредыдущееЗначение = СтараяТаблица[НомерЗначения][Реквизит.Имя];
						Иначе
							ПредыдущееЗначение = Неопределено;
						КонецЕсли;
						Строка[Реквизит.Имя] = ЗначениеРеквизита;
						Если Строка[Реквизит.Имя] <> ПредыдущееЗначение Тогда
							Если РежимИзменения = Запрещено Тогда
								Строка[Реквизит.Имя] = ПредыдущееЗначение;
							ИначеЕсли РежимИзменения = РазрешеноБезПерепроведения Тогда
								ОбъектИзменен = Истина;
							ИначеЕсли РежимИзменения = РазрешеноСПерепроведением Тогда
								ОбъектИзменен = Истина;
								ТребуетсяПерепроведение = Истина;
							КонецЕсли;
						КонецЕсли;
						
					Иначе // реквизит шапки
						
						Если ЗначениеРеквизита <> Неопределено Тогда
							
							ПредыдущееЗначение = Объект[Реквизит.Имя];
							Объект[Реквизит.Имя] = ЗначениеРеквизита;
							Если Объект[Реквизит.Имя] <> ПредыдущееЗначение Тогда
								Если РежимИзменения = Запрещено Тогда
									Объект[Реквизит.Имя] = ПредыдущееЗначение;
								ИначеЕсли РежимИзменения = РазрешеноБезПерепроведения Тогда
									ОбъектИзменен = Истина;
								ИначеЕсли РежимИзменения = РазрешеноСПерепроведением Тогда
									ОбъектИзменен = Истина;
									ТребуетсяПерепроведение = Истина;
								КонецЕсли;
							КонецЕсли;
							
						КонецЕсли;
						
						Прервать; // при заполнении реквизита по таблице используется первая строка
						
					КонецЕсли;
					
				КонецЦикла;
				
			// Из указанного значения.
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантЗначение() Тогда
				
				ЗначениеРеквизита = ПравилоРеквизита.ЗначениеРеквизитаИС;
				
				Если ЗначениеЗаполнено(Реквизит.Таблица) Тогда // реквизит табличной части
					
					СтараяТаблица = ОбновленныеТабличныеЧасти[Реквизит.Таблица];
					Номер = 0;
					Для Каждого Строка Из Объект[Реквизит.Таблица] Цикл
						
						Если Номер < СтараяТаблица.Количество() Тогда
							ПредыдущееЗначение = СтараяТаблица[Номер][Реквизит.Имя];
						Иначе
							ПредыдущееЗначение = Неопределено;
						КонецЕсли;
						Строка[Реквизит.Имя] = ЗначениеРеквизита;
						Если Строка[Реквизит.Имя] <> ПредыдущееЗначение Тогда
							Если РежимИзменения = Запрещено Тогда
								Строка[Реквизит.Имя] = ПредыдущееЗначение;
							ИначеЕсли РежимИзменения = РазрешеноБезПерепроведения Тогда
								ОбъектИзменен = Истина;
							ИначеЕсли РежимИзменения = РазрешеноСПерепроведением Тогда
								ОбъектИзменен = Истина;
								ТребуетсяПерепроведение = Истина;
							КонецЕсли;
						КонецЕсли;
						Номер = Номер + 1;
						
					КонецЦикла;
					
				Иначе // реквизит шапки
					
					ПредыдущееЗначение = Объект[Реквизит.Имя];
					Объект[Реквизит.Имя] = ЗначениеРеквизита;
					Если Объект[Реквизит.Имя] <> ПредыдущееЗначение Тогда
						Если РежимИзменения = Запрещено Тогда
							Объект[Реквизит.Имя] = ПредыдущееЗначение;
						ИначеЕсли РежимИзменения = РазрешеноБезПерепроведения Тогда
							ОбъектИзменен = Истина;
						ИначеЕсли РежимИзменения = РазрешеноСПерепроведением Тогда
							ОбъектИзменен = Истина;
							ТребуетсяПерепроведение = Истина;
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
				
			// Из выражения на встроенном языке.
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантВыражение() Тогда
				
				Параметры = Новый Структура;
				Параметры.Вставить("Источник", ОбъектXDTO);
				Параметры.Вставить("Приемник", Объект);
				Параметры.Вставить("Результат", Неопределено);
				
				ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ПравилоРеквизита.ВычисляемоеВыражение, Параметры);
				
				Если Параметры.Результат <> Неопределено Тогда
					
					Если ЗначениеЗаполнено(Реквизит.Таблица) Тогда // реквизит табличной части
						
						СтараяТаблица = ОбновленныеТабличныеЧасти[Реквизит.Таблица];
						Номер = 0;
						Для Каждого Строка Из Объект[Реквизит.Таблица] Цикл
							
							Если Номер < СтараяТаблица.Количество() Тогда
								ПредыдущееЗначение = СтараяТаблица[Номер][Реквизит.Имя];
							Иначе
								ПредыдущееЗначение = Неопределено;
							КонецЕсли;
							ЗаполнитьПоРезультатуВыраженияНаВстроенномЯзыке(
								Строка,
								Реквизит.Имя,
								Параметры.Результат);
							Если Строка[Реквизит.Имя] <> ПредыдущееЗначение Тогда
								Если РежимИзменения = Запрещено Тогда
									Строка[Реквизит.Имя] = ПредыдущееЗначение;
								ИначеЕсли РежимИзменения = РазрешеноБезПерепроведения Тогда
									ОбъектИзменен = Истина;
								ИначеЕсли РежимИзменения = РазрешеноСПерепроведением Тогда
									ОбъектИзменен = Истина;
									ТребуетсяПерепроведение = Истина;
								КонецЕсли;
							КонецЕсли;
							Номер = Номер + 1;
							
						КонецЦикла;
						
					Иначе // реквизит шапки
						
						ПредыдущееЗначение = Объект[Реквизит.Имя];
						ЗаполнитьПоРезультатуВыраженияНаВстроенномЯзыке(
							Объект,
							Реквизит.Имя,
							Параметры.Результат);
						Если Объект[Реквизит.Имя] <> ПредыдущееЗначение Тогда
							Если РежимИзменения = Запрещено Тогда
								Объект[Реквизит.Имя] = ПредыдущееЗначение;
							ИначеЕсли РежимИзменения = РазрешеноБезПерепроведения Тогда
								ОбъектИзменен = Истина;
							ИначеЕсли РежимИзменения = РазрешеноСПерепроведением Тогда
								ОбъектИзменен = Истина;
								ТребуетсяПерепроведение = Истина;
							КонецЕсли;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе // дополнительный реквизит объекта ИС
			
			СтруктураПоиска = Новый Структура("Свойство", Реквизит.ДополнительныйРеквизитИС);
			СтрокиРеквизита = Объект.ДополнительныеРеквизиты.НайтиСтроки(СтруктураПоиска);
			Если СтрокиРеквизита.Количество() = 0 Тогда
				ОбъектИзменен = Истина;
				СтрокаРеквизита = Объект.ДополнительныеРеквизиты.Добавить();
				СтрокаРеквизита.Свойство = Реквизит.ДополнительныйРеквизитИС;
			Иначе
				СтрокаРеквизита = СтрокиРеквизита[0];
			КонецЕсли;
			
			Если ПравилоРеквизита.Вариант = ВариантРеквизит() Тогда
				
				Если Не ПравилоРеквизита.ЭтоДополнительныйРеквизитДО Тогда
					
					РазделеннаяСтрока = СтрРазделить(ПравилоРеквизита.ИмяРеквизитаОбъектаДО, ".", Ложь);
					Если РазделеннаяСтрока.Количество() = 1 Тогда
						ИмяСвойства = ПравилоРеквизита.ИмяРеквизитаОбъектаДО;
						Если ЯвляетсяЗаявкойНаОплату И ИмяСвойства = "recipient" Тогда
							ЗначенияСвойства.Добавить(СторонаЗаявкиНаОплатуXDTO(ОбъектXDTO, "Получатель")); //@NON-NLS-1
						ИначеЕсли ТипЗнч(ОбъектXDTO[ИмяСвойства]) = Тип("СписокXDTO") Тогда
							Для Каждого ЭлементСпискаXDTO Из ОбъектXDTO[ИмяСвойства] Цикл
								ЗначениеСвойства = ЭлементСпискаXDTO;
								Прервать;
							КонецЦикла;
						Иначе
							ЗначениеСвойства = ОбъектXDTO[ИмяСвойства];
						КонецЕсли;
					ИначеЕсли РазделеннаяСтрока.Количество() = 2 Тогда // "список.свойство"
						СписокXDTO = ОбъектXDTO[РазделеннаяСтрока[0]];
						ИмяСвойства = РазделеннаяСтрока[1];
						Для Каждого ЭлементСпискаXDTO Из СписокXDTO Цикл
							ЗначениеСвойства = ЭлементСпискаXDTO[ИмяСвойства];
							Прервать;
						КонецЦикла;
					ИначеЕсли РазделеннаяСтрока.Количество() = 3 Тогда // "список.список.свойство"
						СписокXDTO = ОбъектXDTO[РазделеннаяСтрока[0]][РазделеннаяСтрока[1]];
						ИмяСвойства = РазделеннаяСтрока[2];
						Для Каждого ЭлементСпискаXDTO Из СписокXDTO Цикл
							ЗначениеСвойства = ЭлементСпискаXDTO[ИмяСвойства];
							Прервать;
						КонецЦикла;
					Иначе
						ВызватьИсключение СтрШаблон(
							НСтр("ru = 'В правилах интеграции указано недопустимое имя реквизита-источника данных заполнения: %1'"), 
							ПравилоРеквизита.ИмяРеквизитаОбъектаДО);
					КонецЕсли;
					
				Иначе // доп. реквизит ДО
					
					Для Каждого Строка Из ОбъектXDTO.additionalProperties Цикл
						Если Строка.objectID.ID = ПравилоРеквизита.ДополнительныйРеквизитДОID
								И Строка.objectID.type = ПравилоРеквизита.ДополнительныйРеквизитДОТип Тогда
							Если Строка.propertySimpleValue <> Неопределено Тогда
								ЗначениеСвойства = Строка.propertySimpleValue;
							ИначеЕсли  Строка.propertyObjectValue <> Неопределено Тогда
								ЗначениеСвойства = Строка.propertyObjectValue;
							Иначе
								ЗначениеСвойства = Неопределено;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					
				КонецЕсли;
				
				Если ТипЗнч(ЗначениеСвойства) = Тип("ОбъектXDTO") Тогда
					ТипРеквизита = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПодходящийТип(
						ЗначениеСвойства.objectID.type,
						Реквизит.Тип.Типы());
				Иначе
					ТипРеквизита = Реквизит.Тип.Типы()[0];
				КонецЕсли;
				
				ЭтоПримитивныйТип = ТипРеквизита = Тип("Строка")
					Или ТипРеквизита = Тип("Дата")
					Или ТипРеквизита = Тип("Число")
					Или ТипРеквизита = Тип("Булево");
				
				ЭтоОбъектноеЗначение = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЭтоОбъектноеЗначение(Прокси, ЗначениеСвойства);
				
				Если ЭтоПримитивныйТип И Не ЭтоОбъектноеЗначение Тогда
					
					ПредыдущееЗначение = СтрокаРеквизита.Значение;
					СтрокаРеквизита.Значение = ЗначениеСвойства;
					ОбъектИзменен = (ОбъектИзменен Или (СтрокаРеквизита.Значение <> ПредыдущееЗначение));
					
				ИначеЕсли ЗначениеСвойства <> Неопределено И ЭтоОбъектноеЗначение Тогда
					
					ЗначениеРеквизита = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗначениеРеквизитаПоОбъектуXDTO(
						ЗначениеСвойства,
						ТипРеквизита,
						Реквизит.Имя,
						ТипЗнч(Объект),
						Объект,
						Истина,
						СтрокаРеквизита.Свойство);
					Если ЗначениеРеквизита <> Неопределено Тогда
						ПредыдущееЗначение = СтрокаРеквизита.Значение;
						СтрокаРеквизита.Значение = ЗначениеРеквизита;
						ОбъектИзменен = (ОбъектИзменен Или (СтрокаРеквизита.Значение <> ПредыдущееЗначение));
					КонецЕсли;
					
				ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипРеквизита) И ТипЗнч(ЗначениеСвойства) = Тип("Строка") Тогда
					
					МетаданныеТипаРеквизита = Метаданные.НайтиПоТипу(ТипРеквизита);
					Если МетаданныеТипаРеквизита = Неопределено Тогда
						ПолноеИмяТипаРеквизита = "";
					Иначе
						ПолноеИмяТипаРеквизита = МетаданныеТипаРеквизита.ПолноеИмя();
					КонецЕсли;
					Если СтрНайти(ПолноеИмяТипаРеквизита, "Справочник.") > 0 Тогда
						Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяТипаРеквизита);
						Ссылка = Менеджер.НайтиПоНаименованию(ЗначениеСвойства, Истина);
						Если ЗначениеЗаполнено(Ссылка) Тогда
							ПредыдущееЗначение = СтрокаРеквизита.Значение;
							СтрокаРеквизита.Значение = Ссылка;
							ОбъектИзменен = (ОбъектИзменен Или (СтрокаРеквизита.Значение <> ПредыдущееЗначение));
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантЗначение() Тогда
				
				Если СтрокаРеквизита.Значение <> ПравилоРеквизита.ЗначениеРеквизитаИС Тогда
					ОбъектИзменен = Истина;
					СтрокаРеквизита.Значение = ПравилоРеквизита.ЗначениеРеквизитаИС;
				КонецЕсли;
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантВыражение() Тогда
				
				Параметры = Новый Структура;
				Параметры.Вставить("Источник", ОбъектXDTO);
				Параметры.Вставить("Приемник", Объект);
				Параметры.Вставить("Результат", Неопределено);
				
				ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ПравилоРеквизита.ВычисляемоеВыражение, Параметры);
				
				Если Параметры.Результат <> Неопределено Тогда
					ПредыдущееЗначение = СтрокаРеквизита.Значение;
					СтрокаРеквизита.Значение = Параметры.Результат;
					ОбъектИзменен = (ОбъектИзменен Или (СтрокаРеквизита.Значение <> ПредыдущееЗначение));
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ТабличнаяЧасть Из ОбновленныеТабличныеЧасти Цикл
		ОбъектИзменен = (ОбъектИзменен
			Или (ЗначениеВСтрокуВнутр(ТабличнаяЧасть.Значение) <>
				ЗначениеВСтрокуВнутр(Объект[ТабличнаяЧасть.Ключ].Выгрузить())));
	КонецЦикла;
	
	Если ОбъектXDTO.Свойства().Получить("deletionMark") <> Неопределено И ОбъектXDTO.Установлено("deletionMark") Тогда
		ОбъектИзменен = (ОбъектИзменен Или (ОбъектXDTO.deletionMark <> Объект.ПометкаУдаления));
		Объект.ПометкаУдаления = ОбъектXDTO.deletionMark;
	КонецЕсли;
	
	Возврат ОбъектИзменен;
	
КонецФункции

// Возвращает вид документа ДО из данных правила интеграции.
//
// Параметры:
//   ДанныеПравила - Структура - данные правила, необходимые для выгрузки, ключи - имена реквизитов и табличных частей.
//
// Возвращаемое значение:
//   Структура:
//     * ID - Строка - ID вида документа.
//     * type - Строка - тип вида документа.
//     * name - Строка - наименование вида документа.
//
Функция ВидДокументаИзПравила(ДанныеПравила) Экспорт
	
	// Определим вид заполняемого документа и получим описание его реквизитов.
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ИмяРеквизитаОбъектаДО", "documentType");
	ПараметрыОтбора.Вставить("ЭтоДополнительныйРеквизитДО", Ложь);
	ПараметрыОтбора.Вставить("Вариант", ВариантЗначение());
	
	// Вид документа может быть задан явно и перекрывать шаблон.
	СтрокиВидДокумента = ДанныеПравила.ПравилаЗаполненияРеквизитовДО.НайтиСтроки(ПараметрыОтбора);
	ВидДокумента = Неопределено;
	Если СтрокиВидДокумента.Количество() > 0 Тогда
		ВидДокумента = Новый Структура("ID, type, name",
			СтрокиВидДокумента[0].ЗначениеРеквизитаДОID,
			СтрокиВидДокумента[0].ЗначениеРеквизитаДОТип,
			СтрокиВидДокумента[0].ЗначениеРеквизитаДО);
		
	Иначе // Вид документа может быть задан только в шаблоне.
		ПараметрыОтбора.Вариант = ВариантШаблон();
		
		СтрокиВидДокумента = ДанныеПравила.ПравилаЗаполненияРеквизитовДО.НайтиСтроки(ПараметрыОтбора);
		Если СтрокиВидДокумента.Количество() > 0 Тогда
			ВидДокумента = Новый Структура("ID, type, name",
				СтрокиВидДокумента[0].ШаблонID,
				СтрокиВидДокумента[0].ШаблонТип,
				СтрокиВидДокумента[0].ШаблонЗначение);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВидДокумента;
	
КонецФункции

// Заполняет реквизиты формы из данных объекта-потребителя.
//
// Параметры:
//   Источник - ЛюбаяСсылка - ссылка на объект ИС.
//   Форма - ФормаКлиентскогоПриложения - форма объекта 1С:Документооборота.
//   Правило - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом, Структура - применяемое правило.
//
Процедура ЗаполнитьФормуОбъектаДОПоПравилу(Источник, Форма, Правило) Экспорт
	
	// Правило, переданное в виде ссылки, нуждается в выборке запросом.
	Если ТипЗнч(Правило) = Тип("СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом") Тогда
		ДанныеПравила = ДанныеПравилаДляВыгрузки(Правило);
	Иначе // передана сразу структура данных
		ДанныеПравила = Правило;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура("documentType", ВидДокументаИзПравила(ДанныеПравила));
	
	СоответствиеСвойствXDTOиРеквизитов = СоответствиеСвойствXDTOиРеквизитовФормыОбъектаДО(Форма.Тип);
	ИсточникОбъект = Источник.ПолучитьОбъект();
	МетаданныеИсточника = Источник.Метаданные();
	НастройкиВида = Неопределено;
	
	РеквизитыОбъектаДО = ПолучитьРеквизитыОбъектаДО(ДанныеПравила.ТипОбъектаДО, ПараметрыЗаполнения, НастройкиВида);
	
	ЯвляетсяЗаявкойНаОплату = Ложь;
	Если НастройкиВида <> Неопределено
			И ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоСуществует(НастройкиВида, "isAPaymentRequest")
			И НастройкиВида.isAPaymentRequest = Истина Тогда
		ЯвляетсяЗаявкойНаОплату = Истина;
	КонецЕсли;
	Если ЯвляетсяЗаявкойНаОплату Тогда
		СоответствиеСвойствXDTOиРеквизитов.Вставить("recipient", "Получатель");
	КонецЕсли;
	
	Для Каждого РеквизитОбъектаДО Из РеквизитыОбъектаДО Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ЭтоДополнительныйРеквизитДО", РеквизитОбъектаДО.ДопРеквизит);
		Если РеквизитОбъектаДО.ДопРеквизит Тогда
			ПараметрыОтбора.Вставить("ДополнительныйРеквизитДОID", РеквизитОбъектаДО.ДопРеквизитID);
			ПараметрыОтбора.Вставить("ДополнительныйРеквизитДОТип", РеквизитОбъектаДО.ДопРеквизитТип);
			ПараметрыОтбора.Вставить("ЭтоТаблица", Ложь);
			ПараметрыОтбора.Вставить("Таблица", "");
		Иначе
			ПараметрыОтбора.Вставить("ИмяРеквизитаОбъектаДО", РеквизитОбъектаДО.Имя);
			ПараметрыОтбора.Вставить("ЭтоТаблица", РеквизитОбъектаДО.ЭтоТаблица);
			ПараметрыОтбора.Вставить("Таблица", РеквизитОбъектаДО.Таблица);
		КонецЕсли;
		
		НайденныеПравилаРеквизитов = ДанныеПравила.ПравилаЗаполненияРеквизитовДО.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеПравилаРеквизитов.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		ПравилоРеквизита = НайденныеПравилаРеквизитов[0]; // СправочникТабличнаяЧастьСтрока.ПравилаИнтеграцииС1СДокументооборотом.ПравилаЗаполненияРеквизитовДО
		
		Если Не РеквизитОбъектаДО.ДопРеквизит Тогда
			
			Если ЗначениеЗаполнено(РеквизитОбъектаДО.Таблица) Тогда
				
				РазделеннаяСтрока = СтрРазделить(РеквизитОбъектаДО.Таблица, ".", Ложь);
				СвойствоВладелец = СоответствиеСвойствXDTOиРеквизитов[РазделеннаяСтрока[0]];
				ОписаниеТаблицы = СвойствоВладелец[РазделеннаяСтрока[1]];
				ИмяТаблицы = ОписаниеТаблицы[0];
				ИмяРеквизита = ОписаниеТаблицы[1][РеквизитОбъектаДО.Имя];
				
			ИначеЕсли РеквизитОбъектаДО.ЭтоТаблица Тогда
				
				РазделеннаяСтрока = СтрРазделить(РеквизитОбъектаДО.Имя, ".", Ложь);
				СвойствоВладелец = СоответствиеСвойствXDTOиРеквизитов[РазделеннаяСтрока[0]];
				ОписаниеТаблицы = СвойствоВладелец[РазделеннаяСтрока[1]];
				ИмяТаблицы = ОписаниеТаблицы[0];
				ИмяРеквизита = "";
				
			Иначе
				
				ИмяТаблицы = "";
				ИмяРеквизита = СоответствиеСвойствXDTOиРеквизитов[РеквизитОбъектаДО.Имя];
				
			КонецЕсли;
			
			Если РеквизитОбъектаДО.ЭтоТаблица Тогда
				
				Форма[ИмяТаблицы].Очистить();
				
				Если ПравилоРеквизита.Вариант = ВариантРеквизит() Тогда // наполним пустыми строками
					
					ТабличнаяЧасть = ИсточникОбъект[ПравилоРеквизита.ИмяРеквизитаОбъектаИС];
					Для Счетчик = 1 По ТабличнаяЧасть.Количество() Цикл
						Форма[ИмяТаблицы].Добавить();
					КонецЦикла;
					
				ИначеЕсли ПравилоРеквизита.Вариант = ВариантРеквизитТаблицы() Тогда // добавим единственную строку
					
					Форма[ИмяТаблицы].Добавить();
					
				ИначеЕсли ПравилоРеквизита.Вариант = ВариантВыражение() Тогда // загрузим из результата-таблицы
					
					Параметры = Новый Структура;
					Параметры.Вставить("Источник", ИсточникОбъект);
					Параметры.Вставить("Результат", Неопределено);
					
					ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ПравилоРеквизита.ВычисляемоеВыражение, Параметры);
					
					СтандартнаяОбработка = Истина;
					ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриЗаполненииСвойстваОбъектаXDTOПоЗначению(
						Параметры.Результат,
						Форма,
						ДанныеПравила.ТипОбъектаДО,
						ИмяТаблицы,
						Неопределено,
						СтандартнаяОбработка);
					
					Если Не СтандартнаяОбработка Тогда
						Продолжить;
					КонецЕсли;
					
					Если ТипЗнч(Параметры.Результат) = Тип("ТаблицаЗначений") Тогда
						КолонкиТаблицы = Параметры.Результат.Колонки; // КоллекцияКолонокТаблицыЗначений
						
						Для Каждого СтрокаРезультата Из Параметры.Результат Цикл
							СтрокаФормы = Форма[ИмяТаблицы].Добавить();
							Для Каждого Колонка Из КолонкиТаблицы Цикл
								Если Не СтрокаФормы.Свойство(Колонка.Имя) Тогда
									Продолжить;
								КонецЕсли;
								Значение = СтрокаРезультата[Колонка.Имя];
								СтрокаФормы[Колонка.Имя] = Значение;
								Если Не СтрокаФормы.Свойство(Колонка.Имя + "ID") Тогда
									Продолжить;
								КонецЕсли;
								Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЕстьУникальныйИдентификатор(Значение) Тогда
									СтрокаФормы[Колонка.Имя + "ID"] = Строка(Значение.УникальныйИдентификатор());
									СтрокаФормы[Колонка.Имя + "Тип"] = Значение.Метаданные().ПолноеИмя();
								ИначеЕсли ЭтоПеречисление(Значение) Тогда
									СтрокаФормы[Колонка.Имя + "ID"] = ОбщегоНазначения.ИмяЗначенияПеречисления(Значение);
									СтрокаФормы[Колонка.Имя + "Тип"] = Значение.Метаданные().ПолноеИмя();
								Иначе
									СтрокаФормы[Колонка.Имя + "ID"] = Строка(Значение);
									СтрокаФормы[Колонка.Имя + "Тип"] = "Строка";
								КонецЕсли;
							КонецЦикла;
						КонецЦикла;
					КонецЕсли;
					
				Иначе // значениями, указанными для реквизитов: добавим единственную строку
					
					Форма[ИмяТаблицы].Добавить();
					
				КонецЕсли;
				
				Продолжить;
				
			КонецЕсли; // это таблица
			
			ТипРеквизитаДО = РеквизитОбъектаДО.Тип[0].Значение;
			
			Если ПравилоРеквизита.Вариант = ВариантРеквизит() Тогда
				
				ИмяРеквизитаОбъектаИС = ПравилоРеквизита.ИмяРеквизитаОбъектаИС;
				
				Если ЗначениеЗаполнено(ПравилоРеквизита.Таблица) Тогда // реквизит табличной части
					
					РазделеннаяСтрока = СтрРазделить(ИмяРеквизитаОбъектаИС, ".");
					Если РазделеннаяСтрока.Количество() = 2 Тогда
						ТабличнаяЧасть = ИсточникОбъект[РазделеннаяСтрока[0]];
						Значения = ТабличнаяЧасть.ВыгрузитьКолонку(РазделеннаяСтрока[1]);
					Иначе
						Значения = Новый Массив;
						Значения.Добавить(ИсточникОбъект[РазделеннаяСтрока[0]]);
					КонецЕсли;
					
				Иначе // обычный или дополнительный реквизит
					
					Значения = Новый Массив;
					
					Если ИмяРеквизитаОбъектаИС = "Представление" Тогда
						
						Значения.Добавить(Строка(ИсточникОбъект));
						
					ИначеЕсли Найти(ИмяРеквизитаОбъектаИС, ".") > 0
							И Не ПравилоРеквизита.ЭтоДополнительныйРеквизитИС Тогда
						
						ЗначениеРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, ИмяРеквизитаОбъектаИС);
						Значения.Добавить(ЗначениеРеквизита);
						
					Иначе
						
						Если ПравилоРеквизита.ЭтоДополнительныйРеквизитИС Тогда
							
							ТекстЗапроса =
								"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
								|	ДополнительныеРеквизиты.Значение КАК ЗначениеРеквизита
								|ИЗ
								|	&ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
								|ГДЕ
								|	ДополнительныеРеквизиты.Ссылка = &Источник
								|	И ДополнительныеРеквизиты.Свойство = &Свойство";
							ТекстЗапроса = СтрЗаменить(
								ТекстЗапроса,
								"&ДополнительныеРеквизиты",
								МетаданныеИсточника.ПолноеИмя() +"." + "ДополнительныеРеквизиты");
							
							Запрос = Новый Запрос(ТекстЗапроса);
							Запрос.УстановитьПараметр("Источник", Источник);
							Запрос.УстановитьПараметр("Свойство", ПравилоРеквизита.ДополнительныйРеквизитИС);
							Значения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЗначениеРеквизита");
							
						Иначе // обычный реквизит
							
							Попытка
								
								Значения.Добавить(ИсточникОбъект[ИмяРеквизитаОбъектаИС]);
								
							Исключение // в случае отсутствия реквизита выдадим понятное сообщение об ошибке
								
								Инфо = ИнформацияОбОшибке();
								Если МетаданныеИсточника.Реквизиты.Найти(ИмяРеквизитаОбъектаИС) = Неопределено
									И ПравилоРеквизита.ИмяРеквизитаОбъектаИС <> "Представление" Тогда
									Найден = Ложь;
									Для Каждого СтандартныйРеквизит Из МетаданныеИсточника.СтандартныеРеквизиты Цикл
										Если СтандартныйРеквизит.Имя = ПравилоРеквизита.ИмяРеквизитаОбъектаИС Тогда
											Найден = Истина;
											Прервать;
										КонецЕсли;
									КонецЦикла;
									Если Не Найден Тогда
										ТекстСообщения = СтрШаблон(
											НСтр("ru = 'Не найден реквизит %1, указанный в правиле интеграции с 1С:Документооборотом.
												|Проверьте правило интеграции или обратитесь к администратору.'",
												ОбщегоНазначения.КодОсновногоЯзыка()),
											ПравилоРеквизита.ИмяРеквизитаОбъектаИС);
										ЗаписьЖурналаРегистрации(
											ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИмяСобытияЖурналаРегистрации(),
											УровеньЖурналаРегистрации.Ошибка,,
											ДанныеПравила.Ссылка,
											ТекстСообщения);
										ВызватьИсключение ТекстСообщения;
									КонецЕсли;
								КонецЕсли;
								
								// Иная ошибка.
								ВызватьИсключение ПодробноеПредставлениеОшибки(Инфо);
								
							КонецПопытки;
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
					Таблица = Форма[ИмяТаблицы];
				Иначе
					Таблица = Новый Массив;
					Таблица.Добавить(Форма);
				КонецЕсли;
				
				Для НомерЗначения = 0 По Значения.Количество() - 1 Цикл
					
					Значение = Значения[НомерЗначения];
					
					СтандартнаяОбработка = Истина;
					ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриЗаполненииСвойстваОбъектаXDTOПоЗначению(
						Значение,
						Таблица[НомерЗначения],
						ДанныеПравила.ТипОбъектаДО,
						ИмяРеквизита,
						ТипРеквизитаДО,
						СтандартнаяОбработка);
					
					Если Не СтандартнаяОбработка Тогда
						Продолжить;
					КонецЕсли;
					
					Для НомерЭлемента = 0 По Таблица.Количество() - 1 Цикл
						
						Если Значения.Количество() > 1 И НомерЗначения <> НомерЭлемента Тогда
							Продолжить;
						КонецЕсли;
						
						Элемент = Таблица[НомерЭлемента];
						
						Если ЭтоПримитивныйТип(ТипРеквизитаДО) Тогда
							Элемент[ИмяРеквизита] = ПривестиЗначениеКПримитивномуТипу(ТипРеквизитаДО, Значение);
						ИначеЕсли ЗначениеЗаполнено(Значение) Тогда
							Элемент[ИмяРеквизита] = Строка(Значение);
							Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЕстьУникальныйИдентификатор(Значение) Тогда
								Элемент[ИмяРеквизита + "ID"] = Строка(Значение.УникальныйИдентификатор());
								Элемент[ИмяРеквизита + "Тип"] = Значение.Метаданные().ПолноеИмя();
							ИначеЕсли ЭтоПеречисление(Значение) Тогда
								Элемент[ИмяРеквизита + "ID"] = ОбщегоНазначения.ИмяЗначенияПеречисления(Значение);
								Элемент[ИмяРеквизита + "Тип"] = Значение.Метаданные().ПолноеИмя();
							Иначе // примитивный тип на стороне ИС
								Элемент[ИмяРеквизита + "ID"] = Строка(Значение);
								Элемент[ИмяРеквизита + "Тип"] = "Строка";
							КонецЕсли;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантЗначение() Тогда
				
				Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
					Таблица = Форма[ИмяТаблицы];
				Иначе
					Таблица = Новый Массив;
					Таблица.Добавить(Форма);
				КонецЕсли;
				
				Для Каждого Элемент Из Таблица Цикл
					
					Если ЭтоПримитивныйТип(ТипРеквизитаДО) Тогда
						Элемент[ИмяРеквизита] = ПривестиЗначениеКПримитивномуТипу(ТипРеквизитаДО, ПравилоРеквизита.ЗначениеРеквизитаДО);
					Иначе
						Элемент[ИмяРеквизита] = ПравилоРеквизита.ЗначениеРеквизитаДО;
						Элемент[ИмяРеквизита + "ID"] = ПравилоРеквизита.ЗначениеРеквизитаДОID;
						Элемент[ИмяРеквизита + "Тип"] = ПравилоРеквизита.ЗначениеРеквизитаДОТип;
					КонецЕсли;
					
				КонецЦикла;
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантВыражение() Тогда
				
				Параметры = Новый Структура;
				Параметры.Вставить("Источник", ИсточникОбъект);
				Параметры.Вставить("Результат", Неопределено);
				Параметры.Вставить("РезультатID", Неопределено);
				Параметры.Вставить("РезультатТип", Неопределено);
				
				ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ПравилоРеквизита.ВычисляемоеВыражение, Параметры);
				
				Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
					Таблица = Форма[ИмяТаблицы];
				Иначе
					Таблица = Новый Массив;
					Таблица.Добавить(Форма);
				КонецЕсли;
				
				Для Каждого Элемент Из Таблица Цикл
					Если ЭтоПримитивныйТип(ТипРеквизитаДО) Тогда
						Элемент[ИмяРеквизита] = ПривестиЗначениеКПримитивномуТипу(ТипРеквизитаДО, Параметры.Результат);
					ИначеЕсли ЗначениеЗаполнено(Параметры.Результат) Тогда // ссылочный тип на стороне ДО
						Элемент[ИмяРеквизита] = Строка(Параметры.Результат);
						Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЕстьУникальныйИдентификатор(Параметры.Результат) Тогда
							Элемент[ИмяРеквизита + "ID"] = Строка(Параметры.Результат.УникальныйИдентификатор());
							Элемент[ИмяРеквизита + "Тип"] = Параметры.Результат.Метаданные().ПолноеИмя();
						ИначеЕсли ЭтоПеречисление(Параметры.Результат) Тогда
							Элемент[ИмяРеквизита + "ID"] = ОбщегоНазначения.ИмяЗначенияПеречисления(Параметры.Результат);
							Элемент[ИмяРеквизита + "Тип"] = Параметры.Результат.Метаданные().ПолноеИмя();
						ИначеЕсли ЗначениеЗаполнено(Параметры.РезультатID)
								И ЗначениеЗаполнено(Параметры.РезультатТип) Тогда
							Элемент[ИмяРеквизита + "ID"] = Параметры.РезультатID;
							Элемент[ИмяРеквизита + "Тип"] = Параметры.РезультатТип;
						Иначе // примитивный тип на стороне ИС
							Элемент[ИмяРеквизита + "Тип"] = "Строка";
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе // дополнительный реквизит на стороне ДО
			
			ТипРеквизитаДО = РеквизитОбъектаДО.Тип[0].Значение;
			
			ПараметрыОтбора = Новый Структура("СвойствоТип, СвойствоID",
				РеквизитОбъектаДО.ДопРеквизитТип,
				РеквизитОбъектаДО.ДопРеквизитID);
			МассивСтрокДопРеквизитов = Форма.Свойства.НайтиСтроки(ПараметрыОтбора);
			Если МассивСтрокДопРеквизитов.Количество() > 0 Тогда
				Элемент = МассивСтрокДопРеквизитов[0];
			Иначе
				// Доп. реквизит принадлежит конкретному виду документа
				Элемент = Форма.Свойства.Добавить();
				Элемент.Свойство = РеквизитОбъектаДО.Представление;
				Элемент.СвойствоТип = РеквизитОбъектаДО.ДопРеквизитТип;
				Элемент.СвойствоID = РеквизитОбъектаДО.ДопРеквизитID;
				Элемент.ЗаполнятьОбязательно = РеквизитОбъектаДО.ОбязательноеЗаполнение;
				Элемент.СписокДоступныхТипов = РеквизитОбъектаДО.Тип;
			КонецЕсли;
			
			Если ПравилоРеквизита.Вариант = ВариантРеквизит() Тогда
				
				ИмяРеквизитаОбъектаИС = ПравилоРеквизита.ИмяРеквизитаОбъектаИС;
				Значение = Неопределено;
				
				Если ИмяРеквизитаОбъектаИС = "Представление" Тогда
					
					Значение = Строка(ИсточникОбъект);
					
				ИначеЕсли Найти(ИмяРеквизитаОбъектаИС, ".") > 0 И Не ПравилоРеквизита.ЭтоДополнительныйРеквизитИС Тогда
					
					Значение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, ИмяРеквизитаОбъектаИС);
					
				ИначеЕсли ПравилоРеквизита.ЭтоДополнительныйРеквизитИС Тогда
					
					ТекстЗапроса =
						"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
						|	ДополнительныеРеквизиты.Значение КАК Значение
						|ИЗ
						|	&ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
						|ГДЕ
						|	ДополнительныеРеквизиты.Ссылка = &Источник
						|	И ДополнительныеРеквизиты.Свойство = &Свойство";
					ТекстЗапроса = СтрЗаменить(
						ТекстЗапроса,
						"&ДополнительныеРеквизиты",
						МетаданныеИсточника.ПолноеИмя() +"." + "ДополнительныеРеквизиты");
					
					Запрос = Новый Запрос(ТекстЗапроса);
					Запрос.УстановитьПараметр("Источник", Источник);
					Запрос.УстановитьПараметр("Свойство", ПравилоРеквизита.ДополнительныйРеквизитИС);
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						Значение = Выборка.Значение;
					КонецЕсли;
					
				Иначе // обычный реквизит объекта
					
					Попытка
						
						Значение = ИсточникОбъект[ИмяРеквизитаОбъектаИС];
						
					Исключение // в случае отсутствия реквизита выдадим понятное сообщение об ошибке
						
						Инфо = ИнформацияОбОшибке();
						Если МетаданныеИсточника.Реквизиты.Найти(ИмяРеквизитаОбъектаИС) = Неопределено
							И ИмяРеквизитаОбъектаИС <> "Представление" Тогда
							Найден = Ложь;
							Для Каждого СтандартныйРеквизит Из МетаданныеИсточника.СтандартныеРеквизиты Цикл
								Если СтандартныйРеквизит.Имя = ИмяРеквизитаОбъектаИС Тогда
									Найден = Истина;
									Прервать;
								КонецЕсли;
							КонецЦикла;
							Если Не Найден Тогда
								ТекстСообщения = СтрШаблон(
									НСтр("ru = 'Не найден реквизит %1, указанный в правиле интеграции с 1С:Документооборотом.
										|Проверьте правило интеграции или обратитесь к администратору.'",
										ОбщегоНазначения.КодОсновногоЯзыка()),
									ИмяРеквизитаОбъектаИС);
								ЗаписьЖурналаРегистрации(
									ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИмяСобытияЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Ошибка,,
									ДанныеПравила.Ссылка,
									ТекстСообщения);
								ВызватьИсключение ТекстСообщения;
							КонецЕсли;
						КонецЕсли;
						
						// Иная ошибка.
						ВызватьИсключение ПодробноеПредставлениеОшибки(Инфо);
						
					КонецПопытки;
					
				КонецЕсли;
				
				Если ЭтоПримитивныйТип(ТипРеквизитаДО) Тогда
					Элемент["Значение"] = ПривестиЗначениеКПримитивномуТипу(ТипРеквизитаДО, Значение);
				ИначеЕсли ЗначениеЗаполнено(Значение) Тогда
					Элемент["Значение"] = Строка(Значение);
					Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЕстьУникальныйИдентификатор(Значение) Тогда
						Элемент["ЗначениеID"] = Строка(Значение.УникальныйИдентификатор());
						Элемент["ЗначениеТип"] = Значение.Метаданные().ПолноеИмя();
					ИначеЕсли ЭтоПеречисление(Значение) Тогда
						Элемент["ЗначениеID"] = ОбщегоНазначения.ИмяЗначенияПеречисления(Значение);
						Элемент["ЗначениеТип"] = Значение.Метаданные().ПолноеИмя();
					Иначе
						Элемент["ЗначениеID"] = Строка(Значение);
						Элемент["ЗначениеТип"] = ТипРеквизитаДО;
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантЗначение() Тогда
				
				Элемент["Значение"] = ПравилоРеквизита.ЗначениеРеквизитаДО;
				Элемент["ЗначениеID"]  = ПравилоРеквизита.ЗначениеРеквизитаДОID;
				Элемент["ЗначениеТип"] = ПравилоРеквизита.ЗначениеРеквизитаДОТип;
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантВыражение() Тогда
				
				Параметры = Новый Структура;
				Параметры.Вставить("Источник", ИсточникОбъект);
				Параметры.Вставить("Результат", Неопределено);
				Параметры.Вставить("РезультатID", Неопределено);
				Параметры.Вставить("РезультатТип", Неопределено);
				
				ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ПравилоРеквизита.ВычисляемоеВыражение, Параметры);
				
				Если ЭтоПримитивныйТип(ТипРеквизитаДО) Тогда
					Элемент["Значение"] = ПривестиЗначениеКПримитивномуТипу(ТипРеквизитаДО, Параметры.Результат);
				ИначеЕсли ЗначениеЗаполнено(Параметры.Результат) Тогда
					Элемент["Значение"] = Строка(Параметры.Результат);
					Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЕстьУникальныйИдентификатор(Параметры.Результат) Тогда
						Элемент["ЗначениеID"] = Строка(Параметры.Результат.УникальныйИдентификатор());
						Элемент["ЗначениеТип"] = Параметры.Результат.Метаданные().ПолноеИмя();
					ИначеЕсли ЭтоПеречисление(Параметры.Результат) Тогда
						Элемент["ЗначениеID"] = ОбщегоНазначения.ИмяЗначенияПеречисления(Параметры.Результат);
						Элемент["ЗначениеТип"] = Параметры.Результат.Метаданные().ПолноеИмя();
					ИначеЕсли ЗначениеЗаполнено(Параметры.РезультатID) И ЗначениеЗаполнено(Параметры.РезультатТип) Тогда
						Элемент["ЗначениеID"] = Параметры.РезультатID;
						Элемент["ЗначениеТип"] = Параметры.РезультатТип;
					Иначе // примитивный тип на стороне ИС
						Элемент["ЗначениеТип"] = "Строка";
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.ПометкаУдаления = ИсточникОбъект.ПометкаУдаления;
	
	КонтрольОтправкиФайлов = ИнтеграцияС1СДокументооборотБазоваяФункциональность.КонтрольОтправкиФайлов();
	
	Если ДанныеПравила.ТипОбъектаДО <> "DMCorrespondent" Тогда
		ПечатныеФормыКСозданию = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПечатныеФормыКСозданию(
			Источник,
			ИнтеграцияС1СДокументооборот.ПрисоединяемыеПечатныеФормы(ДанныеПравила.ПрисоединяемыеПечатныеФормы),
			ДанныеПравила.ТипФайловСохраненияПечатныхФорм,
			КонтрольОтправкиФайлов,
			Ложь,
			Форма.УникальныйИдентификатор);
		Для Каждого ПараметрыСоздания Из ПечатныеФормыКСозданию Цикл
			СтрокаФайлов = Форма.Файлы.Добавить();
			СтрокаФайлов.АдресВременногоХранилищаФайла = ПараметрыСоздания.АдресВременногоХранилищаФайла;
			СтрокаФайлов.Наименование = ПараметрыСоздания.ТекущийФайл.Наименование;
			СтрокаФайлов.Расширение = ПараметрыСоздания.ТекущийФайл.Расширение;
			СтрокаФайлов.ИндексКартинки =
				ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИндексПиктограммыФайла(
					СтрокаФайлов.Расширение);
			СтрокаФайлов.ДатаСоздания = ПараметрыСоздания.ТекущийФайл.ДатаМодификации;
			СтрокаФайлов.ДатаМодификацииУниверсальная = ПараметрыСоздания.ТекущийФайл.ДатаМодификацииУниверсальная;
			СтрокаФайлов.Размер = ПараметрыСоздания.ТекущийФайл.Размер;
			СтрокаФайлов.НомерКартинкиПодписанЗашифрован = -1;
		КонецЦикла;
	КонецЕсли;
	
	Форма.КонтрольОтправкиФайлов = Новый ХранилищеЗначения(КонтрольОтправкиФайлов);
	
	// Заявки на оплату.
	Если ЯвляетсяЗаявкойНаОплату Тогда
		ЗаполнитьДанныеЗаявкиНаОплату(Форма);
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриЗаполненииФормыОбъектаДОПоОбъектуИС(Источник,
		Форма,
		Правило.Ссылка);
	
КонецПроцедуры

// Заполняет структуру реквизитов объекта ДО по объекту ИС и указанному правилу.
//
// Параметры:
//   Источник - ЛюбаяСсылка - ссылка на исходный объект ИС.
//   СтруктураРеквизитов - Структура - предварительно подготовленная по данным объекта ИС структура реквизитов:
//     * Свойства - ТаблицаЗначений:
//         ** СписокДоступныхТипов - СписокЗначений
//   Правило - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом, Структура - применяемое правило.
//   КонтрольОтправкиФайлов - ТаблицаЗначений - см. ИнтеграцияС1СДокументооборотБазоваяФункциональность.КонтрольОтправкиФайлов
//   ТолькоОбновление - Булево - Истина, если нужно заполнять только автоматически обновляемое.
//   НеОбновлятьПоПравилам - Массив из Строка - неявно возвращаемое значение, содержит имена реквизитов объекта ДО,
//     которые не указаны в правилах интеграции, или для которых в правилах не стоит признак "Обновлять".
//
Процедура ЗаполнитьСтруктуруРеквизитовОбъектаДОПоПравилу(Источник, СтруктураРеквизитов, Правило,
		КонтрольОтправкиФайлов, ТолькоОбновление = Ложь, НеОбновлятьПоПравилам = Неопределено) Экспорт
	
	Если ТипЗнч(Правило) = Тип("СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом") Тогда
		ДанныеПравила = ДанныеПравилаДляВыгрузки(Правило);
	Иначе
		ДанныеПравила = Правило;
	КонецЕсли;
	
	// Определим вид заполняемого документа и получим описание его реквизитов.
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ИмяРеквизитаОбъектаДО", "documentType");
	ПараметрыОтбора.Вставить("ЭтоДополнительныйРеквизитДО", Ложь);
	ПараметрыОтбора.Вставить("Вариант", ВариантЗначение());
	
	// Вид документа может быть задан явно и перекрывать шаблон.
	СтрокиВидДокумента = ДанныеПравила.ПравилаЗаполненияРеквизитовДО.НайтиСтроки(ПараметрыОтбора);
	Если СтрокиВидДокумента.Количество() > 0 Тогда
		
		СтрокаВидДокумента = СтрокиВидДокумента[0];
		
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("documentType",
			Новый Структура("ID, type, name",
			СтрокаВидДокумента.ЗначениеРеквизитаДОID,
			СтрокаВидДокумента.ЗначениеРеквизитаДОТип,
			СтрокаВидДокумента.ЗначениеРеквизитаДО));
		
	Иначе // Вид документа может быть задан в шаблоне.
		
		ПараметрыОтбора.Вариант = ВариантШаблон();
		СтрокиВидДокумента = ДанныеПравила.ПравилаЗаполненияРеквизитовДО.НайтиСтроки(ПараметрыОтбора);
		Если СтрокиВидДокумента.Количество() > 0 Тогда
			
			СтрокаВидДокумента = СтрокиВидДокумента[0];
			
			ПараметрыЗаполнения = Новый Структура;
			ПараметрыЗаполнения.Вставить("documentType",
				Новый Структура("ID, type, name",
				СтрокаВидДокумента.ШаблонID,
				СтрокаВидДокумента.ШаблонТип,
				СтрокаВидДокумента.ШаблонЗначение));
			
		КонецЕсли;
		
	КонецЕсли;
	
	НастройкиВида = Неопределено;
	
	РеквизитыОбъектаДО = ПолучитьРеквизитыОбъектаДО(
		ДанныеПравила.ТипОбъектаДО,
		ПараметрыЗаполнения,
		НастройкиВида);
	
	СоответствиеСвойствXDTOиРеквизитов =
		СоответствиеСвойствXDTOиРеквизитовФормыОбъектаДО(ДанныеПравила.ТипОбъектаДО);
	ИсточникОбъект = Источник.ПолучитьОбъект();
	МетаданныеИсточника = Источник.Метаданные();
	
	ЯвляетсяЗаявкойНаОплату = Ложь;
	Если НастройкиВида <> Неопределено
			И ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоСуществует(НастройкиВида, "isAPaymentRequest")
			И НастройкиВида.isAPaymentRequest = Истина Тогда
		ЯвляетсяЗаявкойНаОплату = Истина;
	КонецЕсли;
	Если ЯвляетсяЗаявкойНаОплату Тогда
		СоответствиеСвойствXDTOиРеквизитов.Вставить("recipient", "Получатель");
	КонецЕсли;
	
	Для Каждого РеквизитОбъектаДО Из РеквизитыОбъектаДО Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ЭтоДополнительныйРеквизитДО", РеквизитОбъектаДО.ДопРеквизит);
		Если РеквизитОбъектаДО.ДопРеквизит Тогда
			ПараметрыОтбора.Вставить("ДополнительныйРеквизитДОID", РеквизитОбъектаДО.ДопРеквизитID);
			ПараметрыОтбора.Вставить("ДополнительныйРеквизитДОТип", РеквизитОбъектаДО.ДопРеквизитТип);
			ПараметрыОтбора.Вставить("ЭтоТаблица", Ложь);
			ПараметрыОтбора.Вставить("Таблица", "");
		Иначе
			ПараметрыОтбора.Вставить("ИмяРеквизитаОбъектаДО", РеквизитОбъектаДО.Имя);
			ПараметрыОтбора.Вставить("ЭтоТаблица", РеквизитОбъектаДО.ЭтоТаблица);
			ПараметрыОтбора.Вставить("Таблица", РеквизитОбъектаДО.Таблица);
		КонецЕсли;
		Если ТолькоОбновление Тогда
			ПараметрыОтбора.Вставить("Обновлять", Истина);
		КонецЕсли;
		
		НайденныеПравилаРеквизитов = ДанныеПравила.ПравилаЗаполненияРеквизитовДО.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеПравилаРеквизитов.Количество() = 0 Тогда
			Если РеквизитОбъектаДО.Таблица = ""
					И Не РеквизитОбъектаДО.ДопРеквизит
					И ТипЗнч(НеОбновлятьПоПравилам) = Тип("Массив") Тогда
				Если РеквизитОбъектаДО.ЭтоТаблица Тогда
					ИмяЭлемента = Лев(РеквизитОбъектаДО.Имя, СтрНайти(РеквизитОбъектаДО.Имя, ".") - 1);
				Иначе
					ИмяЭлемента = РеквизитОбъектаДО.Имя;
				КонецЕсли;
				НеОбновлятьПоПравилам.Добавить(ИмяЭлемента);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		ПравилоРеквизита = НайденныеПравилаРеквизитов[0]; // СправочникТабличнаяЧастьСтрока.ПравилаИнтеграцииС1СДокументооборотом.ПравилаЗаполненияРеквизитовДО
		
		Если Не РеквизитОбъектаДО.ДопРеквизит Тогда
			
			Если ЗначениеЗаполнено(РеквизитОбъектаДО.Таблица) Тогда
				РазделеннаяСтрока = СтрРазделить(РеквизитОбъектаДО.Таблица, ".", Ложь);
				СвойствоВладелец = СоответствиеСвойствXDTOиРеквизитов[РазделеннаяСтрока[0]];
				ОписаниеТаблицы = СвойствоВладелец[РазделеннаяСтрока[1]];
				ИмяТаблицы = ОписаниеТаблицы[0];
				ИмяРеквизита = ОписаниеТаблицы[1][РеквизитОбъектаДО.Имя];
			ИначеЕсли РеквизитОбъектаДО.ЭтоТаблица Тогда
				РазделеннаяСтрока = СтрРазделить(РеквизитОбъектаДО.Имя, ".", Ложь);
				СвойствоВладелец = СоответствиеСвойствXDTOиРеквизитов[РазделеннаяСтрока[0]];
				ОписаниеТаблицы = СвойствоВладелец[РазделеннаяСтрока[1]];
				ИмяТаблицы = ОписаниеТаблицы[0];
				ИмяРеквизита = "";
			Иначе
				ИмяТаблицы = "";
				ИмяРеквизита = СоответствиеСвойствXDTOиРеквизитов[РеквизитОбъектаДО.Имя];
			КонецЕсли;
			
			Если РеквизитОбъектаДО.ЭтоТаблица Тогда
				
				СтруктураРеквизитов[ИмяТаблицы].Очистить();
				
				Если ПравилоРеквизита.Вариант = ВариантРеквизит() Тогда // наполним пустыми строками
					
					ТабличнаяЧасть = ИсточникОбъект[ПравилоРеквизита.ИмяРеквизитаОбъектаИС];
					Для Счетчик = 1 По ТабличнаяЧасть.Количество() Цикл
						СтруктураРеквизитов[ИмяТаблицы].Добавить();
					КонецЦикла;
					
				ИначеЕсли ПравилоРеквизита.Вариант = ВариантРеквизитТаблицы() Тогда // добавим единственную строку
					
					СтруктураРеквизитов[ИмяТаблицы].Добавить();
					
				ИначеЕсли ПравилоРеквизита.Вариант = ВариантВыражение() Тогда // загрузим из результата-таблицы
					
					Параметры = Новый Структура;
					Параметры.Вставить("Источник", ИсточникОбъект);
					Параметры.Вставить("Результат", Неопределено);
					
					ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ПравилоРеквизита.ВычисляемоеВыражение, Параметры);
					
					Если ТипЗнч(Параметры.Результат) = Тип("ТаблицаЗначений") Тогда
						
						КолонкиТаблицы = Параметры.Результат.Колонки; // КоллекцияКолонокТаблицыЗначений
						
						Для Каждого СтрокаРезультата Из Параметры.Результат Цикл
							Таблица = СтруктураРеквизитов[ИмяТаблицы]; // ТаблицаЗначений
							Элемент = Таблица.Добавить();
							Для Каждого КолонкаРезультата Из КолонкиТаблицы Цикл
								Если Таблица.Колонки.Найти(КолонкаРезультата.Имя) = Неопределено Тогда
									Продолжить;
								КонецЕсли;
								ЗначениеСвойства = СтрокаРезультата[КолонкаРезультата.Имя];
								Элемент[КолонкаРезультата.Имя] = ЗначениеСвойства;
								ЭтоСсылочныйТип = 
									Таблица.Колонки.Найти(КолонкаРезультата.Имя + "ID") <> Неопределено
									И Таблица.Колонки.Найти(КолонкаРезультата.Имя + "Тип") <> Неопределено;
								Если ЭтоСсылочныйТип Тогда
									Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЕстьУникальныйИдентификатор(ЗначениеСвойства) Тогда
										Элемент[КолонкаРезультата.Имя + "ID"] = Строка(ЗначениеСвойства.УникальныйИдентификатор());
										Элемент[КолонкаРезультата.Имя + "Тип"] = ЗначениеСвойства.Метаданные().ПолноеИмя();
									ИначеЕсли ЭтоПеречисление(ЗначениеСвойства) Тогда
										Элемент[КолонкаРезультата.Имя + "ID"] =
											ОбщегоНазначения.ИмяЗначенияПеречисления(ЗначениеСвойства);
										Элемент[КолонкаРезультата.Имя + "Тип"] = ЗначениеСвойства.Метаданные().ПолноеИмя();
									Иначе
										Элемент[КолонкаРезультата.Имя + "ID"] = Строка(ЗначениеСвойства);
										Элемент[КолонкаРезультата.Имя + "Тип"] = "Строка";
									КонецЕсли;
								КонецЕсли;
							КонецЦикла;
						КонецЦикла;
						
					КонецЕсли;
					
				Иначе // значениями, указанными для реквизитов: добавим единственную строку
					
					СтруктураРеквизитов[ИмяТаблицы].Добавить();
					
				КонецЕсли;
				
				Продолжить;
				
			КонецЕсли; // это таблица
			
			ТипРеквизитаДО = РеквизитОбъектаДО.Тип[0].Значение;
			
			Если ПравилоРеквизита.Вариант = ВариантРеквизит() Тогда
				
				ИмяРеквизитаОбъектаИС = ПравилоРеквизита.ИмяРеквизитаОбъектаИС;
				
				Если ЗначениеЗаполнено(ПравилоРеквизита.Таблица) Тогда // реквизит табличной части
					
					РазделеннаяСтрока = СтрРазделить(ИмяРеквизитаОбъектаИС, ".");
					Если РазделеннаяСтрока.Количество() = 2 Тогда
						ТабличнаяЧасть = ИсточникОбъект[РазделеннаяСтрока[0]];
						Значения = ТабличнаяЧасть.ВыгрузитьКолонку(РазделеннаяСтрока[1]);
					Иначе
						Значения = Новый Массив;
						Значения.Добавить(ИсточникОбъект[РазделеннаяСтрока[0]]);
					КонецЕсли;
					
				Иначе // обычный или дополнительный реквизит
					
					Значения = Новый Массив;
					
					Если ИмяРеквизитаОбъектаИС = "Представление" Тогда
						
						Значения.Добавить(Строка(ИсточникОбъект));
						
					ИначеЕсли Найти(ИмяРеквизитаОбъектаИС, ".") > 0
							И Не ПравилоРеквизита.ЭтоДополнительныйРеквизитИС Тогда
						
						ЗначениеРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, ИмяРеквизитаОбъектаИС);
						Значения.Добавить(ЗначениеРеквизита);
						
					ИначеЕсли ПравилоРеквизита.ЭтоДополнительныйРеквизитИС Тогда
						
						ТекстЗапроса =
							"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
							|	ДополнительныеРеквизиты.Значение КАК ЗначениеРеквизита
							|ИЗ
							|	&ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
							|ГДЕ
							|	ДополнительныеРеквизиты.Ссылка = &Источник
							|	И ДополнительныеРеквизиты.Свойство = &Свойство";
						ТекстЗапроса = СтрЗаменить(
							ТекстЗапроса,
							"&ДополнительныеРеквизиты",
							МетаданныеИсточника.ПолноеИмя() +"." + "ДополнительныеРеквизиты");
						
						Запрос = Новый Запрос(ТекстЗапроса);
						Запрос.УстановитьПараметр("Источник", Источник);
						Запрос.УстановитьПараметр("Свойство", ПравилоРеквизита.ДополнительныйРеквизитИС);
						Значения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЗначениеРеквизита");
						
					Иначе // обычный реквизит объекта
						
						Попытка
							
							Значения.Добавить(ИсточникОбъект[ИмяРеквизитаОбъектаИС]);
							
						Исключение // в случае отсутствия реквизита выдадим понятное сообщение об ошибке
							
							Инфо = ИнформацияОбОшибке();
							Если МетаданныеИсточника.Реквизиты.Найти(ИмяРеквизитаОбъектаИС) = Неопределено
								И ИмяРеквизитаОбъектаИС <> "Представление" Тогда
								Найден = Ложь;
								Для Каждого СтандартныйРеквизит Из МетаданныеИсточника.СтандартныеРеквизиты Цикл
									Если СтандартныйРеквизит.Имя = ИмяРеквизитаОбъектаИС Тогда
										Найден = Истина;
										Прервать;
									КонецЕсли;
								КонецЦикла;
								Если Не Найден Тогда
									ТекстСообщения = СтрШаблон(
										НСтр("ru = 'Не найден реквизит %1, указанный в правиле интеграции с 1С:Документооборотом.
											|Проверьте правило интеграции или обратитесь к администратору.'",
											ОбщегоНазначения.КодОсновногоЯзыка()),
										ПравилоРеквизита.ИмяРеквизитаОбъектаИС);
									ЗаписьЖурналаРегистрации(
										ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИмяСобытияЖурналаРегистрации(),
										УровеньЖурналаРегистрации.Ошибка,,
										ДанныеПравила.Ссылка,
										ТекстСообщения);
									ВызватьИсключение ТекстСообщения;
								КонецЕсли;
							КонецЕсли;
							
							// Иная ошибка.
							ВызватьИсключение ПодробноеПредставлениеОшибки(Инфо);
							
						КонецПопытки;
						
					КонецЕсли;
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
					Таблица = СтруктураРеквизитов[ИмяТаблицы];
				Иначе
					Таблица = Новый Массив;
					Таблица.Добавить(СтруктураРеквизитов);
				КонецЕсли;
				
				Для НомерЗначения = 0 По Значения.Количество() - 1 Цикл
					
					Значение = Значения[НомерЗначения];
					
					СтандартнаяОбработка = Истина;
					ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриЗаполненииСвойстваОбъектаXDTOПоЗначению(
						Значение,
						Таблица[НомерЗначения],
						ДанныеПравила.ТипОбъектаДО,
						ИмяРеквизита,
						ТипРеквизитаДО,
						СтандартнаяОбработка);
					
					Если Не СтандартнаяОбработка Тогда
						Продолжить;
					КонецЕсли;
					
					Для НомерЭлемента = 0 По Таблица.Количество() - 1 Цикл
						
						Если Значения.Количество() > 1 И НомерЗначения <> НомерЭлемента Тогда
							Продолжить;
						КонецЕсли;
						
						Элемент = Таблица[НомерЭлемента];
						
						Если ЭтоПримитивныйТип(ТипРеквизитаДО) Тогда
							Элемент[ИмяРеквизита] = ПривестиЗначениеКПримитивномуТипу(ТипРеквизитаДО, Значение);
						ИначеЕсли ЗначениеЗаполнено(Значение) Тогда // ссылочный тип на стороне ДО
							Элемент[ИмяРеквизита] = Строка(Значение);
							Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЕстьУникальныйИдентификатор(Значение) Тогда
								Элемент[ИмяРеквизита + "ID"] = Строка(Значение.УникальныйИдентификатор());
								Элемент[ИмяРеквизита + "Тип"] = Значение.Метаданные().ПолноеИмя();
							ИначеЕсли ЭтоПеречисление(Значение) Тогда
								Элемент[ИмяРеквизита + "ID"] = ОбщегоНазначения.ИмяЗначенияПеречисления(Значение);
								Элемент[ИмяРеквизита + "Тип"] = Значение.Метаданные().ПолноеИмя();
							Иначе // примитивный тип на стороне ИС
								Элемент[ИмяРеквизита + "ID"] = Строка(Значение);
								Элемент[ИмяРеквизита + "Тип"] = "Строка";
							КонецЕсли;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантЗначение() Тогда
				
				Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
					Таблица = СтруктураРеквизитов[ИмяТаблицы];
				Иначе
					Таблица = Новый Массив;
					Таблица.Добавить(СтруктураРеквизитов);
				КонецЕсли;
				
				Для Каждого Элемент Из Таблица Цикл
					
					Если ЭтоПримитивныйТип(ТипРеквизитаДО) Тогда
						Элемент[ИмяРеквизита] = ПривестиЗначениеКПримитивномуТипу(ТипРеквизитаДО, ПравилоРеквизита.ЗначениеРеквизитаДО);
					Иначе // ссылочный тип на стороне ДО
						Элемент[ИмяРеквизита] = ПравилоРеквизита.ЗначениеРеквизитаДО;
						Элемент[ИмяРеквизита + "ID"] = ПравилоРеквизита.ЗначениеРеквизитаДОID;
						Элемент[ИмяРеквизита + "Тип"] = ПравилоРеквизита.ЗначениеРеквизитаДОТип;
					КонецЕсли;
					
				КонецЦикла;
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантВыражение() Тогда
				
				Параметры = Новый Структура;
				Параметры.Вставить("Источник", ИсточникОбъект);
				Параметры.Вставить("Результат", Неопределено);
				Параметры.Вставить("РезультатID", Неопределено);
				Параметры.Вставить("РезультатТип", Неопределено);
				
				ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ПравилоРеквизита.ВычисляемоеВыражение, Параметры);
				
				СтандартнаяОбработка = Истина;
				ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриЗаполненииСвойстваОбъектаXDTOПоЗначению(
					Параметры.Результат,
					СтруктураРеквизитов,
					ДанныеПравила.ТипОбъектаДО,
					ИмяРеквизита,
					ТипРеквизитаДО,
					СтандартнаяОбработка);
				
				Если СтандартнаяОбработка Тогда
					
					Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
						Таблица = СтруктураРеквизитов[ИмяТаблицы];
					Иначе
						Таблица = Новый Массив;
						Таблица.Добавить(СтруктураРеквизитов);
					КонецЕсли;
					
					Для Каждого Элемент Из Таблица Цикл
						
						Если ЭтоПримитивныйТип(ТипРеквизитаДО) Тогда
							Элемент[ИмяРеквизита] = ПривестиЗначениеКПримитивномуТипу(ТипРеквизитаДО, Параметры.Результат);
						ИначеЕсли ЗначениеЗаполнено(Параметры.Результат) Тогда // ссылочный тип на стороне ДО
							Элемент[ИмяРеквизита] = Строка(Параметры.Результат);
							Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЕстьУникальныйИдентификатор(Параметры.Результат) Тогда
								Элемент[ИмяРеквизита + "ID"] = Строка(Параметры.Результат.УникальныйИдентификатор());
								Элемент[ИмяРеквизита + "Тип"] = Параметры.Результат.Метаданные().ПолноеИмя();
							ИначеЕсли ЭтоПеречисление(Параметры.Результат) Тогда
								Элемент[ИмяРеквизита + "ID"] =
									ОбщегоНазначения.ИмяЗначенияПеречисления(Параметры.Результат);
								Элемент[ИмяРеквизита + "Тип"] = Параметры.Результат.Метаданные().ПолноеИмя();
							ИначеЕсли ЗначениеЗаполнено(Параметры.РезультатID) И ЗначениеЗаполнено(Параметры.РезультатТип) Тогда
								Элемент[ИмяРеквизита + "ID"] = Параметры.РезультатID;
								Элемент[ИмяРеквизита + "Тип"] = Параметры.РезультатТип;
							Иначе // примитивный тип на стороне ИС
								Элемент[ИмяРеквизита + "Тип"] = "Строка";
							КонецЕсли;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе // дополнительный реквизит на стороне ДО
			
			ТипРеквизитаДО = РеквизитОбъектаДО.Тип[0].Значение;
			
			Таблица = СтруктураРеквизитов.Свойства;
			Элемент = Таблица.Добавить();
			Элемент.СвойствоТип = РеквизитОбъектаДО.ДопРеквизитТип;
			Элемент.СвойствоID = РеквизитОбъектаДО.ДопРеквизитID;
			Элемент.Свойство = "";
			
			Для Каждого Тип Из РеквизитОбъектаДО.Тип Цикл
				ДанныеОТипе = Новый Структура("xdtoClassName, presentation");
				ДанныеОТипе.xdtoClassName = Тип.Значение;
				ДанныеОТипе.presentation = Тип.Представление;
				Элемент.СписокДоступныхТипов.Добавить(ДанныеОТипе);
			КонецЦикла;
			
			Если ПравилоРеквизита.Вариант = ВариантРеквизит() Тогда
				
				ИмяРеквизитаОбъектаИС = ПравилоРеквизита.ИмяРеквизитаОбъектаИС;
				Значение = Неопределено;
				
				// Получим значение реквизита объекта ИС.
				Если ИмяРеквизитаОбъектаИС = "Представление" Тогда
					
					Значение = Строка(ИсточникОбъект);
					
				ИначеЕсли Найти(ИмяРеквизитаОбъектаИС, ".") > 0 И Не ПравилоРеквизита.ЭтоДополнительныйРеквизитИС Тогда
					
					Значение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, ИмяРеквизитаОбъектаИС);
					
				ИначеЕсли ПравилоРеквизита.ЭтоДополнительныйРеквизитИС Тогда
					
					ТекстЗапроса =
						"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
						|	ДополнительныеРеквизиты.Значение КАК Значение
						|ИЗ
						|	&ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
						|ГДЕ
						|	ДополнительныеРеквизиты.Ссылка = &Источник
						|	И ДополнительныеРеквизиты.Свойство = &Свойство";
					ТекстЗапроса = СтрЗаменить(
						ТекстЗапроса,
						"&ДополнительныеРеквизиты",
						МетаданныеИсточника.ПолноеИмя() +"." + "ДополнительныеРеквизиты");
					
					Запрос = Новый Запрос(ТекстЗапроса);
					Запрос.УстановитьПараметр("Источник", Источник);
					Запрос.УстановитьПараметр("Свойство", ПравилоРеквизита.ДополнительныйРеквизитИС);
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						Значение = Выборка.Значение;
					КонецЕсли;
					
				Иначе // обычный реквизит объекта
					
					Попытка
						
						Значение = ИсточникОбъект[ИмяРеквизитаОбъектаИС];
						
					Исключение // в случае отсутствия реквизита выдадим понятное сообщение об ошибке
						
						Инфо = ИнформацияОбОшибке();
						Если МетаданныеИсточника.Реквизиты.Найти(ИмяРеквизитаОбъектаИС) = Неопределено
							И ИмяРеквизитаОбъектаИС <> "Представление" Тогда
							Найден = Ложь;
							Для Каждого СтандартныйРеквизит Из МетаданныеИсточника.СтандартныеРеквизиты Цикл
								Если СтандартныйРеквизит.Имя = ИмяРеквизитаОбъектаИС Тогда
									Найден = Истина;
									Прервать;
								КонецЕсли;
							КонецЦикла;
							Если Не Найден Тогда
								ТекстСообщения = СтрШаблон(
									НСтр("ru = 'Не найден реквизит %1, указанный в правиле интеграции с 1С:Документооборотом.
										|Проверьте правило интеграции или обратитесь к администратору.'",
										ОбщегоНазначения.КодОсновногоЯзыка()),
									ИмяРеквизитаОбъектаИС);
								ЗаписьЖурналаРегистрации(
									ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИмяСобытияЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Ошибка,,
									ДанныеПравила.Ссылка,
									ТекстСообщения);
								ВызватьИсключение ТекстСообщения;
							КонецЕсли;
						КонецЕсли;
						
						// Иная ошибка.
						ВызватьИсключение ПодробноеПредставлениеОшибки(Инфо);
						
					КонецПопытки;
					
				КонецЕсли;
				
				Если ЭтоПримитивныйТип(ТипРеквизитаДО) Тогда
					Элемент["Значение"] = ПривестиЗначениеКПримитивномуТипу(ТипРеквизитаДО, Значение);
				ИначеЕсли ЗначениеЗаполнено(Значение) Тогда // ссылочный тип на стороне ДО
					Элемент["Значение"] = Строка(Значение);
					Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЕстьУникальныйИдентификатор(Значение) Тогда
						Элемент["ЗначениеID"] = Строка(Значение.УникальныйИдентификатор());
						Элемент["ЗначениеТип"] = Значение.Метаданные().ПолноеИмя();
					ИначеЕсли ЭтоПеречисление(Значение) Тогда
						Элемент["ЗначениеID"] = ОбщегоНазначения.ИмяЗначенияПеречисления(Значение);
						Элемент["ЗначениеТип"] = Значение.Метаданные().ПолноеИмя();
					Иначе // примитивный тип на стороне ИС
						Элемент["ЗначениеID"] = Строка(Значение);
						Элемент["ЗначениеТип"] = ТипРеквизитаДО;
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантЗначение() Тогда
				
				Элемент["Значение"] = ПравилоРеквизита.ЗначениеРеквизитаДО;
				Элемент["ЗначениеID"] = ПравилоРеквизита.ЗначениеРеквизитаДОID;
				Элемент["ЗначениеТип"] = ПравилоРеквизита.ЗначениеРеквизитаДОТип;
				
			ИначеЕсли ПравилоРеквизита.Вариант = ВариантВыражение() Тогда
				
				Параметры = Новый Структура;
				Параметры.Вставить("Источник", ИсточникОбъект);
				Параметры.Вставить("Результат", Неопределено);
				Параметры.Вставить("РезультатID", Неопределено);
				Параметры.Вставить("РезультатТип", Неопределено);
				
				ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ПравилоРеквизита.ВычисляемоеВыражение, Параметры);
				
				СтандартнаяОбработка = Истина;
				ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриЗаполненииСвойстваОбъектаXDTOПоЗначению(
					Параметры.Результат,
					СтруктураРеквизитов,
					ДанныеПравила.ТипОбъектаДО,
					ИмяРеквизита,
					ТипРеквизитаДО,
					СтандартнаяОбработка);
				
				Если СтандартнаяОбработка Тогда
				
					Если ЭтоПримитивныйТип(ТипРеквизитаДО) Тогда
						Элемент["Значение"] = ПривестиЗначениеКПримитивномуТипу(ТипРеквизитаДО, Параметры.Результат);
					ИначеЕсли ЗначениеЗаполнено(Параметры.Результат) Тогда // ссылочный тип на стороне ДО
						Элемент["Значение"] = Строка(Параметры.Результат);
						Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЕстьУникальныйИдентификатор(Параметры.Результат) Тогда
							Элемент["ЗначениеID"] = Строка(Параметры.Результат.УникальныйИдентификатор());
							Элемент["ЗначениеТип"] = Параметры.Результат.Метаданные().ПолноеИмя();
						ИначеЕсли ЭтоПеречисление(Параметры.Результат) Тогда
							Элемент["ЗначениеID"] = ОбщегоНазначения.ИмяЗначенияПеречисления(Параметры.Результат);
							Элемент["ЗначениеТип"] = Параметры.Результат.Метаданные().ПолноеИмя();
						ИначеЕсли ЗначениеЗаполнено(Параметры.РезультатID) И ЗначениеЗаполнено(Параметры.РезультатТип) Тогда
							Элемент["ЗначениеID"] = Параметры.РезультатID;
							Элемент["ЗначениеТип"] = Параметры.РезультатТип;
						Иначе // примитивный тип на стороне ИС
							Элемент["ЗначениеТип"] = "Строка";
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтруктураРеквизитов.Свойство("ПометкаУдаления") Тогда
		СтруктураРеквизитов.ПометкаУдаления = Источник.ПометкаУдаления;
	КонецЕсли;
	
	Если ДанныеПравила.ТипОбъектаДО <> "DMCorrespondent" Тогда
		СтруктураРеквизитов.Файлы = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПечатныеФормыКСозданию(
			Источник,
			ИнтеграцияС1СДокументооборот.ПрисоединяемыеПечатныеФормы(ДанныеПравила.ПрисоединяемыеПечатныеФормы),
			ДанныеПравила.ТипФайловСохраненияПечатныхФорм,
			КонтрольОтправкиФайлов,
			ТолькоОбновление);
	КонецЕсли;
	
	// Заявки на оплату.
	Если ЯвляетсяЗаявкойНаОплату Тогда
		ЗаполнитьДанныеЗаявкиНаОплату(СтруктураРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

// Обновляет правила интеграции, использующие шаблон, при его изменении на стороне ДО.
//
// Параметры:
//   ШаблонXDTO - ОбъектXDTO - измененный шаблон.
//
Процедура ОбновитьПравилаПоШаблону(ШаблонXDTO) Экспорт
	
	Если ШаблонXDTO.Свойства().Получить("blockDerivedDocuments") <> Неопределено
			И ШаблонXDTO.Установлено("blockDerivedDocuments") Тогда
		ШаблонЗапрещаетИзменение = ШаблонXDTO.blockDerivedDocuments;
	Иначе
		ШаблонЗапрещаетИзменение = Ложь;
	КонецЕсли;
	
	ЗапросПравила = Новый Запрос(
		"ВЫБРАТЬ
		|	Правила.Ссылка КАК Правило,
		|	Правила.ТипОбъектаДО КАК ТипОбъектаДО
		|ИЗ
		|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК Правила
		|ГДЕ
		|	Правила.ШаблонТип = &ШаблонТип
		|	И Правила.ШаблонID = &ШаблонID
		|	И НЕ Правила.ПометкаУдаления");
	ЗапросПравила.УстановитьПараметр("ШаблонТип", ШаблонXDTO.objectID.type);
	ЗапросПравила.УстановитьПараметр("ШаблонID", ШаблонXDTO.objectID.ID);
	
	ВыборкаПравила = ЗапросПравила.Выполнить().Выбрать();
	Пока ВыборкаПравила.Следующий() Цикл
		
		Если ШаблонXDTO.Свойства().Получить("documentType") = Неопределено
				Или Не ШаблонXDTO.Установлено("documentType")
				Или Не ЗначениеЗаполнено(ШаблонXDTO.documentType.objectID.ID)
				Или Не ЗначениеЗаполнено(ШаблонXDTO.documentType.objectID.type) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("documentType",
			Новый Структура("ID, type, name", 
				ШаблонXDTO.documentType.objectID.ID,
				ШаблонXDTO.documentType.objectID.type,
				ШаблонXDTO.documentType.name));
		
		РеквизитыОбъектаДО = ПолучитьРеквизитыОбъектаДО(
			ВыборкаПравила.ТипОбъектаДО,
			ПараметрыЗаполнения);
		
		Правило = ВыборкаПравила.Правило.ПолучитьОбъект();
		Правило.Заблокировать();
		НужнаЗапись = Ложь;
		
		НомерРеквизита = 0;
		Пока НомерРеквизита <= Правило.ПравилаЗаполненияРеквизитовДО.Количество() - 1 Цикл
			
			ПравилоРеквизита = Правило.ПравилаЗаполненияРеквизитовДО[НомерРеквизита]; // СправочникТабличнаяЧастьСтрока.ПравилаИнтеграцииС1СДокументооборотом.ПравилаЗаполненияРеквизитовДО
			ИмяРеквизита = ПравилоРеквизита.ИмяРеквизитаОбъектаДО;
			Если ИмяРеквизита = "performanceDate" Тогда
				ИмяРеквизитаШаблона = "performanceTerm";
			Иначе
				ИмяРеквизитаШаблона = ИмяРеквизита;
			КонецЕсли;
			
			// Проверим, существует ли определенный в правиле реквизит.
			СтруктураОтбора = Новый Структура("ДопРеквизит",
				ПравилоРеквизита.ЭтоДополнительныйРеквизитДО);
			Если ПравилоРеквизита.ЭтоДополнительныйРеквизитДО Тогда 
				СтруктураОтбора.Вставить("ДопРеквизитID",
					ПравилоРеквизита.ДополнительныйРеквизитДОID);
				СтруктураОтбора.Вставить("ЭтоТаблица", Ложь);
				СтруктураОтбора.Вставить("Таблица", "");
			Иначе
				СтруктураОтбора.Вставить("Имя", ИмяРеквизита);
				СтруктураОтбора.Вставить("ЭтоТаблица", ПравилоРеквизита.ЭтоТаблица);
				СтруктураОтбора.Вставить("Таблица", ПравилоРеквизита.Таблица);
			КонецЕсли;
			
			НайденныеРеквизиты = РеквизитыОбъектаДО.НайтиСтроки(СтруктураОтбора);
			Если НайденныеРеквизиты.Количество() = 0 Тогда
				
				Если Не ПравилоРеквизита.ЭтоДополнительныйРеквизитДО Тогда
					ТекстСообщения = СтрШаблон(
						НСтр("ru = 'Очищено правило заполнения реквизита %1, отсутствующего в документе'",
							ОбщегоНазначения.КодОсновногоЯзыка()),
						ИмяРеквизита);
				Иначе
					ТекстСообщения = НСтр("ru = 'Очищено правило заполнения дополнительного реквизита, отсутствующего в документе'",
						ОбщегоНазначения.КодОсновногоЯзыка());
				КонецЕсли;
				ЗаписьЖурналаРегистрации(
					ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИмяСобытияЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Предупреждение,,
					Правило.Ссылка,
					ТекстСообщения);
					
				НужнаЗапись = Истина;
				Правило.ПравилаЗаполненияРеквизитовДО.Удалить(ПравилоРеквизита);
				
				Продолжить;
				
			КонецЕсли;
			
			Если Не ПравилоРеквизита.ЭтоДополнительныйРеквизитДО Тогда
				Если ШаблонXDTO.Свойства().Получить(ИмяРеквизитаШаблона) <> Неопределено Тогда
					РеквизитШаблона = ШаблонXDTO[ИмяРеквизитаШаблона];
				Иначе
					РеквизитШаблона = Неопределено;
				КонецЕсли;
			Иначе
				Если ШаблонXDTO.Установлено("additionalProperties") Тогда
					РеквизитШаблона = Неопределено;
					Для Каждого ДопРеквизит Из ШаблонXDTO.additionalProperties Цикл
						Если ДопРеквизит.objectID.ID = ПравилоРеквизита.ДополнительныйРеквизитДОID
							И ДопРеквизит.objectID.type = ПравилоРеквизита.ДополнительныйРеквизитДОТип Тогда
								
							Если ДопРеквизит.Установлено("propertySimpleValue") Тогда
								РеквизитШаблона = ДопРеквизит.propertySimpleValue;
							ИначеЕсли ДопРеквизит.Установлено("propertyObjectValue") Тогда
								РеквизитШаблона = ДопРеквизит.propertyObjectValue;
							КонецЕсли;
							Прервать;
							
						КонецЕсли;
					КонецЦикла;
				Иначе
					РеквизитШаблона = Неопределено;
				КонецЕсли;
			КонецЕсли;
			
			// Проверим, не изменилось ли значение, заданное шаблоном.
			Если ПравилоРеквизита.Вариант = ВариантШаблон()
				И РеквизитШаблона = Неопределено Тогда
				
				Если Не ПравилоРеквизита.ЭтоДополнительныйРеквизитДО Тогда
					ТекстСообщения = СтрШаблон(
						НСтр("ru = 'Очищено правило заполнения реквизита %1, отсутствующего в шаблоне'",
							ОбщегоНазначения.КодОсновногоЯзыка()),
						ИмяРеквизита);
				Иначе
					ТекстСообщения = НСтр("ru = 'Очищено правило заполнения дополнительного реквизита, отсутствующего в шаблоне'",
						ОбщегоНазначения.КодОсновногоЯзыка());
				КонецЕсли;
				ЗаписьЖурналаРегистрации(
					ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИмяСобытияЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Предупреждение,,
					Правило.Ссылка,
					ТекстСообщения);
					
				НужнаЗапись = Истина;
				Правило.ПравилаЗаполненияРеквизитовДО.Удалить(ПравилоРеквизита);
				Продолжить;
				
			КонецЕсли;
			
			Если ТипЗнч(РеквизитШаблона) = Тип("ОбъектXDTO") Тогда
				РеквизитШаблонаID = РеквизитШаблона.objectID.ID;
				РеквизитШаблонаТип = РеквизитШаблона.objectID.type;
				РеквизитШаблонаЗначение = РеквизитШаблона.name;
				ЗаполненВШаблоне = ЗначениеЗаполнено(РеквизитШаблонаID);
			Иначе // примитивный тип или Неопределено
				РеквизитШаблонаID = "";
				РеквизитШаблонаТип = "";
				РеквизитШаблонаЗначение = РеквизитШаблона;
				ЗаполненВШаблоне = ЗначениеЗаполнено(РеквизитШаблона);
			КонецЕсли;
			
			// Изменилось ли значение?
			Если (ЗначениеЗаполнено(ПравилоРеквизита.ШаблонID)
					И ПравилоРеквизита.ШаблонID <> РеквизитШаблонаID)
				Или (Не ЗначениеЗаполнено(ПравилоРеквизита.ШаблонID)
					И ПравилоРеквизита.ШаблонЗначение <> РеквизитШаблонаЗначение)
				Или (ПравилоРеквизита.ЗаполненВШаблоне <> ЗаполненВШаблоне) Тогда
					
				НужнаЗапись = Истина;
					
				ПравилоРеквизита.ШаблонЗначение = РеквизитШаблонаЗначение;
				ПравилоРеквизита.ШаблонID = РеквизитШаблонаID;
				ПравилоРеквизита.ШаблонТип = РеквизитШаблонаТип;
				ПравилоРеквизита.ЗаполненВШаблоне = ЗаполненВШаблоне;
				
			КонецЕсли;
			
			// Ключевой реквизит должен быть заполнен.
			Если ПравилоРеквизита.Вариант = ВариантШаблон()
				И ПравилоРеквизита.Ключевой
				И Не ЗаполненВШаблоне Тогда
				
				Если Не ПравилоРеквизита.ЭтоДополнительныйРеквизитДО Тогда
					ТекстСообщения = СтрШаблон(
						НСтр("ru = 'Очищен признак ""Ключевой"" у реквизита %1, отсутствующего в шаблоне'",
							ОбщегоНазначения.КодОсновногоЯзыка()),
						ИмяРеквизита);
				Иначе
					ТекстСообщения = НСтр("ru = 'Очищен признак ""Ключевой"" у дополнительного реквизита, отсутствующего в шаблоне'",
						ОбщегоНазначения.КодОсновногоЯзыка());
				КонецЕсли;
				ЗаписьЖурналаРегистрации(
					ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИмяСобытияЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Предупреждение,,
					Правило.Ссылка,
					ТекстСообщения);
					
				НужнаЗапись = Истина;
				
				ПравилоРеквизита.Ключевой = Ложь;
				
			КонецЕсли;
			
			// Шаблон, запрещающий изменение реквизитов, не должен перекрываться правилом с другими вариантами заполнения.
			Если ШаблонЗапрещаетИзменение И ЗаполненВШаблоне И ПравилоРеквизита.Вариант <> ВариантШаблон() Тогда
				
				Если Не ПравилоРеквизита.ЭтоДополнительныйРеквизитДО Тогда
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Установлен вариант заполнения ""Из шаблона"" для реквизита %1, поскольку шаблон запрещает изменение'"),
						ИмяРеквизита);
				Иначе
					ТекстСообщения = НСтр("ru = 'Установлен вариант заполнения ""Из шаблона"" для дополнительного реквизита, поскольку шаблон запрещает изменение'");
				КонецЕсли;
				ЗаписьЖурналаРегистрации(
					ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИмяСобытияЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Предупреждение,,
					Правило.Ссылка,
					ТекстСообщения);
					
				НужнаЗапись = Истина;
				
				ПравилоРеквизита.Вариант = ВариантШаблон();
				ПравилоРеквизита.ЗначениеРеквизитаДО = Неопределено;
				ПравилоРеквизита.ИмяРеквизитаОбъектаИС = "";
				
			КонецЕсли;
			
			НомерРеквизита = НомерРеквизита + 1;
			
		КонецЦикла;
		
		Если НужнаЗапись Тогда
			Правило.Записать();
		КонецЕсли;
		Правило.Разблокировать();
		
	КонецЦикла;
	
КонецПроцедуры

// Проверяет выражение на встроенном языке, используемое для заполнения реквизита при передаче данных в ДО.
//
// Параметры:
//   ВычисляемоеВыражение - Строка - выражение на встроенном языке.
//   ОбъектИС - Произвольный - объект ИС.
//
Процедура ПроверитьВыражениеПравилаВыгрузки(ВычисляемоеВыражение, ОбъектИС) Экспорт
	
	Если СокрЛП(ВычисляемоеВыражение) = "" Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Источник", ОбъектИС);
	Параметры.Вставить("Результат", Неопределено);
	Параметры.Вставить("РезультатID", Неопределено);
	Параметры.Вставить("РезультатТип", Неопределено);
	
	ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ВычисляемоеВыражение, Параметры);
	
КонецПроцедуры

// Проверяет выражение на встроенном языке, используемое для проверки применимости правила при передаче данных в ДО.
//
// Параметры:
//   УсловиеПрименимостиПриВыгрузке - Строка - выражение на встроенном языке.
//   ОбъектИС - Произвольный - объект ИС.
//
Процедура ПроверитьВыражениеУсловияПрименимостиПриВыгрузке(УсловиеПрименимостиПриВыгрузке, ОбъектИС) Экспорт
	
	Если СокрЛП(УсловиеПрименимостиПриВыгрузке) = "" Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Источник", ОбъектИС);
	Параметры.Вставить("Результат", Истина);
	
	ОбщегоНазначения.ВыполнитьВБезопасномРежиме(УсловиеПрименимостиПриВыгрузке, Параметры);
	
КонецПроцедуры

// Проверяет выражение на встроенном языке, используемое для заполнения реквизита при получении данных из ДО.
//
// Параметры:
//   ВычисляемоеВыражение - Строка - выражение на встроенном языке.
//   ОбъектXDTO - ОбъектXDTO - Объект XDTO.
//   ОбъектИС - Произвольный - объект ИС.
//
Процедура ПроверитьВыражениеПравилаЗагрузки(ВычисляемоеВыражение, ОбъектXDTO, ОбъектИС) Экспорт
	
	Если СокрЛП(ВычисляемоеВыражение) = "" Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Источник", ОбъектXDTO);
	Параметры.Вставить("Приемник", ОбъектИС);
	Параметры.Вставить("Результат", Неопределено);
	
	ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ВычисляемоеВыражение, Параметры);
	
КонецПроцедуры

// Проверяет выражение на встроенном языке, используемое для проверки применимости правила при получении данных из ДО.
//
// Параметры:
//   УсловиеПрименимостиПриЗагрузке - Строка - выражение на встроенном языке.
//   ОбъектXDTO - ОбъектXDTO - Объект XDTO.
//
Процедура ПроверитьВыражениеУсловияПрименимостиПриЗагрузке(УсловиеПрименимостиПриЗагрузке, ОбъектXDTO) Экспорт
	
	Если СокрЛП(УсловиеПрименимостиПриЗагрузке) = "" Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Источник", ОбъектXDTO);
	Параметры.Вставить("Результат", Истина);
	
	ОбщегоНазначения.ВыполнитьВБезопасномРежиме(УсловиеПрименимостиПриЗагрузке, Параметры);
	
КонецПроцедуры

#Область АвтоматическоеСоздание

// Добавляет правило передачи реквизита в 1С:Документооборот из интегрируемой системы.
//
// Параметры:
//   Правило - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом - ссылка на правило интеграции.
//   Вариант - ПеречислениеСсылка.ВариантыПравилЗаполненияРеквизитов - вариант заполнения реквизита.
//   ИмяРеквизитаОбъектаДО - Строка - имя реквизита объекта документооборота.
//   ЗначениеЗаполнения - Строка, ОбъектXDTO - значение правила.
//   Обновлять - Булево - обновлять значение.
//   Ключевой - Булево - ключевой реквизит.
//   Таблица - Строка - имя таблицы, к которой относится реквизит.
//
Процедура ДобавитьПравилоЗаполненияРеквизитаДО(Правило, Вариант, ИмяРеквизитаОбъектаДО,
		ЗначениеЗаполнения = Неопределено, Обновлять = Истина, Ключевой = Ложь, Таблица = "") Экспорт
	
	Реквизит = Правило.ПравилаЗаполненияРеквизитовДО.Добавить();
	Реквизит.ИмяРеквизитаОбъектаДО = ИмяРеквизитаОбъектаДО;
	Реквизит.Вариант = Вариант;
	Реквизит.Ключевой = Ключевой;
	Реквизит.Обновлять = Обновлять;
	Реквизит.Таблица = Таблица;
	Реквизит.ЭтоТаблица = (СтрНайти(ИмяРеквизитаОбъектаДО, ".") > 0);
	
	Если Вариант = Перечисления.ВариантыПравилЗаполненияРеквизитов.ВыражениеНаВстроенномЯзыке Тогда
		Реквизит.ВычисляемоеВыражение = ЗначениеЗаполнения;
		
	ИначеЕсли Вариант = Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта Тогда
		Реквизит.ИмяРеквизитаОбъектаИС = ЗначениеЗаполнения;
		
	ИначеЕсли Вариант = Перечисления.ВариантыПравилЗаполненияРеквизитов.УказанноеЗначение Тогда
		Реквизит.ЗначениеРеквизитаДО = ЗначениеЗаполнения.name;
		Реквизит.ЗначениеРеквизитаДОID = ЗначениеЗаполнения.objectID.ID;
		Реквизит.ЗначениеРеквизитаДОТип = ЗначениеЗаполнения.objectID.type;
		
	ИначеЕсли Вариант = Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитТаблицы Тогда
		
	Иначе
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Добавление правила заполнения реквизита с вариантом %1 в автоматическом режиме не поддерживается'"),
			Вариант);
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет правило получения реквизита из 1С:Документооборота в интегрируемую систему.
//
// Параметры:
//   Правило - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом - ссылка на правило интеграции.
//   Вариант - ПеречислениеСсылка.ВариантыПравилЗаполненияРеквизитов - вариант заполнения реквизита.
//   ИмяРеквизитаОбъектаИС - Строка - имя реквизита объекта потребителя.
//   ЗначениеЗаполнения - Строка, ОбъектXDTO - значение правила.
//   Обновлять - Булево - обновлять значение.
//   Ключевой - Булево - ключевой реквизит.
//   РежимИзменения - ПеречислениеСсылка.РежимИзмененияПроведенногоДокументаДанными1СДокументооборота - режим изменения
//     данных проведенного документа.
//   Таблица - Строка - имя таблицы, к которой относится реквизит.
//
Процедура ДобавитьПравилоЗаполненияРеквизитаИС(Правило, Вариант, ИмяРеквизитаОбъектаИС,
		ЗначениеЗаполнения = Неопределено, Обновлять = Истина, Ключевой = Ложь, РежимИзменения = Неопределено,
		Таблица = "") Экспорт
	
	Реквизит = Правило.ПравилаЗаполненияРеквизитовИС.Добавить();
	Реквизит.ИмяРеквизитаОбъектаИС = ИмяРеквизитаОбъектаИС;
	Реквизит.Вариант = Вариант;
	Реквизит.Ключевой = Ключевой;
	Реквизит.Обновлять = Обновлять;
	Реквизит.РежимИзмененияДанныхПроведенногоДокумента = РежимИзменения;
	Реквизит.Таблица = Таблица;
	Реквизит.ЭтоТаблица = Ложь;
	Если ЗначениеЗаполнено(Правило.ТипОбъектаИС) Тогда
		МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(Правило.ТипОбъектаИС);
		Реквизит.ЭтоТаблица = МетаданныеОбъекта <> Неопределено
			И МетаданныеОбъекта.ТабличныеЧасти.Найти(ИмяРеквизитаОбъектаИС) <> Неопределено;
	КонецЕсли;
	
	Если Вариант = Перечисления.ВариантыПравилЗаполненияРеквизитов.ВыражениеНаВстроенномЯзыке Тогда
		Реквизит.ВычисляемоеВыражение = ЗначениеЗаполнения;
		
	ИначеЕсли Вариант = Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта Тогда
		Реквизит.ИмяРеквизитаОбъектаДО = ЗначениеЗаполнения;
		
	ИначеЕсли Вариант = Перечисления.ВариантыПравилЗаполненияРеквизитов.УказанноеЗначение Тогда
		Реквизит.ЗначениеРеквизитаИС = ЗначениеЗаполнения;
		
	ИначеЕсли Вариант = Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитТаблицы Тогда
		
	Иначе
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Добавление правила заполнения реквизита с вариантом %1 в автоматическом режиме не поддерживается'"),
			Вариант);
			
	КонецЕсли;
	
КонецПроцедуры

// Добавляет и правило передачи и правило получения указанного реквизита с вариантом заполнения
// "Из реквизита объекта потребителя".
//
// Параметры:
//   Правило - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом - ссылка на правило интеграции.
//   ИмяРеквизитаОбъектаИС - Строка - имя реквизита объекта интегрируемой системы.
//   ИмяРеквизитаОбъектаДО - Строка - имя реквизита объекта документооборота.
//   ОбновлятьИС - Булево - обновлять значение реквизита в объекте ИС.
//   ОбновлятьДО - Булево - обновлять значение реквизита в объекте ДО.
//   РежимИзменения - ПеречислениеСсылка.РежимИзмененияПроведенногоДокументаДанными1СДокументооборота - режим изменения
//     данных проведенного документа в правиле заполнения реквизита ИС.
//   ТаблицаИС - Строка - имя таблицы ИС, к которой относится реквизит.
//   ТаблицаДО - Строка - имя таблицы ДО, к которой относится реквизит.
//
Процедура ДобавитьПарноеПравило(Правило, ИмяРеквизитаОбъектаИС, ИмяРеквизитаОбъектаДО, ОбновлятьИС = Истина,
		ОбновлятьДО = Истина, РежимИзменения = Неопределено, ТаблицаИС = "", ТаблицаДО = "") Экспорт
	
	ДобавитьПравилоЗаполненияРеквизитаДО(
		Правило,
		Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта,
		ИмяРеквизитаОбъектаДО,
		?(ТаблицаИС = "", "", ТаблицаИС + ".") + ИмяРеквизитаОбъектаИС,
		ОбновлятьДО,,
		ТаблицаДО);
		
	ДобавитьПравилоЗаполненияРеквизитаИС(
		Правило,
		Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта,
		ИмяРеквизитаОбъектаИС,
		?(ТаблицаДО = "", "", ТаблицаДО + ".") + ИмяРеквизитаОбъектаДО,
		ОбновлятьИС,,
		РежимИзменения,
		ТаблицаИС);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СторонаЗаявкиНаОплатуXDTO(ОбъектXDTO, ПредопределенноеИмяСтороны)
	
	Результат = Неопределено;
	
	УстановленаТЧСтороны = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ОбъектXDTO, "partyRows");
	
	Если УстановленаТЧСтороны Тогда
		Для Каждого СторонаXDTO Из ОбъектXDTO.partyRows.rows Цикл
			Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(СторонаXDTO, "description")
					И ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(СторонаXDTO.description, "predefinedName")
					И СторонаXDTO.description.predefinedName = ПредопределенноеИмяСтороны Тогда
				Результат = СторонаXDTO.party;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполняет данные заявки на оплату.
//
// Параметры:
//   СтруктураРеквизитов - Структура:
//     * Стороны - ТаблицаЗначений:
//         ** Наименование - Строка
//
Процедура ЗаполнитьДанныеЗаявкиНаОплату(СтруктураРеквизитов)
	
	СтруктураРеквизитов.Контрагент = "";
	СтруктураРеквизитов.КонтрагентID = "";
	СтруктураРеквизитов.КонтрагентТип = "";
	СтруктураРеквизитов.Контрагенты.Очистить();
	Если (СтруктураРеквизитов.ПолучательТип = "DMCorrespondent"
		Или СтруктураРеквизитов.ПолучательТип = ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ИмяТипаИС(
			"DMCorrespondent"))
				И ЗначениеЗаполнено(СтруктураРеквизитов.ПолучательID) Тогда
		СтрокаКонтрагент = СтруктураРеквизитов.Контрагенты.Добавить();
		СтрокаКонтрагент.Контрагент = СтруктураРеквизитов.Получатель;
		СтрокаКонтрагент.КонтрагентID = СтруктураРеквизитов.ПолучательID;
		СтрокаКонтрагент.КонтрагентТип = СтруктураРеквизитов.ПолучательТип;
		СтруктураРеквизитов.Контрагент = СтруктураРеквизитов.Получатель;
		СтруктураРеквизитов.КонтрагентID = СтруктураРеквизитов.ПолучательID;
		СтруктураРеквизитов.КонтрагентТип = СтруктураРеквизитов.ПолучательТип;
	КонецЕсли;
	
	СтруктураРеквизитов.Стороны.Очистить();
	Если ЗначениеЗаполнено(СтруктураРеквизитов.ОрганизацияID) Тогда
		СторонаПлательщик = СтруктураРеквизитов.Стороны.Добавить();
		СторонаПлательщик.Сторона = СтруктураРеквизитов.Организация;
		СторонаПлательщик.СторонаID = СтруктураРеквизитов.ОрганизацияID;
		СторонаПлательщик.СторонаТип = СтруктураРеквизитов.ОрганизацияТип;
		НаименованиеПлательщикXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПредопределенноеЗначениеДО(
			"DMPartyName", "Плательщик"); //@NON-NLS-2
		СторонаПлательщик.Наименование = НаименованиеПлательщикXDTO.name;
		СторонаПлательщик.НаименованиеID = НаименованиеПлательщикXDTO.objectID.ID;
		СторонаПлательщик.НаименованиеТип = НаименованиеПлательщикXDTO.objectID.type;
	КонецЕсли;
	Если ЗначениеЗаполнено(СтруктураРеквизитов.ПолучательID) Тогда
		СторонаПолучатель = СтруктураРеквизитов.Стороны.Добавить();
		СторонаПолучатель.Сторона = СтруктураРеквизитов.Получатель;
		СторонаПолучатель.СторонаID = СтруктураРеквизитов.ПолучательID;
		СторонаПолучатель.СторонаТип = СтруктураРеквизитов.ПолучательТип;
		НаименованиеПолучательXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПредопределенноеЗначениеДО(
			"DMPartyName", "Получатель"); //@NON-NLS-2
		СторонаПолучатель.Наименование = НаименованиеПолучательXDTO.name;
		СторонаПолучатель.НаименованиеID = НаименованиеПолучательXDTO.objectID.ID;
		СторонаПолучатель.НаименованиеТип = НаименованиеПолучательXDTO.objectID.type;
	КонецЕсли;
	
КонецПроцедуры

// Добавляет реквизит в строку таблицы с заполнением колонок, указанных в качестве параметров.
//
Процедура ДобавитьРеквизит(Реквизиты, Имя, Представление, Тип, ОбязательноеЗаполнение = Ложь)
	
	НоваяСтрока = Реквизиты.Добавить();
	НоваяСтрока.Имя = Имя;
	НоваяСтрока.Представление = Представление;
	
	Если ТипЗнч(Тип) <> Тип("СписокЗначений") Тогда 
		Типы = Новый СписокЗначений;
		Типы.Добавить(Тип);
		НоваяСтрока.Тип = Типы;
	Иначе
		НоваяСтрока.Тип = Тип;
	КонецЕсли;
	
	НоваяСтрока.ОбязательноеЗаполнение = ОбязательноеЗаполнение;
	
КонецПроцедуры

// Добавляет дополнительный реквизит в строку таблицы с заполнением колонок, указанных в качестве параметров.
//
Процедура ДобавитьДополнительныйРеквизит(Реквизиты, Представление, Тип, ДопРеквизитID, ДопРеквизитТип)
	
	НоваяСтрока = Реквизиты.Добавить();
	НоваяСтрока.Представление = Представление;
	
	Если ТипЗнч(Тип) <> Тип("СписокЗначений") Тогда 
		Типы = Новый СписокЗначений;
		Типы.Добавить(Тип);
		НоваяСтрока.Тип = Типы;
	Иначе
		НоваяСтрока.Тип = Тип;
	КонецЕсли;
	
	НоваяСтрока.ДопРеквизит = Истина;
	НоваяСтрока.ДопРеквизитID = ДопРеквизитID;
	НоваяСтрока.ДопРеквизитТип = ДопРеквизитТип;
	
КонецПроцедуры

// Добавляет реквизит шаблона в строку таблицы.
//
Процедура ДобавитьРеквизитШаблона(Прокси, Реквизиты, ИмяСвойстваДокумента, ШаблонXDTO,
	Знач ИмяСвойстваШаблона = Неопределено)
	
	Если ИмяСвойстваШаблона = Неопределено Тогда // по умолчанию совпадают
		ИмяСвойстваШаблона = ИмяСвойстваДокумента;
	КонецЕсли;
	
	Если Не ШаблонXDTO.Установлено(ИмяСвойстваШаблона) Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = Реквизиты.Добавить();
	НоваяСтрока.ИмяРеквизитаОбъектаДО = ИмяСвойстваДокумента;
	НоваяСтрока.ЭтоДополнительныйРеквизитДО = Ложь;
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЭтоОбъектноеСвойство(Прокси, ШаблонXDTO, ИмяСвойстваШаблона) Тогда
		НоваяСтрока.ШаблонЗначение = ШаблонXDTO[ИмяСвойстваШаблона].name;
		НоваяСтрока.ШаблонID = ШаблонXDTO[ИмяСвойстваШаблона].objectID.ID;
		НоваяСтрока.ШаблонТип = ШаблонXDTO[ИмяСвойстваШаблона].objectID.type;
	Иначе // примитивный тип
		НоваяСтрока.ШаблонЗначение = ШаблонXDTO[ИмяСвойстваШаблона];
	КонецЕсли;
	
КонецПроцедуры

// Добавляет таблицу в строку таблицы с заполнением колонок, указанных в качестве параметров.
//
Процедура ДобавитьТаблицу(Реквизиты, Имя, Представление)
	
	НоваяСтрока = Реквизиты.Добавить();
	НоваяСтрока.ЭтоТаблица = Истина;
	НоваяСтрока.Имя = Имя;
	НоваяСтрока.Представление = Представление;
	
КонецПроцедуры

// Добавляет реквизит в подчиненную строку таблицы, с заполнением колонок, указанных в качестве параметров.
//
Процедура ДобавитьРеквизитТаблицы(Реквизиты, ИмяТаблицы, Имя, Представление, Тип, ОбязательноеЗаполнение = Ложь)
	
	НоваяСтрока = Реквизиты.Добавить();
	НоваяСтрока.Таблица = ИмяТаблицы;
	НоваяСтрока.Имя = Имя;
	НоваяСтрока.Представление = Представление;
	
	Если ТипЗнч(Тип) <> Тип("СписокЗначений") Тогда 
		Типы = Новый СписокЗначений;
		Типы.Добавить(Тип);
		НоваяСтрока.Тип = Типы;
	Иначе
		НоваяСтрока.Тип = Тип;
	КонецЕсли;
	
	НоваяСтрока.ОбязательноеЗаполнение = ОбязательноеЗаполнение;
	
КонецПроцедуры

// Возвращает Истина, если имя описывает один из примитивных типов, и Ложь в противном случае.
//
Функция ЭтоПримитивныйТип(ИмяТипа)
	
	Возврат ИмяТипа = "Строка"
		Или ИмяТипа = "Дата"
		Или ИмяТипа = "Число"
		Или ИмяТипа = "ДатаВремя"
		Или ИмяТипа = "Булево";
	
КонецФункции

Функция ПривестиЗначениеКПримитивномуТипу(ИмяТипа, Значение)
	
	Если ИмяТипа = "Строка" И ТипЗнч(Значение) <> Тип("Строка") Тогда
		Возврат Строка(Значение);
		
	ИначеЕсли ИмяТипа = "Число" И (ТипЗнч(Значение) = Тип("Строка") Или ТипЗнч(Значение) = Тип("Булево")) Тогда
		Возврат Число(Значение);
		
	ИначеЕсли (ИмяТипа = "Дата" Или ИмяТипа = "ДатаВремя") И ТипЗнч(Значение) = Тип("Строка") Тогда
		Возврат Дата(Значение);
		
	ИначеЕсли ИмяТипа = "Булево" И (ТипЗнч(Значение) = Тип("Число") Или ТипЗнч(Значение) = Тип("Строка")) Тогда
		Возврат Булево(Значение);
		
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

// Возвращает Истина, если переданное значение является перечислением.
//
Функция ЭтоПеречисление(Значение)
	
	Если Значение = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Перечисления.ТипВсеСсылки().СодержитТип(ТипЗнч(Значение));
	
КонецФункции

// Получает данные правила для выгрузки.
//
// Параметры:
//   Правило - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом - правило, данные которого нужно получить.
//
// Возвращаемое значение:
//   Структура - данные правила, необходимые для выгрузки, ключи - имена реквизитов и табличных частей.
//
Функция ДанныеПравилаДляВыгрузки(Правило) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Правило.Ссылка КАК Ссылка,
		|	Правило.ТипОбъектаИС КАК ТипОбъектаИС,
		|	Правило.ТипОбъектаДО КАК ТипОбъектаДО,
		|	Правило.ТипФайловСохраненияПечатныхФорм КАК ТипФайловСохраненияПечатныхФорм,
		|	Правило.Шаблон КАК Шаблон,
		|	Правило.ШаблонТип КАК ШаблонТип,
		|	Правило.ШаблонID КАК ШаблонID,
		|	Правило.ШаблонЗапрещаетИзменение КАК ШаблонЗапрещаетИзменение,
		|	Правило.НеСоздаватьСвязиПоСсылкам КАК НеСоздаватьСвязиПоСсылкам,
		|	Правило.ПравилаЗаполненияРеквизитовДО.(
		|		ПравилаЗаполненияРеквизитовДО.НомерСтроки КАК НомерСтроки,
		|		ПравилаЗаполненияРеквизитовДО.Вариант КАК Вариант,
		|		ПравилаЗаполненияРеквизитовДО.ВычисляемоеВыражение КАК ВычисляемоеВыражение,
		|		ПравилаЗаполненияРеквизитовДО.ЭтоДополнительныйРеквизитДО КАК ЭтоДополнительныйРеквизитДО,
		|		ПравилаЗаполненияРеквизитовДО.ДополнительныйРеквизитДОID КАК ДополнительныйРеквизитДОID,
		|		ПравилаЗаполненияРеквизитовДО.ДополнительныйРеквизитДОТип КАК ДополнительныйРеквизитДОТип,
		|		ПравилаЗаполненияРеквизитовДО.ЭтоДополнительныйРеквизитИС КАК ЭтоДополнительныйРеквизитИС,
		|		ПравилаЗаполненияРеквизитовДО.ДополнительныйРеквизитИС КАК ДополнительныйРеквизитИС,
		|		ПравилаЗаполненияРеквизитовДО.ЗаполненВШаблоне КАК ЗаполненВШаблоне,
		|		ПравилаЗаполненияРеквизитовДО.ЗначениеРеквизитаДО КАК ЗначениеРеквизитаДО,
		|		ПравилаЗаполненияРеквизитовДО.ЗначениеРеквизитаДОID КАК ЗначениеРеквизитаДОID,
		|		ПравилаЗаполненияРеквизитовДО.ИмяРеквизитаОбъектаДО КАК ИмяРеквизитаОбъектаДО,
		|		ПравилаЗаполненияРеквизитовДО.ИмяРеквизитаОбъектаИС КАК ИмяРеквизитаОбъектаИС,
		|		ПравилаЗаполненияРеквизитовДО.Ключевой КАК Ключевой,
		|		ПравилаЗаполненияРеквизитовДО.Таблица КАК Таблица,
		|		ПравилаЗаполненияРеквизитовДО.Обновлять КАК Обновлять,
		|		ПравилаЗаполненияРеквизитовДО.ЗначениеРеквизитаДОТип КАК ЗначениеРеквизитаДОТип,
		|		ПравилаЗаполненияРеквизитовДО.ШаблонЗначение КАК ШаблонЗначение,
		|		ПравилаЗаполненияРеквизитовДО.ШаблонID КАК ШаблонID,
		|		ПравилаЗаполненияРеквизитовДО.ШаблонТип КАК ШаблонТип,
		|		ПравилаЗаполненияРеквизитовДО.ЭтоТаблица КАК ЭтоТаблица
		|	) КАК ПравилаЗаполненияРеквизитовДО,
		|	Правило.ПрисоединяемыеПечатныеФормы.(
		|		ПрисоединяемыеПечатныеФормы.ИмяКоманды КАК ИмяКоманды,
		|		ПрисоединяемыеПечатныеФормы.МенеджерПечати КАК МенеджерПечати,
		|		ПрисоединяемыеПечатныеФормы.Обновлять КАК Обновлять,
		|		ПрисоединяемыеПечатныеФормы.ПредставлениеКоманды КАК ПредставлениеКоманды
		|	) КАК ПрисоединяемыеПечатныеФормы
		|ИЗ
		|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК Правило
		|ГДЕ
		|	Правило.Ссылка = &Правило
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки");
		
	Запрос.УстановитьПараметр("Правило", Правило);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	ДанныеПравила = Новый Структура;
	Для Каждого Колонка Из Результат.Колонки Цикл
		ДанныеПравила.Вставить(Колонка.Имя, Выборка[Колонка.Имя]);
	КонецЦикла;
	ДанныеПравила.ПравилаЗаполненияРеквизитовДО = ДанныеПравила.ПравилаЗаполненияРеквизитовДО.Выгрузить();
	ДанныеПравила.ПрисоединяемыеПечатныеФормы = ДанныеПравила.ПрисоединяемыеПечатныеФормы.Выгрузить();
	
	Возврат ДанныеПравила;
	
КонецФункции

// Возвращает значение Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта.
//
Функция ВариантРеквизит()
	
	Возврат Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта;
	
КонецФункции

// Возвращает значение Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта.
//
Функция ВариантРеквизитТаблицы()
	
	Возврат Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитТаблицы;
	
КонецФункции

// Возвращает значение Перечисления.ВариантыПравилЗаполненияРеквизитов.УказанноеЗначение.
//
Функция ВариантЗначение()
	
	Возврат Перечисления.ВариантыПравилЗаполненияРеквизитов.УказанноеЗначение;
	
КонецФункции

// Возвращает значение Перечисления.ВариантыПравилЗаполненияРеквизитов.ВыражениеНаВстроенномЯзыке.
//
Функция ВариантВыражение()
	
	Возврат Перечисления.ВариантыПравилЗаполненияРеквизитов.ВыражениеНаВстроенномЯзыке;
	
КонецФункции

// Возвращает значение Перечисления.ВариантыПравилЗаполненияРеквизитов.ИзШаблона.
//
Функция ВариантШаблон()
	
	Возврат Перечисления.ВариантыПравилЗаполненияРеквизитов.ИзШаблона;
	
КонецФункции

// В случае, когда выражение на встроенном языке возвращает имя или синоним значения
// перечисления - процедура заполняет реквизит объекта соответствующим значением.
// Если тип реквизита не является перечислением - реквизит будет заполнен переданным результатом.
//
// Параметры:
//   Объект - Произвольный - произвольный объект, реквизит которого нужно заполнить.
//   ИмяРеквизита - Строка - реквизит, который требуется заполнить.
//   Результат - Произвольный - результат выполнения выражения на встроенном языке.
//
Процедура ЗаполнитьПоРезультатуВыраженияНаВстроенномЯзыке(Объект, ИмяРеквизита, Результат)
	
	Если ТипЗнч(Результат) = Тип("Строка") И Результат <> "" И ЭтоПеречисление(Объект[ИмяРеквизита]) Тогда
		ИмяЗначения = СокрЛП(Результат);
		МенеджерПеречисления = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект[ИмяРеквизита]);
		МетаданныеРеквизита = Объект[ИмяРеквизита].Метаданные();
		Если МетаданныеРеквизита.ЗначенияПеречисления.Найти(ИмяЗначения) = Неопределено Тогда
			// Заполним перечисление по переданному синониму
			Объект[ИмяРеквизита] = МенеджерПеречисления.ПустаяСсылка();
			Для Каждого ЗначениеПеречисления Из МетаданныеРеквизита.ЗначенияПеречисления Цикл
				Если ВРег(СокрЛП(ЗначениеПеречисления.Синоним)) = ВРег(ИмяЗначения) Тогда
					Объект[ИмяРеквизита] = МенеджерПеречисления[ЗначениеПеречисления.Имя];
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			// Заполним перечисление по переданному имени
			Объект[ИмяРеквизита] = МенеджерПеречисления[ИмяЗначения];
		КонецЕсли;
	Иначе
		Объект[ИмяРеквизита] = Результат;
	КонецЕсли;
	
КонецПроцедуры

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриДобавленииОбработчиковОбновления(Обработчики);
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ЗарегистрироватьПравилаИнтеграцииКОбработкеДляПереходаНаНовуюВерсию(
		Параметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ОбработатьПравилаИнтеграцииДляПереходаНаНовуюВерсию(Параметры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли