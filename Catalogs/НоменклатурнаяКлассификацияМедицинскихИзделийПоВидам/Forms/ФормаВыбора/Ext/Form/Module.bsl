
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НастроитьФорму();
	
	КлассификацияМИПоВидамПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокРазделовПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ПоказатьЭлементыВыделенныхРазделов", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если ВернутьВидИРазделИзделия Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ВозвращаемыйРезультат = Новый Структура;
		ВозвращаемыйРезультат.Вставить("РазделИзделия", Элементы.СписокРазделов.ТекущаяСтрока);
		ВозвращаемыйРезультат.Вставить("ВидИзделия"   , Элементы.Список.ТекущаяСтрока);
		
		ОповеститьОВыборе(ВозвращаемыйРезультат);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	КлассификацияМИПоВидамКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Цвет текста строк таблицы Список.
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Отменен", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПросроченныеДанныеЦвет);
	
	// Цвет текста строк таблицы СписокРазделов.
	Элемент = СписокРазделов.УсловноеОформление.Элементы.Добавить();
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Отменен", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПросроченныеДанныеЦвет);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФорму()
	
	Если ЗначениеЗаполнено(Параметры.РазделИзделия) Тогда
		Элементы.СписокРазделов.ТекущаяСтрока = Параметры.РазделИзделия;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ВидИзделия) Тогда
		Элементы.Список.ТекущаяСтрока = Параметры.ВидИзделия;
	КонецЕсли;
	
	ВернутьВидИРазделИзделия = Параметры.ВернутьВидИРазделИзделия;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЭлементыВыделенныхРазделов()
	
	ВыделенныеРазделы = Элементы.СписокРазделов.ВыделенныеСтроки;
	Использование = ВыделенныеРазделы.Найти(ПредопределенноеЗначение("Справочник.РазделыНоменклатурнойКлассификацииМедицинскихИзделийПоВидам.ПустаяСсылка")) = Неопределено;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Разделы.Раздел", ВыделенныеРазделы, ВидСравненияКомпоновкиДанных.ВИерархии,, Использование);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
