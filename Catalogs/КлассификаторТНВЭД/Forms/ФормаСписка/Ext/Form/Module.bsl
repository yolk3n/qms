
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// РаботаСНоменклатурой.ОблачныеКлассификаторы
	ОблачныеКлассификаторы.ПриСозданииНаСервереФормаСпискаТНВЭД(ЭтотОбъект, КоманднаяПанель);
	// Конец РаботаСНоменклатурой.ОблачныеКлассификаторы
	
	Элементы.ПодобратьКлассификатор.Видимость =
		Элементы.Найти("ПодобратьИзСервисаЭлементыТНВЭД") = Неопределено
		И ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.КлассификаторТНВЭД);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьПослеДобавленияТНВЭД" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьКлассификатор(Команда)
	
	НачатьПодоборКлассификатора();
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Текст = НСтр("ru = 'Есть возможность подобрать товарную номенклатуру ВЭД из классификатора.
		|Подобрать?'");
	
	ДополнительныеПараметры = Новый Структура;
	Если Копирование Тогда
		ДополнительныеПараметры.Вставить("ЗначениеКопирования", Элемент.ТекущаяСтрока);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВопросПодобратьКлассификатор", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьПодоборКлассификатора()
	
	Оповестить = Новый ОписаниеОповещения("ПодобратьКлассификаторАльтернативныйСпособ", ЭтотОбъект);
	
	Если Не Элементы.ПодобратьКлассификатор.Видимость Тогда
		НастройкиРежима = Новый Структура("АльтернативныйСпособПодбора", Оповестить);
		ОблачныеКлассификаторыКлиент.ПодобратьИзСервисаЭлементыТНВЭД(НастройкиРежима);
	Иначе
		ВыполнитьОбработкуОповещения(Оповестить);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьКлассификаторАльтернативныйСпособ(Результат, ДополнительныеПараметры) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Подбор", Истина);
	ПараметрыФормы.Вставить("ИмяСправочника", "КлассификаторТНВЭД");
	
	ОткрытьФорму("Справочник.КлассификаторТНВЭД.Форма.ДобавлениеЭлементовВКлассификатор", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПодобратьКлассификатор(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		НачатьПодоборКлассификатора();
	Иначе
		ОткрытьФорму("Справочник.КлассификаторТНВЭД.ФормаОбъекта", ДополнительныеПараметры, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// РаботаСНоменклатурой.ОблачныеКлассификаторы

&НаКлиенте
Процедура Подключаемый_ПодобратьИзСервисаЭлементыТНВЭД(Команда)
	
	НачатьПодоборКлассификатора();
	
КонецПроцедуры

// Конец РаботаСНоменклатурой.ОблачныеКлассификаторы

#КонецОбласти // СтандартныеПодсистемы
