
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Отбор.Свойство("Групповой") Тогда
		Если Параметры.Отбор.Групповой Тогда
			ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Группы;
		Иначе
			ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы;
		КонецЕсли;
		Параметры.Отбор.Удалить("Групповой");
	Иначе
		ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.ГруппыИЭлементы;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Классификатор") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Классификаторы.Классификатор", Параметры.Отбор.Классификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОтобразитьХарактеристикиКТРУ", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПроверитьВыбор(ВыбраннаяСтрока, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ПроверитьВыбор(Значение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ХарактеристикиКТРУПредставление.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"ХарактеристикиКТРУ.ЗначениеКТРУПометкаУдаления", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт"     , ШрифтыСтиля.ШрифтЗачеркнутыйКТРУ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьХарактеристикиКТРУ()
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		ЗаполнитьХарактеристикиКТРУ(ТекущаяСтрока.Ссылка);
	Иначе
		ХарактеристикиКТРУ.ПолучитьЭлементы().Очистить();
	КонецЕсли;
	
	Для Каждого Элемент Из ХарактеристикиКТРУ.ПолучитьЭлементы() Цикл
		Элементы.ХарактеристикиКТРУ.Развернуть(Элемент.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьХарактеристикиКТРУ(Знач КТРУ)
	
	Дерево = РеквизитФормыВЗначение("ХарактеристикиКТРУ");
	Дерево.Строки.Очистить();
	Справочники.КТРУ.ЗаполнитьДеревоЗначенийХарактеристик(КТРУ, Дерево);
	ЗначениеВРеквизитФормы(Дерево, "ХарактеристикиКТРУ");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыбор(Значение, СтандартнаяОбработка)
	
	ТекущаяСтрока = Элементы.Список.ДанныеСтроки(Значение);
	Если ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы И ТекущаяСтрока.Групповой
	 Или ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Группы И Не ТекущаяСтрока.Групповой Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
