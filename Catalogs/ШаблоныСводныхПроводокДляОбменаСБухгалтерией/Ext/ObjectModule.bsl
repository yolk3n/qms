#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Родитель") И ЗначениеЗаполнено(ДанныеЗаполнения.Родитель) Тогда
			РодительГруппа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Родитель, "ЭтоГруппаШаблонов");
			Если Не РодительГруппа Тогда
				ДанныеЗаполнения.Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Родитель, "Родитель");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппаШаблонов Тогда
		ПроверяемыеРеквизиты.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ЭтоНовый() И Операция <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Операция") Тогда
		РеквизитДопУпорядочивания = 0;
	КонецЕсли;
	
	ИндексКартинки = ИндексКартинкиШаблонаПроводки();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

Функция ИндексКартинкиШаблонаПроводки()
	
	Если ЭтоГруппаШаблонов Тогда
		Если ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияШаблоновПроводок.Все Тогда
			Результат = 0;
		Иначе
			Результат = 1;
		КонецЕсли;
	Иначе
		Результат = 2;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		Результат = Результат + 3
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли