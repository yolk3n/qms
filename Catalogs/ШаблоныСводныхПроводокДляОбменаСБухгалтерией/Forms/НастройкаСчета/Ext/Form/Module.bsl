////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ДанныеВыбораКПС.Добавить("", НСтр("ru = 'Фиксированное значение...'"));
	Для Каждого ЗначениеКПС Из Метаданные.Перечисления.ЗначенияКПС.ЗначенияПеречисления Цикл
		ДанныеВыбораКПС.Добавить(Перечисления.ЗначенияКПС[ЗначениеКПС.Имя], ЗначениеКПС.Синоним);
	КонецЦикла;
	
	ДанныеВыбораСчета.Добавить("", НСтр("ru = 'Фиксированное значение...'"));
	Для Каждого ЗначениеСчетаУчета Из Метаданные.Перечисления.ЗначенияСчетаУчета.ЗначенияПеречисления Цикл
		ДанныеВыбораСчета.Добавить(Перечисления.ЗначенияСчетаУчета[ЗначениеСчетаУчета.Имя], ЗначениеСчетаУчета.Синоним);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "КПССчета, ВидКПС, Счет, КЭКСчета");
	
	Если Не ЗначениеЗаполнено(КПССчета) Тогда
		КПССчета = НулевойКПС();
	КонецЕсли;
	
	Элементы.ВидКПС.Доступность = ЭтоФиксированноеЗначение(КПССчета);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	ФиксированноеЗначениеКПС = ЭтоФиксированноеЗначение(КПССчета);
	
	Если Не ФиксированноеЗначениеКПС Тогда
		НепроверяемыеРеквизиты.Добавить("ВидКПС");
	КонецЕсли;
	
	Если ФиксированноеЗначениеКПС И СтрДлина(КПССчета) < 17 Тогда
		ТекстСообщения = НСтр("ru = 'Количество символов не может быть отлично от 17.'");
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", "КПС",,, ТекстСообщения);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "КПССчета",, Отказ);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповестить = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповестить, Отказ, ЗавершениеРаботы,, ТекстПредупреждения);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КПССчетаУчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ДанныеВыбораКПС;
	
КонецПроцедуры

&НаКлиенте
Процедура КПССчетаУчетаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Если ЭтоФиксированноеЗначение(КПССчета) Тогда
			СтандартнаяОбработка = Ложь;
		Иначе
			ВыбранноеЗначение = НулевойКПС();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КПССчетаПриИзменении(Элемент)
	
	ФиксированноеЗначениеКПС = ЭтоФиксированноеЗначение(КПССчета);
	Элементы.ВидКПС.Доступность = ФиксированноеЗначениеКПС;
	Если Не ФиксированноеЗначениеКПС Тогда
		ВидКПС = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ДанныеВыбораСчета;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Если ЭтоФиксированноеЗначение(Счет) Тогда
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КнопкаОк(Команда)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОтмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция НулевойКПС()
	
	Возврат "00000000000000000";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоФиксированноеЗначение(Значение)
	
	Возврат ТипЗнч(Значение) = Тип("Строка");
	
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиСчета = Новый Структура("КПССчета, ВидКПС, Счет, КЭКСчета");
	ЗаполнитьЗначенияСвойств(НастройкиСчета, ЭтотОбъект);
	
	Модифицированность = Ложь;
	Закрыть(НастройкиСчета);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
