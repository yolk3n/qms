
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Владелец = Неопределено;
	Параметры.Отбор.Свойство("Владелец", Владелец);
	ИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	
	ДоступныеЭлементы = УправлениеДоступомБольничнаяАптекаВызовСервера.ПолучитьДоступныеЭлементыДляОтбора(ИмяОбъекта, Параметры, Владелец);
	Если ДоступныеЭлементы <> Неопределено Тогда
		Параметры.Отбор.Вставить("Ссылка", ДоступныеЭлементы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Функция получает Подразделение по умолчанию
//
Функция ПолучитьПодразделениеПоУмолчанию(Организация = Неопределено, ТаблицаОбъекта = Неопределено) Экспорт
	
	ДоступныеПодразделения = ПолучитьДоступные(Организация, ТаблицаОбъекта);
	Если ДоступныеПодразделения.Количество() = 1 Тогда
		Возврат ДоступныеПодразделения[0];
	КонецЕсли;
	
	Возврат Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
	
КонецФункции

// Возвращает доступные подразделения по типу
//
Функция ПолучитьДоступные(Организация = Неопределено, ТаблицаОбъекта = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Подразделение.Ссылка КАК Подразделение
	|ИЗ
	|	ЗначенияДоступа КАК ЗначенияДоступа
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПодразделенияОрганизаций КАК Подразделение
	|	ПО
	|		ЗначенияДоступа.Ссылка = Подразделение.Ссылка
	|ГДЕ
	|	НЕ Подразделение.ПометкаУдаления
	|";
	
	УправлениеДоступомБольничнаяАптека.ЗначенияДоступаРазрешающиеИзменениеОбъекта(ТаблицаОбъекта, Тип("СправочникСсылка.ПодразделенияОрганизаций"), Запрос.МенеджерВременныхТаблиц);
	
	Если Организация <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
			|	И Подразделение.Владелец В (&Организация)";
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Подразделение");
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Команды формы
#Область КомандыФормы

// Заполняет список команд ввода на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании, НастройкиФормы) Экспорт
	
	ВводНаОснованииБольничнаяАптека.ДобавитьКомандыСозданияНаОсновании(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыСоздатьНаОсновании, НастройкиФормы);
	
КонецПроцедуры

#КонецОбласти // КомандыФормы

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли
