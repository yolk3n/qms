
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Владелец", Организация) Тогда
		Параметры.Отбор.Удалить("Владелец");
		
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") И Не ЗначениеЗаполнено(Организация) Тогда
			Организация = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОрганизациюПоУмолчанию();
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", Организация);
		
		Если ЗначениеЗаполнено(Организация) Тогда
			АвтоЗаголовок = Ложь;
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Подразделения %1'"), Организация);
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьОтборСпискаПриСоздании();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Элементы.ПоказыватьТолькоДоступные.Видимость Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ДоступноДляВыбора", Истина,,, ПоказыватьТолькоДоступные);
		Если ПоказыватьТолькоДоступные Тогда
			Элементы.Список.Отображение = ОтображениеТаблицы.Список;
		Иначе
			Элементы.Список.Отображение = ОтображениеТаблицы.Дерево;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПоказыватьТолькоДоступныеПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ДоступноДляВыбора", Истина,,, ПоказыватьТолькоДоступные);
	Если ПоказыватьТолькоДоступные Тогда
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	Иначе
		Элементы.Список.Отображение = ОтображениеТаблицы.Дерево;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если Не Элемент.ТекущиеДанные.ДоступноДляВыбора Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформлениеСписка = ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ПолучитьУсловноеОформлениеДинамическогоСписка(Список);
	
	Элемент = УсловноеОформлениеСписка.Элементы.Добавить();
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Элемент.Отбор, "ДоступноДляВыбора", Ложь);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекста);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСпискаПриСоздании()
	
	ИмяОбъекта = Метаданные.Справочники.ПодразделенияОрганизаций.ПолноеИмя();
	ДоступныеЭлементы = УправлениеДоступомБольничнаяАптекаВызовСервера.ПолучитьДоступныеЭлементыДляОтбора(ИмяОбъекта, Параметры, Организация);
	Если ДоступныеЭлементы <> Неопределено Тогда
		
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Список.ТекстЗапроса);
		
		Колонка = СхемаЗапроса.ПакетЗапросов[0].Колонки.Найти("ДоступноДляВыбора");
		ИндексКолонки = СхемаЗапроса.ПакетЗапросов[0].Колонки.Индекс(Колонка);
		
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля[ИндексКолонки] = Новый ВыражениеСхемыЗапроса("ВЫБОР КОГДА Ссылка В (&ДоступныеЭлементы) ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ");
		
		Список.ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
		Список.Параметры.УстановитьЗначениеПараметра("ДоступныеЭлементы", ДоступныеЭлементы);
		
		ПоказыватьТолькоДоступные = Истина;
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ДоступноДляВыбора", Истина,,, ПоказыватьТолькоДоступные);
		
	Иначе
		Элементы.ПоказыватьТолькоДоступные.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
