#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИНН) Тогда
		
		ДубльКонтрагента = Справочники.Контрагенты.ИННКППУжеИспользуетсяВИнформационнойБазе(ИНН, КПП, Ссылка);
		Если ДубльКонтрагента <> Неопределено Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Данные ИНН и КПП уже указаны для контрагента - %1.'"), ДубльКонтрагента);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ИНН",, Отказ);
		КонецЕсли;
		
	Иначе
		
		Если Не Справочники.Контрагенты.УпрощенныйВводДоступен() Тогда
			
			Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель
			 Или ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
				ТекстСообщения = НСтр("ru = 'Не указан ИНН'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ИНН",, Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РегистрационныйНомер) И ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
		
		ДубльКонтрагента = Справочники.Контрагенты.СтранаРегистрационныйНомерУжеИспользуетсяВИнформационнойБазе(РегистрационныйНомер, СтранаРегистрации, Ссылка);
		Если ДубльКонтрагента <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='Данные регистрационного номера в этой же стране регистрации уже указаны для контрагента - %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДубльКонтрагента);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "РегистрационныйНомер",, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Поставщик И Не ПрочиеОтношения Тогда
		
		ТекстСообщения = НСтр("ru='Необходимо определить хотя бы один тип бизнес-отношений'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Поставщик",, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель
	 Или ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	ИначеЕсли ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо
	      Или ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
		ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИНН) И (ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент)Тогда
		ИНН = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КПП) И Не ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Или Не ЗначениеЗаполнено(ИНН) Тогда
		КПП = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодПоОКПО) И (ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Или ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо) Тогда
		КодПоОКПО = "";
	КонецЕсли;
	
	Если ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
		СтранаРегистрации = ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#КонецЕсли