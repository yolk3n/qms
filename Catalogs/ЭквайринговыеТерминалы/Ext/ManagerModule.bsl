#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Функция определяет эквайринговый терминал по выбранной кассе.
//
// Возвращает эквайринговый терминал, если найден один эквайринговый терминал.
// Возвращает Неопределено, если эквайринговый терминал не найден или эквайринговых терминалов больше одного.
//
// Параметры
//  Организация - СправочникСсылка.Кассы (СправочникСсылка.КассыККМ) - Ссылка на кассу
//
// Возвращаемое значение
//  СправочникСсылка.ЭквайринговыеТерминалы - Найденный эквайринговый терминал
//
Функция ПолучитьЭквайринговыйТерминалПоУмолчанию(Касса, Организация = Неопределено) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 2
	|	ЭквайринговыеТерминалы.Ссылка КАК ЭквайринговыйТерминал
	|ИЗ
	|	Справочник.ЭквайринговыеТерминалы КАК ЭквайринговыеТерминалы
	|ГДЕ
	|	НЕ ЭквайринговыеТерминалы.ПометкаУдаления
	|	И ЭквайринговыеТерминалы.Касса = &Касса
	|	И (ЭквайринговыеТерминалы.Организация = &Организация
	|		ИЛИ &Организация = Неопределено)
	|");
	
	Запрос.УстановитьПараметр("Касса", Касса);
	Запрос.УстановитьПараметр("Организация", ?(ЗначениеЗаполнено(Организация), Организация, Неопределено));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 
	   И Выборка.Следующий() Тогда
		ЭквайринговыйТерминал = Выборка.ЭквайринговыйТерминал;
	Иначе
		ЭквайринговыйТерминал = Справочники.ЭквайринговыеТерминалы.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ЭквайринговыйТерминал;

КонецФункции

// Процедура получения списка видов платежных карт, обслуживаемых по договору эквайринга.
//
// Параметры
//  ЭквайринговыйТерминал - СправочникСсылка.ЭквайринговыеТерминалы - Ссылка на эквайринговый терминал
//
// Возвращаемое значение
//  Массив - Массив видов платежных карт
//
Функция ПолучитьВидыПлатежныхКартЭквайринговогоТерминала(ЭквайринговыйТерминал) Экспорт
	
	ВидыПлатежныхКарт = Новый Массив;
	
	Если ЗначениеЗаполнено(ЭквайринговыйТерминал) Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВидыПлатежныхКарт.ВидПлатежнойКарты КАК ВидПлатежнойКарты
		|ИЗ
		|	Справочник.ЭквайринговыеТерминалы.ВидыПлатежныхКарт КАК ВидыПлатежныхКарт
		|ГДЕ
		|	ВидыПлатежныхКарт.Ссылка = &Ссылка");
		
		Запрос.УстановитьПараметр("Ссылка", ЭквайринговыйТерминал);
		
		ВидыПлатежныхКарт = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидПлатежнойКарты");
		
	КонецЕсли;
	
	Возврат ВидыПлатежныхКарт;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Касса)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти // ПрограммныйИнтерфейс

#КонецЕсли