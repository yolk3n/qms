
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.ДокументИнформационнойБазы) Тогда
		// Найдем ЭМД.
		Запрос_ = Новый Запрос;
		Запрос_.Текст =
			"ВЫБРАТЬ
			|	ФедеральныеВебСервисыЭМД.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ФедеральныеВебСервисыЭМД КАК ФедеральныеВебСервисыЭМД
			|ГДЕ
			|	НЕ ФедеральныеВебСервисыЭМД.ПометкаУдаления
			|	И ФедеральныеВебСервисыЭМД.ДокументИнформационнойБазы = &ДокументИнформационнойБазы";
			
		Запрос_.УстановитьПараметр("ДокументИнформационнойБазы", Параметры.ДокументИнформационнойБазы);
		
		Выборка_ = Запрос_.Выполнить().Выбрать();
		
		ЭМДСсылка_ = Неопределено;
		РеквизитыЭМД_ = Неопределено;
		
		Если Выборка_.Следующий() Тогда
			ЭтотОбъект.ЭМДСсылка = Выборка_.Ссылка;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭтотОбъект.ЭМДСсылка) Тогда
			ЭтотОбъект.ИдентификаторДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.ЭМДСсылка, "ИдентификаторДокумента");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭтотОбъект.ИдентификаторДокумента) Тогда
			ЗаполнитьСообщенияРЭМД();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьСообщенияРЭМД()
	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ
		|	ФедеральныеВебСервисыСообщенияРЭМД.ИдентификаторСообщения КАК ИдентификаторСообщения,
		|	ФедеральныеВебСервисыСообщенияРЭМД.ИдентификаторОтвета КАК ИдентификаторОтвета,
		|	ФедеральныеВебСервисыСообщенияРЭМД.ИдентификаторДокумента КАК ИдентификаторДокумента,
		|	ФедеральныеВебСервисыСообщенияРЭМД.ДатаСообщения КАК ДатаСообщения,
		|	ФедеральныеВебСервисыСообщенияРЭМД.ДатаОтвета КАК ДатаОтвета,
		|	ФедеральныеВебСервисыСообщенияРЭМД.emdrId КАК emdrId,
		|	ФедеральныеВебСервисыСообщенияРЭМД.ТекстОшибки КАК ТекстОшибки,
		|	ФедеральныеВебСервисыСообщенияРЭМД.ПутьКФайлу КАК ПутьКФайлу
		|ИЗ
		|	РегистрСведений.ФедеральныеВебСервисыСообщенияРЭМД КАК ФедеральныеВебСервисыСообщенияРЭМД
		|ГДЕ
		|	ФедеральныеВебСервисыСообщенияРЭМД.ИдентификаторДокумента = &ИдентификаторДокумента
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаСообщения";
		
	Запрос_.УстановитьПараметр("ИдентификаторДокумента", ЭтотОбъект.ИдентификаторДокумента);
	Выборка_ = Запрос_.Выполнить().Выбрать();
	
	Пока Выборка_.Следующий() Цикл
		НоваяСтрокаТЗ_ = ЭтотОбъект.СообщенияРЭМД.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТЗ_, Выборка_);
	КонецЦикла;
	
КонецПроцедуры

