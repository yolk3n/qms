#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ДокументИнформационнойБазы) Тогда
		ЗаполнитьСписокПодписей(ЭтотОбъект.Параметры.ДокументИнформационнойБазы);

		СтатусРЭМД_ = ФедеральныеВебСервисыРЭМД.ПолучитьСтатусРегистрацииДокументаВРЭМД(ЭтотОбъект.Параметры.ДокументИнформационнойБазы);
		
		ЭтотОбъект.СтатусСтрокой = СтатусРЭМД_.СтатусСтрокой;
		ЭтотОбъект.КодСтатуса = СтатусРЭМД_.КодСтатуса;
		ЭтотОбъект.Описание = СтатусРЭМД_.Описание;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОбменСРЭМД(Команда)
	ПараметрыФормы = Новый Структура("ДокументИнформационнойБазы", ЭтотОбъект.Параметры.ДокументИнформационнойБазы);
	ОткрытьФорму("Справочник.ФедеральныеВебСервисыЭМД.Форма.СообщенияОтправленныеВРЭМД", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура СписокПодписейСотрудникПодписантНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры


#КонецОбласти

#Область СлужебеныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокПодписей(ДокументИнформационнойБазы)
	ЭтотОбъект.СписокПодписей.Очистить();
	
	// Получим ожидаемые подписи.
	СтрокаТЗЗаголовок1_ = ЭтотОбъект.СписокПодписей.Добавить();
	СтрокаТЗЗаголовок1_.ТекстЗаголовка = "Ожидаемые подписи (0):";
	СтрокаТЗЗаголовок1_.ЭтоЗоголовок = Истина;
	
	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ
		|	ОтложенноеПодписаниеЭП.Сотрудник КАК Сотрудник,
		|	ОтложенноеПодписаниеЭП.КоличествоПодписей КАК КоличествоПодписей,
		|	ОтложенноеПодписаниеЭП.РольПриПодписании КАК РольПриПодписании
		|ИЗ
		|	РегистрСведений.ОтложенноеПодписаниеЭП КАК ОтложенноеПодписаниеЭП
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФедеральныеВебСервисыЭМД КАК ЭМД
		|		ПО ОтложенноеПодписаниеЭП.ПодписываемыйОбъект = ЭМД.Ссылка
		|			И (ЭМД.ПометкаУдаления = ЛОЖЬ)
		|ГДЕ
		|	ЭМД.ДокументИнформационнойБазы = &ДокументИнформационнойБазы
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник";
	
	Запрос_.УстановитьПараметр("ДокументИнформационнойБазы", ДокументИнформационнойБазы);
	Выборка_ = Запрос_.Выполнить().Выбрать();
	
	ДолжностиДляПодписания_ = Неопределено;
	КоличествоПодписей_ = 0;
	Пока Выборка_.Следующий() Цикл
		НоваяСтрокаТЗ_ = ЭтотОбъект.СписокПодписей.Добавить();
		
		Если ЗначениеЗаполнено(Выборка_.КоличествоПодписей) Тогда
			НоваяСтрокаТЗ_.КоличествоПодписей = Выборка_.КоличествоПодписей;
			КоличествоПодписей_ = КоличествоПодписей_ + Выборка_.КоличествоПодписей;
		Иначе
			КоличествоПодписей_ = КоличествоПодписей_ + 1;
		КонецЕсли;
		
		НоваяСтрокаТЗ_.ЭтоОжиданиеПодписи = Истина;
		
		Если Не ЗначениеЗаполнено(Выборка_.Сотрудник) И ЗначениеЗаполнено(Выборка_.РольПриПодписании) Тогда
			НоваяСтрокаТЗ_.Роль = Выборка_.РольПриПодписании;
			
			Если ДолжностиДляПодписания_ = Неопределено Тогда
				ДолжностиДляПодписания_ = ФедеральныеВебСервисыРЭМД.ПолучитьДолжностиДляПодписания(ДокументИнформационнойБазы);
			КонецЕсли;
			
			НайденныеДолжности_ = ДолжностиДляПодписания_.НайтиСтроки(Новый Структура("РольРЭМД", Выборка_.РольПриПодписании));
			
			СписокДолжностей_ = "";
			Для Каждого Должность_ Из НайденныеДолжности_ Цикл
				ДолжностьСсылка_ = ФедеральныеВебСервисыПереопределяемый.ПолучитьСсылкуПоКодуМинздрава(Должность_.КодМинздраваДолжности, "1.2.643.5.1.13.13.11.1002");
				Если СписокДолжностей_ = "" Тогда
					СписокДолжностей_ = Строка(ДолжностьСсылка_);
				Иначе
					СписокДолжностей_ = СписокДолжностей_ + "," + ДолжностьСсылка_;
				КонецЕсли;
			КонецЦикла;
			
			НоваяСтрокаТЗ_.ТекстСписокДолжностей = СписокДолжностей_;
		Иначе
			НоваяСтрокаТЗ_.СотрудникПодписант = Выборка_.Сотрудник;
			НоваяСтрокаТЗ_.Роль = ФедеральныеВебСервисыРЭМД.ПолучитьРольСотрудникаПриПодписанииПоДокументу(ДокументИнформационнойБазы, Выборка_.Сотрудник);
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоПодписей_ = 0 Тогда
		НоваяСтрокаТЗ_ = ЭтотОбъект.СписокПодписей.Добавить();
		НоваяСтрокаТЗ_.ТекстЗаписиОтсутствуют = "отсутствуют";
	Иначе
		СтрокаТЗЗаголовок1_.ТекстЗаголовка = стрЗаменить(СтрокаТЗЗаголовок1_.ТекстЗаголовка, "(0)", "(" + КоличествоПодписей_ + ")");
	КонецЕсли;
		
	// Получим поставленные подписи.
	СтрокаТЗЗаголовок2_ = ЭтотОбъект.СписокПодписей.Добавить();
	СтрокаТЗЗаголовок2_.ТекстЗаголовка = "Поставленные подписи (0):";
	СтрокаТЗЗаголовок2_.ЭтоЗоголовок = Истина;

	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ
		|	ФедеральныеВебСервисыЭМДЭлектронныеПодписиЭМД.Сотрудник КАК Сотрудник,
		|	ФедеральныеВебСервисыЭМДЭлектронныеПодписиЭМД.РольРЭМД КАК РольРЭМД,
		|	ФедеральныеВебСервисыЭМДЭлектронныеПодписиЭМД.ЭтоПодписьМО КАК ЭтоПодписьМО
		|ИЗ
		|	Справочник.ФедеральныеВебСервисыЭМД.ЭлектронныеПодписиЭМД КАК ФедеральныеВебСервисыЭМДЭлектронныеПодписиЭМД
		|ГДЕ
		|	НЕ ФедеральныеВебСервисыЭМДЭлектронныеПодписиЭМД.Ссылка.ПометкаУдаления
		|	И ФедеральныеВебСервисыЭМДЭлектронныеПодписиЭМД.Ссылка.ДокументИнформационнойБазы = &ДокументИнформационнойБазы
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник";

	Запрос_.УстановитьПараметр("ДокументИнформационнойБазы", ДокументИнформационнойБазы);
	Выборка_ = Запрос_.Выполнить().Выбрать();
	
	КоличествоПодписей_ = 0;
	Пока Выборка_.Следующий() Цикл
		НоваяСтрокаТЗ_ = ЭтотОбъект.СписокПодписей.Добавить();
		НоваяСтрокаТЗ_.ЭтоОжиданиеПодписи = Ложь;
		НоваяСтрокаТЗ_.СотрудникПодписант = Выборка_.Сотрудник;
		НоваяСтрокаТЗ_.Роль = Выборка_.РольРЭМД;
		НоваяСтрокаТЗ_.ЭтоПодписьМО = Выборка_.ЭтоПодписьМО;
		
		Если Выборка_.ЭтоПодписьМО Тогда
			НоваяСтрокаТЗ_.ТекстПодписьМО = "Подпись МО";
			НоваяСтрокаТЗ_.СотрудникПодписант = Неопределено;
		КонецЕсли;
		
		КоличествоПодписей_ = КоличествоПодписей_ + 1;
	КонецЦикла;
	
	Если КоличествоПодписей_ = 0 Тогда
		НоваяСтрокаТЗ_ = ЭтотОбъект.СписокПодписей.Добавить();
		НоваяСтрокаТЗ_.ТекстЗаписиОтсутствуют = "отсутствуют";
	Иначе
		СтрокаТЗЗаголовок2_.ТекстЗаголовка = стрЗаменить(СтрокаТЗЗаголовок2_.ТекстЗаголовка, "(0)", "(" + КоличествоПодписей_ + ")");
	КонецЕсли;
КонецПроцедуры


#КонецОбласти