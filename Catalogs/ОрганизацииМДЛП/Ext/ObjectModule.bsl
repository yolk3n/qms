#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не СобственнаяОрганизация Тогда
		НепроверяемыеРеквизиты.Добавить("ИдентификаторКлиента");
		НепроверяемыеРеквизиты.Добавить("СекретКлиента");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПометкаУдаления Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОрганизацииМДЛП.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ОрганизацииМДЛП КАК ОрганизацииМДЛП
		|ГДЕ
		|	ОрганизацииМДЛП.РегистрационныйНомерУчастника = &РегистрационныйНомерУчастника
		|	И НЕ ОрганизацииМДЛП.ПометкаУдаления
		|	И ОрганизацииМДЛП.Ссылка <> &Ссылка
		|");
		Запрос.УстановитьПараметр("РегистрационныйНомерУчастника", РегистрационныйНомерУчастника);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Регистрационный номер участника %1 уже указан для организации %2'"), РегистрационныйНомерУчастника, Выборка.Ссылка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли