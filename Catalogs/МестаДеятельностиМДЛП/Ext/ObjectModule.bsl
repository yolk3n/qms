#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПометкаУдаления Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	МестаДеятельностиМДЛП.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.МестаДеятельностиМДЛП КАК МестаДеятельностиМДЛП
		|ГДЕ
		|	МестаДеятельностиМДЛП.Идентификатор = &Идентификатор
		|	И НЕ МестаДеятельностиМДЛП.ПометкаУдаления
		|	И МестаДеятельностиМДЛП.Ссылка <> &Ссылка
		|");
		Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Идентификатор %1 уже указан для места деятельности %2'"), Идентификатор, Выборка.Ссылка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли