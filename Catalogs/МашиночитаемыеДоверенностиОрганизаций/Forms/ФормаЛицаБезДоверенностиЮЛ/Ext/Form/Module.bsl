
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Параметры.СтруктураДанных;
	
	Если СтруктураДанных.Свойство("ТолькоПросмотрФормы") И СтруктураДанных.ТолькоПросмотрФормы Тогда
		Элементы.ЛицоБезДовЮЛ_НаимОрг.ТолькоПросмотр 	= Истина;
		Элементы.ЛицоБезДовЮЛ_ИНН.ТолькоПросмотр 		= Истина;
		Элементы.ЛицоБезДовЮЛ_КПП.ТолькоПросмотр 		= Истина;
		Элементы.ЛицоБезДовЮЛ_ОГРН.ТолькоПросмотр 		= Истина;
		Элементы.ФормаКнопкаСохранить.Доступность 		= Ложь;
	КонецЕсли;
	
	Если СтруктураДанных <> Неопределено Тогда
		ЛицоБезДовЮЛ_НаимОрг 	= СтруктураДанных.ЛицоБезДовЮЛ_НаимОрг;
		ЛицоБезДовЮЛ_ИНН 		= СтруктураДанных.ЛицоБезДовЮЛ_ИНН;
		ЛицоБезДовЮЛ_КПП 		= СтруктураДанных.ЛицоБезДовЮЛ_КПП;
		ЛицоБезДовЮЛ_ОГРН 		= СтруктураДанных.ЛицоБезДовЮЛ_ОГРН;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если НЕ СохранениеВозможно() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ЛицоБезДовЮЛ_НаимОрг", 	ЛицоБезДовЮЛ_НаимОрг);
	СтруктураДанных.Вставить("ЛицоБезДовЮЛ_ИНН", 		ЛицоБезДовЮЛ_ИНН);
	СтруктураДанных.Вставить("ЛицоБезДовЮЛ_КПП", 		ЛицоБезДовЮЛ_КПП);
	СтруктураДанных.Вставить("ЛицоБезДовЮЛ_ОГРН", 		ЛицоБезДовЮЛ_ОГРН);
	
	Закрыть(СтруктураДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция СохранениеВозможно()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(ЛицоБезДовЮЛ_НаимОрг) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задано наименование организации.'"),,
			"ЛицоБезДовЮЛ_НаимОрг",, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЛицоБезДовЮЛ_ИНН) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задан ИНН организации.'"),,
			"ЛицоБезДовЮЛ_ИНН",, Отказ);
	ИначеЕсли НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЛицоБезДовЮЛ_ИНН) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Указан некорректный ИНН организации.'"),,
			"ЛицоБезДовЮЛ_ИНН",, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЛицоБезДовЮЛ_КПП) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задан КПП организации.'"),,
			"ЛицоБезДовЮЛ_КПП",, Отказ);
	ИначеЕсли СтрДлина(ЛицоБезДовЮЛ_КПП) <> 9 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Указан некорректный КПП организации.'"),,
			"ЛицоБезДовЮЛ_КПП",, Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЛицоБезДовЮЛ_ОГРН) И СтрДлина(ЛицоБезДовЮЛ_ОГРН) <> 13 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Указан некорректный ОГРН организации.'"),,
			"ЛицоБезДовЮЛ_ОГРН",, Отказ);
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

#КонецОбласти
