
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ДокументВид.СписокВыбора.Очистить();
	ВидыДокументов = МашиночитаемыеДоверенностиКлиентСервер.ВидыДокументовФизическихЛиц();
	Для Каждого ВидДокумента Из ВидыДокументов Цикл
		Элементы.ДокументВид.СписокВыбора.Добавить(ВидДокумента.Ключ, ВидДокумента.Значение);
	КонецЦикла;
	
	СтруктураДанных = Параметры.СтруктураДанных;
	
	Если СтруктураДанных <> Неопределено Тогда
		ДокументВид 				= СтруктураДанных.ДокументВид;
		ДокументДатаВыдачи 			= СтруктураДанных.ДокументДатаВыдачи;
		ДокументКемВыдан 			= СтруктураДанных.ДокументКемВыдан;
		ДокументКодПодразделения 	= СтруктураДанных.ДокументКодПодразделения;
		ДокументНомер 				= СтруктураДанных.ДокументНомер;
		ДокументСерия 				= СтруктураДанных.ДокументСерия;
	КонецЕсли;
	
	Если СтруктураДанных.Свойство("ТолькоПросмотрФормы") И СтруктураДанных.ТолькоПросмотрФормы Тогда
		Элементы.ДокументВид.ТолькоПросмотр = Истина;
		Элементы.ДокументСерия.ТолькоПросмотр = Истина;
		Элементы.ДокументНомер.ТолькоПросмотр = Истина;
		Элементы.ДокументДатаВыдачи.ТолькоПросмотр = Истина;
		Элементы.ДокументКемВыдан.ТолькоПросмотр = Истина;
		Элементы.ДокументКодПодразделения.ТолькоПросмотр = Истина;
		Элементы.ФормаКнопкаСохранить.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если НЕ СохранениеВозможно() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ДокументВид", 				ДокументВид);
	СтруктураДанных.Вставить("ДокументСерия", 				ДокументСерия);
	СтруктураДанных.Вставить("ДокументНомер", 				ДокументНомер);
	СтруктураДанных.Вставить("ДокументДатаВыдачи", 			ДокументДатаВыдачи);
	СтруктураДанных.Вставить("ДокументКемВыдан", 			ДокументКемВыдан);
	СтруктураДанных.Вставить("ДокументКодПодразделения",	ДокументКодПодразделения);
	
	Закрыть(СтруктураДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция СохранениеВозможно()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(ДокументВид) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не задан вид документа.'"), , "ДокументВид", , Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументВид) Тогда
		КодВидаДокумента = ПолучитьКодВидаДокументаФизическогоЛица(ДокументВид);
		ЭтоНеподдерживаемыйВидДокумента =
			КодВидаДокумента <> "07" И КодВидаДокумента <> "10" И КодВидаДокумента <> "11" И КодВидаДокумента <> "12"
			И КодВидаДокумента <> "13" И КодВидаДокумента <> "15" И КодВидаДокумента <> "19" И КодВидаДокумента <> "21"
			И КодВидаДокумента <> "24"; 
		Если ЭтоНеподдерживаемыйВидДокумента Тогда
			ТекстСообщения = НСтр("ru = 'Вид документа не поддерживается.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ДокументВид",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(ДокументСерия) + СокрЛП(ДокументНомер)) Тогда
		ТекстСообщения = НСтр("ru = 'Не заданы серия и номер документа.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ДокументСерия", , Отказ);
	КонецЕсли;
	
	МаксимальноДопустимаяДлинаНомераДокумента = 25;
	Если СтрДлина(СокрЛП(ДокументСерия) + СокрЛП(ДокументНомер)) > МаксимальноДопустимаяДлинаНомераДокумента Тогда
		ТекстСообщения = НСтр("ru = 'Длина серии и номера документа больше %1 символов.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, МаксимальноДопустимаяДлинаНомераДокумента);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ДокументСерия", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументДатаВыдачи) Тогда
		ТекстСообщения = НСтр("ru = 'Не задана дата выдачи документа.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ДокументДатаВыдачи", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументКемВыдан) И КодВидаДокумента = "21" Тогда
		ТекстСообщения = НСтр("ru = 'Не задано наименование органа, выдавшего документ.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ДокументКемВыдан", , Отказ);
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКодВидаДокументаФизическогоЛица(ДокументВид)
	
	Если Не ЗначениеЗаполнено(ДокументВид) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДокументВид;
	
КонецФункции

#КонецОбласти
