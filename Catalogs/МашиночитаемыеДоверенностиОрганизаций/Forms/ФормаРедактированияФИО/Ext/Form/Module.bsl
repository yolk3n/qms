
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокСвойств = Неопределено;
	Если Параметры.Свойство("СписокСвойств", СписокСвойств) И ЗначениеЗаполнено(СписокСвойств) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокСвойств);
	КонецЕсли;
	
	Если Параметры.Свойство("ТолькоПросмотрФормы") И Параметры.ТолькоПросмотрФормы Тогда
		Элементы.Фамилия.ТолькоПросмотр = Истина;
		Элементы.Имя.ТолькоПросмотр = Истина;
		Элементы.Отчество.ТолькоПросмотр = Истина;
		Элементы.ЗакрытьССохранением.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьБезСохранения(Команда)
	
	ВозвращаемоеЗначение = НеОпределено;
	Модифицированность = Ложь;
	Закрыть(ВозвращаемоеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьССохранением(Команда)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("Фамилия", Фамилия);
	ВозвращаемоеЗначение.Вставить("Имя", Имя);
	ВозвращаемоеЗначение.Вставить("Отчество", Отчество);
	
	Модифицированность = Ложь;
	Закрыть(ВозвращаемоеЗначение);
	
КонецПроцедуры

#КонецОбласти
