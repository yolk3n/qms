#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	ВидНоменклатуры = Справочники.ВидыНоменклатуры.ПустаяСсылка();
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ВидНоменклатуры");
	ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.ВидыНоменклатуры") Тогда
		ВидНоменклатуры = Владелец;
	КонецЕсли;
	
	ПараметрыСерий = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПараметрыСерийНоменклатуры(ВидНоменклатуры);
	
	Если ПараметрыСерий.ИспользоватьНомерСерии <> Истина Тогда 
		НепроверяемыеРеквизиты.Добавить("Номер");
	КонецЕсли;
	
	Если ПараметрыСерий.ИспользоватьСрокГодностиСерии <> Истина Тогда
		НепроверяемыеРеквизиты.Добавить("ГоденДо");
	КонецЕсли;
		
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ВидНоменклатуры = Справочники.ВидыНоменклатуры.ПустаяСсылка();
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ВидНоменклатуры");
	ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.ВидыНоменклатуры") Тогда
		ВидНоменклатуры = Владелец;
	КонецЕсли;
	
	ПараметрыСерий = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПараметрыСерийНоменклатуры(ВидНоменклатуры);
	
	Номер = СокрЛП(Номер);
	Если ПараметрыСерий.ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоЧасов Тогда
		ГоденДо = НачалоЧаса(ГоденДо);
	Иначе
		ГоденДо = НачалоДня(ГоденДо);
	КонецЕсли;
	
	Наименование = Справочники.СерииНоменклатуры.ПредставлениеСерии(ПараметрыСерий, ЭтотОбъект);
	
	Если Не ПометкаУдаления И Константы.КонтролироватьУникальностьРабочегоНаименованияНоменклатурыИСерии.Получить() Тогда
		ПолноеИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(Ссылка);
		Дубли = ПоискИУдалениеДублей.НайтиДублиЭлемента(ПолноеИмяТаблицы, ЭтотОбъект, Новый Структура("Режим", "КонтрольПоНаименованию"));
		Если Дубли.Количество() > 0 Тогда
			ТекстИсключения = НСтр("ru = 'Значение поля ""Наименование"" не уникально.'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#КонецЕсли