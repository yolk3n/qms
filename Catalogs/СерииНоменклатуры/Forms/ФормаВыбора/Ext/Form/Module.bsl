
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеФормы();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		ВыбранныеСтроки = Значение;
	Иначе
		ВыбранныеСтроки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Значение);
	КонецЕсли;
	
	ЕстьЗабракованныеСерии = Ложь;
	Для Каждого Серия Из ВыбранныеСтроки Цикл
		ТекущаяСтрока = Элементы.Список.ДанныеСтроки(Серия);
		Если ТекущаяСтрока.Забракована Тогда
			ЕстьЗабракованныеСерии = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьЗабракованныеСерии Тогда
		СтандартнаяОбработка = Ложь;
		Если ВыбранныеСтроки.Количество() = 1 Тогда
			ТекстВопроса = НСтр("ru='Выбранная серия забракована.
									|Выполнить выбор этой серии?'");
		Иначе
			ТекстВопроса = НСтр("ru='Среди выбранных серий есть забракованые.
									|Выполнить выбор этих серий?'");
		КонецЕсли;
		Обработчик = Новый ОписаниеОповещения("ОбработатьВыборЗабракованнойСерии", ЭтотОбъект, Значение);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	УсловноеОформлениеСписка = ОбщегоНазначенияБольничнаяАптекаКлиентСервер.ПолучитьУсловноеОформлениеДинамическогоСписка(
		Список,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный);
	
	// Цвет текста, Шрифт в строках списка Список
	Элемент = УсловноеОформлениеСписка.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Список: серия забракована'");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Забракована", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.СерияЗабракованаЦветТекста);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.СерияЗабракованаШрифт);
	
КонецПроцедуры

// Возвращает выбранное значение забракованной серии владельцу формы.
// Продолжение процедуры СписокВыбор.
//
// Параметры:
//  Ответ          - КодВозвратаДиалога
//  ВыбраннаяСерия - СправочникСсылка.СерииНоменклатуры, Массив - выбранная серия номенклатуры
//
&НаКлиенте
Процедура ОбработатьВыборЗабракованнойСерии(Ответ, ВыбраннаяСерия) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОповеститьОВыборе(ВыбраннаяСерия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
