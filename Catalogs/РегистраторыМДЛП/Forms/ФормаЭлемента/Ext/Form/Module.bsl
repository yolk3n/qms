
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолучитьИнформациюОбУстройстве(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Адрес) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Адрес'")),
			,
			"Объект.Адрес");
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ОбработатьПолучениеИнформацииОбУстройстве", ЭтотОбъект);
	ПараметрыПодключения = ТранспортМДЛПКлиентСервер.ПолучитьПараметрыСервера(Объект.Адрес);
	ПараметрыПодключения.Вставить("Регистратор", Объект.Наименование);
	ПараметрыПодключения.Вставить("ПрефиксВерсии", "/v1/");
	ТранспортМДЛПРВКлиент.ВыполнитьАвторизованныйЗапрос(ПараметрыПодключения, Обработчик, "ПолучитьИнформациюОбУстройстве");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолучениеИнформацииОбУстройстве(Результат, _) Экспорт
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.ОписаниеОшибки;
	КонецЕсли;
	
	Если Результат.Данные.Количество() = 1 Тогда
		ЗаполнитьЗначенияСвойств(Объект, Результат.Данные[0]);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
