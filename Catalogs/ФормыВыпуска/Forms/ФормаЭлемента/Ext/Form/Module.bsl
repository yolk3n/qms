
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииНовогоПриЧтенииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииНовогоПриЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ВариантПараметров = "НормализованныеДанные" Тогда
		ТекущийОбъект.НеНормализованныеДанные = "";
	Иначе
		ТекущийОбъект.Количество = 0;
		ТекущийОбъект.ЕдиницаИзмерения = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьНаименованиеПоШаблону(Команда)
	
	СформироватьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПолноеНаименованиеПоШаблону(Команда)
	
	СформироватьПолноеНаименование();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЛекарственнаяФормаПриИзменении(Элемент)
	
	СформироватьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПараметровПриИзменении(Элемент)
	
	УстановитьДоступностьПараметровФормыВыпуска(ЭтотОбъект);
	СформироватьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура НеНормализованныеДанныеПриИзменении(Элемент)
	
	СформироватьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	СформироватьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	СформироватьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	
	СформироватьНаименование();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНовогоПриЧтенииНаСервере()
	
	ВариантПараметров = "НеНормализованныеДанные";
	Если Не Объект.ЕдиницаИзмерения.Пустая() Тогда
		ВариантПараметров = "НормализованныеДанные";
	КонецЕсли;
	
	УстановитьДоступностьПараметровФормыВыпуска(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьПараметровФормыВыпуска(Форма)
	
	ДанныеНормализованы = (Форма.ВариантПараметров = "НормализованныеДанные");
	Форма.Элементы.НеНормализованныеДанные.Доступность = Не ДанныеНормализованы;
	Форма.Элементы.Количество.Доступность       =ДанныеНормализованы;
	Форма.Элементы.ЕдиницаИзмерения.Доступность =ДанныеНормализованы;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьНаименование()
	
	Наименование = СокрЛП(Объект.ЛекарственнаяФорма);
	
	Если ВариантПараметров = "НормализованныеДанные" Тогда
		Наименование = Наименование + ", " + Формат(Объект.Количество, "ЧРД=.") + " " + СокрЛП(Объект.ЕдиницаИзмерения);
	Иначе
		Если Не ПустаяСтрока(Объект.НеНормализованныеДанные) Тогда
			МаксимальнаяДлинаНаименования = 150;
			РасчетноеНаименование = Наименование + " " + СокрЛП(Объект.НеНормализованныеДанные) + " " + СокрЛП(Объект.Характеристика);
			Если СтрДлина(РасчетноеНаименование) <= МаксимальнаяДлинаНаименования Тогда
				Наименование = Наименование + " " + СокрЛП(Объект.НеНормализованныеДанные);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Характеристика) Тогда
		Наименование = Наименование + " " + СокрЛП(Объект.Характеристика);
	КонецЕсли;
	
	Объект.Наименование = Наименование;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПолноеНаименование()
	
	НаименованиеПолное = СокрЛП(Объект.ЛекарственнаяФорма);
	
	Если ВариантПараметров = "НормализованныеДанные" Тогда
		НаименованиеПолное = НаименованиеПолное + ", " + Формат(Объект.Количество, "ЧРД=.") + " " + СокрЛП(Объект.ЕдиницаИзмерения);
	Иначе
		НаименованиеПолное = НаименованиеПолное + ?(ПустаяСтрока(Объект.НеНормализованныеДанные), "", ", ") + СокрЛП(Объект.НеНормализованныеДанные);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Характеристика) Тогда
		НаименованиеПолное = НаименованиеПолное + " " + СокрЛП(Объект.Характеристика);
	КонецЕсли;
	
	Объект.НаименованиеПолное = НаименованиеПолное;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

///////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовБольничнаяАптекаКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти // СтандартныеПодсистемы
