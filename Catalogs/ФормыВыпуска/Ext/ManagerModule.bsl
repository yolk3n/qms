#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Возвращает сформированное наименование формы выпуска
//
// Параметры:
//  ФормаВыпуска - СправочникОбъект.ФормаВыпуска - Форма выпуска, для которой необходимо сформировать наименование
//
// Возвращаемое значение:
//  Наименование - Строка - сформированное наименование
//
Функция СформироватьНаименование(ФормаВыпуска) Экспорт
	
	Наименование = СокрЛП(ФормаВыпуска.ЛекарственнаяФорма);
	
	Если ЗначениеЗаполнено(ФормаВыпуска.ЕдиницаИзмерения) Тогда
		Наименование = Наименование + " " + Формат(ФормаВыпуска.Количество, "ЧРД=.") + " " + СокрЛП(ФормаВыпуска.ЕдиницаИзмерения);
	Иначе
		Если Не ПустаяСтрока(ФормаВыпуска.НеНормализованныеДанные) Тогда
			МаксимальнаяДлинаНаименования = 150;
			РасчетноеНаименование = Наименование + " " + СокрЛП(ФормаВыпуска.НеНормализованныеДанные) + " " + СокрЛП(ФормаВыпуска.Характеристика);
			Если СтрДлина(РасчетноеНаименование) <= МаксимальнаяДлинаНаименования Тогда
				Наименование = Наименование + " " + СокрЛП(ФормаВыпуска.НеНормализованныеДанные);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФормаВыпуска.Характеристика) Тогда
		Наименование = Наименование + " " + СокрЛП(ФормаВыпуска.Характеристика);
	КонецЕсли;
	
	Возврат Наименование;
	
КонецФункции

// Возвращает сформированное полное наименование формы выпуска
//
// Параметры:
//  ФормаВыпуска - СправочникОбъект.ФормаВыпуска - Форма выпуска, для которой необходимо сформировать полное наименование
//
// Возвращаемое значение:
//  НаименованиеПолное - Строка - сформированное полное наименование
//
Функция СформироватьПолноеНаименование(ФормаВыпуска) Экспорт
	
	НаименованиеПолное = СокрЛП(ФормаВыпуска.ЛекарственнаяФорма);
	
	Если ЗначениеЗаполнено(ФормаВыпуска.ЕдиницаИзмерения) Тогда
		НаименованиеПолное = НаименованиеПолное + " " + Формат(ФормаВыпуска.Количество, "ЧРД=.") + " " + СокрЛП(ФормаВыпуска.ЕдиницаИзмерения);
	Иначе
		НаименованиеПолное = НаименованиеПолное + ?(ПустаяСтрока(ФормаВыпуска.НеНормализованныеДанные), "", " ") + СокрЛП(ФормаВыпуска.НеНормализованныеДанные);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФормаВыпуска.Характеристика) Тогда
		НаименованиеПолное = НаименованиеПолное + " " + СокрЛП(ФормаВыпуска.Характеристика);
	КонецЕсли;
	
	Возврат НаименованиеПолное;
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМ
#Область СтандартныеПодсистемы

// Возвращает описание блокируемых реквизитов
//
// Возвращаемое значение:
//  Массив - имена блокируемых реквизитов
//   Элемент массива - Строка в формате:
//     ИмяРеквизита[;ИмяЭлементаФормы,...]
//      где
//       ИмяРеквизита     - имя реквизита объекта
//       ИмяЭлементаФормы - имя элемента формы, связанного с реквизитом
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	БлокируемыеРеквизиты.Добавить("ЛекарственнаяФорма");
	БлокируемыеРеквизиты.Добавить("НеНормализованныеДанные;ВариантПараметровНеНормализованныеДанные");
	БлокируемыеРеквизиты.Добавить("ЕдиницаИзмерения;ВариантПараметровКоличество");
	БлокируемыеРеквизиты.Добавить("Количество;ВариантПараметровКоличество");
	БлокируемыеРеквизиты.Добавить("Характеристика");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

#КонецОбласти // СтандартныеПодсистемы

#КонецЕсли