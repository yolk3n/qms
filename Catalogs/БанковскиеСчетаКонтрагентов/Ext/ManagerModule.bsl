#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Функция определяет банковский счет выбранного контрагента или физического лица.
//
// Возвращает банковский счет получателя, если найден один банковский счет.
// Возвращает Неопределено, если банковский счет не найден или счетов больше одного
//
// Параметры:
//  Получатель - СправочникСсылка.Контрагенты или СправочникСсылка.ФизическиеЛица - Ссылка на получателя
//
// Возвращаемое значение:
//	СправочникСсылка.БанковскиеСчетаПолучателей - Найденный банковский счет получателя
//
Функция ПолучитьБанковскийСчетПоУмолчанию(Получатель, Валюта = Неопределено) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 2
	|	БанковскиеСчета.Ссылка КАК БанковскийСчетПолучателя
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчета
	|ГДЕ
	|	НЕ БанковскиеСчета.ПометкаУдаления
	|	И БанковскиеСчета.Владелец = &Получатель
	|	И (БанковскиеСчета.ВалютаДенежныхСредств = &Валюта
	|		ИЛИ &Валюта = Неопределено)
	|");
	
	Запрос.УстановитьПараметр("Получатель", Получатель);
	Запрос.УстановитьПараметр("Валюта", ?(ЗначениеЗаполнено(Валюта), Валюта, Неопределено));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1
	   И Выборка.Следующий() Тогда
		БанковскийСчетПолучателя = Выборка.БанковскийСчетПолучателя;
	Иначе
		БанковскийСчетПолучателя = Неопределено;
	КонецЕсли;
	
	Возврат БанковскийСчетПолучателя;

КонецФункции

// Функция проверяет соответствие владельца банковского счета выбранному контрагенту или физическому лицу.
//
// Параметры:
//  БанковскийСчет - СправочникСсылка.БанковскиеСчетаПолучателей - Ссылка на банковский счет
//  Владелец - СправочникСсылка.Контрагенты или СправочникСсылка.ФизическиеЛица - Ссылка на владельца
//
// Возвращаемое значение:
//	Булево - Признак принадлежности банковского счета указанному владельцу
//
Функция ПроверитьВладельцаБанковскогоСчета(БанковскийСчет, Владелец) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	БанковскиеСчетаПолучателей.Ссылка КАК БанковскийСчетПолучателя
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаПолучателей
	|ГДЕ
	|	БанковскиеСчетаПолучателей.Ссылка = &БанковскийСчет
	|	И БанковскиеСчетаПолучателей.Владелец = &Владелец
	|");
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		СчетСоответствуетВладельцу = Ложь;
	Иначе
		СчетСоответствуетВладельцу = Истина;
	КонецЕсли;
	
	Возврат СчетСоответствуетВладельцу;

КонецФункции

// Функция получает реквизиты банковского счета.
//
// Параметры:
//  БанковскийСчетОрганизации - СправочникСсылка.БанковскиеСчетаОрганизаций - Ссылка на банковский счет
//
// Возвращаемое значение:
//	Структура - Организация и валюта банковского счета
//
Функция ПолучитьРеквизитыБанковскогоСчета(БанковскийСчет) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	БанковскиеСчета.ВалютаДенежныхСредств  КАК Валюта,
	|	БанковскиеСчета.ТекстНазначения        КАК ТекстНазначения,
	|	БанковскиеСчета.НомерСчета             КАК НомерСчета,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.НаименованиеБанка
	|		ИНАЧЕ БанковскиеСчета.Банк
	|	КОНЕЦ                                  КАК Банк,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.НаименованиеБанка
	|		ИНАЧЕ БанковскиеСчета.Банк.Наименование
	|	КОНЕЦ                                  КАК НаименованиеБанка,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.БИКБанка
	|		ИНАЧЕ БанковскиеСчета.Банк.Код
	|	КОНЕЦ                                  КАК БИК,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.КоррСчетБанка
	|		ИНАЧЕ БанковскиеСчета.Банк.КоррСчет
	|	КОНЕЦ                                  КАК КоррСчет,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.АдресБанка
	|		ИНАЧЕ БанковскиеСчета.Банк.Адрес
	|	КОНЕЦ                                  КАК АдресБанка,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов
	|			ТОГДА БанковскиеСчета.НаименованиеБанкаДляРасчетов
	|		ИНАЧЕ БанковскиеСчета.БанкДляРасчетов
	|	КОНЕЦ                                  КАК БанкДляРасчетов,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов
	|			ТОГДА БанковскиеСчета.НаименованиеБанкаДляРасчетов
	|		ИНАЧЕ БанковскиеСчета.БанкДляРасчетов.Наименование
	|	КОНЕЦ                                  КАК НаименованиеБанкаДляРасчетов,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов
	|			ТОГДА БанковскиеСчета.БИКБанкаДляРасчетов
	|		ИНАЧЕ БанковскиеСчета.БанкДляРасчетов.Код
	|	КОНЕЦ                                  КАК БИКБанкаДляРасчетов,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов
	|			ТОГДА БанковскиеСчета.КоррСчетБанкаДляРасчетов
	|		ИНАЧЕ БанковскиеСчета.БанкДляРасчетов.КоррСчет
	|	КОНЕЦ                                  КАК КоррСчетБанкаДляРасчетов,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов
	|			ТОГДА БанковскиеСчета.АдресБанкаДляРасчетов
	|		ИНАЧЕ БанковскиеСчета.БанкДляРасчетов.Адрес
	|	КОНЕЦ                                  КАК АдресБанкаДляРасчетов
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Ссылка = &БанковскийСчет
	|");
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстНазначения              = Выборка.ТекстНазначения;
		НомерСчета                   = Выборка.НомерСчета;
		Банк                         = Выборка.Банк;
		НаименованиеБанка            = Выборка.НаименованиеБанка;
		БИК                          = Выборка.БИК;
		КоррСчет                     = Выборка.КоррСчет;
		АдресБанка                   = Выборка.АдресБанка;
		БанкДляРасчетов              = Выборка.БанкДляРасчетов;
		НаименованиеБанкаДляРасчетов = Выборка.НаименованиеБанка;
		БИКБанкаДляРасчетов          = Выборка.БИКБанкаДляРасчетов;
		КоррСчетБанкаДляРасчетов     = Выборка.КоррСчетБанкаДляРасчетов;
		АдресБанкаДляРасчетов        = Выборка.АдресБанкаДляРасчетов;
	Иначе
		ТекстНазначения              = "";
		НомерСчета                   = "";
		Банк                         = Неопределено;
		НаименованиеБанка            = "";
		БИК                          = "";
		КоррСчет                     = "";
		АдресБанка                   = "";
		БанкДляРасчетов              = Неопределено;
		НаименованиеБанкаДляРасчетов = "";
		БИКБанкаДляРасчетов          = "";
		КоррСчетБанкаДляРасчетов     = "";
		АдресБанкаДляРасчетов        = "";
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ТекстНазначения"             , ТекстНазначения);
	СтруктураРеквизитов.Вставить("НомерСчета"                  , НомерСчета);
	СтруктураРеквизитов.Вставить("Банк"                        , Банк);
	СтруктураРеквизитов.Вставить("НаименованиеБанка"           , НаименованиеБанка);
	СтруктураРеквизитов.Вставить("БИК"                         , БИК);
	СтруктураРеквизитов.Вставить("КоррСчет"                    , КоррСчет);
	СтруктураРеквизитов.Вставить("АдресБанка"                  , АдресБанка);
	СтруктураРеквизитов.Вставить("БанкДляРасчетов"             , БанкДляРасчетов);
	СтруктураРеквизитов.Вставить("НаименованиеБанкаДляРасчетов", НаименованиеБанкаДляРасчетов);
	СтруктураРеквизитов.Вставить("БИКБанкаДляРасчетов"         , БИКБанкаДляРасчетов);
	СтруктураРеквизитов.Вставить("КоррСчетБанкаДляРасчетов"    , КоррСчетБанкаДляРасчетов);
	СтруктураРеквизитов.Вставить("АдресБанкаДляРасчетов"       , АдресБанкаДляРасчетов);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Команды формы
#Область КомандыФормы

// Заполняет список команд ввода на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании, НастройкиФормы) Экспорт
	
	ВводНаОснованииБольничнаяАптека.ДобавитьКомандыСозданияНаОсновании(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыСоздатьНаОсновании, НастройкиФормы);
	
КонецПроцедуры

#КонецОбласти // КомандыФормы

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// Возвращает описание блокируемых реквизитов
//
// Возвращаемое значение:
//  Массив - имена блокируемых реквизитов
//   Элемент массива - Строка в формате:
//     ИмяРеквизита[;ИмяЭлементаФормы,...]
//      где
//       ИмяРеквизита     - имя реквизита объекта
//       ИмяЭлементаФормы - имя элемента формы, связанного с реквизитом
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("ВалютаДенежныхСредств");
	Результат.Добавить("Владелец");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти // СтандартныеПодсистемы

#КонецЕсли