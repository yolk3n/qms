#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипЗначения = ТипЗнч(ДанныеЗаполнения);
	Если ТипЗначения = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Номенклатура", Номенклатура) И ЗначениеЗаполнено(Номенклатура) Тогда
			Основная = Не СуществуетОсновнаяПропись(Номенклатура);
		КонецЕсли;
	КонецЕсли;
	
	ИнициализироватьСправочник();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Статус = Перечисления.СтатусыЛекарственныхПрописей.ВРазработке Тогда
		ПроверяемыеРеквизиты.Очистить();
		Возврат;
	КонецЕсли;
	
	МетаданныеОбъекта = Метаданные();
	Для Каждого СтрокаПрописи Из Товары Цикл
		
		Если Не (ЗначениеЗаполнено(СтрокаПрописи.ДействующиеВеществаМНН) Или ЗначениеЗаполнено(СтрокаПрописи.ТорговоеНаименование)
				Или ЗначениеЗаполнено(СтрокаПрописи.Номенклатура)) Тогда
			
			ТекстСообщения = НСтр("ru='Не заполнена колонка ""Действующие вещества (МНН)"" или ""Торговое наименование"" или ""Номенклатура"" в строке %НомерСтроки% списка ""%ИмяТабличнойЧасти%""'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", СтрокаПрописи.НомерСтроки);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяТабличнойЧасти%", МетаданныеОбъекта.ТабличныеЧасти.Товары.Синоним);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект, 
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаПрописи.НомерСтроки, "ДействующиеВеществаМНН"),,
				Отказ);
				
		КонецЕсли;
			
		Если (ЗначениеЗаполнено(СтрокаПрописи.ДействующиеВеществаМНН) Или ЗначениеЗаполнено(СтрокаПрописи.ТорговоеНаименование))
				И Не ЗначениеЗаполнено(СтрокаПрописи.ФормаВыпуска) Тогда
				
			ТекстСообщения = НСтр("ru='Не заполнена колонка ""ФормаВыпуска"" в строке %НомерСтроки% списка ""%ИмяТабличнойЧасти%""'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", СтрокаПрописи.НомерСтроки);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяТабличнойЧасти%", МетаданныеОбъекта.ТабличныеЧасти.Товары.Синоним);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект, 
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаПрописи.НомерСтроки, "ФормаВыпуска"),,
				Отказ);
				
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Статус = Перечисления.СтатусыЛекарственныхПрописей.ВРазработке;
	Основная = Не СуществуетОсновнаяПропись(Номенклатура);
	
	ИнициализироватьСправочник();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Основная И (ЭтоНовый() Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Основная")) Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Ссылка
		|ИЗ
		|	Справочник.ЛекарственныеПрописи
		|ГДЕ
		|	Номенклатура = &Номенклатура
		|	И Основная
		|	И Ссылка <> &Ссылка
		|");
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.Основная = Ложь;
			Объект.Записать();
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Инициализация и заполнение справочника
#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьСправочник()
	
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти // ИнициализацияИЗаполнение

////////////////////////////////////////////////////////////////////////////////
// Прочее
#Область Прочее

Функция СуществуетОсновнаяПропись(Номенклатура)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Справочник.ЛекарственныеПрописи
	|ГДЕ
	|	Номенклатура = &Номенклатура
	|	И Основная
	|");
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти // Прочее

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли