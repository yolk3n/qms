#Область ПрограммныйИнтерфейс


//////////////////////////////////////////////////
//// События формы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Подсистема Архивирование
	АрхивированиеСервер.ФормаЭлементаПриСозданииНаСервере(ЭтотОбъект, , "Архивирование_Статус");
	// конец Подсистема Архивирование
	
	Если ЗначениеЗаполнено(Объект.КодТипаМД) Тогда
		ЗаполнитьПравилаПодписания();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Подсистема Архивирование
	Архивирование.УстановитьВидимостьЭлементовАрхивности(ЭтотОбъект);
	// конец Подсистема Архивирование
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Подсистема Архивирование
	Если ИмяСобытия = Архивирование.ИмяОповещения() Тогда
		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			Поиск_ = Параметр.Найти(Объект.Ссылка);
			Если Поиск_ <> Неопределено Тогда
				Архивирование.УстановитьВидимостьЭлементовАрхивности(ЭтотОбъект);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	// конец Подсистема Архивирование
	
КонецПроцедуры

#КонецОбласти

#Область СлужебеныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПравилаПодписания()

	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ
		|	РолиПриПодписанииРЭМДПравилаПодписи.МинимальноеКоличествоПодписей КАК МинимальноеКоличествоПодписей,
		|	РолиПриПодписанииРЭМДПравилаПодписи.МаксимальноеКоличествоПодписей КАК МаксимальноеКоличествоПодписей,
		|	РолиПриПодписанииРЭМДПравилаПодписи.Ссылка КАК РольРЭМД,
		|	РолиПриПодписанииРЭМДПравилаПодписи.КодМинздраваДолжности КАК КодМинздраваДолжности
		|ИЗ
		|	Справочник.РолиПриПодписанииРЭМД.ПравилаПодписи КАК РолиПриПодписанииРЭМДПравилаПодписи
		|ГДЕ
		|	РолиПриПодписанииРЭМДПравилаПодписи.КодМинздраваВидаДокумента = &КодМинздраваВидаДокумента
		|	И РолиПриПодписанииРЭМДПравилаПодписи.Ссылка.Актуальность = ИСТИНА
		|	И РолиПриПодписанииРЭМДПравилаПодписи.Ссылка.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	РольРЭМД,
		|	КодМинздраваДолжности";
		
	Запрос_.УстановитьПараметр("КодМинздраваВидаДокумента", Объект.КодМинздрава);
	Выборка_ = Запрос_.Выполнить().Выбрать();
	
	Пока Выборка_.Следующий() Цикл
		НоваяСтрокаТЗ_ = ЭтотОбъект.ПравилаПодписания.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТЗ_, Выборка_);
		
		Если ЗначениеЗаполнено(Выборка_.КодМинздраваДолжности) Тогда
			НоваяСтрокаТЗ_.Должность = ФедеральныеВебСервисыПереопределяемый.ПолучитьСсылкуПоКодуМинздрава(Выборка_.КодМинздраваДолжности, "1.2.643.5.1.13.13.11.1002");
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти