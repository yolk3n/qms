#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроверитьДубльПоCodeCodeSystem() = Истина Тогда
		Отказ = Истина;
		
		Сообщение_ = Новый СообщениеПользователю;
		Сообщение_.Текст = СтрШаблон(
			"Тип медицинского докумета с КодМинздрава = %1 и OIDКлассификатораМинздрава = %2 уже существует.",
			ЭтотОбъект.КодМинздрава,
			ЭтотОбъект.OIDКлассификатораМинздрава
		);
		Сообщение_.Сообщить();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры



Функция ПроверитьДубльПоCodeCodeSystem()
	Если ЭтотОбъект.ПометкаУдаления Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	Запрос_ = Новый Запрос(
		"ВЫБРАТЬ
		|	МДТип.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ТипыМДРегистрируемыхВРЭМД КАК МДТип
		|ГДЕ
		|	МДТип.ПометкаУдаления = ЛОЖЬ
		|	И МДТип.OIDКлассификатораМинздрава = &codeSystem
		|	И МДТип.КодМинздрава = &АтрибутCode
		|	И МДТип.Ссылка <> &Ссылка"
	);
	Запрос_.УстановитьПараметр("codeSystem", ЭтотОбъект.OIDКлассификатораМинздрава);
	Запрос_.УстановитьПараметр("АтрибутCode", ЭтотОбъект.КодМинздрава);
	Запрос_.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);

	Результат_ = Запрос_.Выполнить();
	
	Возврат Не Результат_.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли