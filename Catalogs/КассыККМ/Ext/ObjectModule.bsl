#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ВалютаДенежныхСредств = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Склад = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьРозничныйСкладПоУмолчанию(Склад);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
	   И ДанныеЗаполнения.Свойство("Владелец") Тогда
		ЕстьВладелец = Истина;
	Иначе
		ЕстьВладелец = Ложь;
	КонецЕсли;
	
	Если Не ЕстьВладелец Тогда
		Владелец = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьОрганизациюПоУмолчанию(Владелец);
	КонецЕсли;
	
	Если Не ОбщегоНазначенияБольничнаяАптека.ИспользоватьПодключаемоеОборудование() Тогда
		ИспользоватьБезПодключенияОборудования = Истина;
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если ТипКассы = Перечисления.ТипыКассККМ.АвтономнаяККМ
	 Или ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор И ИспользоватьБезПодключенияОборудования Тогда
		
		НепроверяемыеРеквизиты.Добавить("ПодключаемоеОборудование");
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипКассы = Перечисления.ТипыКассККМ.АвтономнаяККМ Тогда
		ИспользоватьБезПодключенияОборудования = Ложь;
		ПодключаемоеОборудование = Неопределено;
		АвтоматическаяИнкассация = Ложь;
	КонецЕсли;
	
	Если ИспользоватьБезПодключенияОборудования Тогда
		ПодключаемоеОборудование = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПодключаемоеОборудование) Тогда
		УстановитьПривилегированныйРежим(Истина);
		Запрос = Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	Справочник.КассыККМ КАК КассыККМ
		|ГДЕ
		|	КассыККМ.ПодключаемоеОборудование = &ПодключаемоеОборудование
		|	И КассыККМ.Ссылка <> &Ссылка
		|");
		
		Запрос.УстановитьПараметр("ПодключаемоеОборудование", ПодключаемоеОборудование);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			ТекстСообщения = НСтр("ru = 'Указанное оборудование уже используется в другой кассе ККМ.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПодключаемоеОборудование",, Отказ);
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#КонецЕсли

