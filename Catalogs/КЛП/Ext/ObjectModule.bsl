#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКИ СОБЫТИЙ
#Область ОбработкиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	НепроверяемыеРеквизиты.Добавить("ТорговоеНаименование");
	НепроверяемыеРеквизиты.Добавить("СМНН");
	НепроверяемыеРеквизиты.Добавить("ДействующиеВеществаМНН");
	
	Если Не ЗначениеЗаполнено(ТорговоеНаименование) И Не ЗначениеЗаполнено(ДействующиеВеществаМНН) Тогда
		ТекстСообщения = НСтр("ru='Необходимо указать ""Торговое наименование"" или ""Действующие вещества(МНН)"".'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ТорговоеНаименование",, Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СрокГодности) Тогда
		ПроверяемыеРеквизиты.Добавить("ЕдиницаИзмеренияСрокаГодности");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	КодЕСКЛП                         = "";
	УникальныйИдентификаторЕСКЛП     = Неопределено;
	КонтрольноеЗначениеДанныхСервиса = "";
	ДатаСозданияЗаписиКЛП            = Дата(1, 1, 1);
	ДатаОкончанияДействия            = Дата(1, 1, 1);
	ДатаПоследнегоИзменения          = Дата(1, 1, 1);
	КодРосздравнадзора               = 0;
	УникальныйКодРосздравнадзора     = 0;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Наименование = НаименованиеПолное;
	
	Если ЗначениеЗаполнено(ДействующиеВеществаМНН) И ЗначениеЗаполнено(СМНН) Тогда
		ДействующиеВеществаМННСМНН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СМНН, "ДействующиеВеществаМНН");
		Если ДействующиеВеществаМНН <> ДействующиеВеществаМННСМНН Тогда
			ТекстОшибки = НСтр("ru = 'Значение реквизита ""Действующие вещества (МНН)"" не совпадает со значением из СМНН.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект,,, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Справочники.КЛП.ЗаполнитьУпаковкиПрепарата(Ссылка);
	
	Если Не ЭтоНовый() И Не ДополнительныеСвойства.Свойство("ПропуститьОбновлениеДанныхНоменклатуры") Тогда
		ОбновитьДанныеСвязаннойНоменклатуры(Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработкиСобытий

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

// Процедура обновляет данные номенклатуры по элементу КАТ
//
Процедура ОбновитьДанныеСвязаннойНоменклатуры(Отказ)
	
	НоменклатураСсылка = АптечныеТовары.ПолучитьНоменклатуруПоЭлементуКАТ(Ссылка);
	
	Если Не ЗначениеЗаполнено(НоменклатураСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ПереносимыеРеквизиты = Новый Структура;
	ПереносимыеРеквизиты.Вставить("Упаковка"     , Упаковка);
	ПереносимыеРеквизиты.Вставить("ГруппаПКУ"    , ГруппаПКУ);
	ПереносимыеРеквизиты.Вставить("Производитель", ФирмаПроизводитель);
	Если ЗначениеЗаполнено(СрокГодности) Тогда
		ПереносимыеРеквизиты.Вставить("СрокГодности", СрокГодности);
		ПереносимыеРеквизиты.Вставить("ЕдиницаИзмеренияСрокаГодности", ЕдиницаИзмеренияСрокаГодности);
	КонецЕсли;
	
	Если Справочники.ЕдиницыИзмерения.МерныеТипыЕдиницИзмерений().Найти(БазоваяЕдиницаИзмеренияКАТ.ТипЕдиницы) = Неопределено Тогда
		ПереносимыеРеквизиты.Вставить("ЕдиницаИзмерения", БазоваяЕдиницаИзмеренияКАТ);
	КонецЕсли;
	
	НоменклатураОбъект = Неопределено;
	
	РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоменклатураСсылка, ОбщегоНазначенияБольничнаяАптекаКлиентСервер.КлючиКоллекции(ПереносимыеРеквизиты));
	Для Каждого Реквизит Из ПереносимыеРеквизиты Цикл
		
		Если Реквизит.Значение <> РеквизитыНоменклатуры[Реквизит.Ключ] Тогда
			Если НоменклатураОбъект = Неопределено Тогда
				НоменклатураОбъект = НоменклатураСсылка.ПолучитьОбъект();
				Попытка
					ЗаблокироватьДанныеДляРедактирования(НоменклатураСсылка);
				Исключение
					ОбщегоНазначения.СообщитьПользователю(ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), ЭтотОбъект,,, Отказ);
					Возврат;
				КонецПопытки;
			КонецЕсли;
			НоменклатураОбъект[Реквизит.Ключ] = Реквизит.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НоменклатураОбъект <> Неопределено Тогда
		
		Попытка
			НоменклатураОбъект.ДополнительныеСвойства.Вставить("ПропуститьОбновлениеДанныхПрепарата");
			НоменклатураОбъект.Записать();
			РазблокироватьДанныеДляРедактирования(НоменклатураСсылка);
		Исключение
			РазблокироватьДанныеДляРедактирования(НоменклатураСсылка);
			ОбщегоНазначения.СообщитьПользователю(ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), ЭтотОбъект,,, Отказ);
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли