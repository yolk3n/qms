
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ГраницаОперативногоХранилища = ТранспортМДЛПАПИВызовСервера.ГраницаОперативногоХранилищаСпискаДокументов();
	
	Текст = НСтр("ru = 'Выбранный период меньше границы оперативного хранилища списка документов. Граница оперативного хранилища %1'");
	Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ГраницаОперативногоХранилища);
	
	Если ГраницаОперативногоХранилища > ГраницаЗагрузкиВходящихДокументовМДЛП Тогда
		ОбщегоНазначения.СообщитьПользователю(Текст,, "ГраницаЗагрузкиВходящихДокументовМДЛП",, Отказ);
	КонецЕсли;
	Если ГраницаОперативногоХранилища > ГраницаЗагрузкиИсходящихДокументовМДЛП Тогда
		ОбщегоНазначения.СообщитьПользователю(Текст,, "ГраницаЗагрузкиИсходящихДокументовМДЛП",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	УстановитьГраницы();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПолучитьГраницы();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьГраницы()
	
	ИдентификаторОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "РегистрационныйНомерУчастника");
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Новый Структура("ГраницаЗагрузкиВходящихДокументовМДЛП, ГраницаЗагрузкиИсходящихДокументовМДЛП",
		ТранспортМДЛПАПИВызовСервера.ПолучитьГраницуЗагрузкиДокументовСписка("ВХОДЯЩИЕ" , ИдентификаторОрганизации),
		ТранспортМДЛПАПИВызовСервера.ПолучитьГраницуЗагрузкиДокументовСписка("ИСХОДЯЩИЕ", ИдентификаторОрганизации)
	));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьГраницы()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "РегистрационныйНомерУчастника");
	
	КлючиГраниц = Новый Массив;
	КлючиГраниц.Добавить("ГраницаЗагрузкиВходящихДокументовМДЛП");
	КлючиГраниц.Добавить("ГраницаЗагрузкиИсходящихДокументовМДЛП");
	
	Для Каждого Ключ Из КлючиГраниц Цикл
		
		Границы = Константы[Ключ].Получить().Получить();
		Если Границы <> Неопределено Тогда
			РедактируемыеГраницы = Новый Соответствие(Границы);
		Иначе
			РедактируемыеГраницы = Новый Соответствие;
		КонецЕсли;
		
		РедактируемыеГраницы.Вставить(ИдентификаторОрганизации, ЭтотОбъект[Ключ]);
		НовыеГраницы = Новый ФиксированноеСоответствие(РедактируемыеГраницы);
		Константы[Ключ].Установить(Новый ХранилищеЗначения(НовыеГраницы));
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
