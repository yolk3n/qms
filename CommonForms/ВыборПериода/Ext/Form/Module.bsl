
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Значение", ВыбираемыйПериод);
	Если Не ЗначениеЗаполнено(ВыбираемыйПериод) Тогда
		ВыбираемыйПериод = НачалоМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Если Не Параметры.Свойство("ЗапрашиватьРежимВыбораПериодаУВладельца", ЗапрашиватьРежимВыбораПериодаУВладельца) Тогда
		ЗапрашиватьРежимВыбораПериодаУВладельца = Ложь;
	КонецЕсли;
	
	УстановитьРежимВыбораПериода(ЭтотОбъект, Параметры.РежимВыбораПериода);
	
	ЦветФонаКнопкиВыбранногоПериода = ЦветаСтиля.ФонПомеченнойКнопкиЦвет;
	ЦветФонаКнопки = ЦветаСтиля.ЦветФонаКнопки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьОтображение();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыбираемыйПериодПриИзменении(Элемент)
	
	ИзменилсяГод = Истина;
	ОтмеченныйПериод = Неопределено;
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), Месяц(ВыбираемыйПериод));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаМесяц(Команда)
	
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), Число(Прав(Команда.Имя, 2)));
	ОтмеченныйПериод = ВыбираемыйПериод;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаКвартал(Команда)
	
	Квартал = Число(Прав(Команда.Имя, 1));
	МесяцКвартала = (Квартал - 1) * 3 + 1;
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), МесяцКвартала);
	ОтмеченныйПериод = ВыбираемыйПериод;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПолугодие(Команда)
	
	Полугодие = Число(Прав(Команда.Имя, 1));
	МесяцПолугодия = (Полугодие - 1) * 6 + 1;
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), МесяцПолугодия);
	ОтмеченныйПериод = ВыбираемыйПериод;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	ВыполнитьВыбор();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура КомандаПролистатьГодВМинус(Команда)
	
	ВыбираемыйГод = Год(ВыбираемыйПериод) - 1;
	ОтмеченныйПериод = Неопределено;
	УстановитьВыбираемыйПериод(ВыбираемыйГод, Месяц(ВыбираемыйПериод));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПролистатьГодВПлюс(Команда)
	
	ВыбираемыйГод = Год(ВыбираемыйПериод) + 1;
	ОтмеченныйПериод = Неопределено;
	УстановитьВыбираемыйПериод(ВыбираемыйГод, Месяц(ВыбираемыйПериод));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьРежимВыбораПериода()
	
	Если ЗапрашиватьРежимВыбораПериодаУВладельца Тогда
		УстановитьРежимВыбораПериода(ЭтотОбъект, ВладелецФормы.РежимВыбораПериода(ВыбираемыйПериод));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьРежимВыбораПериода(Форма, Знач РежимВыбора)
	
	Если Не ЗначениеЗаполнено(РежимВыбора) Тогда
		РежимВыбора = "Месяц";
	КонецЕсли; 
	
	Если Форма.РежимВыбораПериода = ВРег(РежимВыбора) Тогда
		Возврат;
	КонецЕсли; 
	
	Форма.РежимВыбораПериода = ВРег(РежимВыбора);
	
	ГруппаМесяцыВидимость = Ложь;
	ГруппаКварталыВидимость = Ложь;
	ГруппаПолугодияВидимость = Ложь;
	
	Если Форма.РежимВыбораПериода = "МЕСЯЦ" Тогда
		
		ГруппаМесяцыВидимость = Истина;
		Форма.ВыбираемыйПериод = НачалоМесяца(Форма.ВыбираемыйПериод);
		
	ИначеЕсли Форма.РежимВыбораПериода = "КВАРТАЛ" Тогда
		
		ГруппаКварталыВидимость = Истина;
		НомерКвартала = Цел((Месяц(Форма.ВыбираемыйПериод) - 1) / 3 + 1);
		Форма.ВыбираемыйПериод = Дата(Год(Форма.ВыбираемыйПериод), (НомерКвартала - 1) * 3 + 1, 1);
		
	ИначеЕсли Форма.РежимВыбораПериода = "ПОЛУГОДИЕ" Тогда
		
		ГруппаПолугодияВидимость = Истина;
		Форма.ВыбираемыйПериод = Дата(Год(Форма.ВыбираемыйПериод), ?(Месяц(Форма.ВыбираемыйПериод) < 7, 1, 7), 1);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ГруппаМесяцы",
		"Видимость",
		ГруппаМесяцыВидимость);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ГруппаКварталы",
		"Видимость",
		ГруппаКварталыВидимость);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ГруппаПолугодия",
		"Видимость",
		ГруппаПолугодияВидимость);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображение()
	
	ПроверитьРежимВыбораПериода();
	
	Если РежимВыбораПериода = "МЕСЯЦ" Тогда
		
		Для НомерМесяца = 1 По 12 Цикл
			
			КнопкаМесяца = Элементы["КомандаМесяц" + Формат(НомерМесяца, "ЧЦ=2; ЧВН=")];
			Если НомерМесяца = Месяц(ВыбираемыйПериод) Тогда
				
				Если КнопкаМесяца.ЦветФона <> ЦветФонаКнопкиВыбранногоПериода Тогда
					КнопкаМесяца.ЦветФона = ЦветФонаКнопкиВыбранногоПериода;
				КонецЕсли;
				ТекущийЭлемент = КнопкаМесяца;
				
			Иначе
				
				Если КнопкаМесяца.ЦветФона <> ЦветФонаКнопки Тогда
					КнопкаМесяца.ЦветФона = ЦветФонаКнопки;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ПериодСтрокой = Формат(ВыбираемыйПериод, "ДФ='ММММ гггг'")
		
	ИначеЕсли РежимВыбораПериода = "КВАРТАЛ" Тогда
		
		КварталМесяца = (Месяц(ВыбираемыйПериод) - 1) / 3 + 1;
		Для НомерКвартала = 1 По 4 Цикл
			
			КнопкаКвартала = Элементы["КомандаКвартал" + Формат(НомерКвартала, "ЧЦ=1")];
			Если НомерКвартала = КварталМесяца Тогда
				
				Если КнопкаКвартала.ЦветФона <> ЦветФонаКнопкиВыбранногоПериода Тогда
					КнопкаКвартала.ЦветФона = ЦветФонаКнопкиВыбранногоПериода;
				КонецЕсли;
				
				ТекущийЭлемент = КнопкаКвартала;
				
			Иначе
				
				Если КнопкаКвартала.ЦветФона <> ЦветФонаКнопки Тогда
					КнопкаКвартала.ЦветФона = ЦветФонаКнопки;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ПериодСтрокой = Формат(КварталМесяца, "ЧЦ=1") + " квартал " + Формат(ВыбираемыйПериод,"ДФ=гггг");
		
	Иначе
		
		Если Месяц(ВыбираемыйПериод) = 1 Тогда
			Элементы.КомандаПолугодие1.ЦветФона = ЦветФонаКнопкиВыбранногоПериода;
			Элементы.КомандаПолугодие2.ЦветФона = ЦветФонаКнопки;
			ПериодСтрокой = "1 полугодие " + Формат(ВыбираемыйПериод,"ДФ=гггг");
		Иначе
			Элементы.КомандаПолугодие1.ЦветФона = ЦветФонаКнопки;
			Элементы.КомандаПолугодие2.ЦветФона = ЦветФонаКнопкиВыбранногоПериода;
			ПериодСтрокой = "2 полугодие " + Формат(ВыбираемыйПериод,"ДФ=гггг");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВыбираемыйПериод(Год, Месяц)
	
	Если ОтмеченныйПериод = Дата(Год, Месяц, 1) И НЕ ИзменилсяГод Тогда
		ВыполнитьВыбор();
	КонецЕсли; 
	
	Если Год < 1 Тогда
		Год = 1;
	КонецЕсли; 
	
	ИзменилсяГод = Ложь;
	ВыбираемыйПериод = Дата(Год, Месяц, 1);
	
	ОбновитьОтображение();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыбор()
	Закрыть(ВыбираемыйПериод);
КонецПроцедуры

#КонецОбласти
