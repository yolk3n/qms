#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураАдреса = Константы.АдресРеестраМЧД.ПрочитатьАдрес();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураАдреса);
	
	НастроитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНаСервере();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоУмолчанию(Команда)
	
	ЗаполнитьПоУмолчаниюНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьВидимостьДоступность()
	
	ЕстьПравоИзменения = ПравоДоступа("Изменение", Метаданные.Константы.АдресРеестраМЧД);
	
	КоманднаяПанель.Видимость = ЕстьПравоИзменения;
	Элементы.ФормаЗаписатьИЗакрыть.Видимость = ЕстьПравоИзменения;
	Элементы.ФормаЗаписать.Видимость = ЕстьПравоИзменения;
	Элементы.ФормаЗаполнитьПоУмолчанию.Видимость = ЕстьПравоИзменения;
	
	ТолькоПросмотр = Не ЕстьПравоИзменения;
	Элементы.АдресСервера.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.РесурсКорняAPI.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ИспользоватьРасширенияAPI.ТолькоПросмотр = ТолькоПросмотр;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	СтруктураАдреса = МашиночитаемыеДоверенности.СтруктураАдресаСервераМЧД();
	ЗаполнитьЗначенияСвойств(СтруктураАдреса, ЭтотОбъект);
	Константы.АдресРеестраМЧД.ЗаписатьАдрес(СтруктураАдреса);
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоУмолчаниюНаСервере()
	
	СтруктураАдреса = МашиночитаемыеДоверенности.СтруктураАдресаСервераМЧД();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураАдреса);
	
КонецПроцедуры

#КонецОбласти