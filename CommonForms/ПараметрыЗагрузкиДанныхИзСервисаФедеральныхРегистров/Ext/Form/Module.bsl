
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыЗагрузки = Константы.ПараметрыЗагрузкиДанныхИзСервисаФедеральныхРегистров.Получить().Получить();
	Если ТипЗнч(ПараметрыЗагрузки) = Тип("ФиксированнаяСтруктура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыЗагрузки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповестить = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповестить, Отказ, ЗавершениеРаботы,, ТекстПредупреждения);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНаКлиенте();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

// Продолжение процедуры ПередЗакрытием
//
&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаписатьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНаКлиенте()
	
	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьНаСервере();
	
	// Для механизма подтверждения закрытия формы.
	// (см. процедуру ПередЗакрытием).
	Модифицированность = Ложь;
	
	Оповестить("Запись_ПараметрыЗагрузкиДанныхИзСервисаФедеральныхРегистров");
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	Константа = Константы.ПараметрыЗагрузкиДанныхИзСервисаФедеральныхРегистров;
	Менеджер = Константа.СоздатьМенеджерЗначения();
	ПараметрыЗагрузки = Новый Структура(Менеджер.ЗначениеПоУмолчанию().Получить());
	ЗаполнитьЗначенияСвойств(ПараметрыЗагрузки, ЭтотОбъект);
	
	Константы.ПараметрыЗагрузкиДанныхИзСервисаФедеральныхРегистров.Установить(Новый ХранилищеЗначения(Новый ФиксированнаяСтруктура(ПараметрыЗагрузки)));
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
