
////////////////////////////////////////////////////////////////////////////////
// ОПИСАНИЕ ПЕРЕМЕННЫХ
#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения;

#КонецОбласти // ОписаниеПеременных

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТаблицаНоменклатурыПоставщика = ПолучитьИзВременногоХранилища(Параметры.АдресТоваровВХранилище);
	
	// Подготовка номенклатуры поставщика для сопоставления
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Товары.НоменклатураПоставщика КАК Ссылка,
		|	Товары.НомерСтроки            КАК НомерСтроки
		|ПОМЕСТИТЬ
		|	Товары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|ВЫБРАТЬ
		|	Товары.Ссылка.Артикул      КАК АртикулПоиск,
		|	Товары.Ссылка.Наименование КАК НаименованиеПоиск,
		|	Товары.Ссылка              КАК Ссылка,
		|	Товары.НомерСтроки         КАК НомерСтроки
		|ИЗ
		|	Товары КАК Товары
		|");
		
	Запрос.УстановитьПараметр("Товары", ТаблицаНоменклатурыПоставщика);
	ТаблицаНоменклатурыПоставщика = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекСтрока Из ТаблицаНоменклатурыПоставщика Цикл
		
		ТекСтрока.АртикулПоиск = СтрЗаменить(ТекСтрока.АртикулПоиск, " ", "");
		ТекСтрока.НаименованиеПоиск = СтрЗаменить(ТекСтрока.НаименованиеПоиск, " ", "");
		
	КонецЦикла;
	
	// Подготовка номенклатуры для сопоставления
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Номенклатура.Артикул        КАК АртикулПоиск,
		|	Номенклатура.Наименование   КАК НаименованиеПоиск,
		|	Номенклатура.Ссылка         КАК Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	НЕ Номенклатура.ЭтоГруппа
		|	И НЕ Номенклатура.ПометкаУдаления
		|");
		
	ТаблицаНоменклатуры = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекСтрока Из ТаблицаНоменклатуры Цикл
		
		ТекСтрока.АртикулПоиск = СтрЗаменить(ТекСтрока.АртикулПоиск, " ", "");
		ТекСтрока.НаименованиеПоиск = СтрЗаменить(ТекСтрока.НаименованиеПоиск, " ", "");
		
	КонецЦикла;
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	НоменклатураПоставщика.НомерСтроки         КАК НомерСтроки,
		|	НоменклатураПоставщика.АртикулПоиск        КАК АртикулПоиск,
		|	НоменклатураПоставщика.НаименованиеПоиск   КАК НаименованиеПоиск,
		|	ВЫРАЗИТЬ(НоменклатураПоставщика.Ссылка КАК Справочник.НоменклатураКонтрагентов)  КАК Ссылка
		|ПОМЕСТИТЬ НоменклатураПоставщика
		|ИЗ
		|	&НоменклатураПоставщика КАК НоменклатураПоставщика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НаименованиеПоиск,
		|	АртикулПоиск
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Номенклатура.АртикулПоиск        КАК АртикулПоиск,
		|	Номенклатура.НаименованиеПоиск   КАК НаименованиеПоиск,
		|	Номенклатура.Ссылка              КАК Ссылка
		|ПОМЕСТИТЬ Номенклатура
		|ИЗ
		|	&Номенклатура КАК Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НаименованиеПоиск,
		|	АртикулПоиск
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НоменклатураПоставщика.НомерСтроки              КАК НомерСтроки,
		|	НоменклатураПоставщика.Ссылка                   КАК НоменклатураПоставщика,
		|	НоменклатураПоставщика.Ссылка.Номенклатура      КАК НоменклатураПоставщикаНоменклатура,
		|	НоменклатураПоставщика.Ссылка.ЕдиницаИзмерения  КАК НоменклатураПоставщикаЕдиницаИзмерения,
		|	Номенклатура.Ссылка                             КАК Номенклатура
		|ИЗ
		|	НоменклатураПоставщика КАК НоменклатураПоставщика
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		Номенклатура КАК Номенклатура
		|	ПО
		|		(НоменклатураПоставщика.АртикулПоиск = Номенклатура.АртикулПоиск И НоменклатураПоставщика.АртикулПоиск <> """")
		|		ИЛИ (НоменклатураПоставщика.НаименованиеПоиск = Номенклатура.НаименованиеПоиск)
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|ИТОГИ ПО
		|	НомерСтроки
		|");
		
	Запрос.УстановитьПараметр("НоменклатураПоставщика", ТаблицаНоменклатурыПоставщика);
	Запрос.УстановитьПараметр("Номенклатура", ТаблицаНоменклатуры);
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.НомерСтроки = Выборка.НомерСтроки;
		ВыборкаНоменклатура = Выборка.Выбрать();
		
		Если ВыборкаНоменклатура.Количество() = 1 Тогда
			ВыборкаНоменклатура.Следующий();
			НоваяСтрока.НоменклатураПоставщика = ВыборкаНоменклатура.НоменклатураПоставщика;
			Если ЗначениеЗаполнено(ВыборкаНоменклатура.НоменклатураПоставщикаНоменклатура) Тогда
				НоваяСтрока.Номенклатура = ВыборкаНоменклатура.НоменклатураПоставщикаНоменклатура;
				НоваяСтрока.ЕдиницаИзмерения = ВыборкаНоменклатура.НоменклатураПоставщикаЕдиницаИзмерения;
			ИначеЕсли ЗначениеЗаполнено(ВыборкаНоменклатура.Номенклатура) Тогда
				НоваяСтрока.Номенклатура = ВыборкаНоменклатура.Номенклатура;
				НоваяСтрока.ЕдиницаИзмерения = НоменклатураСервер.ОсновнаяЕдиницаИзмерения(ВыборкаНоменклатура.Номенклатура, НоменклатураКлиентСервер.ВидЕдиницы_ПотребительскаяУпаковка());
				НоваяСтрока.КоличествоНоменклатурыДляВыбора = НоваяСтрока.КоличествоНоменклатурыДляВыбора + 1;
				ДобавитьНоменклатуруДляВыбора(ВыборкаНоменклатура.НоменклатураПоставщика, ВыборкаНоменклатура.Номенклатура);
			КонецЕсли;
		Иначе
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				Если Не ЗначениеЗаполнено(НоваяСтрока.НоменклатураПоставщика) Тогда
					НоваяСтрока.НоменклатураПоставщика = ВыборкаНоменклатура.НоменклатураПоставщика;
					Если ЗначениеЗаполнено(ВыборкаНоменклатура.НоменклатураПоставщикаНоменклатура) Тогда
						НоваяСтрока.Номенклатура = ВыборкаНоменклатура.НоменклатураПоставщикаНоменклатура;
						НоваяСтрока.ЕдиницаИзмерения = ВыборкаНоменклатура.НоменклатураПоставщикаЕдиницаИзмерения;
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ВыборкаНоменклатура.Номенклатура) Тогда
					НоваяСтрока.КоличествоНоменклатурыДляВыбора = НоваяСтрока.КоличествоНоменклатурыДляВыбора + 1;
					ДобавитьНоменклатуруДляВыбора(ВыборкаНоменклатура.НоменклатураПоставщика, ВыборкаНоменклатура.Номенклатура);
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Товары.Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru = 'Номенклатура не будет перенесена в документ. Проложить?'");
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстСообщения, "ЗакрытьБезПодтверждения");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	
	ЗакрытьБезПодтверждения = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		ЗакрытьБезПодтверждения = Истина;
		АдресТоваровВХранилище = ПоместитьВоВременноеХранилищеНаСервере();
		Закрыть(АдресТоваровВХранилище);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ПроверитьУпаковкуПоВладельцу(), ТекущаяСтрока.ЕдиницаИзмерения);
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЕдиницуИзмерения(), НоменклатураКлиентСервер.ВидЕдиницы_ПотребительскаяУпаковка());
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииЯчейки(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если Элемент.ТекущийЭлемент.Имя = "ТоварыНоменклатура" И ТекущаяСтрока <> Неопределено Тогда 
		СписокВыбораНоменклатура = Элементы.ТоварыНоменклатура.СписокВыбора;
		СписокВыбораНоменклатура.Очистить();
		
		ТекущаяНоменклатураПоставщика = Элементы.Товары.ТекущиеДанные.НоменклатураПоставщика;
		НайденныеСтроки = НоменклатураДляВыбора.НайтиСтроки(Новый Структура("НоменклатураПоставщика", ТекущаяНоменклатураПоставщика));
		Для Каждого Строка Из НайденныеСтроки Цикл
			СписокВыбораНоменклатура.Добавить(Строка.Номенклатура, Строка.НоменклатураПоставщика);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьНоменклатуруДляВыбора(НоменклатураПоставщика, Номенклатура)
	
	СтруктураПоиска = Новый Структура();
	СтруктураПоиска.Вставить("НоменклатураПоставщика", НоменклатураПоставщика);
	СтруктураПоиска.Вставить("Номенклатура", Номенклатура);
	НайденныеСтроки = НоменклатураДляВыбора.НайтиСтроки(СтруктураПоиска);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		НоваяСтрока = НоменклатураДляВыбора.Добавить();
		НоваяСтрока.НоменклатураПоставщика = НоменклатураПоставщика;
		НоваяСтрока.Номенклатура = Номенклатура;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьВоВременноеХранилищеНаСервере()
	
	ОтборСтрок = Новый Массив();
	Для Каждого ТекущаяСтрока Из Товары Цикл
		Если ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
			ОтборСтрок.Добавить(ТекущаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(Товары.Выгрузить(ОтборСтрок, "НомерСтроки, Номенклатура, ЕдиницаИзмерения"));
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
