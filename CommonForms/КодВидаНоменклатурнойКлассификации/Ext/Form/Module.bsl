#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьДанные()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И  НЕ ПустаяСтрока(ТекущиеДанные.КодВидаНоменклатурнойКлассификации) Тогда
		СтруктураВозврата = Новый Структура();
		СтруктураВозврата.Вставить("КодВидаНоменклатурнойКлассификации", ТекущиеДанные.КодВидаНоменклатурнойКлассификации);
		СтруктураВозврата.Вставить("НаименованиеВидаНоменклатурнойКлассификации", ТекущиеДанные.НаименованиеВидаНоменклатурнойКлассификации);
		Закрыть(СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанные()
	
	УстановитьПривилегированныйРежим(Истина);
	Данные = ПолучитьОбщийМакет("КодВидаНоменклатурнойКлассификации").ПолучитьТекст() ;
	
	НоменклатурнаяКлассификация = Неопределено;
	
	Если Не ПустаяСтрока(Данные) Тогда
		
		ДеревоРек = РеквизитФормыВЗначение("Дерево");
		
		ЧтениеXML = Новый ЧтениеXML; 
		ЧтениеXML.УстановитьСтроку(Данные);
		ЧтениеXML.ПерейтиКСодержимому();
		
		Если ЧтениеXML.Имя = "КодВидаНоменклатурнойКлассификации" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда  
			Пока ЧтениеXML.Прочитать() Цикл  
				Если ЧтениеXML.Имя = "НоменклатурнаяКлассификация" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда  
					НоменклатурнаяКлассификация = ДеревоРек.Строки.Добавить();
					НоменклатурнаяКлассификация.НаименованиеВидаНоменклатурнойКлассификации = ЧтениеXML.ЗначениеАтрибута("Имя");
					НоменклатурнаяКлассификация.КодВидаНоменклатурнойКлассификации = "";
				ИначеЕсли ЧтениеXML.Имя = "ВидНоменклатурнойКлассификации" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда 
					Если НоменклатурнаяКлассификация <> Неопределено Тогда
						ВидНоменклатурнойКлассификации = НоменклатурнаяКлассификация.Строки.Добавить();
						ВидНоменклатурнойКлассификации.НаименованиеВидаНоменклатурнойКлассификации = ЧтениеXML.ЗначениеАтрибута("Имя");
						ВидНоменклатурнойКлассификации.КодВидаНоменклатурнойКлассификации = ЧтениеXML.ЗначениеАтрибута("КодВида");
					КонецЕсли
				КонецЕсли;
			КонецЦикла;           
		КонецЕсли;
		
		ЗначениеВРеквизитФормы(ДеревоРек, "Дерево");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
