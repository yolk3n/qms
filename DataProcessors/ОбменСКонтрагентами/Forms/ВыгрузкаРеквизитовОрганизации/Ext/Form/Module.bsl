
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Организация = Параметры.Организация;
	СтруктураДанных = ИнициализироватьРеквизиты();
	ИнтеграцияЭДО.ПолучитьРеквизитыОрганизацииДляВыгрузкиВФайл(Организация, СтруктураДанных);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураДанных);
	
	ТаблБанковскиеСчета = Неопределено; 
	ИнтеграцияЭДО.ПолучитьБанковскиеСчета(Организация, ТаблБанковскиеСчета);
	БанковскиеСчета.Загрузить(ТаблБанковскиеСчета);
	
	Если СпособВыгрузки = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту Тогда
		АдресВыгрузки = ПолучитьУчетнуюЗапись();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(СпособВыгрузки) Тогда
		СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог");
	КонецЕсли;
	
	Если СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту")
		И НЕ ЗначениеЗаполнено(АдресВыгрузки) Тогда
		АдресВыгрузки = ПолучитьУчетнуюЗапись();
	КонецЕсли;
		
	ОбновитьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособВыгрузкиПриИзменении(Элемент)
	
	АдресВыгрузки = "";
	Если СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		АдресВыгрузки = ПолучитьУчетнуюЗапись();
	КонецЕсли;
	ОбновитьФорму();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УчетнаяЗаписьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	АдресВыгрузки = ПредопределенноеЗначение("Справочник.УчетныеЗаписиЭлектроннойПочты.ПустаяСсылка");
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскиеСчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "БанковскийСчет" Тогда
		ПоказатьЗначение(, Элемент.ТекущиеДанные.БанковскийСчет);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскиеСчетаВыбранПриИзменении(Элемент)
	
	Если Элементы.БанковскиеСчета.ТекущиеДанные.Выбран Тогда
		НомерСтроки = 0;
		ТекущийНомер = Элементы.БанковскиеСчета.ТекущаяСтрока + 1;
		Для Каждого Строка Из БанковскиеСчета Цикл
			НомерСтроки = НомерСтроки + 1;
			Если НомерСтроки <> ТекущийНомер Тогда
				Строка.Выбран = Ложь;
			КонецЕсли;
		КонецЦикла
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыгрузитьРеквизиты(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		Если НЕ ЗначениеЗаполнено(АдресВыгрузки) Тогда
			ТекстСообщения = НСтр("ru = 'Не выбрана учетная запись электронной почты.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	АдресХранилища = Неопределено;
	ВыгрузитьРеквизитыОрганизации(АдресХранилища, УникальныйИдентификатор);
	Если АдресХранилища = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
		
		ИмяФайлаПоУмолчанию = Наименование;
		ИмяФайлаПоУмолчанию = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайлаПоУмолчанию, "");
		
		ПолучитьФайл(АдресХранилища, ИмяФайлаПоУмолчанию + ".xml");

	ИначеЕсли СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		ИмяФайлаДанных = Наименование + ".xml";
		ИмяФайлаДанных = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайлаДанных, "");
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Тема", "Реквизиты " + Наименование);
		ПараметрыФормы.Вставить("УчетнаяЗапись", АдресВыгрузки);
		ИмяФайла = ИмяФайлаДанных;
		Пока Истина Цикл
			Поз = Макс(СтрНайти(ИмяФайла, "\"), СтрНайти(ИмяФайла, "/"));
			Если Поз = 0 Тогда
				Прервать;
			КонецЕсли;
			ИмяФайла = Сред(ИмяФайла, Поз + 1);
		КонецЦикла;
		
		Вложения = Новый Массив;
		ДанныеВложения = Новый Структура();
		ДанныеВложения.Вставить("Представление", ИмяФайла);
		ДанныеВложения.Вставить("АдресВоВременномХранилище", АдресХранилища);
		Вложения.Добавить(ДанныеВложения);
		ПараметрыФормы.Вставить("Вложения", Вложения);
		Форма = ОткрытьФорму("ОбщаяФорма.ОтправкаСообщения", ПараметрыФормы);
	КонецЕсли;
	
	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыгрузитьРеквизитыОрганизации(АдресХранилища, УникальныйИдентификатор)
	
	Ошибки = Неопределено; // служебная переменная для хранения списка возникших ошибок
	
	Попытка
		
		КонтрагентXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Контрагент", "4.02");
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(КонтрагентXDTO, "ИД", ИНН + "_" + КПП, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(КонтрагентXDTO, "Наименование", Наименование, Истина, Ошибки);
		
		ЭтоФизЛицо = ИнтеграцияЭДО.ЭтоФизЛицо(Организация);
		Если НЕ ЭтоФизЛицо Тогда
			РеквизитыКонтрагентаXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("РеквизитыЮрЛица", "4.02");
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
				РеквизитыКонтрагентаXDTO, "ОфициальноеНаименование", НаименованиеПолное, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РеквизитыКонтрагентаXDTO, "КПП", КПП, , Ошибки);
			
			Если ЗначениеЗаполнено(Руководитель) Тогда
				РуководительXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Контрагент", "4.02");
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РуководительXDTO, "ИД", Руководитель, Истина, Ошибки);
				ФизЛицоРуководительXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Контрагент.ФизЛицо", "4.02");
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					ФизЛицоРуководительXDTO, "ПолноеНаименование", Руководитель, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					ФизЛицоРуководительXDTO, "Должность", ДолжностьРуководителя, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					РуководительXDTO, "ФизЛицо", ФизЛицоРуководительXDTO, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					РеквизитыКонтрагентаXDTO, "Руководитель", РуководительXDTO, Истина, Ошибки);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЮридическийАдрес) Тогда
				АдресXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Адрес", "4.02");
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(АдресXDTO, "Представление", ЮридическийАдрес, Истина, Ошибки);
				Если ЗначениеЗаполнено(ЗначенияПолейЮридическийАдрес) Тогда
					РазобратьАдрес(АдресXDTO, ЗначенияПолейЮридическийАдрес, Ошибки);
				КонецЕсли;
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РеквизитыКонтрагентаXDTO, "ЮридическийАдрес", АдресXDTO, Истина, Ошибки);
			КонецЕсли;
			
			ИмяСвойства = "ЮрЛицо";
		Иначе
			РеквизитыКонтрагентаXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("РеквизитыФизЛица", "4.02");
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РеквизитыКонтрагентаXDTO, "ПолноеНаименование", НаименованиеПолное, Истина, Ошибки);
			
			Если ЗначениеЗаполнено(ЮридическийАдрес) Тогда
				АдресXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Адрес", "4.02");
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(АдресXDTO, "Представление", ЮридическийАдрес, Истина, Ошибки);
				Если ЗначениеЗаполнено(ЗначенияПолейЮридическийАдрес) Тогда
					РазобратьАдрес(АдресXDTO, ЗначенияПолейЮридическийАдрес, Ошибки);
				КонецЕсли;
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РеквизитыКонтрагентаXDTO, "АдресРегистрации", АдресXDTO, Истина, Ошибки);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СвидетельствоДата) И ЗначениеЗаполнено(СвидетельствоНомер) Тогда
				СвидетельствоXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("РеквизитыФизЛица.Свидетельство", "4.02");
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					СвидетельствоXDTO, "Номер", СвидетельствоНомер, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					СвидетельствоXDTO, "ДатаВыдачи", СвидетельствоДата, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					РеквизитыКонтрагентаXDTO, "Свидетельство", СвидетельствоXDTO, Истина, Ошибки);
			КонецЕсли;
			ИмяСвойства = "ФизЛицо";
		КонецЕсли;
		
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РеквизитыКонтрагентаXDTO, "ИНН", ИНН, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РеквизитыКонтрагентаXDTO, "ОКПО", ОКПО, , Ошибки);
		
		Если ЗначениеЗаполнено(ФактическийАдрес) Тогда
			АдресXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Адрес", "4.02");
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(АдресXDTO, "Представление", ФактическийАдрес, Истина, Ошибки);
			Если ЗначениеЗаполнено(ЗначенияПолейФактАдрес) Тогда
				РазобратьАдрес(АдресXDTO, ЗначенияПолейФактАдрес, Ошибки);
			КонецЕсли;
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(КонтрагентXDTO, "Адрес", АдресXDTO, Истина, Ошибки);
		КонецЕсли;
		
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(КонтрагентXDTO, ИмяСвойства, РеквизитыКонтрагентаXDTO, Истина, Ошибки);
		
		ТЗБанковскиеСчета = БанковскиеСчета.Выгрузить(Новый Структура("Выбран", Истина));
		БанковскиеРеквизиты = Неопределено; 
		ИнтеграцияЭДО.ПолучитьБанковскиеРеквизиты(
			ТЗБанковскиеСчета.ВыгрузитьКолонку("БанковскийСчет"), БанковскиеРеквизиты);
		
		Если БанковскиеРеквизиты.Количество() > 0 Тогда
			
			РасчетныеСчетаXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Контрагент.РасчетныеСчета", "4.02");
			
			Для Каждого Счет Из БанковскиеРеквизиты Цикл
				РасчетныйСчетXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("РасчетныйСчет", "4.02");
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					РасчетныйСчетXDTO, "НомерСчета", Счет.РасчетныйСчет, Истина, Ошибки);
				БанкXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Банк", "4.02");
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(БанкXDTO, "БИК", Счет.БИК, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					БанкXDTO, "СчетКорреспондентский", Счет.КорреспондентскийСчет, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(БанкXDTO, "Наименование", Счет.Банк, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РасчетныйСчетXDTO, "Банк",БанкXDTO, Истина, Ошибки);
				
				Если ЗначениеЗаполнено(Счет.БанкДляРасчетов) Тогда
					БанкXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Банк", "4.02");
					РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(БанкXDTO, "БИК", Счет.БанкДляРасчетовБИК, Истина, Ошибки);
					РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
						БанкXDTO, "СчетКорреспондентский", Счет.БанкДляРасчетовКоррСчет, Истина, Ошибки);
					РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
						БанкXDTO, "Наименование", Счет.БанкДляРасчетов, Истина, Ошибки);
					РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
						РасчетныйСчетXDTO, "БанкКорреспондент", БанкXDTO, Истина, Ошибки);
				КонецЕсли;
					
				РасчетныеСчетаXDTO.РасчетныйСчет.Добавить(РасчетныйСчетXDTO);
			КонецЦикла;
			
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
				КонтрагентXDTO, "РасчетныеСчета", РасчетныеСчетаXDTO, Истина, Ошибки);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Телефон) Тогда
			КонтактнаяИнформацияXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("КонтактнаяИнформация", "4.02");
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
				КонтактнаяИнформацияXDTO, "Тип", НСтр("ru = 'Телефон рабочий'"), Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(КонтактнаяИнформацияXDTO, "Значение", Телефон, Истина, Ошибки);
			
			КонтактыXDTO = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Контрагент.Контакты", "4.02");
			КонтактыXDTO.Контакт.Добавить(КонтактнаяИнформацияXDTO);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(КонтрагентXDTO, "Контакты", КонтактыXDTO, Истина, Ошибки);
		КонецЕсли;
		
		КонтрагентXDTO.Проверить();
		
		Если Не ЗначениеЗаполнено(Ошибки) Тогда
			ИмяФайла = РаботаСФайламиБЭД.ТекущееИмяВременногоФайла("xml");
			НоваяЗаписьXML = Новый ЗаписьXML;
			НоваяЗаписьXML.ОткрытьФайл(ИмяФайла, "UTF-8");
			НоваяЗаписьXML.ЗаписатьОбъявлениеXML();
			ФабрикаXDTO.ЗаписатьXML(НоваяЗаписьXML, КонтрагентXDTO, , , , НазначениеТипаXML.Явное);
			НоваяЗаписьXML.Закрыть();
			ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайла);
			РаботаСФайламиБЭД.УдалитьВременныеФайлы(ИмяФайла);
			
			АдресХранилища = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		Иначе
			ТекстОшибки = ОбщегоНазначенияБЭД.СоединитьОшибки(Ошибки);
			ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Формирование ЭД'"),
																						ТекстОшибки,
																						ТекстОшибки);
		КонецЕсли;
		
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Формирование ЭД'"),
																					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
																					ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура РазобратьАдрес(ОбъектXDTO, Значение, Ошибки)

	Значение = СтрЗаменить(Значение, "Индекс", НСтр("ru = 'Почтовый индекс'"));
	Значение = СтрЗаменить(Значение, "НаселенныйПункт", НСтр("ru = 'Населенный пункт'"));
	ДопустимыеТипы = "Почтовый индекс, Страна, Регион, Район, Населенный пункт, Город, Улица, Дом, Корпус, Квартира";
	
	Для Индекс=1 По СтрЧислоСтрок(Значение) Цикл
		ТекСтрока = СтрПолучитьСтроку(Значение, Индекс);
		Тип = Сред(ТекСтрока, 1, СтрНайти(ТекСтрока, "=") - 1);
		Если СтрНайти(ДопустимыеТипы, Тип) > 0 Тогда
			АдресноеПоле = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Адрес.АдресноеПоле", "4.02");
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(АдресноеПоле, "Тип", Тип, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(АдресноеПоле, "Значение", Сред(ТекСтрока, СтрНайти(ТекСтрока, "=") + 1), Истина, Ошибки);
			ОбъектXDTO.АдресноеПоле.Добавить(АдресноеПоле);
		КонецЕсли;
	КонецЦикла
	
КонецПроцедуры

&НаСервере
Функция ПолучитьУчетнуюЗапись()
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УчетныеЗаписиЭлектроннойПочты.Ссылка
	|ИЗ
	|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
	|ГДЕ
	|	УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления = ЛОЖЬ
	|	И УчетныеЗаписиЭлектроннойПочты.ИспользоватьДляОтправки = ИСТИНА";
		
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Количество() = 1 Тогда
		Результат.Следующий();
		Возврат Результат.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.УчетныеЗаписиЭлектроннойПочты.ПустаяСсылка();

КонецФункции

&НаСервере
Функция ИнициализироватьРеквизиты()
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("Наименование");
	СтруктураВозврата.Вставить("НаименованиеПолное");
	СтруктураВозврата.Вставить("ИНН");
	СтруктураВозврата.Вставить("КПП");
	СтруктураВозврата.Вставить("ОКПО");
	СтруктураВозврата.Вставить("ДолжностьРуководителя");
	СтруктураВозврата.Вставить("Руководитель");
	СтруктураВозврата.Вставить("ЮрФизЛицо");
	СтруктураВозврата.Вставить("СвидетельствоДата");
	СтруктураВозврата.Вставить("СвидетельствоНомер");
	СтруктураВозврата.Вставить("ЮридическийАдрес");
	СтруктураВозврата.Вставить("ЗначенияПолейЮрАдрес");
	СтруктураВозврата.Вставить("ФактическийАдрес");
	СтруктураВозврата.Вставить("ЗначенияПолейФактАдрес");
	СтруктураВозврата.Вставить("Телефон");
	
	Возврат СтруктураВозврата;

КонецФункции

&НаКлиенте
Процедура ОбновитьФорму()
	
	Если СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		Элементы.СтраницыПолучателей.ТекущаяСтраница = Элементы.СтраницаПисьмо;
	ИначеЕсли СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
		Элементы.СтраницыПолучателей.ТекущаяСтраница = Элементы.СтраницаКаталог;
	КонецЕсли;
	Если ТипУчастника = ИндивидуальныйПредприниматель Тогда
		Элементы.ГруппаРуководитель.Видимость = Ложь;
		Элементы.КПП.Видимость = Ложь;
		
		НадписьСвидетельство = НСтр("ru ='Свидетельство №%1 от %2'");
		НадписьСвидетельство = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НадписьСвидетельство,
			СвидетельствоНомер, Формат(СвидетельствоДата, "ДЛФ=D"));
	Иначе
		Элементы.Свидетельство.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	ИндивидуальныйПредприниматель = ИнтеграцияЭДО.НайтиПеречисление("ЮрФизЛицо", "ИндивидуальныйПредприниматель");
	
	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ГруппаРуководитель.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЮрФизЛицо");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ИндивидуальныйПредприниматель;

	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);

КонецПроцедуры

#КонецОбласти
