
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("Ошибка") Тогда 
		Возврат;
	КонецЕсли;
	
	ОписаниеОшибки = РаботаСНоменклатуройСлужебныйКлиентСервер.ОписаниеОшибкиПокупкиНоменклатуры();
	Если Не ТипЗнч(Параметры.Ошибка) = ТипЗнч(ОписаниеОшибки) Тогда 
		Возврат;
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Новый УникальныйИдентификатор();
	
	Ошибка = Параметры.Ошибка;
	
	Элементы.ГруппаПодключениеСтартовогоПакета.Видимость = РаботаСНоменклатурой.ДоступноПодключениеТестовогоПериода();
	Элементы.ПодключитьСтартовыйПакетЛокально.Видимость = Не ОбщегоНазначения.РазделениеВключено();
	Элементы.ПодключитьСтартовыйПакетСервис.Видимость = ОбщегоНазначения.РазделениеВключено();
	Элементы.ГруппаСостояниеПодключенияУспех.Видимость = Ложь;
	
	КлючСКоличествомКарточек = "";
	
	Если Ошибка.Свойство("ЗагружаемоеКоличество") Тогда
		КлючСКоличествомКарточек = "ЗагружаемоеКоличество";
	ИначеЕсли Ошибка.Свойство("ПокупаемоеКоличество") Тогда	
		КлючСКоличествомКарточек = "ПокупаемоеКоличество";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КлючСКоличествомКарточек) Тогда
		Возврат;
	КонецЕсли;
	
	Если Ошибка.ДоступныйОстаток = 0 Тогда 
		
		ПоказатьЗакончилисьКарточки()
		
	ИначеЕсли Ошибка.ДоступныйОстаток > 0 И Ошибка[КлючСКоличествомКарточек] > Ошибка.ДоступныйОстаток Тогда
		
		ПоказатьПревышенЛимитДоступныхКарточек(Ошибка.ДоступныйОстаток, Ошибка[КлючСКоличествомКарточек]);
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	КлючСохраненияПоложенияОкна = "";
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодключитьСтартовыйПакетНажатие(Элемент)
	
	ПодключитьСтартовыйПакет();
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоказатьЗакончилисьКарточки()
	
	Элементы.ГруппаУведомленияСервисаИзображение.Картинка = БиблиотекаКартинок.Предупреждение32;
	Элементы.ГруппаУведомленияСервисаКонтекстЗаголовок.Заголовок = НСтр("ru = 'Отсутствуют подключенные пакеты карточек'");
	
	Информация = Новый Массив;
	Информация.Добавить(НСтр("ru = 'Лимит карточек исчерпан или срок активных пакетов истек, необходимо приобрести платный пакет у обслуживающего Вас партнера фирмы ""1С"".'"));
	Информация.Добавить(Символы.ПС);
	Информация.Добавить(Символы.ПС);
	Информация.Добавить(НСтр("ru = 'Если у Вас нет обслуживающего партнера, Вы можете выбрать его из'") + " ");
	Адрес = "http://its.1c.ru/news/redirect?utm_medium=prog&url=http://its.1c.ru/partners/";
	Информация.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'списка партнеров в Вашем регионе'"),,,,Адрес));
	Информация.Добавить(".");
	Элементы.ГруппаУведомленияСервисаКонтекстИнформация.Заголовок = Новый ФорматированнаяСтрока(Информация);
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьПревышенЛимитДоступныхКарточек(ДоступныйОстаток, ЗагружаемоеКоличество)
	
	Элементы.ГруппаУведомленияСервисаИзображение.Картинка = БиблиотекаКартинок.Предупреждение32;
	Элементы.ГруппаУведомленияСервисаКонтекстЗаголовок.Заголовок = НСтр("ru = 'Превышен лимит доступных карточек'");
	
	Информация = Новый Массив;
	Информация.Добавить(НСтр("ru = 'Вы попытались получить'") + " ");
	Информация.Добавить(Новый ФорматированнаяСтрока(Формат(ЗагружаемоеКоличество, "ЧГ=3,2,0"), Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, ,,Истина)));
	Информация.Добавить(НСтр("ru = ', но доступно'") + " ");
	Информация.Добавить(Новый ФорматированнаяСтрока(Формат(ДоступныйОстаток, "ЧГ=3,2,0"), Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, ,,Истина)));
	Информация.Добавить(".");
	Информация.Добавить(Символы.ПС);
	Информация.Добавить(НСтр("ru = 'Выберите меньшее количество карточек и повторите попытку.'"));
	Информация.Добавить(Символы.ПС);
	Информация.Добавить(Символы.ПС);
	Информация.Добавить(НСтр("ru = 'Так же Вы можете приобрести дополнительный платный пакет карточек у обслуживающего Вас партнера фирмы ""1С"".'"));
	Информация.Добавить(Символы.ПС);
	Информация.Добавить(НСтр("ru = 'Если у Вас нет обслуживающего партнера, Вы можете выбрать его из'") + " ");
	Адрес = "http://its.1c.ru/news/redirect?utm_medium=prog&url=http://its.1c.ru/partners/";
	Информация.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'списка партнеров в Вашем регионе'"),,,,Адрес));
	Информация.Добавить(".");
	Элементы.ГруппаУведомленияСервисаКонтекстИнформация.Заголовок = Новый ФорматированнаяСтрока(Информация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьСтартовыйПакет()
	
	ОбработчикЗавершения = Новый ОписаниеОповещения(
		"ПодключитьСтартовыйПакетЗавершение",
		ЭтотОбъект);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОбработчикЗавершения", ОбработчикЗавершения);
	
	РаботаСНоменклатуройКлиент.ПодключитьТестовыйПериод(ЭтотОбъект, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьСтартовыйПакетЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Элементы.ПодключитьСтартовыйПакетЛокально.Видимость = Не Результат;
	Элементы.ГруппаСостояниеПодключенияУспех.Видимость = Результат;
	
КонецПроцедуры

#КонецОбласти
