
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("КонтактнаяИнформация") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.КонтактнаяИнформация);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПроверитьЗаполнениеАдресаЭлектроннойПочты();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КонтактноеЛицоПриИзменении(Элемент)
	Email = ЭлектроннаяПочтаКонтактногоЛица(КонтактноеЛицо);
	ПроверитьЗаполнениеАдресаЭлектроннойПочты();
КонецПроцедуры

&НаКлиенте
Процедура EmailПриИзменении(Элемент)
	ПроверитьЗаполнениеАдресаЭлектроннойПочты();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Закрыть(КонтактнаяИнформация());
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция КонтактнаяИнформация()
	КонтактнаяИнформация = Новый Структура;
	КонтактнаяИнформация.Вставить("КонтактноеЛицо", "");
	КонтактнаяИнформация.Вставить("Должность", "");
	КонтактнаяИнформация.Вставить("Email", "");
	ЗаполнитьЗначенияСвойств(КонтактнаяИнформация, ЭтотОбъект);
	Возврат КонтактнаяИнформация;
КонецФункции

&НаСервереБезКонтекста
Функция ЭлектроннаяПочтаКонтактногоЛица(Знач КонтактноеЛицо)
	Контакты = РаботаСНоменклатуройСлужебный.ОписаниеКонтактнойИнформацииПользователя();
	РаботаСНоменклатуройПереопределяемый.ПолучитьКонтактнуюИнформациюПользователя(КонтактноеЛицо, Контакты);
	Возврат Контакты.ЭлектроннаяПочта;
КонецФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеАдресаЭлектроннойПочты()
	ТекстПодсказки = "";
	Если ПустаяСтрока(Email) Тогда
		ТекстПодсказки = НСтр("ru = 'Введите адрес электронной почты'");
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Email) Тогда
		ТекстПодсказки = НСтр("ru = 'Адрес электронной почты введен неверно'");
	КонецЕсли;
	Элементы.Email.РасширеннаяПодсказка.Заголовок = ТекстПодсказки;
КонецПроцедуры

#КонецОбласти