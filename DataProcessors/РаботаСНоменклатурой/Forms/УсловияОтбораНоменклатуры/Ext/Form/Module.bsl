
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьКомпоновщикаНастроек();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	Закрыть(КомпоновщикНастроекВыгрузки.Настройки);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьКомпоновщикаНастроек()
	
	ИспользоватьПодбор = РаботаСНоменклатурой.ИспользоватьПодборНоменклатурыСХарактеристиками();
	
	Если ИспользоватьПодбор Тогда
		ТекстЗапроса = "";
		РаботаСНоменклатуройПереопределяемый.ИнициализацияЗапросаОтбораНоменклатурыСХарактеристиками(ТекстЗапроса);
	Иначе
		ТекстЗапроса = РаботаСНоменклатуройСлужебный.ТекстЗапросаОтборНоменклатуры();
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
		ВыражениеNULL = Новый ВыражениеСхемыЗапроса("NULL");
		КоличествоПолей = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля.Количество();
		Для ОбратныйИндекс = 1 По КоличествоПолей Цикл
			ПроверяемоеПоле = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля[КоличествоПолей
				- ОбратныйИндекс];
			Если ПроверяемоеПоле = ВыражениеNULL Тогда
				СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля.Удалить(КоличествоПолей - ОбратныйИндекс);
			КонецЕсли;
		КонецЦикла;
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;

	СКД = РаботаСНоменклатуройСлужебный.СхемаКомпоновкиДанных(ТекстЗапроса);
	АдресСхемыКомпоновки = ПоместитьВоВременноеХранилище(СКД, УникальныйИдентификатор);
	КомпоновщикНастроекВыгрузки.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновки));

	Если Параметры.Свойство("НастройкиОтбора") И ТипЗнч(Параметры.НастройкиОтбора) = Тип("НастройкиКомпоновкиДанных") Тогда
		КомпоновщикНастроекВыгрузки.ЗагрузитьНастройки(Параметры.НастройкиОтбора);
	Иначе
		КомпоновщикНастроекВыгрузки.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти