
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Параметры.Свойство("ТекущийРеквизит", ТекущийРеквизит);
	Параметры.Свойство("ВидНоменклатуры", ВидНоменклатуры);
	Параметры.Свойство("НаименованиеКатегории", НаименованиеКатегории);
	Параметры.Свойство("ИдентификаторыКатегорий", ИдентификаторыКатегорий);
	Параметры.Свойство("ИдентификаторРеквизитаКатегории", ИдентификаторРеквизитаКатегории);
	
	ЗаполнитьПодсказкиФормы();
	
	Элементы.ГруппаДекорацииДлительнойОперации.Видимость = Ложь;
	
	НастроитьФормуПриДлительнойОперации(Истина, "Чтение");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПолучитьЗначенияРеквизита();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПояснениеКОперацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		
	РаботаСНоменклатуройКлиент.ОткрытьФормуКарточкиКатегории(НавигационнаяСсылкаФорматированнойСтроки, Неопределено, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗначенияРеквизитаВидаНоменклатуры

&НаКлиенте
Процедура ЗначенияРеквизитаВидаНоменклатурыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияРеквизитаВидаНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПривязатьЗначениеВидаНоменклатуры(ВыбраннаяСтрока);
	
	ОбновитьКоличествоСопоставленныхЗначений();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияРеквизитаВидаНоменклатурыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ЗначенияРеквизитаВидаНоменклатуры.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущееЗначениеРеквизита = ТекущиеДанные.ЗначениеРеквизита;
	
	Если РежимОтбораПоЗначению Тогда		
		ИзменитьРежимОтбораПохожих(Истина);
	КонецЕсли;	
	
	УстановитьЗаголовокГиперссылкиОтбора();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗначенияРеквизитаКатегории

&НаКлиенте
Процедура ЗначенияРеквизитаКатегорииПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияРеквизитаКатегорииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОтвязатьЗначениеВидаНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНастройкуСопоставлений();
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЗначение(Команда)
	
	ПривязатьЗначениеВидаНоменклатуры(Элементы.ЗначенияРеквизитаВидаНоменклатуры.ТекущаяСтрока);
		
КонецПроцедуры

&НаКлиенте
Процедура УбратьЗначение(Команда)
	
	ОтвязатьЗначениеВидаНоменклатуры();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсеЗначения(Команда)
	
	Если ЗначенияРеквизитаВидаНоменклатуры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьФормуПриДлительнойОперации(Истина, "Сопоставление");
	
	ПредставлениеЗначений = Новый Массив;	
	
	Для каждого ЭлементКоллекции Из ЗначенияРеквизитаВидаНоменклатуры Цикл
		ПредставлениеЗначений.Добавить(Строка(ЭлементКоллекции.ЗначениеРеквизита));
	КонецЦикла;
	
	ПараметрыЗапроса = Новый Структура;
	
	ПараметрыЗапроса.Вставить("ИдентификаторыКатегорий", ИдентификаторыКатегорий.ВыгрузитьЗначения());
	ПараметрыЗапроса.Вставить("ИдентификаторДополнительногоРеквизита", ИдентификаторРеквизитаКатегории);
	ПараметрыЗапроса.Вставить("ПредставлениеЗначений", ПредставлениеЗначений);

	ПараметрыЗавершения = Новый Структура;
	
	ПолучитьНоменклатуруЗавершение = Новый ОписаниеОповещения("ПолучитьЗначенияСоответствующиеЗаданнымЗавершение", ЭтотОбъект, ПараметрыЗавершения);
		
	РаботаСНоменклатуройКлиент.ПолучитьЗначенияСоответствующиеЗаданным(
		ПолучитьНоменклатуруЗавершение, ПараметрыЗапроса, ЭтотОбъект, ИдентификаторЗадания, Элементы.КартинкаДлительнойОперацииЧтения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЗначенияСоответствующиеЗаданнымЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ИдентификаторЗадания <> ДополнительныеПараметры.ИдентификаторЗадания Тогда 
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗадания = Неопределено;
	
	ЗаполнитьЗначенияПоРезультатуПоиска(Результат);
	
	Если ЗначенияРеквизитаВидаНоменклатуры.Количество() = 0 Тогда
		СпрятатьГиперссылкуОтбора();
	КонецЕсли;
	
	ОбновитьКоличествоСопоставленныхЗначений();
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияПоРезультатуПоиска(Результат)
	
	НастроитьФормуПриДлительнойОперации(Ложь, "Сопоставление");
	
	ЗначенияДляПодстановки = РаботаСНоменклатурой.ДанныеВременногоХранилища(Результат.АдресРезультата);
	
	Если ТипЗнч(ЗначенияДляПодстановки) <> Тип("ТаблицаЗначений")
		ИЛИ ЗначенияДляПодстановки.Количество() = 0 Тогда
		
		Возврат;
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////////
	
	СтрокиКУдалению = Новый Массив;
	
	Для Счетчик = 0 По ЗначенияРеквизитаВидаНоменклатуры.Количество() - 1 Цикл
		
		ЗначениеРеквизита = ЗначенияРеквизитаВидаНоменклатуры[Счетчик].ЗначениеРеквизита;
		
		ПредставлениеЗначения = Строка(ЗначениеРеквизита);
		
		СтрокаДанных = СтрокаДанныхПоПредставлениюЗначения(ЗначенияДляПодстановки, ПредставлениеЗначения);
		
		Если СтрокаДанных = Неопределено
			ИЛИ Не ЗначениеЗаполнено(СтрокаДанных.Идентификатор) Тогда
			
			Продолжить;
		КонецЕсли;
				
		СтрокиПоИдентификаторам = ЗначенияРеквизитаКатегории.НайтиСтроки(
			Новый Структура("ИдентификаторЗначенияРеквизитаКатегории", СтрокаДанных.Идентификатор));
		
		Если СтрокиПоИдентификаторам.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокиПоИдентификаторам[0].ЗначениеРеквизитаВидаНоменклатуры) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиПоИдентификаторам[0].ЗначениеРеквизитаВидаНоменклатуры = ЗначениеРеквизита;
			
		СтрокиКУдалению.Добавить(ЗначенияРеквизитаВидаНоменклатуры[Счетчик]);
			
	КонецЦикла;
	
	Для каждого ЭлементКоллекции Из СтрокиКУдалению Цикл
		ЗначенияРеквизитаВидаНоменклатуры.Удалить(ЭлементКоллекции);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СтрокаДанныхПоПредставлениюЗначения(ЗначенияДляПодстановки, ПредставлениеЗначения)
	
	Результат = Неопределено;
	
	Для каждого ЭлементКоллекции Из ЗначенияДляПодстановки Цикл
		Если ВРег(ЭлементКоллекции.Значение) = Врег(ПредставлениеЗначения) Тогда
			Результат = ЭлементКоллекции;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура УбратьВсеЗначения(Команда)
	
	Для каждого ЭлементКоллекции Из ЗначенияРеквизитаКатегории Цикл
		
		Если Не ЗначениеЗаполнено(ЭлементКоллекции.ЗначениеРеквизитаВидаНоменклатуры) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ЗначенияРеквизитаВидаНоменклатуры.Добавить();
		
		НоваяСтрока.ЗначениеРеквизита = ЭлементКоллекции.ЗначениеРеквизитаВидаНоменклатуры;
		
		ЭлементКоллекции.ЗначениеРеквизитаВидаНоменклатуры = Неопределено;
		
	КонецЦикла;
	
	ОбновитьКоличествоСопоставленныхЗначений();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗначение(Команда)
	
	ТекущиеДанные = Элементы.ЗначенияРеквизитаКатегории.ТекущиеДанные;
	
	НаименованиеЗначения = "";
	Если ТекущиеДанные <> Неопределено Тогда
		НаименованиеЗначения = Строка(ТекущиеДанные.ЗначениеРеквизитаКатегории);
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РеквизитСсылка", ТекущийРеквизит);
	ПараметрыОткрытия.Вставить("Наименование",   Строка(НаименованиеЗначения));
	
	РаботаСНоменклатуройКлиентПереопределяемый.ОткрытьФормуДополнительногоЗначения(ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПохожихЗначенийНажатие(Элемент, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	
	РежимОтбораПоЗначению = НЕ РежимОтбораПоЗначению;
	
	ИзменитьРежимОтбораПохожих(РежимОтбораПоЗначению);
		
	УстановитьЗаголовокГиперссылкиОтбора();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СпрятатьГиперссылкуОтбора()
	
	РежимОтбораПоЗначению = Ложь;
	
	Элементы.ОтборПохожихЗначений.Видимость = Ложь;
	
	ИзменитьРежимОтбораПохожих(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЗначенияРеквизита()
	
	ПараметрыЗавершения = Новый Структура;
	
	ПолучитьНоменклатуруЗавершение = Новый ОписаниеОповещения("ПолучитьЗначенияРеквизитаЗавершение", ЭтотОбъект, ПараметрыЗавершения);
	
	Идентификаторы = Новый Массив;
	
	ПараметрыПоиска = Новый Массив;
	
	Для каждого ЭлементКоллекции Из ИдентификаторыКатегорий Цикл
	
		ПараметрыПоиска = Новый Структура();
		
		ПараметрыПоиска.Вставить("ИдентификаторДополнительногоРеквизита", ИдентификаторРеквизитаКатегории);
		ПараметрыПоиска.Вставить("ИдентификаторКатегории",                ЭлементКоллекции.Значение);
		
		Идентификаторы.Добавить(ПараметрыПоиска);
			
	КонецЦикла;
			
	РаботаСНоменклатуройКлиент.ПолучитьЗначенияДополнительногоРеквизитаКатегории(
		ПолучитьНоменклатуруЗавершение, 
		Идентификаторы, 
		ЭтотОбъект, 
		ИдентификаторЗадания, 
		Элементы.ГруппаДекорацииДлительнойОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЗначенияРеквизитаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ИдентификаторЗадания <> ДополнительныеПараметры.ИдентификаторЗадания Тогда 
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗадания = Неопределено;
	
	ЗаполнитьЗначенияРеквизитов(Результат.АдресРезультата);
	
	ОбновитьКоличествоСопоставленныхЗначений();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияРеквизитов(АдресРезультата)
	
	НастроитьФормуПриДлительнойОперации(Ложь, "Чтение");
	
	ТаблицаРезультата = РаботаСНоменклатурой.ДанныеВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(ТаблицаРезультата) <> Тип("ТаблицаЗначений") Тогда
		Возврат;
	КонецЕсли; 
	
	////////////////////////////////////////////////////////////////////////////////
	
	ЗаполнитьРеквизитыКатегории(ТаблицаРезультата);
	
	ЗаполнитьРеквизитыВидаНоменклатуры();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкуСопоставлений()
	
	РаботаСНоменклатурой.ЗаписатьСоответствиеЗначенийРеквизита(ВидНоменклатуры, ТекущийРеквизит, ЗначенияРеквизитаКатегории.Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", ТекстНеЗаполненоЗначение());
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(200, 200, 200));
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ЗначенияРеквизитаКатегории.ЗначениеРеквизитаВидаНоменклатуры");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЗначенияРеквизитаКатегорииЗначениеРеквизитаВидаНоменклатуры.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если ЭтоЗначениеДополнительногоРеквизита(НовыйОбъект) Тогда
		
		ТекущиеДанные = Элементы.ЗначенияРеквизитаКатегории.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ЗначениеРеквизитаВидаНоменклатуры) Тогда
			НоваяСтрока = ЗначенияРеквизитаВидаНоменклатуры.Добавить();
			НоваяСтрока.ЗначениеРеквизита = НовыйОбъект;
		Иначе
			ТекущиеДанные.ЗначениеРеквизитаВидаНоменклатуры = НовыйОбъект;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоЗначениеДополнительногоРеквизита(НовыйОбъект)
	
	ТипыДополнительныхЗначений = Метаданные.ОпределяемыеТипы.ЗначенияСвойствОбъектовРаботаСНоменклатурой.Тип.Типы();
	
	Если ТипыДополнительныхЗначений.Количество() = 0  Тогда
		ТипДополнительныхЗначений = Тип("Неопределено");
	Иначе
		ТипДополнительныхЗначений = ТипыДополнительныхЗначений[0];	
	КонецЕсли;

	Возврат ТипЗнч(НовыйОбъект) = ТипДополнительныхЗначений;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьКоличествоСопоставленныхЗначений()
	
	ОсталосьСопоставитьЗначений = ЗначенияРеквизитаВидаНоменклатуры.Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРежимОтбораПохожих(ОтображатьТолькоПохожие)
	
	Если ОтображатьТолькоПохожие Тогда
		
		СтруктураОтбора = Новый Структура;
		
		СтруктураОтбора.Вставить("ЗначениеРеквизитаКатегории", Строка(ТекущееЗначениеРеквизита));
		
		Элементы.ЗначенияРеквизитаКатегории.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураОтбора);
		
	Иначе
		Элементы.ЗначенияРеквизитаКатегории.ОтборСтрок = Неопределено;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодсказкиФормы()
	
	Элементы.ПояснениеКОперации.Заголовок = Новый ФорматированнаяСтрока(ТекстПоясненияОперации());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыВидаНоменклатуры()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	
	"ВЫБРАТЬ
	|	СоответствиеЗначенийРеквизитовРаботаСНоменклатурой.Значение КАК Значение,
	|	СоответствиеЗначенийРеквизитовРаботаСНоменклатурой.ИдентификаторЗначенияРеквизитаКатегории КАК ИдентификаторЗначенияРеквизитаКатегории
	|ПОМЕСТИТЬ ВТСопоставленныеРеквизиты
	|ИЗ
	|	РегистрСведений.СоответствиеЗначенийРеквизитовРаботаСНоменклатурой КАК СоответствиеЗначенийРеквизитовРаботаСНоменклатурой
	|ГДЕ
	|	СоответствиеЗначенийРеквизитовРаботаСНоменклатурой.ОбъектСопоставления = &ОбъектСопоставления
	|	И СоответствиеЗначенийРеквизитовРаботаСНоменклатурой.РеквизитОбъекта = &ТекущийРеквизит
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияСвойствОбъектов.Ссылка КАК ЗначениеРеквизитаВидаНоменклатуры,
	|	ЕСТЬNULL(ВТСопоставленныеРеквизиты.ИдентификаторЗначенияРеквизитаКатегории, """") КАК ИдентификаторЗначенияРеквизитаКатегории
	|ИЗ
	|	&ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСопоставленныеРеквизиты КАК ВТСопоставленныеРеквизиты
	|		ПО ЗначенияСвойствОбъектов.Ссылка = ВТСопоставленныеРеквизиты.Значение
	|ГДЕ
	|	ЗначенияСвойствОбъектов.Владелец = &ТекущийРеквизит";
	
	Запрос.УстановитьПараметр("ОбъектСопоставления", ВидНоменклатуры);
	Запрос.УстановитьПараметр("ТекущийРеквизит", ТекущийРеквизит);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ЗначенияСвойствОбъектов", 
		Метаданные.ОпределяемыеТипы.ЗначенияСвойствОбъектовРаботаСНоменклатурой.Тип.ПривестиЗначение().Метаданные().ПолноеИмя());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(Выборка.ИдентификаторЗначенияРеквизитаКатегории) Тогда
			НоваяСтрока = ЗначенияРеквизитаВидаНоменклатуры.Добавить();
			НоваяСтрока.ЗначениеРеквизита = Выборка.ЗначениеРеквизитаВидаНоменклатуры;
		Иначе
			СтрокиЗначений = ЗначенияРеквизитаКатегории.НайтиСтроки(
				Новый Структура("ИдентификаторЗначенияРеквизитаКатегории", Выборка.ИдентификаторЗначенияРеквизитаКатегории));	
				
			Если СтрокиЗначений.Количество() <> 0 Тогда
				СтрокиЗначений[0].ЗначениеРеквизитаВидаНоменклатуры = Выборка.ЗначениеРеквизитаВидаНоменклатуры;
			КонецЕсли;	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыКатегории(РеквизитыЗначенияКатегории)
	
	ЗначенияРеквизитаКатегории.Очистить();
	
	Для каждого ТекущаяКатегория Из РеквизитыЗначенияКатегории Цикл		
		Для каждого ЭлементКоллекции Из ТекущаяКатегория.Значения Цикл
			
			Если ЗначенияРеквизитаКатегории.НайтиСтроки(
				Новый Структура("ИдентификаторЗначенияРеквизитаКатегории", ЭлементКоллекции.Идентификатор)).Количество() > 0 Тогда
				
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ЗначенияРеквизитаКатегории.Добавить();
			
			НоваяСтрока.ЗначениеРеквизитаКатегории = ЭлементКоллекции.Наименование;
			НоваяСтрока.ИдентификаторЗначенияРеквизитаКатегории = ЭлементКоллекции.Идентификатор;
		КонецЦикла;	
	КонецЦикла;
			
КонецПроцедуры

&НаКлиенте
Процедура ПривязатьЗначениеВидаНоменклатуры(ВыбраннаяСтрока)
	
	СтрокаКатегории = Элементы.ЗначенияРеквизитаКатегории.ТекущиеДанные;
	
	Если СтрокаКатегории = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаВидаНоменклатуры = ЗначенияРеквизитаВидаНоменклатуры.НайтиПоИдентификатору(ВыбраннаяСтрока);

	Если ЗначениеЗаполнено(СтрокаКатегории.ЗначениеРеквизитаВидаНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаКатегории.ЗначениеРеквизитаВидаНоменклатуры = СтрокаВидаНоменклатуры.ЗначениеРеквизита;
	
	ЗначенияРеквизитаВидаНоменклатуры.Удалить(СтрокаВидаНоменклатуры);
	
	СпозиционироватьНаНовойСтроке();
	
	ОбновитьКоличествоСопоставленныхЗначений();
	
	Если ЗначенияРеквизитаВидаНоменклатуры.Количество() = 0 Тогда
		СпрятатьГиперссылкуОтбора();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтвязатьЗначениеВидаНоменклатуры()
	
	ТекущиеДанные = Элементы.ЗначенияРеквизитаКатегории.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.ЗначениеРеквизитаВидаНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	// Создаем строку в приемнике
	
	НоваяСтрока = ЗначенияРеквизитаВидаНоменклатуры.Добавить();
	
	НоваяСтрока.ЗначениеРеквизита = ТекущиеДанные.ЗначениеРеквизитаВидаНоменклатуры;
		
	// Очищаем в источнике
	
	ТекущиеДанные.ЗначениеРеквизитаВидаНоменклатуры = Неопределено;
	
	ОбновитьКоличествоСопоставленныхЗначений();
		
КонецПроцедуры

&НаКлиенте
Процедура СпозиционироватьНаНовойСтроке()
	
	Для каждого ЭлементКоллекции Из ЗначенияРеквизитаКатегории Цикл
		Если НЕ ЗначениеЗаполнено(ЭлементКоллекции.ЗначениеРеквизитаВидаНоменклатуры) Тогда
			Элементы.ЗначенияРеквизитаКатегории.ТекущаяСтрока = ЭлементКоллекции.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПриДлительнойОперации(ЭтоНачалоДлительнойОперации, ВидОперации)
	
	Если ВидОперации = "Чтение" Тогда
		Если ЭтоНачалоДлительнойОперации Тогда
			Элементы.ГруппаСтраницДанныхСервиса.ТекущаяСтраница = Элементы.СтраницаДлительнойОперации;
		Иначе
			Элементы.ГруппаСтраницДанныхСервиса.ТекущаяСтраница = Элементы.СтраницаЗначений;	
		КонецЕсли;
	ИначеЕсли ВидОперации = "Сопоставление" Тогда	
		Элементы.ГруппаДекорацииДлительнойОперации.Видимость = ЭтоНачалоДлительнойОперации;		
	КонецЕсли;
		
	Элементы.ГруппаТаблицыРеквизитов.Доступность       = Не ЭтоНачалоДлительнойОперации;
	Элементы.СоздатьЗначение.Доступность               = Не ЭтоНачалоДлительнойОперации;
	Элементы.ГруппаКнопокУправленияСписком.Доступность = Не ЭтоНачалоДлительнойОперации;
	
КонецПроцедуры

#КонецОбласти

#Область СтроковыеКонстанты

&НаКлиенте
Процедура УстановитьЗаголовокГиперссылкиОтбора()
	
	Элементы.ОтборПохожихЗначений.Видимость = ЗначенияРеквизитаВидаНоменклатуры.Количество() > 0;
	
	Если РежимОтбораПоЗначению Тогда
		ОтборПохожихЗначений = СтрШаблон(НСтр("ru = 'Убрать отбор по значению %1'"), ТекущееЗначениеРеквизита);	
	Иначе
		ОтборПохожихЗначений = СтрШаблон(НСтр("ru = 'Отобрать похожие на %1'"), ТекущееЗначениеРеквизита);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ТекстПоясненияОперации()
	
	МассивПодстрок = Новый Массив;
	
	МассивПодстрок.Добавить(НСтр("ru = 'Сопоставление значений реквизита'") + " ");
	МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(Строка(ТекущийРеквизит),Новый Шрифт(,,Истина)));
	МассивПодстрок.Добавить(" " + НСтр("ru = 'вида номенклатуры'") + " ");
	МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(Строка(ВидНоменклатуры),Новый Шрифт(,,Истина)));

	Если ИдентификаторыКатегорий.Количество() = 1 Тогда
		МассивПодстрок.Добавить(" " + НСтр("ru = 'и категории 1С:Номенклатуры'") + " ");
		МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(НаименованиеКатегории,,,,ИдентификаторКатегории));
	КонецЕсли;
	
	Возврат МассивПодстрок;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстНеЗаполненоЗначение()
	
	Возврат НСтр("ru = '<Выберите значение или создайте новое>'");
	
КонецФункции

#КонецОбласти