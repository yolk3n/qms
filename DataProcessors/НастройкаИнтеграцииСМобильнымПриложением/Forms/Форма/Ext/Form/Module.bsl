
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.КлиентПодключенЧерезВебСервер() Тогда
		АдресИнформационнойБазы = ПолучитьНавигационнуюСсылкуИнформационнойБазы();
	КонецЕсли;
	
	ВывестиШтрихкод();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АдресИнформационнойБазыПриИзменении(Элемент)
	
	ВывестиШтрихкод(Истина);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВывестиШтрихкод(Знач РучнойВвод = Ложь)
	
	РезультатПроверки = ТарификацияБольничнаяАптека.УслугаДоступна_ПоддержкаСмартфонаДляСканированияШтрихкодов();
	Если РезультатПроверки.УслугаДоступна Тогда
		
		НастройкиПодключения = ИнтеграцияСМобильнымПриложением.СформироватьНастройкиОбменаСМобильнымПриложением(АдресИнформационнойБазы);
		Если НастройкиПодключения <> Неопределено Тогда
			НастройкиПодключенияJSON = ИнтеграцияСМобильнымПриложением.СтрокаJSONИзДанных(НастройкиПодключения);
			ШтрихкодИнтеграции = СформироватьQRКод(НастройкиПодключенияJSON, УникальныйИдентификатор);
		Иначе
			Если РучнойВвод Тогда
				Текст = НСтр("ru = 'Некорректный адрес информационной базы (на веб-сервере).'");
				ОбщегоНазначения.СообщитьПользователю(Текст,, "АдресИнформационнойБазы");
			Иначе
				АдресИнформационнойБазы = "";
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ГруппаДанныеURL.Видимость = НастройкиПодключения = Неопределено Или РучнойВвод;
		
	Иначе
		Элементы.ДекорацияШтрихкодИнтеграцииИнформация.Заголовок = РезультатПроверки.ТекстОшибки;
		Элементы.ГруппаДанныеURL.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьQRКод(ДанныеКода, УникальныйИдентификатор)
	
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(ДанныеКода, 0, 250);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеQRКода, УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
