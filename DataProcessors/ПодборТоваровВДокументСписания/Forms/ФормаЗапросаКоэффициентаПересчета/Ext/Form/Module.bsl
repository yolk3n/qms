
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗатребованнаяНоменклатура = Параметры.АналитикаОписанияОтбораНоменклатуры;
	ЕдиницаИзмеренияТребования = Параметры.ЕдиницаИзмерения;
	Номенклатура = Параметры.Номенклатура;
	
	Если Не (ЗначениеЗаполнено(Номенклатура) И ЗначениеЗаполнено(ЗатребованнаяНоменклатура) И ЗначениеЗаполнено(ЕдиницаИзмеренияТребования)) Тогда
		ВызватьИсключение НСтр("ru = 'Обработка используется только вместе с объектами информационной базы'");
	КонецЕсли;
	
	ЕдиницаИзмеренияНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения");
	
	Элементы.Коэффициент.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Количество единиц номенклатуры (%1) в затребованной единице (%2)'"),
		СокрЛП(ЕдиницаИзмеренияНоменклатуры),
		СокрЛП(ЕдиницаИзмеренияТребования));
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА КОМАНД ФОРМЫ
#Область ОбработкаКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		ПараметрЗакрытия = Новый Структура;
		ПараметрЗакрытия.Вставить("Коэффициент", Коэффициент);
		Закрыть(ПараметрЗакрытия);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработкаКомандФормы
