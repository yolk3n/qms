#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Продукт = "csp40";
	ЗаполнитьРегистрационныеДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(КонтактноеЛицо) И ЗначениеЗаполнено(ЭлектроннаяПочта) Тогда
		ТекущийЭлемент = Элементы.СерийныйНомер;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ЭлектроннаяПочта)
		И Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(ЭлектроннаяПочта) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Некорректно заполнен адрес электронной почты.'"),, "ЭлектроннаяПочта",, Отказ);
	КонецЕсли;
	
	СимволыСерийногоНомера = Новый Массив;
	Если ЗначениеЗаполнено(СерийныйНомер) Тогда
		Для Индекс = 1 По СтрДлина(СерийныйНомер) Цикл
			ТекущийСимвол = ВРег(Сред(СерийныйНомер, Индекс, 1));
			Код = КодСимвола(ТекущийСимвол);
			Если (Код > 64 И Код < 91) ИЛИ (Код > 47 И Код < 58) Тогда
				СимволыСерийногоНомера.Добавить(ТекущийСимвол);
			КонецЕсли;
		КонецЦикла;
		
		ЧистыйСерийныйНомер = СтрСоединить(СимволыСерийногоНомера, "");
		Если СтрДлина(ЧистыйСерийныйНомер) <> 25 Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Некорректно заполнен серийный номер.'"),, "СерийныйНомер",, Отказ);
		ИначеЕсли Лев(ЧистыйСерийныйНомер, 2) <> "39" И Лев(ЧистыйСерийныйНомер, 2) <> "40" Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Серийный номер не соответствует продуктам CryptoPro CSP 3.9 и CryptoPro CSP 4.0.
                      |Автоматическая установка невозможна'"),, "СерийныйНомер",, Отказ);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СерийныйНомерПриИзменении(Элемент)
	
	СерийныйНомер = СокрЛП(СерийныйНомер);
	Если ЗначениеЗаполнено(СерийныйНомер) Тогда
		Если Лев(СерийныйНомер, 2) = "39" Тогда
			Продукт = "csp39";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		ПараметрыРегистрации = КриптографияБЭДСлужебныйКлиент.НовыеПараметрыРегистрацииПрограммыКриптографии();
		ПараметрыРегистрации.КонтактноеЛицо = СокрЛП(КонтактноеЛицо);
		ПараметрыРегистрации.ЭлектроннаяПочта = СокрЛП(ЭлектроннаяПочта);
		ПараметрыРегистрации.СерийныйНомер = СокрЛП(СерийныйНомер);
		ПараметрыРегистрации.Продукт = Продукт;
		ПараметрыРегистрации.ВыполнятьКонтрольЦелостности = ВыполнятьКонтрольЦелостности;
		ПараметрыРегистрации.ИмяПрограммы = НСтр("ru = 'CryptoPro CSP'");
		
		Если НЕ ЗначениеЗаполнено(СерийныйНомер) Тогда
			ТекстВопроса = НСтр("ru = 'Без указания серийного номера программа CryptoPro CSP будет работать ограниченное время.
                                 |
                                 |Продолжить?'");
			Оповещение = Новый ОписаниеОповещения("ЗагрузитьПослеОтветаНаВопрос", ЭтотОбъект, ПараметрыРегистрации);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
		Иначе
			ЗагрузитьПослеОтветаНаВопрос(КодВозвратаДиалога.Да, ПараметрыРегистрации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьПослеОтветаНаВопрос(Ответ, ВходящийКонтекст) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		Закрыть(ВходящийКонтекст);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРегистрационныеДанные()

	ТекущийПользователь = Пользователи.ТекущийПользователь();
	КонтактноеЛицо = ТекущийПользователь;
	
	КонтактнаяИнформация = ИнтеграцияБСПБЭД.КонтактнаяИнформацияОбъекта(ТекущийПользователь, "АдресЭлектроннойПочты");
	Если ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(КонтактнаяИнформация.Представление) Тогда
		ЭлектроннаяПочта = КонтактнаяИнформация.Представление;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

