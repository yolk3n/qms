#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	Документ = Параметры.Документ;
	ЗаполнитьПодписантов();
	СведенияОДлительнойОперации = Новый Структура;
	СведенияОДлительнойОперации.Вставить("Имя", "");
	СведенияОДлительнойОперации.Вставить("ДлительнаяОперация");
	ЭлектронноеАктированиеЕИС.НачатьПолучениеПодписантов(ЭтотОбъект);
	
	ЕстьПравоНаИзменение = ПравоДоступа("Изменение",
		Метаданные.РегистрыСведений.ПодписантыДокументовЕИС);
	ТолькоПросмотр = НЕ ЕстьПравоНаИзменение;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ПеренестиВДокумент Тогда
		ОповеститьОВыборе(СтруктураРезультат);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ТолькоПросмотр Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Подписанты.Количество() = 0;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПолученияПодписантов", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(СведенияОДлительнойОперации.ДлительнаяОперация,
			ОповещениеОЗавершении,
			ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Выбран", Истина);
	Строки = Подписанты.НайтиСтроки(Отбор);
	Если Строки.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='Не выбран подписант для документа.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПеренестиВДокумент = Истина;
	ЗаполнитьРезультат();
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПодписантов()
	
	// Заполнение из параметров.
	Для каждого Подписант Из Параметры.Подписанты Цикл
		Строка = Подписанты.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, Подписант);
		Строка.Выбран = Истина;
	КонецЦикла;
	
	// Заполнение из справочника подписантов.
	Отбор = Новый Структура;
	Отбор.Вставить("Идентификатор", 0);
	
	ПодписантыИзСправочника = ЭлектронноеАктированиеЕИС.ПодписантыОрганизации(Организация);
	Для Каждого Подписант Из ПодписантыИзСправочника Цикл
		Отбор.Идентификатор = Подписант.Идентификатор;
		Строки = Подписанты.НайтиСтроки(Отбор);
		Если Строки.Количество() = 0 Тогда
			Строка = Подписанты.Добавить();
			ЗаполнитьЗначенияСвойств(Строка, Подписант);
			Строка.ФИО = Подписант.Ссылка; 
			Если ПодписантыИзСправочника.Количество() = 1 Тогда
				// Если подписант один, то выбираем его автоматически.
				Строка.Выбран = Истина;
			Иначе
				Строка.Выбран = Подписант.ЭтоРуководительОрганизации;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияПодписантов(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьДанныеПодписантов(Результат);
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьДанныеПодписантов(Результат)
	
	Если ЗначениеЗаполнено(Результат) И Результат.Статус = "Выполнено" Тогда
		
		РезультатПолучения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если НЕ РезультатПолучения.Выполнено Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ПодписантыССервера = РезультатПолучения.СведенияОПоставщике.Подписанты;
		СоответствиеПодписантов = Новый Соответствие;
		Для каждого Подписант Из ПодписантыССервера Цикл
			СоответствиеПодписантов.Вставить(Подписант.Идентификатор, Подписант);
		КонецЦикла;
		
		// Удаляем тех, кого нет на сервере или заблокированных.
		ПодписантКУдалению = Новый Массив;
		Для каждого Подписант Из Подписанты Цикл
			ПодписантСервера = СоответствиеПодписантов[Подписант.Идентификатор];
			Если ПодписантСервера = Неопределено
				ИЛИ ПодписантСервера.Статус = Перечисления.СтатусыПодписантовЕИС.Заблокирован Тогда
				ПодписантКУдалению.Добавить(Подписант);
			КонецЕсли;
		КонецЦикла;
		
		Для каждого Подписант Из ПодписантКУдалению Цикл
			Подписанты.Удалить(Подписант);
		КонецЦикла;
		
		// Добавляем новых пописантов.
		Отбор = Новый Структура;
		Отбор.Вставить("Идентификатор", 0);
		СтатусПодписантаПоУмолчанию = "3";
		Для каждого Подписант Из ПодписантыССервера Цикл
			
			Если НЕ Подписант.АктуальностьСвязиСРеестровойЗаписьюЕРУЗ Тогда
				Продолжить;
			КонецЕсли;
			
			Отбор.Идентификатор = Подписант.Идентификатор;
			Строки = Подписанты.НайтиСтроки(Отбор);
			Если Строки.Количество() = 0 Тогда
				Строка = Подписанты.Добавить();
			Иначе
				Строка = Строки[0];
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(Строка, Подписант, , "Статус");
			СсылкаНаПодписанта =
				ЭлектронноеАктированиеЕИС.СохранитьПодписанта(Организация, Подписант);
			Строка.ФИО = СсылкаНаПодписанта;
			Если СсылкаНаПодписанта.ЭтоРуководительОрганизации Тогда
				Строка.Выбран = Истина;
			КонецЕсли;
			
			Если ПустаяСтрока(Строка.Статус) Тогда
				Полномочия = Подписант.Полномочия[0];
				Строка.ОбластьПолномочий = Полномочия.КодОбласти;
				Строка.Статус = СтатусПодписантаПоУмолчанию;
				Строка.ОснованиеПолномочий = Полномочия.ОснованиеПолномочий;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьРезультат()
	
	НаборЗаписейПодписанты = РегистрыСведений.ПодписантыДокументовЕИС.СоздатьНаборЗаписей();
	НаборЗаписейПодписанты.Отбор.Документ.Установить(Документ);
	Для каждого Подписант Из Подписанты Цикл
		Если Подписант.Выбран = Истина Тогда
			ЗаписьПодписант = НаборЗаписейПодписанты.Добавить();
			ЗаписьПодписант.Активность = Ложь;
			ЗаписьПодписант.Документ = Документ;
			ЗаписьПодписант.Организация = Организация;
			ЗаписьПодписант.Подписант = Подписант.ФИО;
			ЗаписьПодписант.ОбластьПолномочий = Подписант.ОбластьПолномочий;
			ЗаписьПодписант.ОснованиеПолномочий = Подписант.ОснованиеПолномочий;	
		КонецЕсли;
	КонецЦикла;
	НаборЗаписейПодписанты.Записать();
	
КонецПроцедуры

#КонецОбласти