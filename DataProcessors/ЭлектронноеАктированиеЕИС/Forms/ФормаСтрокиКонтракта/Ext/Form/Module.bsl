
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Контракт = Параметры.ГосударственныйКонтракт;
	ДанныеКонтракта = ЭлектронноеАктированиеЕИС.ДанныеГосударственногоКонтракта(Контракт);
	Идентификатор = Параметры.Идентификатор;
	ТолькоПросмотр = Параметры.ТолькоПросмотр;
	ЭтотОбъект.ТолькоПросмотр = Параметры.ТолькоПросмотр;
	Элементы.ВыбратьСтроку.Видимость = НЕ ТолькоПросмотр;
	Если ТолькоПросмотр Тогда
		ЭтотОбъект.Заголовок = НСтр("ru='Просмотр данных строки контракта'");
	Иначе
		ЭтотОбъект.Заголовок = НСтр("ru='Выбор строки контракта'");
	КонецЕсли;
	
	Для каждого ОбъектЗакупки Из ДанныеКонтракта.ОбъектыЗакупки Цикл
		Если ОбъектЗакупки.Идентификатор = Идентификатор Тогда
			
			НомерСтроки = ОбъектЗакупки.НомерСтроки;
			ТипОбъектаЗакупки = ОбъектЗакупки.Тип;
			Наименование = ОбъектЗакупки.Наименование;
			ВнутреннийИдентификаторЕИС = ОбъектЗакупки.ВнутреннийИдентификаторЕИС;
			Количество = ОбъектЗакупки.Количество;
			Цена = ОбъектЗакупки.Цена;
			Сумма = ОбъектЗакупки.Сумма;
			Если ОбъектЗакупки.Классификатор = "КТРУ" Тогда
				Элементы.ГруппаОКПД.Заголовок = "ОКПД2";
				КодКТРУ = ОбъектЗакупки.КТРУ.Код;
				НаименованиеКТРУ = ОбъектЗакупки.КТРУ.Наименование;
				Если ЗначениеЗаполнено(ОбъектЗакупки.КТРУ.ОКПД2) Тогда
					КодОКПД = ОбъектЗакупки.КТРУ.ОКПД2.Код;
					НаименованиеОКПД = ОбъектЗакупки.КТРУ.ОКПД2.Наименование;
				КонецЕсли;
			ИначеЕсли ОбъектЗакупки.Классификатор = "ОКПД2" Тогда
				Элементы.ГруппаКТРУ.Видимость = Ложь;
				Элементы.ГруппаОКПД.Заголовок = "ОКПД2";
				КодОКПД = ОбъектЗакупки.ОКПД2.Код;
				НаименованиеОКПД = ОбъектЗакупки.ОКПД2.Наименование;
			ИначеЕсли ОбъектЗакупки.Классификатор = "ОКПД" Тогда
				Элементы.ГруппаКТРУ.Видимость = Ложь;
				КодОКПД = ОбъектЗакупки.ОКПД.Код;
				НаименованиеОКПД = ОбъектЗакупки.ОКПД.Наименование;
			КонецЕсли;
			
			СтавкаНДС = ЭлектронноеАктированиеЕИС.СтавкаНДСОбъектаЗакупки(ОбъектЗакупки);
			СуммаНДС = ОбъектЗакупки.СуммаНДС;
			
			КодЕдиницыИзмерения = ОбъектЗакупки.ОКЕИ.Код;
			ПолноеНаименованиеЕдиницыИзмерения = ОбъектЗакупки.ОКЕИ.Наименование;
			НаименованиеЕдиницыИзмерения = ОбъектЗакупки.ОКЕИ.НациональныйКод;
			
		КонецЕсли;
	КонецЦикла;
	
	Элементы.СуммаНДС.Видимость = ЗначениеЗаполнено(СуммаНДС);
	
КонецПроцедуры

