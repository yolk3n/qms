
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРезультатЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()
	
	ОбновитьРезультатЗагрузки();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьИсходныеДанные(Команда)
	
	ТекущаяСтрока = Элементы.РезультатыЗагрузкиТоваров.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Или ПустаяСтрока(ТекущаяСтрока.ИсходныеДанные) Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("РезультатыЗагрузкиТоваровВыборЗавершение", ЭтотОбъект);
	ЗаголовокПросмотра = СтрЗаменить(НСтр("ru='Исходные данные: %1'"), "%1", Строка(ТекущаяСтрока.АптечныйТовар));
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(ОповещениеОЗавершении, ТекущаяСтрока.ИсходныеДанные, ЗаголовокПросмотра);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборПриИзменении(Элемент)
	
	УстановитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатыЗагрузкиТоваровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("РезультатыЗагрузкиТоваровВыборЗавершение", ЭтотОбъект);
	Если Поле.Имя = "РезультатыЗагрузкиТоваровАптечныйТовар" Тогда
		
		АптечныйТовар = Элементы.РезультатыЗагрузкиТоваров.ДанныеСтроки(ВыбраннаяСтрока).АптечныйТовар;
		Если ЗначениеЗаполнено(АптечныйТовар) Тогда
			ПоказатьЗначение(ОповещениеОЗавершении, АптечныйТовар);
		КонецЕсли;
		
	ИначеЕсли Поле.Имя = "РезультатыЗагрузкиТоваровКомментарий" Тогда
		
		Текст = Элементы.РезультатыЗагрузкиТоваров.ДанныеСтроки(ВыбраннаяСтрока).Комментарий;
		Если Не ПустаяСтрока(Текст) Тогда
			ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(ОповещениеОЗавершении, Текст, НСтр("ru='Комментарий'"));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатыЗагрузкиТоваровВыборЗавершение(НеиспользуемыйПараметр1 = Неопределено, НеиспользуемыйПараметр2 = Неопределено) Экспорт
	Возврат;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьРезультатЗагрузки()
	
	ТекущаяСтрока = Элементы.РезультатыЗагрузкиТоваров.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		НомерРЛС = ТекущаяСтрока.НомерРЛС;
	КонецЕсли;
	
	ЗаполнитьРезультатЗагрузки();
	
	Если НомерРЛС <> Неопределено Тогда
		НайденныеСтроки = Объект.РезультатыЗагрузкиТоваров.НайтиСтроки(Новый Структура("НомерРЛС", НомерРЛС));
		Если НайденныеСтроки.Количество() > 0 Тогда
			Элементы.РезультатыЗагрузкиТоваров.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРезультатЗагрузки()
	
	Результаты = ПолучитьИзВременногоХранилища(Параметры.АдресРезультатовВХранилище);
	Если Результаты <> Неопределено Тогда
		Объект.РезультатыЗагрузкиТоваров.Загрузить(Результаты);
		Объект.РезультатыЗагрузкиТоваров.Сортировать("НомерРЛС");
		УстановитьОтбор();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтбор()
	
	Если ПустаяСтрока(Отбор) Тогда
		ОтборСтрок = Новый Структура;
	ИначеЕсли Отбор = "Успешно" Тогда
		ОтборСтрок = Новый Структура("КартинкаСтатуса", 0);
	Иначе
		ОтборСтрок = Новый Структура("Статус", Отбор);
	КонецЕсли;
	
	Элементы.РезультатыЗагрузкиТоваров.ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборСтрок);
	
	ВсегоСтрок = Объект.РезультатыЗагрузкиТоваров.Количество();
	Если ОтборСтрок.Количество() = 0 Тогда
		КоличествоОтобранныхСтрок = СтрШаблон("(%1)", ВсегоСтрок);
	Иначе
		КоличествоСтрок = Объект.РезультатыЗагрузкиТоваров.НайтиСтроки(ОтборСтрок).Количество();
		КоличествоОтобранныхСтрок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '(%1 из %2)'"), КоличествоСтрок, ВсегоСтрок);
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
