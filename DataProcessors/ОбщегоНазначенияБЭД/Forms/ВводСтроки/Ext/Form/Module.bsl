

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Инициализировать();
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ОформитьДляМобильногоКлиента();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставлениеДанныхНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Заголовок", ПредставлениеДанных);
	ПараметрыОткрытия.Вставить("СписокДанных", Данные);
	ОткрытьФорму("Обработка.ОбщегоНазначенияБЭД.Форма.СписокДанных", ПараметрыОткрытия, ЭтотОбъект,
		УникальныйИдентификатор,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Обязательность И Не ЗначениеЗаполнено(Строка) Тогда
		ТекстСообщения = КомментарийОбязательностиВвода;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Строка");
		Возврат; 
	КонецЕсли;
	
	Закрыть(Строка);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура Инициализировать()
	Если ЗначениеЗаполнено(Параметры.ЗаголовокФормы) Тогда
		Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ДополнительноеОписание) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДополнительноеОписание", "Ширина", 40);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДополнительноеОписание", "Высота", 5);
		ДополнительноеОписаниеВидимость = ЗначениеЗаполнено(Параметры.ДополнительноеОписание);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "ДополнительноеОписание", "Видимость", ДополнительноеОписаниеВидимость);
		ДополнительноеОписание = Параметры.ДополнительноеОписание;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Параметры.Строка) Тогда
		Строка = Параметры.Строка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.НазваниеКнопкиПоУмолчанию) Тогда
		Элементы.ОК.Заголовок = Параметры.НазваниеКнопкиПоУмолчанию;
	КонецЕсли;
	
	Элементы.Строка.МногострочныйРежим = Параметры.Многострочность;
	Если Элементы.Строка.МногострочныйРежим Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Строка", "Ширина", 40);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Строка", "Высота", 5);	
	КонецЕсли;
	Обязательность = Параметры.Обязательность;
	Если ЗначениеЗаполнено(Параметры.КомментарийОбязательностиВвода) Тогда
		КомментарийОбязательностиВвода = Параметры.КомментарийОбязательностиВвода;
	Иначе
		КомментарийОбязательностиВвода = НСтр("ru = 'Необходимо ввести текст'");
	КонецЕсли;	
	
	ПредставлениеДанных = Параметры.ПредставлениеДанных;
	Если Не ЗначениеЗаполнено(ПредставлениеДанных) Тогда
		ПредставлениеДанных = НСтр("ru = 'Обрабатываемые данные'");
	КонецЕсли;
	Если Параметры.Данные <> Неопределено Тогда
		Если Параметры.ОбработчикПолученияПредставлений <> "" Тогда
			ПараметрыВычисления = Новый Структура;
			ПараметрыВычисления.Вставить("Данные", Параметры.Данные);
			ПредставленияДанных = ОбщегоНазначения.ВычислитьВБезопасномРежиме(
				Параметры.ОбработчикПолученияПредставлений + "(Параметры.Данные)",
				ПараметрыВычисления);
		Иначе
			ПредставленияДанных = Неопределено;
		КонецЕсли;
		
		Для каждого ЭлементДанных Из Параметры.Данные Цикл
			ПредставлениеЭлементаДанных = ?(ПредставленияДанных = Неопределено, ЭлементДанных, ПредставленияДанных[ЭлементДанных]);
			Данные.Добавить(ЭлементДанных, ПредставлениеЭлементаДанных);
		КонецЦикла;
		
		Элементы.ПредставлениеДанных.Видимость = Истина;
		Если Данные.Количество() > 1 Тогда
			ПредставлениеДанных = ПредставлениеДанных + СтрШаблон(" (%1)", Данные.Количество());
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОформитьДляМобильногоКлиента()
	Элементы.ГруппаМК.Видимость = Истина;
	
	ОбщегоНазначенияБЭД.ПереместитьЭлемент(Элементы, "ОК", "ГруппаМК");
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОК", "Шрифт", ШрифтыСтиля.КрупныйШрифтТекста);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОК", "ГоризонтальноеПоложениеВГруппе", ГоризонтальноеПоложениеЭлемента.Центр);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОК", "РастягиватьПоГоризонтали", Истина);
	
	ОбщегоНазначенияБЭД.ПереместитьЭлемент(Элементы, "Отмена", "ГруппаМК");
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отмена", "ГоризонтальноеПоложениеВГруппе", ГоризонтальноеПоложениеЭлемента.Центр);
	Элементы.Строка.ЦветФона = ЦветаСтиля.ЦветФонаПоляВводаМКБЭД;
КонецПроцедуры

#КонецОбласти
