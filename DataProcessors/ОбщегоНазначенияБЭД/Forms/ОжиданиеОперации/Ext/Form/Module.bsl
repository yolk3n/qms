#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыДлительнойОперации = ОжиданиеОперацийБЭДСлужебныйКлиентСервер.НовыеПараметры();
	ЗаполнитьЗначенияСвойств(ПараметрыДлительнойОперации, Параметры);
	ОбновитьФормуПоПараметрам(ПараметрыДлительнойОперации);
	
	Элементы.КартинкаУспешно.Видимость = Ложь;
	Элементы.ФормаЗакрыть.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, КонтекстДлительнойОперации, Источник)
	
	Если ИмяСобытия = ОжиданиеОперацийБЭДСлужебныйКлиентСервер.ИмяСобытияЗакрытияФормы()
		Или ИмяСобытия = ОжиданиеОперацийБЭДСлужебныйКлиентСервер.ИмяСобытияПередИнтерактивнымДействием() Тогда
		Если КонтекстДлительнойОперации.ИдентификаторОперации = Параметры.ИдентификаторОперации Тогда
			ЗакрытьФорму(Ложь);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = ОжиданиеОперацийБЭДСлужебныйКлиентСервер.ИмяСобытияОбновленияПрогресса() Тогда
		Если КонтекстДлительнойОперации.ИдентификаторОперации = Параметры.ИдентификаторОперации Тогда
			ОбновитьФормуПоПараметрам(КонтекстДлительнойОперации.ПараметрыОжиданияОперации);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	ЗакрытьФорму(Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	
	ЗакрытьФорму(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБезОповещения(Команда)
	
	ЗакрытьФорму(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьФормуПоПараметрам(ПараметрыДлительнойОперации)

	Заголовок = ПараметрыДлительнойОперации.Заголовок;
	Если ЗначениеЗаполнено(ПараметрыДлительнойОперации.ТекстСообщения) Тогда
		Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = ПараметрыДлительнойОперации.ТекстСообщения;
	Иначе
		Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = НСтр("ru = 'Пожалуйста, подождите...'");
	КонецЕсли;
	
	Если ПараметрыДлительнойОперации.ОперацияЗавершена	Тогда
			ПерейтиВСостояниеЗавершеннойОперации();
	Иначе
		Если ПараметрыДлительнойОперации.ПроцентПрогресса <> Неопределено Тогда
			Элементы.ИндикаторПрогресса.Видимость = Истина;
			ИндикаторПрогресса = ПараметрыДлительнойОперации.ПроцентПрогресса;
		Иначе
			Элементы.ИндикаторПрогресса.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиВСостояниеЗавершеннойОперации()
	
	Элементы.ИндикаторПрогресса.Видимость = Ложь;
	Элементы.ДекорацияДлительнаяОперация.Видимость = Ложь;
	Элементы.ФормаОтмена.Видимость = Ложь;
	Элементы.КартинкаУспешно.Видимость = Истина;
	Элементы.ФормаЗакрыть.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(РезультатЗакрытия)
	
	Если Открыта() Тогда
		Закрыть(РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
