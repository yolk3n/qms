
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Номенклатура) Тогда
		ВызватьИсключение НСтр("ru='Предусмотрено открытие формы только из подбора.'");
	КонецЕсли;
	
	Склад = Параметры.Склад;
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Склад", Склад));
	
	Номенклатура = Параметры.Номенклатура;
	СерияНоменклатуры = Параметры.СерияНоменклатуры;
	Партия = Параметры.Партия;
	Количество = Параметры.Количество;
	ЕдиницаИзмерения = Параметры.ЕдиницаИзмерения;
	Цена = Параметры.Цена;
	ВидЦены = Параметры.ВидЦены;
	Если ЗначениеЗаполнено(ВидЦены) Тогда
		Валюта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидЦены, "ВалютаЦены");
	Иначе
		Валюта = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	КонецЕсли;
	Дата = ТекущаяДатаСеанса();
	ИсточникФинансирования = Параметры.ИсточникФинансирования;
	ОбщегоНазначенияБольничнаяАптекаКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИсточникФинансирования", "ТолькоПросмотр", Параметры.ЗапретитьИзменениеИсточникаФинансирования);
	ОбщегоНазначенияБольничнаяАптекаКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИсточникФинансирования", "Видимость", Не Параметры.СкрытьИсточникФинансирования);
	
	МестоХранения = Параметры.МестоХранения;
	ОбщегоНазначенияБольничнаяАптекаКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "МестоХранения", "Видимость", Не Параметры.СкрытьМестоХранения);
	
	Заказ = Параметры.Заказ;
	КодСтроки = Параметры.КодСтроки;
	НоменклатураЗаказа = Параметры.НоменклатураЗаказа;
	ЕдиницаИзмеренияЗаказа = Параметры.ЕдиницаИзмеренияЗаказа;
	КОформлению = Параметры.КОформлению;
	КоличествоВЕдиницахЗаказа = Параметры.КОформлению;
	КоэффициентЕдиницыЗаказа = Параметры.КоэффициентЕдиницыЗаказа;
	
	Если ЗначениеЗаполнено(НоменклатураЗаказа) Тогда
		
		Элементы.НадписьСверхЗаказа.Видимость = Ложь;
		
		ЕдиницыНоменклатуры = НоменклатураСервер.ЕдиницыИзмерения(Номенклатура);
		Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) И ЕдиницыНоменклатуры.Найти(ЕдиницаИзмеренияЗаказа) <> Неопределено Тогда
			ЕдиницаИзмерения = ЕдиницаИзмеренияЗаказа;
		КонецЕсли;
		
		Если КоэффициентЕдиницыЗаказа = 0 Тогда
			КоэффициентЕдиницыЗаказа = ОтборНоменклатуры.КоэффициентЕдиницыЗаказа(НоменклатураЗаказа, ЕдиницаИзмеренияЗаказа, Номенклатура);
		КонецЕсли;
		
	Иначе
		
		Элементы.ГруппаОписаниеЗаказа.Видимость = Ложь;
		Элементы.ГруппаКоличествоВЕдиницахЗаказа.Видимость = Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) И ЗначениеЗаполнено(Номенклатура) Тогда
		ЕдиницаИзмерения = НоменклатураСервер.ОсновнаяЕдиницаИзмерения(Номенклатура);
	КонецЕсли;
	
	Коэффициент = НоменклатураСервер.КоэффициентЕдиницыИзмерения(Номенклатура, ЕдиницаИзмерения);
	Если Не ЗначениеЗаполнено(Количество) Тогда
		Количество = 1;
	КонецЕсли;
	
	Если КоэффициентЕдиницыЗаказа <> 0 Тогда
		КоличествоВЕдиницахЗаказа = Количество * Коэффициент / КоэффициентЕдиницыЗаказа;
		Элементы.КоэффициентЕдиницыЗаказа.ТолькоПросмотр = Истина;
		Элементы.КоэффициентЕдиницыЗаказа.ПропускатьПриВводе = Истина;
	КонецЕсли;
	
	Элементы.Цена.ТолькоПросмотр = Не Параметры.РедактироватьЦену;
	Элементы.Цена.ПропускатьПриВводе = Не Параметры.РедактироватьЦену;
	
	Если Параметры.СкрытьЦену Тогда
		Элементы.Цена.Видимость = Ложь;
		Элементы.Валюта.Видимость = Ложь;
		Заголовок = НСтр("ru = 'Ввод количества'");
	КонецЕсли;
	
	Если Параметры.СкрытьСерию И Параметры.СкрытьПартию Тогда
		Элементы.СерияНоменклатуры.Видимость = Ложь;
		Элементы.Партия.Видимость = Ложь;
	Иначе
		Действия = ОбработкаТабличнойЧастиКлиентСервер;
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьПараметрыУчета(), Параметры.ПараметрыУчетаНоменклатуры);
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТабличнойЧасти(ЭтотОбъект, СтруктураДействий, Неопределено);
		
		СтатусыУказанияСерий = ЗапасыКлиентСерверПовтИсп.СтатусыУказанияСерий();
		Если Параметры.СкрытьСерию Или СтатусУказанияСерий = СтатусыУказанияСерий.СтатусСерииНеУказываются Тогда
			Элементы.СерияНоменклатуры.Видимость = Ложь;
		КонецЕсли;
		СтатусыУказанияПартий = ЗапасыКлиентСерверПовтИсп.СтатусыУказанияПартий();
		Если Параметры.СкрытьПартию Или СтатусУказанияПартий = СтатусыУказанияПартий.СтатусПартииНеУказываются Тогда
			Элементы.Партия.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	СформироватьПредставлениеЗаказа();
	СформироватьПоясняющуюНадписьКоэффициентаЕдиницыЗаказа();
	
	УправляемаяФорма.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
	ТекущаяЕдиницаИзмерения = ЕдиницаИзмерения;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не ЗначениеЗаполнено(НоменклатураЗаказа) Тогда
		НепроверяемыеРеквизиты.Добавить("КоличествоВЕдиницахЗаказа");
		НепроверяемыеРеквизиты.Добавить("КоэффициентЕдиницыЗаказа");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		ВыбранноеЗначение = Новый Структура;
		ВыбранноеЗначение.Вставить("Заказ", Заказ);
		ВыбранноеЗначение.Вставить("КодСтроки", КодСтроки);
		ВыбранноеЗначение.Вставить("НоменклатураЗаказа", НоменклатураЗаказа);
		ВыбранноеЗначение.Вставить("ЕдиницаИзмеренияЗаказа", ЕдиницаИзмеренияЗаказа);
		ВыбранноеЗначение.Вставить("КоэффициентЕдиницыЗаказа", КоэффициентЕдиницыЗаказа);
		ВыбранноеЗначение.Вставить("КоличествоВЕдиницахЗаказа", КоличествоВЕдиницахЗаказа);
		
		ВыбранноеЗначение.Вставить("Номенклатура", Номенклатура);
		ВыбранноеЗначение.Вставить("СерияНоменклатуры", СерияНоменклатуры);
		ВыбранноеЗначение.Вставить("Партия", Партия);
		ВыбранноеЗначение.Вставить("Количество", Количество);
		ВыбранноеЗначение.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
		ВыбранноеЗначение.Вставить("Коэффициент", Коэффициент);
		ВыбранноеЗначение.Вставить("Цена", Цена);
		ВыбранноеЗначение.Вставить("ИсточникФинансирования", ИсточникФинансирования);
		ВыбранноеЗначение.Вставить("МестоХранения", МестоХранения);
		
		ОповеститьОВыборе(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КоэффициентЕдиницыЗаказаПриИзменении(Элемент)
	
	Если КоличествоВЕдиницахЗаказа <> 0 Тогда
		Количество = КоличествоВЕдиницахЗаказа * КоэффициентЕдиницыЗаказа;
		Если Коэффициент <> 0 Тогда
			Количество = Количество / Коэффициент;
		КонецЕсли;
	Иначе
		Если КоэффициентЕдиницыЗаказа <> 0 Тогда
			КоличествоВЕдиницахЗаказа = Количество * Коэффициент / КоэффициентЕдиницыЗаказа;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СерияНоменклатурыПриИзменении(Элемент)
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЦенуПродажи(), Действия.ПолучитьПараметрыЗаполненияЦены(ЭтотОбъект));
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ЭтотОбъект, СтруктураДействий, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПартияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОтборПартий = Новый Структура;
	ОтборПартий.Вставить("Организация"  , ВладелецФормы.Организация);
	ОтборПартий.Вставить("Склад"        , ВладелецФормы.Склад);
	
	ПараметрыВыбораПартии = ОбработкаТабличнойЧастиКлиент.ПолучитьПараметрыВыбораПартии(ОтборПартий, ЭтотОбъект);
	ПараметрыВыбораПартии.ВыборВОтделении = ПодборТоваровКлиентСервер.ПодборВОтделении(ВладелецФормы);
	
	ОбработкаТабличнойЧастиКлиент.ВыбратьПартиюНоменклатуры(ЭтотОбъект, Элемент, ПараметрыВыбораПартии, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПартияПриИзменении(Элемент)
	
	Действия = ОбработкаТабличнойЧастиКлиентСервер;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЦенуПродажи(), Действия.ПолучитьПараметрыЗаполненияЦены(ЭтотОбъект));
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ЭтотОбъект, СтруктураДействий, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	Если КоэффициентЕдиницыЗаказа <> 0 Тогда
		КоличествоВЕдиницахЗаказа = Количество * Коэффициент / КоэффициентЕдиницыЗаказа;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	Если ТекущаяЕдиницаИзмерения <> ЕдиницаИзмерения Тогда
		Действия = ОбработкаТабличнойЧастиКлиентСервер;
		
		КоэффициентДоИзменения = Коэффициент;
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить(Действия.Действие_ПересчитатьКоэффициент());
		Если КоэффициентДоИзменения = 0 Тогда
			СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьЦенуПродажи(), Действия.ПолучитьПараметрыЗаполненияЦены(ЭтотОбъект));
		КонецЕсли;
		ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТабличнойЧасти(ЭтотОбъект, СтруктураДействий, Неопределено);
		
		Если КоэффициентДоИзменения <> 0 Тогда
			Цена = Цена / КоэффициентДоИзменения * Коэффициент;
		КонецЕсли;
		
		Если КоэффициентЕдиницыЗаказа <> 0 Тогда
			КоличествоВЕдиницахЗаказа = Количество * Коэффициент / КоэффициентЕдиницыЗаказа;
		КонецЕсли;
		
		ТекущаяЕдиницаИзмерения = ЕдиницаИзмерения;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьПоясняющуюНадписьКоэффициентаЕдиницыЗаказа()
	
	РеквизитыНоменклатуры = НоменклатураСервер.ОписаниеНоменклатуры(Номенклатура);
	
	ЧастиПояснения = Новый Массив;
	ЧастиПояснения.Добавить(Новый ФорматированнаяСтрока(
		Строка(РеквизитыНоменклатуры.ЕдиницаИзмерения) + ?(РеквизитыНоменклатуры.ЭтоЛекарственноеСредство, "(" + РеквизитыНоменклатуры.ФормаВыпуска + ")", ""),
		,
		,
		,
		ПолучитьНавигационнуюСсылку(Номенклатура)));
	ЧастиПояснения.Добавить(" " + НСтр("ru='выбранной номенклатуры в'") + " ");
	ЧастиПояснения.Добавить(Новый ФорматированнаяСтрока(Строка(ЕдиницаИзмеренияЗаказа),,,, ПолучитьНавигационнуюСсылку(ЕдиницаИзмеренияЗаказа)));
	ЧастиПояснения.Добавить(" " + НСтр("ru='заказа.'"));
	
	ПояснениеКоэффициентЕдиницыЗаказа = Новый ФорматированнаяСтрока(ЧастиПояснения);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеЗаказа()
	
	ПредставлениеЗаказа = СокрЛП(НоменклатураЗаказа) + " " + КОформлению + " " + ЕдиницаИзмеренияЗаказа;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
