
#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Номенклатура     = Параметры.Номенклатура;
	Характеристика   = Параметры.Характеристика;
	Серия            = Параметры.Серия;
	Упаковка         = Параметры.Упаковка;
	ЕдиницаИзмерения = Параметры.ЕдиницаИзмерения;
	
	ГосударственныеИнформационныеСистемыПереопределяемый.УстановитьПараметрыВыбораНоменклатуры(ЭтотОбъект, Элементы.Номенклатура.Имя);
	ГосударственныеИнформационныеСистемыПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(ЭтотОбъект, Элементы.Характеристика.Имя, "Номенклатура");
	ГосударственныеИнформационныеСистемыПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(ЭтотОбъект, Элементы.Серия.Имя, "Номенклатура");
	ГосударственныеИнформационныеСистемыПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(ЭтотОбъект, Элементы.Упаковка.Имя, "Номенклатура");
	
	ПараметрыУказанияСерий = ГосударственныеИнформационныеСистемыПереопределяемый.ПараметрыУказанияСерийФормыОбъекта(ЭтотОбъект, Обработки.ПолучитьСоставТранспортныхУпаковокМДЛП);
	
	Элементы.Характеристика.Видимость = ИнтеграцияМДЛП.ИспользоватьХарактеристикиНоменклатуры();
	Элементы.Упаковка.Видимость       = ИнтеграцияМДЛП.ИспользоватьУпаковкиНоменклатуры();
	Элементы.Серия.Видимость          = ИнтеграцияМДЛП.ИспользоватьСерииНоменклатуры();
	
	СобытияФормМДЛППереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодтвердитьВыбор(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("Номенклатура"    , Номенклатура);
	РезультатВыбора.Вставить("Характеристика"  , Характеристика);
	РезультатВыбора.Вставить("Серия"           , Серия);
	РезультатВыбора.Вставить("Упаковка"        , Упаковка);
	РезультатВыбора.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Новый Структура("Номенклатура, Характеристика, Серия, Упаковка, ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ЭтотОбъект);
	
	СобытияФормМДЛПКлиентПереопределяемый.НачалоВыбораНоменклатуры(ЭтотОбъект, ТекущиеДанные, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Новый Структура("Номенклатура, Характеристика, Серия, Упаковка, ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ЭтотОбъект);
	
	ПараметрыЗаполнения = ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
	ПараметрыЗаполнения.ПроверитьСериюРассчитатьСтатус = Истина;
	ПараметрыЗаполнения.ПараметрыУказанияСерий = ПараметрыУказанияСерий;
	
	СобытияФормМДЛПКлиентПереопределяемый.ПриИзмененииНоменклатуры(ЭтотОбъект, ТекущиеДанные, КэшированныеЗначения, ПараметрыЗаполнения);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Новый Структура("Номенклатура, Характеристика, Серия, Упаковка, ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ЭтотОбъект);
	
	СобытияФормМДЛПКлиентПереопределяемый.НачалоВыбораХарактеристики(ЭтотОбъект, ТекущиеДанные, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаПриИзменении(Элемент)
	
	ТекущиеДанные = Новый Структура("Номенклатура, Характеристика, Серия, Упаковка, ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ЭтотОбъект);
	
	ПараметрыЗаполнения = ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
	
	СобытияФормМДЛПКлиентПереопределяемый.ПриИзмененииХарактеристики(ЭтотОбъект, ТекущиеДанные, Новый Структура, ПараметрыЗаполнения);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСерияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Новый Структура("Номенклатура, Характеристика, Серия, Упаковка, ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ЭтотОбъект);
	
	СобытияФормМДЛПКлиентПереопределяемый.НачалоВыбораСерии(ЭтотОбъект, ТекущиеДанные, ПараметрыУказанияСерий, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСерияПриИзменении(Элемент)
	
	ТекущиеДанные = Новый Структура("Номенклатура, Характеристика, Серия, Упаковка, ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ЭтотОбъект);
	
	ПараметрыЗаполнения = ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
	ПараметрыЗаполнения.ПараметрыУказанияСерий = ПараметрыУказанияСерий;
	
	СобытияФормМДЛПКлиентПереопределяемый.ПриИзмененииСерии(ЭтотОбъект, ТекущиеДанные, Новый Структура, ПараметрыЗаполнения);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыУпаковкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Новый Структура("Номенклатура, Характеристика, Серия, Упаковка, ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ЭтотОбъект);
	
	СобытияФормМДЛПКлиентПереопределяемый.НачалоВыбораУпаковки(ЭтотОбъект, ТекущиеДанные, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыУпаковкаПриИзменении(Элемент)
	
	ТекущиеДанные = Новый Структура("Номенклатура, Характеристика, Серия, Упаковка, ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ЭтотОбъект);
	
	ПараметрыЗаполнения = ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
	
	СобытияФормМДЛПКлиентПереопределяемый.ПриИзмененииУпаковки(ЭтотОбъект, ТекущиеДанные, Новый Структура, ПараметрыЗаполнения);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы
