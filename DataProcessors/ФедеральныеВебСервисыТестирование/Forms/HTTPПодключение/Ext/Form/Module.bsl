#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПо(СтрокаПодключения)
	Если НЕ ЗначениеЗаполнено(СтрокаПодключения) Тогда 
		Возврат;
	КонецЕсли;
	
	СтруктураURI_ = ФедеральныеВебСервисыСервер.СтруктураURI(СтрокаПодключения);
	
	ЭтотОбъект.Сервер = СтруктураURI_.Хост;
	ЭтотОбъект.РесурсНаСервере = СтруктураURI_.ПутьНаСервере;
	Если ЭтотОбъект.РежимТестирования = "GetWSDL" Тогда 
		Если НЕ СтрЗаканчиваетсяНа(ЭтотОбъект.РесурсНаСервере, "?wsdl") Тогда 
			ЭтотОбъект.РесурсНаСервере = ЭтотОбъект.РесурсНаСервере + "?wsdl";
		КонецЕсли;
	ИначеЕсли ЭтотОбъект.РежимТестирования = "PostToTEST" Тогда 
		Если НЕ СтрЗаканчиваетсяНа(ЭтотОбъект.РесурсНаСервере, "/Test") Тогда 
			ЭтотОбъект.РесурсНаСервере = ЭтотОбъект.РесурсНаСервере + "/Test";
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураURI_.Порт) Тогда 
		ЭтотОбъект.ПортПоУмолчанию = Ложь;
		ЭтотОбъект.Порт = СтруктураURI_.Порт;
	Иначе
		ЭтотОбъект.ПортПоУмолчанию = Истина;
		ЭтотОбъект.Порт = 0;
	КонецЕсли;
	
	ЭтотОбъект.Пользователь = СтруктураURI_.Логин;
	ЭтотОбъект.Пароль = СтруктураURI_.Пароль;
	Если НРег(СтруктураURI_.Схема) = "https" Тогда 
		ЭтотОбъект.ЗащищенноеСоединение = Истина;
	Иначе
		ЭтотОбъект.ЗащищенноеСоединение = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СоздатьHTTPСоединение()
	Сервер_ = ЭтотОбъект.Сервер;
	Порт_ = Неопределено;
	Если НЕ ЭтотОбъект.ПортПоУмолчанию Тогда 
		Порт_ = ЭтотОбъект.Порт;
	КонецЕсли;
	Пользователь_ = Неопределено;
	Пароль_ = Неопределено;
	Если ЗначениеЗаполнено(ЭтотОбъект.Пользователь) Тогда 
		Пользователь_ = ЭтотОбъект.Пользователь;
		Пароль_ = ЭтотОбъект.Пароль;
	КонецЕсли;
	ЗащищенноеСоединение_ = Неопределено;
	Протокол_ = "http";
	Если ЭтотОбъект.ЗащищенноеСоединение Тогда 
		ЗащищенноеСоединение_ = Новый ЗащищенноеСоединениеOpenSSL();
		Протокол_ = "https";
	КонецЕсли;
	Прокси_ = Неопределено;
	Если НЕ ЭтотОбъект.ПроксиПоУмолчанию Тогда 
		Прокси_ = Новый ИнтернетПрокси(Ложь);
		Прокси_.Установить(
			Протокол_,
			ЭтотОбъект.Прокси
		);
	КонецЕсли;
	Соединение_ = Новый HTTPСоединение(
		Сервер_,
		Порт_,
		Пользователь_,
		Пароль_,
		Прокси_,
		60,
		ЗащищенноеСоединение_
	);
	Возврат Соединение_;
КонецФункции

&НаСервере
Процедура ТестированиеСоединение()
	Соединение_ = СоздатьHTTPСоединение();
	Если Соединение_ = Неопределено Тогда 
		ВызватьИсключение("Не удалось создать HTTPСоединение");
	КонецЕсли;
	Запрос_ = Новый HTTPЗапрос(ЭтотОбъект.РесурсНаСервере);
	
	Если ЭтотОбъект.РежимТестирования = "GetWSDL" Тогда 
		HTTPОтвет_ = Соединение_.Получить(Запрос_);
	ИначеЕсли ЭтотОбъект.РежимТестирования = "PostToTEST" Тогда 
		HTTPОтвет_ = Соединение_.ОтправитьДляОбработки(Запрос_);
	КонецЕсли;
	Если HTTPОтвет_.КодСостояния <> 200 Тогда
		ВызватьИсключение(СтрШаблон("Ошибка обращения к ресурсу: %1", HTTPОтвет_.ПолучитьТелоКакСтроку()));
	КонецЕсли;
	Сообщить("Тест успешно пройден");
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЭлементы(ЭлементыФормы, РеквизитПортПоУмолчанию, РеквизитПорт, РеквизитПроксиПоУмолчанию, РеквизитПрокси)
	Если РеквизитПортПоУмолчанию Тогда 
		РеквизитПорт = 0;
	КонецЕсли;
	ЭлементыФормы.Порт.Доступность = НЕ РеквизитПортПоУмолчанию;
	
	Если РеквизитПроксиПоУмолчанию Тогда 
		РеквизитПрокси = "";
	КонецЕсли;
	ЭлементыФормы.Прокси.Доступность = НЕ РеквизитПроксиПоУмолчанию;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтотОбъект.РежимТестирования = ВыбратьЗаполненное(ЭтотОбъект.Параметры.РежимТестирования, "GetWSDL");
	
	ЭтотОбъект.ПроксиПоУмолчанию = Истина;
	
	Если ЗначениеЗаполнено(ЭтотОбъект.Параметры.АдресПодключения) Тогда 
		ЗаполнитьПо(ЭтотОбъект.Параметры.АдресПодключения);
	КонецЕсли;
	
	ОбновитьЭлементы(
		ЭтотОбъект.Элементы, 
		ЭтотОбъект.ПортПоУмолчанию, 
		ЭтотОбъект.Порт, 
		ЭтотОбъект.ПроксиПоУмолчанию, 
		ЭтотОбъект.Прокси
	);
КонецПроцедуры

&НаКлиенте
Процедура ПортПоУмолчаниюПриИзменении(Элемент)
	ОбновитьЭлементы(
		ЭтотОбъект.Элементы, 
		ЭтотОбъект.ПортПоУмолчанию, 
		ЭтотОбъект.Порт, 
		ЭтотОбъект.ПроксиПоУмолчанию, 
		ЭтотОбъект.Прокси
	);
КонецПроцедуры

&НаКлиенте
Процедура ПроксиПоУмолчаниюПриИзменении(Элемент)
	ОбновитьЭлементы(
		ЭтотОбъект.Элементы, 
		ЭтотОбъект.ПортПоУмолчанию, 
		ЭтотОбъект.Порт, 
		ЭтотОбъект.ПроксиПоУмолчанию, 
		ЭтотОбъект.Прокси
	);
КонецПроцедуры

&НаКлиенте
Процедура Подключиться(Команда)
	ТестированиеСоединение();
КонецПроцедуры

#КонецОбласти
