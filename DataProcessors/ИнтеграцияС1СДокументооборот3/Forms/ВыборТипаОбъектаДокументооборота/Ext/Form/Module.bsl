#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры["Заголовок"];
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокКоманды") Тогда
		Элементы.Готово.Заголовок = Параметры["ЗаголовокКоманды"];
	КонецЕсли;
	
	Если Параметры.Свойство("ОбъектИС") Тогда
		ТипОбъектаИС = Параметры.ОбъектИС.Метаданные().ПолноеИмя();
	КонецЕсли;
	
	Элементы.ГруппаНастроитьПравило.Видимость =
		ПравоДоступа("Добавление", Метаданные.Справочники.ПравилаИнтеграцииС1СДокументооборотом3);
	
	ТипыXDTO = Новый Массив;
	ТипыXDTO.Добавить("DMDocument");
	ТипыXDTO.Добавить("DMCorrespondent");
	ТипыXDTO.Добавить("DMMeeting");
	
	Для Каждого ТипXDTO Из ТипыXDTO Цикл
		НоваяСтрока = ТипыОбъектов.Добавить();
		НоваяСтрока.Тип = ТипXDTO;
		НоваяСтрока.Представление = ИнтеграцияС1СДокументооборот3КлиентСервер.ПредставлениеТипаОбъектаXDTO(
			ТипXDTO, "ЕдинственноеЧисло");
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДекорацияНастроитьПравилоНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъектаИС", ТипОбъектаИС);
	
	ОткрытьФорму("Справочник.ПравилаИнтеграцииС1СДокументооборотом3.Форма.ФормаЭлемента",
		ПараметрыФормы,
		ЭтотОбъект);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипыОбъектовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	
	ТекущиеДанные = Элементы.ТипыОбъектов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(ТекущиеДанные.Тип);
	
КонецПроцедуры

#КонецОбласти