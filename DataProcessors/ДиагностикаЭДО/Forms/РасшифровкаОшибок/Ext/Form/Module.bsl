
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Заголовок", Заголовок);
	Если Параметры.Свойство("Ошибки") Тогда
		
		Сч = 0;
		Для каждого Ошибка Из Параметры.Ошибки Цикл
			ДекорацияОшибка = Элементы.Добавить("ДекорацияОшибка_" + XMLСтрока(Сч), Тип("ДекорацияФормы"));
			ДекорацияОшибка.Заголовок = Ошибка.ПодробноеПредставление.Текст;
			ДекорацияОшибка.УстановитьДействие("ОбработкаНавигационнойСсылки", "Подключаемый_ОбработкаНавигационнойСсылки");
			НоваяСтрока = Ошибки.Добавить();
			НоваяСтрока.Индекс = Сч;
			НоваяСтрока.Ошибка = Ошибка;
			Сч = Сч + 1;
		КонецЦикла; 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИндексОшибки = СтрРазделить(Элемент.Имя, "_")[1];
	ПараметрыОбработчика = Ошибки[0].Ошибка.ПодробноеПредставление.ОбработчикиНажатия.Получить(НавигационнаяСсылкаФорматированнойСтроки);
	Если ПараметрыОбработчика <> Неопределено Тогда
		ПолноеИмяПроцедуры = ПараметрыОбработчика.ИмяПроцедуры;
		ЧастиИмениПроцедуры = СтрРазделить(ПолноеИмяПроцедуры, ".");
		ИмяМодуля = ЧастиИмениПроцедуры[0];
		ИмяПроцедуры = ЧастиИмениПроцедуры[1];
		Оповещение = Новый ОписаниеОповещения(ИмяПроцедуры, ОбщегоНазначенияКлиент.ОбщийМодуль(ИмяМодуля));
		ВыполнитьОбработкуОповещения(Оповещение, ПараметрыОбработчика.Параметр);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти