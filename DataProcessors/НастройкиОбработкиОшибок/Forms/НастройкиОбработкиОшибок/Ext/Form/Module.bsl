
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РегистрацияОшибок.СоздатьНастройкиОбработкиОшибок(ЭтотОбъект, Элементы.ТекущиеЗначенияРегистрацииОшибок);
	РегистрацияОшибок.СоздатьНастройкиОбработкиОшибокПоУмолчанию(ЭтотОбъект, Элементы.ЗначенияПоУмолчаниюРегистрацииОшибок);
	
	ЗаполнитьТекущиеНастройкиОбработкиОшибок();
	
	ЗаполнитьТекущиеНастройкиЦентраМониторинга();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ПередЗакрытиемЗавершение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(ПередЗакрытиемЗавершение, Отказ, Ложь);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьРекомендуемыеЗначения(Команда)
	
	УстановитьРекомендуемыеЗначенияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	СохранитьНастройкиНаКлиенте();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Перезапустить(Команда)
	
	СохранитьНастройкиНаКлиенте();
	ЗавершитьРаботуСистемы(Истина, Истина);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЦентрМониторингаПриИзменении(Элемент)
	
	ЦентрМониторингаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииНастроекРегистрацииОшибок(Элемент)
	
	УправлениеЭлементамиРегистрацииОшибок();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СохранитьНастройкиНаКлиенте();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиНаКлиенте()
	
	СохранитьНастройкиНаСервере();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	
	РегистрацияОшибок.СохранитьНастройкиРегистрацииОшибок(ЭтотОбъект);
	
КонецПроцедуры

#Область НастройкиОбработкиОшибок

&НаСервере
Процедура ЗаполнитьТекущиеНастройкиОбработкиОшибок()
	
	НастройкиРегистрацииОшибок = РегистрацияОшибок.ПолучитьНастройкиРегистрацииОшибок();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиРегистрацииОшибок);
	
	НастройкиРегистрацииОшибокПоУмолчанию = РегистрацияОшибок.ПолучитьНастройкиРегистрацииОшибокПоУмолчанию();
	Для Каждого КлючИЗначение Из НастройкиРегистрацииОшибокПоУмолчанию Цикл
		ЭтотОбъект[КлючИЗначение.Ключ + "ПоУмолчанию"] = КлючИЗначение.Значение;
	КонецЦикла;
	
	УправлениеЭлементамиРегистрацииОшибок();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРекомендуемыеЗначенияНаСервере()
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РегистрацияОшибок.ПолучитьНастройкиРегистрацииОшибокПоУмолчанию());
	
	УправлениеЭлементамиРегистрацииОшибок();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиРегистрацииОшибок()
	
	Элементы.ГруппаПерезапустить.Видимость = Модифицированность;
	
КонецПроцедуры

#КонецОбласти // НастройкиОбработкиОшибок

#Область НастройкиЦентраМониторинга

&НаСервере
Процедура ЗаполнитьТекущиеНастройкиЦентраМониторинга()
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		ЦентрМониторингаВключен = ЦентрМониторинга.ЦентрМониторингаВключен();
		УправлениеЭлементамиЦентраМониторинга();
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НастройкиЦентраМониторинга", "Видимость", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЦентрМониторингаПриИзмененииНаСервере()
	
	СохранитьНастройкиЦентраМониторинга();
	УправлениеЭлементамиЦентраМониторинга();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиЦентраМониторинга()
	
	Если ЦентрМониторингаВключен Тогда
		ЦентрМониторинга.ВключитьПодсистему();
		РегистрацияОшибок.СохранитьПараметрыЦентраМониторинга();
		РезультатЗапуска = ЦентрМониторингаСлужебный.ЗапускОтправкиОзнакомительногоПакета();
		Если РезультатЗапуска <> Неопределено Тогда
			// Значения реквизитов используются в параметрах глобальной команды НастройкиЦентраМониторингаСсылка.
			ЦентрМониторингаИдентификаторЗадания = РезультатЗапуска.ИдентификаторЗадания;
			ЦентрМониторингаАдресРезультатаЗадания = РезультатЗапуска.АдресРезультата;
		КонецЕсли;
	Иначе
		ЦентрМониторинга.ОтключитьПодсистему();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиЦентраМониторинга()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НастройкиЦентраМониторингаСсылка", "Доступность", ЦентрМониторингаВключен);
	
КонецПроцедуры

#КонецОбласти // НастройкиЦентраМониторинга

#КонецОбласти // СлужебныеПроцедурыИФункции
