////////////////////////////////////////////////////////////////////////////////
// Модуль формы Обработка.СопоставлениеНоменклатурыБЭД.Форма
//  
////////////////////////////////////////////////////////////////////////////////
// Терминология:
// НоменклатураКонтрагента - Структура - См. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента.
// НаборНоменклатурыКонтрагентов - массив из НоменклатураКонтрагента.
// НоменклатураИБ - Структура - См. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы.
// ЗаписьСопоставления (Запись) - данные строки табличной части "Сопоставление" со служебной информацией.
// ЭлементСопоставления - структура:
//  * НоменклатураКонтрагента - Структура - См. НоменклатураКонтрагента.
//  * НоменклатураИБ - Структура - См. НоменклатураИБ.
// НаборЭлементовСопоставления - массив из ЭлементСопоставления.
// ДействиеСЭлементомСопоставления (ОписаниеДействия) - Структура - описание действия выполняемого над элементом сопоставления:
//  * Действие - название действия.
//  * ЭлементСопоставления - См. ЭлементСопоставления.
// ТаблицаВариантовСопоставления - временное хранилище с таблицей вариантов сопоставления.
////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте

Перем СохраненныеЭлементы;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Инициализация.
	СопоставленоОтбор = "Все";
	РазрешитьСохранение = Истина;
	ЕстьПравоДобавленияНоменклатуры = ЕстьПравоДобавленияНоменклатуры();
	
	// Обработка параметров формы.
	Если Параметры.Свойство("ДополнительныеРеквизитыСопоставления") Тогда
		СоздатьДополнительныеРеквизитыСопоставления(Параметры.ДополнительныеРеквизитыСопоставления);
	КонецЕсли;
	Если Параметры.Свойство("НоменклатураКонтрагентов") Тогда
		ЗаполнитьНоменклатуруКонтрагентов(Параметры.НоменклатураКонтрагентов);
		Модифицированность = Ложь;
	КонецЕсли;
	Если Параметры.Свойство("ОграничениеТипаНоменклатуры") Тогда
		Элементы.СопоставлениеНоменклатура.ОграничениеТипа = Параметры.ОграничениеТипаНоменклатуры;
	Иначе
		Элементы.СопоставлениеНоменклатура.ОграничениеТипа = Метаданные.ОпределяемыеТипы.НоменклатураБЭД.Тип;
	КонецЕсли;
	Если Параметры.Свойство("ОтключитьПоискПоНатуральнымКлючам") Тогда
		ОтключитьПоискПоНатуральнымКлючам = Параметры.ОтключитьПоискПоНатуральнымКлючам;
	КонецЕсли;
	Если Параметры.Свойство("ОтключитьПоискПоШтрихкодамКомбинаций") Тогда
		ОтключитьПоискПоШтрихкодамКомбинаций = Параметры.ОтключитьПоискПоШтрихкодамКомбинаций;
	КонецЕсли;
	Если Параметры.Свойство("ОтключитьПоискПоСловарю") Тогда
		ОтключитьПоискПоСловарю = Параметры.ОтключитьПоискПоСловарю;
	КонецЕсли;
	Если Параметры.Свойство("РазрешитьСохранение") Тогда
		РазрешитьСохранение = Параметры.РазрешитьСохранение;
	КонецЕсли;
	Если Параметры.Свойство("ВидимостьКолонокСопоставления") Тогда
		УстановитьВидимостьКолонокСопоставления(Параметры.ВидимостьКолонокСопоставления);
	КонецЕсли;
	Если Параметры.Свойство("ДополнительныеПараметрыПоиска") Тогда
	   	ДополнительныеПараметрыПоиска = Параметры.ДополнительныеПараметрыПоиска;
	КонецЕсли;
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	Если Параметры.Свойство("ТочностьПоискаПоУмолчанию") Тогда
		ТочностьПоискаПоУмолчанию = Параметры.ТочностьПоискаПоУмолчанию;
	Иначе
		ТочностьПоискаПоУмолчанию = 50;
	КонецЕсли;
	
	ОписаниеТиповВладельца = СопоставлениеНоменклатурыКонтрагентовСлужебный.ОписаниеТиповВладельцаНоменклатурыСопоставленияБЭД();
	
	Элементы.СопоставлениеВладелец.ОграничениеТипа = ОписаниеТиповВладельца;
	Элементы.ВладелецОтбор.ОграничениеТипа = ОписаниеТиповВладельца;
		
	СписокТиповМетаданныхУпаковок.ЗагрузитьЗначения(Метаданные.ОпределяемыеТипы.УпаковкаНоменклатурыБЭД.Тип.Типы());
	ГиперссылкаЦвет    = ЦветаСтиля.ГиперссылкаЦвет;
	
	УстановитьРежимПоискаВариантовСопоставления(Истина);
	
	// Настройка формы.
	ОпределитьИспользованиеСервисаНоменклатуры();

	УстановитьСвойстваЭлементовФормы();
	УстановитьСвойстваЭлементаОтбораПоВладельцу();
	УстановитьСвойстваПереопределяемыхЭлементовФормы();
	УстановитьСвойстваЭлементаОтбораПоСопоставлению();
	УстановитьСвойстваЭлементовНоменклатурыСервиса();
	
	УстановитьВидимостьОшибкаСохранения(Ложь);
	УстановитьВидимостьОшибкаПоиска(Ложь);
	
	Если ТочностьПоиска = 0 Тогда
		ТочностьПоиска            = ТочностьПоискаПоУмолчанию;
		ТочностьПоискаДоИзменения = ТочностьПоискаПоУмолчанию;
	КонецЕсли;

	// Поиск вариантов сопоставления в фоновом режиме.
	ЗапуститьПоискВариантов();
	
	ТолькоПросмотр = НЕ СопоставлениеНоменклатурыКонтрагентовСлужебный.ЕстьПравоСопоставленияНоменклатуры();
	
	Если НЕ Отказ Тогда
		Контекст = КонтекстФормы(ЭтотОбъект);

		ЭлектронноеВзаимодействиеПереопределяемый.ПриСозданииФормыПодсистемы(Контекст, Отказ, СтандартнаяОбработка);
	КонецЕсли;
	
	НовыеДанныеПравилаПоиска = СопоставлениеНоменклатурыКонтрагентовСлужебный.НовоеПравилоПоискаПоЧастиНаименования();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОжидатьЗавершениеПоискаВариантов(ИспользоватьСервис);
	
	Если НЕ Отказ Тогда
		Контекст = КонтекстФормы(ЭтотОбъект);
		ЭлектронноеВзаимодействиеКлиентПереопределяемый.ПриОткрытииФормыПодсистемы(Контекст, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		Если НЕ ЗавершениеРаботы Тогда
			ПоказатьВопросОСохраненииПередЗакрытием();
		КонецЕсли;
		
	Иначе
		
		ОтборНеСопоставленныхЗаписей = Новый Структура("Сопоставлено", Ложь);
		НеСопоставленныеСтроки = Объект.Сопоставление.НайтиСтроки(ОтборНеСопоставленныхЗаписей);
		
		Если НЕ ЗавершениеРаботы И ЗначениеЗаполнено(НеСопоставленныеСтроки) Тогда
			Отказ = Истина;
			ПоказатьВопросОСопоставленииПередЗакрытием();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяДата = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("ДатаНачала"   , НачалоДня(ТекущаяДата));
	ПараметрыОткрытия.Вставить("ДатаОкончания", КонецДня(ТекущаяДата));
	ПараметрыОткрытия.Вставить("Уровень"      , "Ошибка");
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СохранениеСопоставления" Тогда
		Подсистема = НСтр("ru = 'Электронное взаимодействие.Сопоставление номенклатуры контрагентов'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
		ПараметрыОткрытия.Вставить("СобытиеЖурналаРегистрации", Подсистема);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПоискСопоставления" Тогда
		ПараметрыОткрытия.Вставить("Данные"        , НСтр("ru = 'Поиск вариантов сопоставления номенклатуры'"));
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма.ЖурналРегистрации", ПараметрыОткрытия, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ЭтоДействиеСПравиломПоиска(ВыбранноеЗначение) Тогда
		ОбработатьДействиеСПравиломПоиска(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВладелецОтборПриИзменении(Элемент)
	
	ОбработатьОтборЗаписейСопоставления();
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставленоОтборПриИзменении(Элемент)
	
	ОбработатьОтборЗаписейСопоставления();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьРаботаСНоменклатуройПодсказкаНажатие(Элемент)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЭлектронноеВзаимодействие.РаботаСНоменклатурой") Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Заголовок", НСтр("ru = 'Сервис 1С:Номенклатура'"));
		ПараметрыОткрытия.Вставить("ОписаниеРаздела",
			НСтр("ru = 'Для возможности загрузки номенклатуры необходимо включить использование сервиса 1С:Номенклатура.'"));
		ПараметрыОткрытия.Вставить("Раздел", "НастройкиРаботаСНоменклатурой");
		
		Оповещение = Новый ОписаниеОповещения("РаботаСНоменклатуройПодсказкаЗавершение", ЭтотОбъект);
		
		МодульРаботаСНоменклатуройКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСНоменклатуройКлиент");
		МодульРаботаСНоменклатуройКлиент.ОткрытьФормуПанелиАдминистрирования(
			ПараметрыОткрытия, ЭтотОбъект, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТочностьПоискаПриИзменении(Элемент)
	
	Если ТочностьПоиска > 100 Тогда
		ТочностьПоиска = 100;
	ИначеЕсли ТочностьПоиска < 30 Тогда
		ТочностьПоиска = 30;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗапускПоискаВариантовПриИзмененииТочности", 0.3, Истина);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСопоставление

&НаКлиенте
Процедура СопоставлениеПриИзменении(Элемент)
	
	СопоставлениеПриИзмененииНаСервере(Элемент.ТекущийЭлемент.Имя);
	
	ОжидатьЗавершениеПоискаВариантов(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Сопоставление.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийИдентификаторСопоставления = ТекущиеДанные.Идентификатор;
	
	ЗаполнитьСписокДействийСЭлементомСопоставления();
	УстановитьПараметрыВыбораНоменклатуры();
	УстановитьВидимостьДействийСПравиломПоискаВКонтекстомМеню();
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	// Обработаем выбор действия с элементом сопоставления в подчиненной форме.
	Если ЭтоДействиеСЭлементомСопоставления(ВыбранноеЗначение) Тогда
		
		ОбработатьДействиеСЭлементомСопоставления(ВыбранноеЗначение);
		
	ИначеЕсли ЭтоДействиеСПравиломПоиска(ВыбранноеЗначение) Тогда
		
		ОбработатьДействиеСПравиломПоиска(ВыбранноеЗначение);
		Элементы.Сопоставление.ЗакончитьРедактированиеСтроки(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	// Обработаем создание номенклатуры по данным контрагента в подчиненной форме.
	Если Источник.ОписаниеОповещенияОЗакрытии <> Неопределено Тогда
		
		ДополнительныеПараметры = Источник.ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры;
		
		Если ЭтоЭлементСопоставления(ДополнительныеПараметры) Тогда
			
			ЗакончитьСозданиеНоменклатурыИнформационнойБазыВручную(ДополнительныеПараметры, НовыйОбъект);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Область Номенклатура

&НаКлиенте
Процедура СопоставлениеНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	// Обработаем выбор действия с элементом сопоставления из выпадающего списка.
	Если ЭтоДействиеСЭлементомСопоставления(ВыбранноеЗначение) Тогда
	
		СтандартнаяОбработка = Ложь;
		ОбработатьДействиеСЭлементомСопоставления(ВыбранноеЗначение);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Сопоставление.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
		ТекущиеДанные.ХешПоисковойСтроки = "";
		ТекущиеДанные.ПодобраноПоПравилуПоиска = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Характеристика

&НаКлиенте
Процедура СопоставлениеХарактеристикаПриИзменении(Элемент)
	
	Подключаемый_ЭлементПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеХарактеристикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Подключаемый_ЭлементНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеХарактеристикаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Подключаемый_ЭлементНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеХарактеристикаОчистка(Элемент, СтандартнаяОбработка)
	
	Подключаемый_ЭлементОчистка(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеХарактеристикаСоздание(Элемент, СтандартнаяОбработка)
	
	Подключаемый_ЭлементСоздание(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеХарактеристикаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Подключаемый_ЭлементОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеХарактеристикаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Подключаемый_ЭлементИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеХарактеристикаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Подключаемый_ЭлементАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеХарактеристикаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Подключаемый_ЭлементОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область Упаковка

&НаКлиенте
Процедура СопоставлениеУпаковкаПриИзменении(Элемент)
	
	Подключаемый_ЭлементПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеУпаковкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Подключаемый_ЭлементНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
	Если НЕ СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокТиповМетаданныхУпаковок.Количество() > 1 Тогда
		
		ТекущаяСтрока = Элементы.Сопоставление.ТекущаяСтрока;
		
		Если ТекущаяСтрока = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Запись = Объект.Сопоставление.НайтиПоИдентификатору(ТекущаяСтрока);
		
		Элемент.КнопкаВыбора = Ложь;
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Новый СписокЗначений;
		
		Если ЗначениеЗаполнено(Запись.Номенклатура) Тогда
			
			КлючЗаписи = Новый Структура("Владелец,Идентификатор,Номенклатура");
			ЗаполнитьЗначенияСвойств(КлючЗаписи, Запись);
			
			СтруктураДанных = ПолучитьВариантыСопоставленияХарактеристикИУпаковок(КлючЗаписи, АдресТаблицыВариантовСопоставления);
			
			Если ЗначениеЗаполнено(СтруктураДанных) Тогда
				Если СтруктураДанных.Свойство("ВариантыСопоставленияУпаковки") Тогда
					ДанныеВыбора.ЗагрузитьЗначения(СтруктураДанных.ВариантыСопоставленияУпаковки);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого ТипМетаданных Из СписокТиповМетаданныхУпаковок Цикл
			ЗначениеТипаМетаданных = ТипМетаданных.Значение;
			
			ПредставлениеУпаковки = Новый ФорматированнаяСтрока(
				СтрШаблон(НСтр("ru = 'Выбрать из: %1'"), Строка(ЗначениеТипаМетаданных)), , ГиперссылкаЦвет, , Строка(ЗначениеТипаМетаданных));
			ДанныеВыбора.Добавить(ЗначениеТипаМетаданных, ПредставлениеУпаковки);

		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеУпаковкаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Подключаемый_ЭлементНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеУпаковкаОчистка(Элемент, СтандартнаяОбработка)
	
	Подключаемый_ЭлементОчистка(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеУпаковкаСоздание(Элемент, СтандартнаяОбработка)
	
	Подключаемый_ЭлементСоздание(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеУпаковкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если СписокТиповМетаданныхУпаковок.Количество() > 1 Тогда
		
		ТекущаяСтрока = Элементы.Сопоставление.ТекущаяСтрока;
		
		Если ТекущаяСтрока = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Запись = Объект.Сопоставление.НайтиПоИдентификатору(ТекущаяСтрока);
		
		НайденныйТип = СписокТиповМетаданныхУпаковок.НайтиПоЗначению(ВыбранноеЗначение);
		Если НайденныйТип <> Неопределено Тогда
			ИмяФормыВыбора = ПолучитьНаименованиеПоТипу(ВыбранноеЗначение);
			Если ИмяФормыВыбора <> Неопределено Тогда
				
				Подключаемый_ЭлементОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
				
				Если СтандартнаяОбработка Тогда
					
					ПараметрыВыбора = Новый Структура;
					Если ЗначениеЗаполнено(Элемент.СвязиПараметровВыбора) Тогда
						СвязиПараметровВыбора = Элемент.СвязиПараметровВыбора[0];
						ПараметрыВыбора.Вставить(СтрЗаменить(СвязиПараметровВыбора.Имя, "Отбор.", ""), Запись.Номенклатура);
					КонецЕсли;
					
					ПараметрыФормы = Новый Структура;
					ПараметрыФормы.Вставить("Отбор", ПараметрыВыбора);
					ПараметрыФормы.Вставить("ТекущаяСтрока", Запись.Упаковка);

					ОбработкаОповещения = Новый ОписаниеОповещения("ЗавершитьОбработкуВыбораУпаковки", ЭтотОбъект, Запись);
					ОткрытьФорму(ИмяФормыВыбора, ПараметрыФормы, ЭтаФорма, , , , ОбработкаОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
					
					Если ЗначениеЗаполнено(Запись.Упаковка) Тогда
						ВыбранноеЗначение = Запись.Упаковка;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	Иначе
		Подключаемый_ЭлементОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеУпаковкаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Подключаемый_ЭлементИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеУпаковкаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Подключаемый_ЭлементАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеУпаковкаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Подключаемый_ЭлементОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	СохраненныеЭлементы = СохранитьСопоставление();
	
	Если СохраненныеЭлементы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(СохраненныеЭлементы);
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохраненныеЭлементы = СохранитьСопоставление();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНоменклатуру(Команда)
	
	ВыделенныеЭлементыСопоставления = Новый Массив;
	
	Для Каждого Идентификатор Из Элементы.Сопоставление.ВыделенныеСтроки Цикл
		
		Запись = Объект.Сопоставление.НайтиПоИдентификатору(Идентификатор);
		ЭлементСопоставления = ЭлементСопоставленияПоЗаписи(Запись);
		ВыделенныеЭлементыСопоставления.Добавить(ЭлементСопоставления);
		
	КонецЦикла;
	
	ПоказатьВопросОСозданииНоменклатуры(ВыделенныеЭлементыСопоставления);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьСловарьСопоставления(Команда)
	
	ОчиститьСообщения();
	
	ТекстВопроса = НСтр("ru = 'Внимание! Операция перезаполнения словаря может занимать от нескольких минут до нескольких часов. 
	|После запуска операции можете продолжить работу в программе. Не рекомендуется прерывать работу процедуры для корректной работы функционала сопоставления.
	|Не рекомендуется во время перезаполнения словаря изменять наименование номенклатуры, характеристики, если используется,
	|или же их необходимо будет перезаписать после перезаполнения словаря, чтобы измененные данные попали в словарь сопоставления.'");

	ОбработкаОтвета = Новый ОписаниеОповещения("ОбработатьВопросОПерезаполнениеСловаря", ЭтотОбъект);

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КнопкаПоУмолчанию", КодВозвратаДиалога.Отмена);
	ДополнительныеПараметры.Вставить("ПредлагатьБольшеНеЗадаватьЭтотВопрос", Ложь);
	ДополнительныеПараметры.Вставить("Картинка", БиблиотекаКартинок.Предупреждение32);
	ДополнительныеПараметры.Вставить("Заголовок", НСтр("ru = 'Перезаполнение словаря сопоставления.'"));
	
	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(ОбработкаОтвета, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПравилоПоискаПоЧастиНаименования(Команда)
	
	ОткрытьФормуПравилаПоиска("ДобавитьПравилоПоиска");
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПравилоПоискаПоЧастиНаименования(Команда)
	
	ОткрытьФормуПравилаПоиска("ИзменитьПравилоПоиска");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСФормой

&НаСервере
Процедура УстановитьУсловноеОформление()

	// Управление видимостью поля характеристики контрагента. Отключаем видимость, если поле не заполнено.
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.НаименованиеХарактеристики");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СопоставлениеНаименованиеХарактеристики.Имя);
	
	// Управление видимостью поля характеристики ИБ, если она не используется или не заполнена номенклатура.
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.ИспользоватьХарактеристики");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СопоставлениеХарактеристика.Имя);
	
	// Включение отметки незаполненного поля характеристики ИБ, если она используется.
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	ОтборГруппа = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;

	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.ИспользоватьХарактеристики");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.ОбязательноеЗаполнениеХарактеристики");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.Характеристика");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СопоставлениеХарактеристика.Имя);
	
	// Включение подсказки ввода для незаполненной номенклатуры ИБ.
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ШрифтПодсказкиВвода = Метаданные.ЭлементыСтиля.МелкийНаклонныйШрифтБЭД.Значение;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтПодсказкиВвода);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("Объект.Сопоставление.ПодсказкаНайденныхВариантовНоменклатуры"));
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.Номенклатура");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СопоставлениеНоменклатура.Имя);
	
	// Включение подсказки ввода для незаполненной характеристики ИБ.
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтПодсказкиВвода);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("Объект.Сопоставление.ПодсказкаНайденныхВариантовХарактеристики"));
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.Характеристика");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СопоставлениеХарактеристика.Имя);
	
	// Включение подсказки количества вариантов найденной упаковки для незаполненной упаковки ИБ.

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтПодсказкиВвода);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("Объект.Сопоставление.ПодсказкаНайденныхВариантовУпаковки"));
	
	ОтборГруппа = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.Упаковка");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.Номенклатура");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СопоставлениеУпаковка.Имя);
	
	// Включение подсказки ввода базовой единиц измерения номенклатуры Иб, если не используются упаковки.

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтПодсказкиВвода);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("Объект.Сопоставление.ЕдиницаИзмеренияПоУмолчанию"));
	
	ОтборГруппа = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.Упаковка");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.Номенклатура");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.ИспользоватьУпаковки");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СопоставлениеУпаковка.Имя);
	
	// Включение отметки незаполненного поля упаковки ИБ, если она используется.
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	ГруппаЭлементовОтбораДанных = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаЭлементовОтбораДанных.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	ГруппаЭлементовОтбораДанных.Использование = Истина;

	ОтборЭлемента = ГруппаЭлементовОтбораДанных.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.ИспользоватьУпаковки");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаЭлементовОтбораДанных.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.Упаковка");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СопоставлениеУпаковка.Имя);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	Элементы.КомандаСохранить.Видимость = РазрешитьСохранение;
	
	ЗаголовокСохранитьИЗакрыть = НСтр("ru = 'Сохранить и закрыть'");
	Если НЕ РазрешитьСохранение Тогда
		ЗаголовокСохранитьИЗакрыть = НСтр("ru = 'Готово'");
	КонецЕсли;
	Элементы.КомандаСохранитьИЗакрыть.Заголовок = ЗаголовокСохранитьИЗакрыть;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваПереопределяемыхЭлементовФормы()
	
	МетаданныеСопоставления = СопоставлениеНоменклатурыКонтрагентовСлужебный.МетаданныеСопоставленияНоменклатуры();
	
	Если ЕстьНашаНоменклатура И ЕстьНоменклатураКонтрагента Или ЕстьНашаНоменклатура Тогда
		ВладелецНоменклатурыВРодительномПадеже   = НСтр("ru = 'владельца'");
		ВладелецНоменклатурыПредставлениеОбъекта = НСтр("ru = 'Владелец'");
	Иначе
		ВладелецНоменклатурыВРодительномПадеже   = НРег(МетаданныеСопоставления.ВладелецНоменклатурыВРодительномПадеже);
		ВладелецНоменклатурыПредставлениеОбъекта = МетаданныеСопоставления.ВладелецНоменклатурыПредставлениеОбъекта;
	КонецЕсли;
	
	Элементы.КомандаСоздатьНоменклатуру.Заголовок  = СтрШаблон(НСтр("ru = 'Создать по данным %1'"), ВладелецНоменклатурыВРодительномПадеже);
	Элементы.СопоставлениеГруппаНоменклатураКонтрагента.Заголовок = СтрШаблон(НСтр("ru = 'Данные %1'"), ВладелецНоменклатурыВРодительномПадеже);
	
	Элементы.ВладелецОтбор.Заголовок               = ВладелецНоменклатурыПредставлениеОбъекта;
	Элементы.СопоставлениеВладелец.Заголовок       = ВладелецНоменклатурыПредставлениеОбъекта;
	
	Элементы.СопоставлениеНоменклатура.Заголовок   = МетаданныеСопоставления.НоменклатураПредставлениеОбъекта;
	Элементы.СопоставлениеХарактеристика.Заголовок = МетаданныеСопоставления.ХарактеристикаПредставлениеОбъекта;
	Элементы.СопоставлениеУпаковка.Заголовок       = МетаданныеСопоставления.УпаковкаПредставлениеОбъекта;
	
	Если ЗначениеЗаполнено(МетаданныеСопоставления.ИмяПараметраСвязиХарактеристики) Тогда
		
		НоваяСвязь = Новый СвязьПараметраВыбора(МетаданныеСопоставления.ИмяПараметраСвязиХарактеристики, 
			"Элементы.Сопоставление.ТекущиеДанные.Номенклатура");
		ВсеСвязи = Новый Массив();
		ВсеСвязи.Добавить(НоваяСвязь);
		Элементы.СопоставлениеХарактеристика.СвязиПараметровВыбора = Новый ФиксированныйМассив(ВсеСвязи);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МетаданныеСопоставления.ИмяПараметраСвязиУпаковки) Тогда
		
		НоваяСвязь = Новый СвязьПараметраВыбора(МетаданныеСопоставления.ИмяПараметраСвязиУпаковки, 
			"Элементы.Сопоставление.ТекущиеДанные.Номенклатура");
		ВсеСвязи = Новый Массив();
		ВсеСвязи.Добавить(НоваяСвязь);
		Элементы.СопоставлениеУпаковка.СвязиПараметровВыбора = Новый ФиксированныйМассив(ВсеСвязи);
		
	КонецЕсли;
	
	Элементы.КомандаСоздатьНоменклатуру.Видимость = ЕстьПравоДобавленияНоменклатуры;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРежимПоискаВариантовСопоставления(ПоискВключен)
	
	Элементы.ГруппаОтображенияСопоставления.ТекущаяСтраница = ?(ПоискВключен, Элементы.ГруппаПоискСопоставления, Элементы.ГруппаСопоставление);
	Элементы.КомандаСохранитьИЗакрыть.Доступность           = НЕ ПоискВключен;
	Элементы.КомандаСохранить.Доступность                   = НЕ ПоискВключен;
	Элементы.КомандаНайти.Доступность                       = НЕ ПоискВключен;
	Элементы.КомандаОтменитьПоиск.Доступность               = НЕ ПоискВключен;
	Элементы.КомандаСоздатьНоменклатуру.Доступность         = НЕ ПоискВключен;
	Элементы.ГруппаПоискВариантов.Видимость                 = ПоискВключен;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементаОтбораПоВладельцу()
	
	ВсеВладельцы = Новый Массив;
	Для Каждого Запись Из Объект.Сопоставление Цикл
		Если ВсеВладельцы.Найти(Запись.Владелец) = Неопределено Тогда
			ВсеВладельцы.Добавить(Запись.Владелец);
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ВладелецОтбор.СписокВыбора.ЗагрузитьЗначения(ВсеВладельцы);
	Элементы.ВладелецОтбор.СписокВыбора.СортироватьПоЗначению();
	
	Если ВсеВладельцы.Количество() = 1 Тогда
		ВладелецОтбор = ВсеВладельцы[0];
		Элементы.СопоставлениеВладелец.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементаОтбораПоСопоставлению()
	
	ВсегоЗаписей = Объект.Сопоставление.Количество();
	ВсеСопоставленныеЗаписи = Объект.Сопоставление.НайтиСтроки(Новый Структура("Сопоставлено", Истина));
	СопоставленоЗаписей     = ВсеСопоставленныеЗаписи.Количество();
	НеСопоставленоЗаписей   = ВсегоЗаписей - СопоставленоЗаписей;
	
	Элементы.СопоставленоОтбор.СписокВыбора[0].Представление = СтрШаблон(НСтр("ru = 'Все (%1)'"), ВсегоЗаписей);
	Элементы.СопоставленоОтбор.СписокВыбора[1].Представление = СтрШаблон(НСтр("ru = 'Сопоставленные (%1)'"), СопоставленоЗаписей);
	Элементы.СопоставленоОтбор.СписокВыбора[2].Представление = СтрШаблон(НСтр("ru = 'Несопоставленные (%1)'"), НеСопоставленоЗаписей);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовНоменклатурыСервиса()
	
	Элементы.СопоставлениеГруппаНоменклатураСервиса.Видимость = ИспользоватьСервис;
	Элементы.ГруппаРаботаСНоменклатуройПодсказка.Видимость    = ОтображатьПодсказкуСервиса;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьИспользованиеСервисаНоменклатуры()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.РаботаСНоменклатурой") Тогда
		
		МодульРаботаСНоменклатурой = ОбщегоНазначения.ОбщийМодуль("РаботаСНоменклатурой");
		
		ИспользоватьСервисРаботаСНоменклатурой = МодульРаботаСНоменклатурой.ДоступнаФункциональностьПодсистемы();
		
		ИспользоватьСервис = ИспользоватьСервисРаботаСНоменклатурой
			И МодульРаботаСНоменклатурой.ПравоИзмененияДанных();
		
		ОтображатьПодсказкуСервиса = НЕ ИспользоватьСервисРаботаСНоменклатурой
			И МодульРаботаСНоменклатурой.ПравоИзмененияНастроек()
			И ПереданыИдентификаторыНоменклатурыСервиса();
		
	Иначе
		ИспользоватьСервис         = Ложь;
		ОтображатьПодсказкуСервиса = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьОтборЗаписейСопоставления()
	
	СтруктураОтбора = Новый Структура;
	
	Если ЗначениеЗаполнено(ВладелецОтбор) Тогда
		СтруктураОтбора.Вставить("Владелец", ВладелецОтбор);
	КонецЕсли;
	Если СопоставленоОтбор = "Сопоставленные" Тогда
		СтруктураОтбора.Вставить("Сопоставлено", Истина);
	ИначеЕсли СопоставленоОтбор = "Несопоставленные" Тогда
		СтруктураОтбора.Вставить("Сопоставлено", Ложь);
	КонецЕсли;
	
	Элементы.Сопоставление.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураОтбора);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКолонокСопоставления(ВидимостьКолонокСопоставления)
	
	НастройкиВидимости = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеНастройкиВидимостиКолонокСопоставления();
	ЗаполнитьЗначенияСвойств(НастройкиВидимости, ВидимостьКолонокСопоставления);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"СопоставлениеНаименованиеУпаковкиДляОтображения", "Видимость", НастройкиВидимости.Упаковка);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"СопоставлениеЕдиницаИзмерения", "Видимость", НастройкиВидимости.ЕдиницаИзмерения);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"СопоставлениеАртикул", "Видимость", НастройкиВидимости.Артикул);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"СопоставлениеСтавкаНДС", "Видимость", НастройкиВидимости.СтавкаНДС);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"СопоставлениеШтрихкодКомбинации", "Видимость", НастройкиВидимости.Штрихкод);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьДополнительныеРеквизитыСопоставления(ДополнительныеРеквизитыСопоставления)
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Каждого НоваяКолонка Из ДополнительныеРеквизитыСопоставления Цикл
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(НоваяКолонка.Имя, НоваяКолонка.Тип, "Объект.Сопоставление", НоваяКолонка.Имя));
	КонецЦикла;
	
	Если ДобавляемыеРеквизиты.Количество() > 0 Тогда
		ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		Для Каждого НоваяКолонка Из ДополнительныеРеквизитыСопоставления Цикл
			Элемент = Элементы.Добавить("Сопоставление" + НоваяКолонка.Имя, Тип("ПолеФормы"), Элементы.СопоставлениеГруппаНоменклатураКонтрагентаОсновное);
			Элемент.ПутьКДанным = "Объект.Сопоставление." + НоваяКолонка.Имя;
			Элемент.Вид         = ВидПоляФормы.ПолеВвода;
			Элемент.Заголовок   = НоваяКолонка.Представление;
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыВыбораНоменклатуры()
	
	ЭлементСопоставления = ТекущийЭлементСопоставления();
	Если ЭлементСопоставления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеПараметраВыбора = Новый Структура;
	ЗначениеПараметраВыбора.Вставить("НоменклатураКонтрагента", ЭлементСопоставления.НоменклатураКонтрагента);
	
	ПараметрыВыбораНоменклатуры = Новый ПараметрВыбора("Дополнительно.СопоставлениеНоменклатурыКонтрагентов", ЗначениеПараметраВыбора);
	
	ВсеПараметрыВыбора = Новый Массив;
	ВсеПараметрыВыбора.Добавить(ПараметрыВыбораНоменклатуры);
	
	Элементы.СопоставлениеНоменклатура.ПараметрыВыбора   = Новый ФиксированныйМассив(ВсеПараметрыВыбора);
	Элементы.СопоставлениеХарактеристика.ПараметрыВыбора = Новый ФиксированныйМассив(ВсеПараметрыВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗапускПоискаВариантовПриИзмененииТочности()
	
	ТекстВопроса = НСтр("ru = 'Выберите для каких позиций выполнить поиск вариантов сопоставления'");
	ВывестиВопросПовторногоПоискаВариантов(ТекстВопроса);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНаименованиеПоТипу(НайденныйТип)
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(НайденныйТип);
	
	ЭтоСправочник = ОбщегоНазначения.ЭтоСправочник(ОбъектМетаданных);
	
	Если ЭтоСправочник Тогда
		Возврат СтрШаблон("Справочник.%1.ФормаВыбора", ОбъектМетаданных.Имя);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьОбработкуВыбораУпаковки(Результат, ЗаписьСопоставления) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьСопоставления.Упаковка = Результат;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьОшибкаПоиска(Знач ВключитьВидимость)
	
	Элементы.ГруппаОшибкаВыполненияПоиска.Видимость = ВключитьВидимость;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьОшибкаСохранения(Знач ВключитьВидимость)
	
	Элементы.ГруппаОшибкаВыполненияСохранения.Видимость = ВключитьВидимость;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСВариантами

&НаСервере
Процедура ЗапуститьПоискВариантов(Знач ПолныйПоиск = Истина)
	
	ОчиститьНайденныеВариантыСопоставленияПриПовторномПолномПоиске(ПолныйПоиск);
	
	НачатьПоискВариантов(Неопределено, , ПолныйПоиск);

КонецПроцедуры

&НаСервере
Процедура НачатьПоискВариантов(НаборНоменклатурыКонтрагентов, Знач ТребуетсяПоискНоменклатуры = Истина, Знач ПолныйПоиск = Истина, Знач ОтборПоиска = Неопределено)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Поиск вариантов сопоставления номенклатуры'");
	
	Если НаборНоменклатурыКонтрагентов = Неопределено Тогда
		НаборНоменклатурыКонтрагентов = ПолучитьЭлементыСопоставления(ОтборПоиска, "НоменклатураКонтрагента");
	КонецЕсли;
	
	Если НЕ НаборНоменклатурыКонтрагентов.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("НаборНоменклатурыКонтрагентов"     , НаборНоменклатурыКонтрагентов);
	ПараметрыМетода.Вставить("ПроцентТочностиПоискаНоменклатуры" , ТочностьПоиска);
	ПараметрыМетода.Вставить("ТребуетсяПоискНоменклатуры"        , ТребуетсяПоискНоменклатуры);
	ПараметрыМетода.Вставить("ПолныйПоиск"                       , ПолныйПоиск);
	ПараметрыМетода.Вставить("ИспользоватьСервис"                , ИспользоватьСервис);
	Если ЭтоАдресВременногоХранилища(АдресТаблицыВариантовСопоставления) Тогда
		ПараметрыМетода.Вставить("ТаблицаВариантовСопоставления" , ПолучитьИзВременногоХранилища(АдресТаблицыВариантовСопоставления));
	КонецЕсли;
	ПараметрыМетода.Вставить("ТаблицаСопоставления"              , Объект.Сопоставление.Выгрузить());
	ПараметрыМетода.Вставить("ОтключитьПоискПоШтрихкодамКомбинаций", ОтключитьПоискПоШтрихкодамКомбинаций);
	ПараметрыМетода.Вставить("ОтключитьПоискПоНатуральнымКлючам" , ОтключитьПоискПоНатуральнымКлючам);
	ПараметрыМетода.Вставить("ОтключитьПоискПоСловарю"           , ОтключитьПоискПоСловарю);
	ПараметрыМетода.Вставить("ДополнительныеПараметрыПоиска"     , ДополнительныеПараметрыПоиска);
			
	ИмяМетода = "Обработка.СопоставлениеНоменклатурыБЭД.МодульОбъекта.НайтиВариантыСопоставленияНоменклатуры";
	
	УстановитьРежимПоискаВариантовСопоставления(Истина);
	
	ОперацияПоискаВариантов = ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыМетода, ПараметрыВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеПоискаВариантов(ЭтоПоискПоИдентификаторам = Ложь, Знач ПолныйПоиск = Истина)
	
	Если ОперацияПоискаВариантов = Неопределено Тогда
		УстановитьРежимПоискаВариантовСопоставления(Ложь);
		Возврат;
	КонецЕсли;
	
	ОбработкаЗавершенияПоискаВариантов = Новый ОписаниеОповещения("ЗакончитьПоискВариантов", ЭтотОбъект, 
		Новый Структура("ПолныйПоиск", ПолныйПоиск));
	
	ПараметрыОжидания                                 = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения      = Ложь;
	ПараметрыОжидания.ВыводитьОкноОжидания            = Ложь;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Ложь;
	ПараметрыОжидания.ВыводитьСообщения               = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОперацияПоискаВариантов, ОбработкаЗавершенияПоискаВариантов, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьПоискВариантов(Результат, ДополнительныеПараметры) Экспорт
	
	ЗакончитьПоискВариантовНаСервере(Результат, ДополнительныеПараметры);
	ЗаполнитьСписокДействийСЭлементомСопоставления();
	
	ОжидатьЗавершениеПоискаВариантов(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗакончитьПоискВариантовНаСервере(Результат, ДополнительныеПараметры)
	
	ОперацияПоискаВариантов = Неопределено;
	Элементы.ГруппаПоискВариантов.Видимость = Ложь;
		
	Если Результат = Неопределено
		ИЛИ Результат.Статус <> "Выполнено" Тогда
		УстановитьРежимПоискаВариантовСопоставления(Ложь);
		УстановитьВидимостьОшибкаПоиска(Истина);
		Возврат;
	КонецЕсли;
	
	СтруктураРезультата = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если СтруктураРезультата = Неопределено Тогда
		УстановитьВидимостьОшибкаПоиска(Истина);
		ТекстСообщения = НСтр("ru = 'Не удалось выполнить поиск вариантов сопоставления по причине:'")
							+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	УстановитьВидимостьОшибкаПоиска(Ложь);
	Модифицированность = Истина;
	
	Если СтруктураРезультата.Свойство("ТаблицаВариантовСопоставления") Тогда
		АдресТаблицыВариантовСопоставления = ПоместитьВоВременноеХранилище(СтруктураРезультата.ТаблицаВариантовСопоставления, УникальныйИдентификатор);
	КонецЕсли;
	Объект.Сопоставление.Загрузить(СтруктураРезультата.ТаблицаСопоставления);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Идентификатор", ТекущийИдентификаторСопоставления);
	НайденныеСтроки = Объект.Сопоставление.НайтиСтроки(ПараметрыОтбора);
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
		Элементы.Сопоставление.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
	РезультатПоиска = СтрШаблон(НСтр("ru = 'Поиск вариантов завершен. Автоматически сопоставлено: %1'"), СтруктураРезультата.СопоставленоАвтоматически);
	Элементы.НадписьРезультатПоискаВариантов.Заголовок = РезультатПоиска;
	Элементы.НадписьРезультатПоискаВариантов.Видимость = Истина;
	
	УстановитьСвойстваЭлементаОтбораПоСопоставлению();
	
	УстановитьРежимПоискаВариантовСопоставления(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВариантыСопоставления(Знач ЭлементСопоставления)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЭлементСопоставления"              , ЭлементСопоставления);
	ПараметрыФормы.Вставить("АдресТаблицыВариантовСопоставления", АдресТаблицыВариантовСопоставления);
	
	ОткрытьФорму("Обработка.СопоставлениеНоменклатурыБЭД.Форма.ФормаВариантов",
		ПараметрыФормы, Элементы.Сопоставление, ЭлементСопоставления,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаССопоставлением

&НаКлиентеНаСервереБезКонтекста
Функция ЭлементСопоставленияПоЗаписи(Знач Запись)
	
	ЭлементСопоставления = Новый Структура;
	ЭлементСопоставления.Вставить("НоменклатураКонтрагента", 
		НоменклатураКонтрагентаПоЗаписиСопоставления(Запись));
		
	ЭлементСопоставления.Вставить("НоменклатураИБ", 
		НоменклатураИБПоЗаписиСопоставления(Запись));
		
	ЭлементСопоставления.Вставить("КоличествоВариантовНоменклатуры"            , Запись.КоличествоВариантовНоменклатуры);
	ЭлементСопоставления.Вставить("ПоискВариантовНеТребуется"                  , Запись.ПоискВариантовНеТребуется);
	ЭлементСопоставления.Вставить("ПодсказкаНайденныхВариантовХарактеристики"  , Запись.ПодсказкаНайденныхВариантовХарактеристики);
	ЭлементСопоставления.Вставить("ПодсказкаНайденныхВариантовУпаковки"        , Запись.ПодсказкаНайденныхВариантовУпаковки);
	ЭлементСопоставления.Вставить("ПодсказкаНайденныхВариантовНоменклатуры"    , Запись.ПодсказкаНайденныхВариантовНоменклатуры);
	ЭлементСопоставления.Вставить("ИспользоватьХарактеристики"                 , Запись.ИспользоватьХарактеристики);

	Возврат ЭлементСопоставления;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НоменклатураКонтрагентаПоЗаписиСопоставления(Знач Запись)
	
	НоменклатураКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента();
	ЗаполнитьЗначенияСвойств(НоменклатураКонтрагента, Запись);
	НоменклатураКонтрагента.Характеристика = Запись.НаименованиеХарактеристики;
	НоменклатураКонтрагента.Вставить("НоменклатураИБ"              , Запись.Номенклатура);
	НоменклатураКонтрагента.Вставить("ХарактеристикаИБ"            , Запись.Характеристика);
	НоменклатураКонтрагента.Вставить("УпаковкаИБ"                  , Запись.Упаковка);
	НоменклатураКонтрагента.Вставить("ЕдиницаИзмеренияПоУмолчанию" , Запись.Упаковка);
	НоменклатураКонтрагента.Вставить("ХешПравилаПоиска"            , Запись.ХешПоисковойСтроки);
	
	Возврат НоменклатураКонтрагента;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НоменклатураИБПоЗаписиСопоставления(Знач Запись)
	
	НоменклатураИБ = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы();
	ЗаполнитьЗначенияСвойств(НоменклатураИБ, Запись);
	Возврат НоменклатураИБ;
	
КонецФункции

&НаКлиенте
Функция ЭтоДействиеСЭлементомСопоставления(Знач ПроверяемоеЗначение)
	
	Если ТипЗнч(ПроверяемоеЗначение) = Тип("Структура") Тогда
		
		Если ПроверяемоеЗначение.Свойство("Действие") 
			И ПроверяемоеЗначение.Свойство("ЭлементСопоставления") Тогда
			
			ДопустимыеДействия = Новый Массив;
			ДопустимыеДействия.Добавить("ПоказатьВарианты");
			ДопустимыеДействия.Добавить("ИдетПоискВариантов");
			ДопустимыеДействия.Добавить("ПрименитьВариант");
			ДопустимыеДействия.Добавить("ОткрытьСписок");
			ДопустимыеДействия.Добавить("СоздатьНоменклатуру");
			ДопустимыеДействия.Добавить("ЗагрузитьИзСервиса");
			ДопустимыеДействия.Добавить("ПодобратьИзСервиса");
			ДопустимыеДействия.Добавить("ДобавитьПравилоПоиска");
			ДопустимыеДействия.Добавить("ИзменитьПравилоПоиска");
			
			Если ДопустимыеДействия.Найти(ПроверяемоеЗначение.Действие) <> Неопределено Тогда
			
				Возврат Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьДействиеСЭлементомСопоставления(Знач ОписаниеДействия)
	
	Действие = ОписаниеДействия.Действие;
	ЭлементСопоставления = ОписаниеДействия.ЭлементСопоставления;
	Если ЭлементСопоставления = Неопределено Тогда
		ЭлементСопоставления = ТекущийЭлементСопоставления();
	КонецЕсли;
	
	Если Действие = "ПоказатьВарианты" Тогда
		
		ПоказатьВариантыСопоставления(ЭлементСопоставления);
		
	ИначеЕсли Действие = "ИдетПоискВариантов" Тогда
		
		ТекстСообщения = НСтр("ru = 'Поиск вариантов еще не завершен. Попробуйте позже.'");
		ПоказатьПредупреждение(, ТекстСообщения);
		
	ИначеЕсли Действие = "ПрименитьВариант" Тогда
		
		ОбработатьЭлементыСопоставленияНоменклатуры(ЭлементСопоставления, Ложь);
		Элементы.Сопоставление.ЗакончитьРедактированиеСтроки(Ложь);
		
		ОжидатьЗавершениеПоискаВариантов(Ложь);
		
	ИначеЕсли Действие = "СоздатьНоменклатуру" Тогда
		
		ПоказатьВопросОСозданииНоменклатуры(ЭлементСопоставления);
		Элементы.Сопоставление.ЗакончитьРедактированиеСтроки(Ложь);
		
	ИначеЕсли Действие = "ОткрытьСписок" Тогда
		
		ОткрытьФормуВыбораНоменклатурыИнформационнойБазы(ЭлементСопоставления);
		
	ИначеЕсли Действие = "ПодобратьИзСервиса" Тогда
		
		ПоказатьПодборИзСервиса(ЭлементСопоставления);
		Элементы.Сопоставление.ЗакончитьРедактированиеСтроки(Ложь);
		
	ИначеЕсли Действие = "ЗагрузитьИзСервиса" Тогда
		
		НачатьЗагрузкуНоменклатуруИзСервиса(ЭлементСопоставления);
		Элементы.Сопоставление.ЗакончитьРедактированиеСтроки(Ложь);
		
	ИначеЕсли Действие = "ДобавитьПравилоПоиска" Тогда
		
		ОткрытьФормуПравилаПоиска("ДобавитьПравилоПоиска");
		
	ИначеЕсли Действие = "ИзменитьПравилоПоиска" Тогда
		
		ОткрытьФормуПравилаПоиска("ИзменитьПравилоПоиска");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокДействийСЭлементомСопоставления()
	
	СписокДействий = Элементы.СопоставлениеНоменклатура.СписокВыбора;
	СписокДействий.Очистить();
	
	СписокДействийХарактеристики = Элементы.СопоставлениеХарактеристика.СписокВыбора;
	СписокДействийХарактеристики.Очистить();
	
	СписокДействийУпаковки = Элементы.СопоставлениеУпаковка.СписокВыбора;
	СписокДействийУпаковки.Очистить();
	
	ЭлементСопоставления = ТекущийЭлементСопоставления();
	Если ЭлементСопоставления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Номенклатура = ЭлементСопоставления.НоменклатураИБ.Номенклатура;
	
	// Показать варианты.
	Если ОперацияПоискаВариантов = Неопределено Тогда
		
		Если ЭлементСопоставления.КоличествоВариантовНоменклатуры > 0 Тогда
			
			НовоеДействие = Новый Структура("Действие,ЭлементСопоставления", "ПоказатьВарианты", Неопределено);
			Представление = СтрШаблон(НСтр("ru = 'Найдено в моей базе (%1)'"), ЭлементСопоставления.КоличествоВариантовНоменклатуры);
			СписокДействий.Добавить(НовоеДействие, Представление,, БиблиотекаКартинок.ВыбратьЗначение);
			
			Если ЗначениеЗаполнено(Номенклатура)
				И ЭтоАдресВременногоХранилища(АдресТаблицыВариантовСопоставления) Тогда
				
				КлючЗаписи = Новый Структура("Владелец,Идентификатор,Номенклатура");
				КлючЗаписи.Владелец      = ЭлементСопоставления.НоменклатураКонтрагента.Владелец;
				КлючЗаписи.Идентификатор = ЭлементСопоставления.НоменклатураКонтрагента.Идентификатор;
				КлючЗаписи.Номенклатура  = Номенклатура;
				
				СтруктураДанных = ПолучитьВариантыСопоставленияХарактеристикИУпаковок(КлючЗаписи, АдресТаблицыВариантовСопоставления);
				
				Если ЗначениеЗаполнено(СтруктураДанных) Тогда
					Если СтруктураДанных.Свойство("ВариантыСопоставленияХарактеристики") Тогда
						СписокДействийХарактеристики.ЗагрузитьЗначения(СтруктураДанных.ВариантыСопоставленияХарактеристики);
					КонецЕсли;
					Если СписокТиповМетаданныхУпаковок.Количество() = 1
						И СтруктураДанных.Свойство("ВариантыСопоставленияУпаковки") Тогда
						СписокДействийУпаковки.ЗагрузитьЗначения(СтруктураДанных.ВариантыСопоставленияУпаковки);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		НовоеДействие = Новый Структура("Действие,ЭлементСопоставления", "ИдетПоискВариантов", ЭлементСопоставления);
		Представление = НСтр("ru = 'Идет поиск вариантов'");
		СписокДействий.Добавить(НовоеДействие, Представление,, БиблиотекаКартинок.ДлительнаяОперация16);
			
	КонецЕсли;
	
	// Создать по данным контрагента.
	Если ЕстьПравоДобавленияНоменклатуры Тогда
		НовоеДействие = Новый Структура("Действие,ЭлементСопоставления", "СоздатьНоменклатуру", ЭлементСопоставления);
		Представление = СтрШаблон(НСтр("ru = 'Создать по данным %1'"), ВладелецНоменклатурыВРодительномПадеже);
		СписокДействий.Добавить(НовоеДействие, Представление,, БиблиотекаКартинок.СоздатьЭлементСписка);
	КонецЕсли;
	
	// Загрузить/Подобрать из сервиса 1С:Номенклатура.
	Если ИспользоватьСервис Тогда
		
		Если ЗначениеЗаполнено(ЭлементСопоставления.НоменклатураКонтрагента.ИдентификаторНоменклатурыСервиса) Тогда
			
			НовоеДействие = Новый Структура("Действие,ЭлементСопоставления", "ЗагрузитьИзСервиса", ЭлементСопоставления);
			Представление = НСтр("ru = 'Загрузить из 1С:Номенклатура'");
			СписокДействий.Добавить(НовоеДействие, Представление, , БиблиотекаКартинок["ДобавитьРаботаСНоменклатурой"]);
			
		Иначе
			
			НовоеДействие = Новый Структура("Действие,ЭлементСопоставления", "ПодобратьИзСервиса", ЭлементСопоставления);
			Представление = НСтр("ru = 'Подобрать из 1С:Номенклатура'");
			СписокДействий.Добавить(НовоеДействие, Представление, , БиблиотекаКартинок["ИконкаБелыйФонРаботаСНоменклатурой"]);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДобавитьДействиеСПравиломПоиска(СписокДействий, ЭлементСопоставления);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВариантыСопоставленияХарактеристикИУпаковок(Знач КлючЗаписи, Знач АдресТаблицыВариантовСопоставления)
	
	Если ЭтоАдресВременногоХранилища(АдресТаблицыВариантовСопоставления) Тогда
		ТаблицаВариантовСопоставления = ПолучитьИзВременногоХранилища(АдресТаблицыВариантовСопоставления);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТаблицаВариантовСопоставления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	СтрокиВариантовСопоставления = ТаблицаВариантовСопоставления.НайтиСтроки(КлючЗаписи);
	
	Если ЗначениеЗаполнено(СтрокиВариантовСопоставления) Тогда
		
		СтруктураДанных = Новый Структура;
		
		Если СтрокиВариантовСопоставления[0].ВариантыСопоставленияХарактеристики.Количество() Тогда
			СтруктураДанных.Вставить("ВариантыСопоставленияХарактеристики", СтрокиВариантовСопоставления[0].ВариантыСопоставленияХарактеристики);
		КонецЕсли;
		Если СтрокиВариантовСопоставления[0].ВариантыСопоставленияУпаковки.Количество() Тогда
			СтруктураДанных.Вставить("ВариантыСопоставленияУпаковки", СтрокиВариантовСопоставления[0].ВариантыСопоставленияУпаковки);
		КонецЕсли;
		
		Возврат СтруктураДанных;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

&НаКлиенте
Функция ЭтоЭлементСопоставления(Знач ПроверяемоеЗначение)
	
	Если ТипЗнч(ПроверяемоеЗначение) = Тип("Структура") Тогда
		
		Если ПроверяемоеЗначение.Свойство("НоменклатураКонтрагента") 
			И ПроверяемоеЗначение.Свойство("НоменклатураИБ") Тогда
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Функция ТекущийЭлементСопоставления()
	
	Запись = Элементы.Сопоставление.ТекущиеДанные;
	Если Запись = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ЭлементСопоставленияПоЗаписи(Запись);
	
КонецФункции

&НаСервере
Функция ПолучитьЭлементыСопоставления(Знач Отбор = Неопределено, Режим = "Полный")
	
	ЗаписиСопоставления = Объект.Сопоставление;
	Если ЗначениеЗаполнено(Отбор) Тогда
		Если ТипЗнч(Отбор) = Тип("Структура") Тогда
			ЗаписиСопоставления = Объект.Сопоставление.НайтиСтроки(Отбор);
		ИначеЕсли ТипЗнч(Отбор) = Тип("Массив") Тогда
			ЗаписиСопоставления = Отбор;
		КонецЕсли;
	КонецЕсли;
	
	ВыбранныеЭлементыСопоставления = Новый Массив;
	
	Для Каждого Запись Из ЗаписиСопоставления Цикл
		
		ЭлементСопоставления = ЭлементСопоставленияПоЗаписи(Запись);
		
		Если Режим = "НоменклатураКонтрагента" Тогда
			ВыбранныеЭлементыСопоставления.Добавить(ЭлементСопоставления.НоменклатураКонтрагента);
		Иначе
			ВыбранныеЭлементыСопоставления.Добавить(ЭлементСопоставления);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВыбранныеЭлементыСопоставления;
	
КонецФункции

&НаСервере
Процедура УстановитьПризнакСопоставленияСтрокамСопоставления()
	
	Для Каждого Запись Из Объект.Сопоставление Цикл
		Запись.Сопоставлено = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьЭлементыСопоставленияНоменклатуры(Знач ЭлементыСопоставления, Знач СозданиеЭлементов = Истина)
	
	Если ТипЗнч(ЭлементыСопоставления) <> Тип("Массив") Тогда
		ЭлементыСопоставления = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭлементыСопоставления);
	КонецЕсли;
	
	НаборНоменклатурыКонтрагента = Новый Массив;
	НаборНоменклатурИБ           = Новый Массив;
	ДанныеДляОбработки           = Новый Массив;
	Для Каждого Элемент Из ЭлементыСопоставления Цикл
		
		ТребуетсяПоискВариантов = Ложь;
		Если НЕ СозданиеЭлементов Тогда
			ЗаполнитьСопоставлениеИзВариантов(Элемент);
			Если НЕ Элемент.ПоискВариантовНеТребуется Тогда
				ТребуетсяПоискВариантов = Истина;
			КонецЕсли;
		Иначе
			ТребуетсяПоискВариантов = Истина;
		КонецЕсли;

		НаборНоменклатурИБ.Добавить(Элемент.НоменклатураИБ.Номенклатура);
		ДанныеДляОбработки.Добавить(Элемент);
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(НаборНоменклатурИБ) Тогда
		
		СвойстваНоменклатурИБ =
			СопоставлениеНоменклатурыКонтрагентовСлужебный.СвойстваНоменклатурыИнформационнойБазы(НаборНоменклатурИБ);
		
		Для Каждого Элемент Из ДанныеДляОбработки Цикл
			Свойства = СвойстваНоменклатурИБ.Получить(Элемент.НоменклатураИБ.Номенклатура);
			Если Свойства <> Неопределено Тогда
				
				ПараметрыОтбора = Новый Структура("Владелец,Идентификатор");
				ЗаполнитьЗначенияСвойств(ПараметрыОтбора, Элемент.НоменклатураКонтрагента);
				
				ИзменитьЗаписиСопоставленияНоменклатуры(
					Элемент, ПараметрыОтбора, ТребуетсяПоискВариантов, НаборНоменклатурыКонтрагента, Свойства);
				
				Если Свойства.ИспользоватьХарактеристики Тогда
					
					ТребуетсяПоискВариантов = Истина;
					ПараметрыОтбора = Новый Структура("Владелец,ИдентификаторНоменклатуры,Номенклатура");
					ЗаполнитьЗначенияСвойств(ПараметрыОтбора, Элемент.НоменклатураКонтрагента);
					ПараметрыОтбора.Номенклатура = ПустаяНоменклатураБЭД;
					
					ИзменитьЗаписиСопоставленияНоменклатуры(Элемент,
															ПараметрыОтбора,
															ТребуетсяПоискВариантов,
															НаборНоменклатурыКонтрагента,
															Свойства);
					
				КонецЕсли;
				
			КонецЕсли;

		КонецЦикла;
		
	КонецЕсли;
	
	Если ТребуетсяПоискВариантов Тогда
		НачатьПоискВариантов(НаборНоменклатурыКонтрагента, Ложь, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьЗаписиСопоставленияНоменклатуры(Элемент, ПараметрыОтбора,ТребуетсяПоискВариантов, НаборНоменклатурыКонтрагента, Свойства = Неопределено)
	
	ЗаписиСопоставления = Объект.Сопоставление.НайтиСтроки(ПараметрыОтбора);
	
	ЕстьДополнительныеПоля = Элемент.Свойство("ПодсказкаНайденныхВариантовНоменклатуры");
	
	Для Каждого Запись Из ЗаписиСопоставления Цикл
		
		Если Свойства <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(Запись, Свойства);
		КонецЕсли;
		
		Запись.Номенклатура                            = Элемент.НоменклатураИБ.Номенклатура;
		Запись.ПоискВариантовНеТребуется               = НЕ ТребуетсяПоискВариантов;
		Запись.ПодсказкаНайденныхВариантовНоменклатуры = ?(ЕстьДополнительныеПоля, Элемент.ПодсказкаНайденныхВариантовНоменклатуры, "");
		
		Если Не ПустаяСтрока(Запись.ИдентификаторУпаковки)
			И Запись.ИдентификаторУпаковки = Элемент.НоменклатураКонтрагента.ИдентификаторУпаковки Тогда
			Запись.Упаковка                            = Элемент.НоменклатураИБ.Упаковка;
			Запись.ПодсказкаНайденныхВариантовУпаковки = ?(ЕстьДополнительныеПоля, Элемент.ПодсказкаНайденныхВариантовУпаковки, "");
		Иначе
			Запись.Упаковка                            = ПустаяСсылкаУпаковкиНоменклатурыБЭД;
			Запись.ПодсказкаНайденныхВариантовУпаковки = "";
		КонецЕсли;
		Если Не ПустаяСтрока(Запись.ИдентификаторХарактеристики)
			И Запись.ИдентификаторХарактеристики = Элемент.НоменклатураКонтрагента.ИдентификаторХарактеристики Тогда
			Запись.Характеристика                            = Элемент.НоменклатураИБ.Характеристика;
			Запись.ПодсказкаНайденныхВариантовХарактеристики = ?(ЕстьДополнительныеПоля, Элемент.ПодсказкаНайденныхВариантовХарактеристики, "");
		Иначе
			Запись.Характеристика                            = ПустаяСсылкаХарактеристикиБЭД;
			Запись.ПодсказкаНайденныхВариантовХарактеристики = "";
		КонецЕсли;
		
		ОбработатьЗаписьСопоставления(Запись);
		
		Если ТребуетсяПоискВариантов Тогда
			НоменклатураКонтрагента = НоменклатураКонтрагентаПоЗаписиСопоставления(Запись);
			НаборНоменклатурыКонтрагента.Добавить(НоменклатураКонтрагента);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьЗаписьСопоставления(Запись, СвойстваНоменклатурИБ = Неопределено)
	
	Модифицированность = Истина;
	
	ЗаписьСопоставлено = Запись.Сопоставлено;
	
	Обработки.СопоставлениеНоменклатурыБЭД.ЗаполнитьПризнакСопоставления(Запись, СвойстваНоменклатурИБ);
		
	Если ЗаписьСопоставлено <> Запись.Сопоставлено Тогда
		УстановитьСвойстваЭлементаОтбораПоСопоставлению();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СохранитьСопоставление()
	
	Если РазрешитьСохранение Тогда
		Отказ       = Ложь;
		СохранитьСопоставленнуюНоменклатуруКонтрагентов(Отказ);
		Если Отказ Тогда
			УстановитьВидимостьОшибкаСохранения(Истина);
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимостьОшибкаСохранения(Ложь);
	
	ОтборЭлементовДляСохранения = Новый Структура("Сопоставлено,Сохранено", Истина, Ложь);
	СохраненныеЗаписи = Объект.Сопоставление.НайтиСтроки(ОтборЭлементовДляСохранения);
	
	Для Каждого Запись Из СохраненныеЗаписи Цикл
		
		ЗаполнитьУпаковкуПоУмолчанию(Запись);
		Запись.Сохранено = Истина;
		
	КонецЦикла;
	
	ОтборСохраненныхЭлементов = Новый Структура("Сопоставлено,Сохранено", Истина,Истина);
	СохраненныеЭлементы = ПолучитьЭлементыСопоставления(ОтборСохраненныхЭлементов);
	
	Модифицированность = Ложь;
	
	ПроверитьВключитьФункциональностьСловаря();
	
	Возврат СохраненныеЭлементы;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьВопросОСохраненииПередЗакрытием()
	
	ОбработкаОтвета = Новый ОписаниеОповещения("ОбработатьВопросОСохраненииПередЗакрытием", ЭтотОбъект);
	
	ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
	Если НЕ РазрешитьСохранение Тогда
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Применить изменения?'");
	КонецЕсли;
	
	ПоказатьВопрос(ОбработкаОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВопросОСохраненииПередЗакрытием(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		СохраненныеЭлементы = СохранитьСопоставление();
		
		Если СохраненныеЭлементы = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Закрыть(СохраненныеЭлементы);
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		
		Модифицированность = Ложь;
		
		УстановитьПризнакСопоставленияСтрокамСопоставления();
		Если РазрешитьСохранение Тогда
			Закрыть(СохраненныеЭлементы);
		Иначе
			Закрыть();
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВопросОЗапускаТочностиПоиска(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПолныйПоиск = Истина;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		ПолныйПоиск = Ложь;
	Иначе
		ТочностьПоиска = ТочностьПоискаДоИзменения;
		Возврат;
	КонецЕсли;
	
	ТочностьПоискаДоИзменения = ТочностьПоиска;
	
	ПовторныйПоиск = Истина;
	ЗапуститьПоискВариантов(ПолныйПоиск);
	
	ОжидатьЗавершениеПоискаВариантов(ИспользоватьСервис, ПолныйПоиск);

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросОСопоставленииПередЗакрытием()
	
	ОбработкаОтвета = Новый ОписаниеОповещения("ОбработатьВопросОСопоставленииПередЗакрытием", ЭтотОбъект);
	
	ТекстВопроса = НСтр("ru = 'Сопоставлены не все позиции номенклатуры. Закрыть помощник сопоставления?'");
	
	ПоказатьВопрос(ОбработкаОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВопросОСопоставленииПередЗакрытием(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		УстановитьПризнакСопоставленияСтрокамСопоставления();
		Закрыть(СохраненныеЭлементы);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораНоменклатурыИнформационнойБазы(Знач ЭлементСопоставления)
	
	ПараметрыФормы = Новый Структура;
		
	ОбщегоНазначенияБЭДКлиентСервер.УстановитьСвойствоСтруктуры(
		ПараметрыФормы,
		"ДополнительныеПараметры.СопоставлениеНоменклатурыКонтрагентов.НоменклатураКонтрагента",
		ЭлементСопоставления.НоменклатураКонтрагента);
		
	ПараметрыФормы.Вставить("ОграничениеТипаНоменклатуры", Элементы.СопоставлениеНоменклатура.ОграничениеТипа);
		
	СопоставлениеНоменклатурыКонтрагентовКлиентПереопределяемый.ОткрытьФормуВыбораНоменклатуры(
		ПараметрыФормы, Элементы.СопоставлениеНоменклатура, ЭлементСопоставления);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатуруКонтрагентов(НаборНоменклатурыКонтрагентов)
	
	Объект.Сопоставление.Очистить();
	
	СопоставлениеНоменклатурыКонтрагентовСлужебный.ОтобратьУникальныеЗаписиНоменклатурыКонтрагентов(НаборНоменклатурыКонтрагентов);
	
	НаборНоменклатурИБ = Новый Массив;
	
	ВариантУказанияНоменклатураОрганизации = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.ВариантУказанияНоменклатураОрганизации();
	ВариантУказанияНоменклатураКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.ВариантУказанияНоменклатураКонтрагента();
	
	ЕстьНашаНоменклатура        = Ложь;
	ЕстьНоменклатураКонтрагента = Ложь;
	Для Каждого НоменклатураКонтрагента Из НаборНоменклатурыКонтрагентов Цикл
		
		НоваяЗапись = Объект.Сопоставление.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, НоменклатураКонтрагента);
		НоваяЗапись.НаименованиеХарактеристики = НоменклатураКонтрагента.Характеристика;
		
		Если ЗначениеЗаполнено(НоваяЗапись.Номенклатура) Тогда
			НаборНоменклатурИБ.Добавить(НоваяЗапись.Номенклатура);
		Иначе
			ОбработатьЗаписьСопоставления(НоваяЗапись);
		КонецЕсли;
		
		Если НоменклатураКонтрагента.ВариантУказанияНоменклатуры = ВариантУказанияНоменклатураКонтрагента Тогда
			ЕстьНоменклатураКонтрагента = Истина;
		ИначеЕсли НоменклатураКонтрагента.ВариантУказанияНоменклатуры = ВариантУказанияНоменклатураОрганизации Тогда
			ЕстьНашаНоменклатура = Истина; 
		КонецЕсли;
		
		Если НоменклатураКонтрагента.НаименованиеУпаковки = "" Тогда
			НоваяЗапись.НаименованиеУпаковкиДляОтображения = НоменклатураКонтрагента.ЕдиницаИзмерения;
		Иначе
			НоваяЗапись.НаименованиеУпаковкиДляОтображения = НоменклатураКонтрагента.НаименованиеУпаковки;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(НаборНоменклатурИБ) Тогда
		СвойстваНоменклатурИБ = СопоставлениеНоменклатурыКонтрагентовСлужебный.СвойстваНоменклатурыИнформационнойБазы(НаборНоменклатурИБ);
		Для Каждого Запись Из Объект.Сопоставление Цикл
			ОбработатьЗаписьСопоставления(Запись, СвойстваНоменклатурИБ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СопоставлениеПриИзмененииНаСервере(Знач ИмяЭлемента)
	
	ТекущаяСтрока = Элементы.Сопоставление.ТекущаяСтрока;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запись = Объект.Сопоставление.НайтиПоИдентификатору(ТекущаяСтрока);
	Запись.Сохранено = Ложь;

	Если ИмяЭлемента = "СопоставлениеНоменклатура" Тогда
		Если ЗначениеЗаполнено(Запись.Номенклатура) Тогда
			ЭлементСопоставления = ЭлементСопоставленияПоЗаписи(Запись);
			ОбработатьЭлементыСопоставленияНоменклатуры(ЭлементСопоставления, Ложь);
			Возврат;
		Иначе
			Запись.Характеристика = ПустаяСсылкаХарактеристикиБЭД;
			Запись.Упаковка       = ПустаяСсылкаУпаковкиНоменклатурыБЭД;
			
			СвойстваНоменклатурыИБ = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеСвойстваНоменклатурыИБ();
			ЗаполнитьЗначенияСвойств(Запись, СвойстваНоменклатурыИБ);
			
			ОбработатьЗаписьСопоставления(Запись);

		КонецЕсли;
	Иначе
		
		ОбработатьЗаписьСопоставления(Запись);

		Если ИмяЭлемента = "СопоставлениеХарактеристика" Тогда
			ОбработатьЭлементыСопоставленияХарактеристики(Запись);
		ИначеЕсли ИмяЭлемента = "СопоставлениеУпаковка" Тогда
			ОбработатьЭлементыСопоставленияУпаковки(Запись);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиВопросПовторногоПоискаВариантов(ТекстВопроса)
	
	ОбработкаОтвета = Новый ОписаниеОповещения("ОбработатьВопросОЗапускаТочностиПоиска", ЭтотОбъект);
	
	СписокВариантов = Новый СписокЗначений;
	СписокВариантов.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Для всех позиций'"));
	СписокВариантов.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Только для незаполненных'"));
	СписокВариантов.Добавить(КодВозвратаДиалога.Отмена);
	
	ПоказатьВопрос(ОбработкаОтвета, ТекстВопроса, СписокВариантов, , КодВозвратаДиалога.Отмена);

КонецПроцедуры

&НаСервере
Процедура ОчиститьНайденныеВариантыСопоставленияПриПовторномПолномПоиске(ПолныйПоиск)
	
	Если ПовторныйПоиск И ПолныйПоиск
		И Объект.Сопоставление.Количество() Тогда
		
		АдресТаблицыВариантовСопоставления = "";
		
		Для Каждого Запись Из Объект.Сопоставление Цикл
			
			Запись.Номенклатура   = ПустаяНоменклатураБЭД;
			Запись.Характеристика = ПустаяСсылкаХарактеристикиБЭД;
			Запись.Упаковка       = ПустаяСсылкаУпаковкиНоменклатурыБЭД;
			
			Запись.ПодсказкаНайденныхВариантовХарактеристики = "";
			Запись.ПодсказкаНайденныхВариантовУпаковки       = "";
			Запись.ПодсказкаНайденныхВариантовНоменклатуры   = "";
			Запись.КоличествоВариантовНоменклатуры           = 0;
			Запись.ПоискВариантовНеТребуется                 = Ложь;
			Запись.Сопоставлено                              = Ложь;
			Запись.ИспользоватьХарактеристики                = Ложь;
			Запись.ИспользоватьУпаковки                      = Ложь;
			Запись.ОбязательноеЗаполнениеХарактеристики      = Ложь;
			Запись.Сохранено                                 = Ложь;
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУпаковкуПоУмолчанию(ЭлементЗаписи)
	
	Если Не ЭлементЗаписи.ИспользоватьУпаковки
		И Не ЗначениеЗаполнено(ЭлементЗаписи.Упаковка) Тогда
		ЭлементЗаписи.Упаковка = ЭлементЗаписи.ЕдиницаИзмеренияПоУмолчанию;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСопоставлениеИзВариантов(Запись)
	
	Если ЭтоАдресВременногоХранилища(АдресТаблицыВариантовСопоставления) Тогда
		ТаблицаВариантовСопоставления = ПолучитьИзВременногоХранилища(АдресТаблицыВариантовСопоставления);
	Иначе
		Возврат;
	КонецЕсли;

	НоменклатураИБ = Запись.НоменклатураИБ;
	
	КлючЗаписи = Новый Структура("Владелец,Идентификатор,Номенклатура");
	ЗаполнитьЗначенияСвойств(КлючЗаписи, Запись.НоменклатураКонтрагента);
	КлючЗаписи.Номенклатура = НоменклатураИБ.Номенклатура;

	НайденныеСтроки = ТаблицаВариантовСопоставления.НайтиСтроки(КлючЗаписи);
	
	Если ЗначениеЗаполнено(НайденныеСтроки)
		И ЗначениеЗаполнено(НайденныеСтроки[0].Номенклатура) Тогда
		НоменклатураИБ.Характеристика    = НайденныеСтроки[0].Характеристика;
		НоменклатураИБ.Упаковка          = НайденныеСтроки[0].Упаковка;
		Запись.ПоискВариантовНеТребуется = НайденныеСтроки[0].ПоискВариантовНеТребуется;
	Иначе
		Запись.ПоискВариантовНеТребуется = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПереданыИдентификаторыНоменклатурыСервиса()
	
	Для Каждого ДанныеСопоставления Из Объект.Сопоставление Цикл
		Если НЕ ПустаяСтрока(ДанныеСопоставления.ИдентификаторНоменклатурыСервиса) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ОбработатьЭлементыСопоставленияУпаковки(Знач ЭлементСопоставления)
	
	Если Не ЗначениеЗаполнено(ЭлементСопоставления.Упаковка)
		Или ПустаяСтрока(ЭлементСопоставления.ИдентификаторУпаковки) Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецУпаковкиНоменклатура = Ложь;
	
	СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ВладелецУпаковкиЕдиницыИзмеренияНоменклатура(
		ЭлементСопоставления.Упаковка, ВладелецУпаковкиНоменклатура);

	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Владелец"             , ЭлементСопоставления.Владелец);
	ПараметрыОтбора.Вставить("ИдентификаторУпаковки", ЭлементСопоставления.ИдентификаторУпаковки);
	ЗаписиСопоставления = Объект.Сопоставление.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого Запись Из ЗаписиСопоставления Цикл
		
		Если ЗначениеЗаполнено(Запись.Упаковка)
			ИЛИ НЕ ЗначениеЗаполнено(Запись.Номенклатура)
			ИЛИ Запись.ИспользоватьУпаковки И ВладелецУпаковкиНоменклатура
			И Запись.Номенклатура <> ЭлементСопоставления.Номенклатура Тогда
			Продолжить;
		КонецЕсли;
		
		Запись.Упаковка = ЭлементСопоставления.Упаковка;
		ОбработатьЗаписьСопоставления(Запись);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьЭлементыСопоставленияХарактеристики(Знач ЭлементСопоставления)
	
	Если Не ЗначениеЗаполнено(ЭлементСопоставления.Характеристика)
		Или ПустаяСтрока(ЭлементСопоставления.ИдентификаторХарактеристики) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец"                   , ЭлементСопоставления.Владелец);
	Запрос.УстановитьПараметр("ИдентификаторХарактеристики", ЭлементСопоставления.ИдентификаторХарактеристики);
	Запрос.УстановитьПараметр("ПустаяНоменклатура"         , ПустаяНоменклатураБЭД);
	Запрос.УстановитьПараметр("Характеристика"             , ЭлементСопоставления.Характеристика);
	Запрос.УстановитьПараметр("ПустаяХарактеристика"       , ПустаяСсылкаХарактеристикиБЭД);
	Запрос.УстановитьПараметр("ТаблицаСопоставления"       , Объект.Сопоставление.Выгрузить());

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаСопоставления.Номенклатура КАК НоменклатураИБ,
		|	ТаблицаСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ТаблицаСопоставления
		|ИЗ
		|	&ТаблицаСопоставления КАК ТаблицаСопоставления
		|ГДЕ
		|	ТаблицаСопоставления.ИдентификаторХарактеристики = &ИдентификаторХарактеристики
		|	И ТаблицаСопоставления.Владелец = &Владелец
		|	И ТаблицаСопоставления.Характеристика = &ПустаяХарактеристика
		|	И ТаблицаСопоставления.Номенклатура <> &ПустаяНоменклатура
		|	И ТаблицаСопоставления.ИспользоватьХарактеристики
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ТаблицаСопоставления.Номенклатура,
		|	Идентификатор";
		
	ТекстЗапроса = "";
	
	СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ТекстЗапросаОтбораСтрокСопоставленияДляЗаполненияХарактеристик(ТекстЗапроса);
		
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Владелец", ЭлементСопоставления.Владелец);

	Пока Выборка.Следующий() Цикл
		
		ПараметрыОтбора.Вставить("Идентификатор", Выборка.Идентификатор);
		НайденныеСтроки = Объект.Сопоставление.НайтиСтроки(ПараметрыОтбора);
		Для Каждого Строка Из НайденныеСтроки Цикл
			Строка.Характеристика = ЭлементСопоставления.Характеристика;
			ОбработатьЗаписьСопоставления(Строка);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВопросОПерезаполнениеСловаря(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ.Значение = КодВозвратаДиалога.ОК Тогда
		
		УстановитьРежимПерезаполненияСловаря(Истина);
		
		ПараметрыПроцедуры = Новый Структура;
		
		ОперацияПерезаполненияСловаря = 
			СопоставлениеНоменклатурыКонтрагентовСлужебныйВызовСервера.ЗапуститьПерезаполнениеСловаряСопоставленияНоменклатурыВФоне();
		
		ОбработкаЗавершенияПерезаполненияСловаря = Новый ОписаниеОповещения("ЗавершитьПерезаполнениеСловаря", ЭтотОбъект);
		
		ПараметрыОжидания                                 = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьПрогрессВыполнения      = Ложь;
		ПараметрыОжидания.ВыводитьОкноОжидания            = Ложь;
		ПараметрыОжидания.ОповещениеПользователя.Показать = Ложь;
		ПараметрыОжидания.ВыводитьСообщения               = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ОперацияПерезаполненияСловаря, ОбработкаЗавершенияПерезаполненияСловаря, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПерезаполнениеСловаря(Результат, ДополнительныеПараметры) Экспорт
	
	УстановитьРежимПерезаполненияСловаря(Ложь);
	
	Если Результат <> Неопределено Тогда
		РезультатВыполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатВыполнения.Свойство("ВыполненоСОшибками") Тогда
			Если РезультатВыполнения.ВыполненоСОшибками Тогда
				ТекстСообщения = НСтр("ru = 'При перезаполнении словаря сопоставления произошли ошибки. Подробнее см. в журнале регистрации.'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Иначе
				ТекстСообщения = НСтр("ru = 'Перезаполнение словаря выполнено успешно.'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРежимПерезаполненияСловаря(ОжиданиеВыполнения)
	
	Элементы.ГруппаОтображенияСопоставления.ТекущаяСтраница = ?(ОжиданиеВыполнения, Элементы.СтраницаОжиданияПерезаполненияСловаря, Элементы.ГруппаСопоставление);
	Элементы.КомандаСохранитьИЗакрыть.Доступность           = НЕ ОжиданиеВыполнения;
	Элементы.КомандаСохранить.Доступность                   = НЕ ОжиданиеВыполнения;
	Элементы.КомандаНайти.Доступность                       = НЕ ОжиданиеВыполнения;
	Элементы.КомандаОтменитьПоиск.Доступность               = НЕ ОжиданиеВыполнения;
	Элементы.КомандаСоздатьНоменклатуру.Доступность         = НЕ ОжиданиеВыполнения;
		
КонецПроцедуры

&НаСервере
Функция НоваяТаблицаНоменклатурыКонтрагентовНаСохранение()
	
	Строка1000 = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(1000));
	
	ТаблицаНоменклатуры = Новый ТаблицаЗначений;
	ТаблицаНоменклатуры.Колонки.Добавить("Владелец", Метаданные.ОпределяемыеТипы.ВладелецНоменклатурыБЭД.Тип);
	ТаблицаНоменклатуры.Колонки.Добавить("Идентификатор",  Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(300)));
	ТаблицаНоменклатуры.Колонки.Добавить("НаименованиеНоменклатуры", Строка1000);
	ТаблицаНоменклатуры.Колонки.Добавить("НаименованиеХарактеристики", Строка1000);
	ТаблицаНоменклатуры.Колонки.Добавить("НаименованиеУпаковки", Строка1000);
	
	Возврат ТаблицаНоменклатуры;
	
КонецФункции

&НаСервере
Процедура СохранитьСопоставленнуюНоменклатуруКонтрагентов(Отказ)
		
	ТаблицаНоменклатурыКонтрагентовНаСохранение = НоваяТаблицаНоменклатурыКонтрагентовНаСохранение();
	ВариантУказанияНоменклатураКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.ВариантУказанияНоменклатураКонтрагента();
	
	ОтборЭлементовДляСохранения = Новый Структура;
	ОтборЭлементовДляСохранения.Вставить("Сопоставлено", Истина);
	ОтборЭлементовДляСохранения.Вставить("Сохранено"   , Ложь);
	ОтборЭлементовДляСохранения.Вставить("ВариантУказанияНоменклатуры", ВариантУказанияНоменклатураКонтрагента);
	ЭлементыДляСохранения = Объект.Сопоставление.НайтиСтроки(ОтборЭлементовДляСохранения);
	
	ДополнительныеПараметры = СопоставлениеНоменклатурыКонтрагентов.НовыеДополнительныеПараметрыПриЗаписиНоменклатурыКонтрагентов();
	ДополнительныеПараметры.ТребуетсяПоискСсылки = Ложь;
	ДополнительныеПараметры.ТребуетсяПоискЕдиницыИзмеренияПоОКЕИ = Ложь;
	
	ТекстОшибкиПользователю = "";
	
	Для Каждого Элемент Из ЭлементыДляСохранения Цикл
		
		ТекстОшибки = "";
		ЗаполнитьУпаковкуПоУмолчанию(Элемент);
		
		Если Не ЗначениеЗаполнено(Элемент.НоменклатураКонтрагента) Тогда
			НоваяСтрока = ТаблицаНоменклатурыКонтрагентовНаСохранение.Добавить();
			НоваяСтрока.Владелец                   = Элемент.Владелец;
			НоваяСтрока.Идентификатор              = Элемент.Идентификатор;
			НоваяСтрока.НаименованиеНоменклатуры   = Элемент.Наименование;
			НоваяСтрока.НаименованиеХарактеристики = Элемент.НаименованиеХарактеристики;
			НоваяСтрока.НаименованиеУпаковки       = Элемент.НаименованиеУпаковки;
		Иначе
			
			ЭлементНоменклатурыКонтрагента = НоменклатураКонтрагентаПоЗаписиСопоставления(Элемент);
			ЭлементНоменклатураИБ          = НоменклатураИБПоЗаписиСопоставления(Элемент);
			
			СопоставлениеНоменклатурыКонтрагентов.СоздатьОбновитьНоменклатуруКонтрагента(
				ЭлементНоменклатурыКонтрагента, ЭлементНоменклатураИБ, Отказ, ТекстОшибки, ДополнительныеПараметры);
			Если Не ПустаяСтрока(ТекстОшибки) Тогда
				ТекстОшибкиПользователю = ?(ПустаяСтрока(ТекстОшибкиПользователю), "", ТекстОшибкиПользователю + Символы.ПС) + ТекстОшибки;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Отказ 
		Или Не ЗначениеЗаполнено(ТаблицаНоменклатурыКонтрагентовНаСохранение) Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = ВыборкаСсылокНоменклатурыКонтрагентовПоТаблице(ТаблицаНоменклатурыКонтрагентовНаСохранение);
		
	Для Каждого Элемент Из ЭлементыДляСохранения Цикл

		Если ЗначениеЗаполнено(Элемент.НоменклатураКонтрагента) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементНоменклатурыКонтрагента = НоменклатураКонтрагентаПоЗаписиСопоставления(Элемент);
		ЭлементНоменклатураИБ          = НоменклатураИБПоЗаписиСопоставления(Элемент);

		ПараметрыПоиска = Новый Структура;
		ПараметрыПоиска.Вставить("Владелец"     , Элемент.Владелец);
		ПараметрыПоиска.Вставить("Идентификатор", Элемент.Идентификатор);
		Если Выборка.НайтиСледующий(ПараметрыПоиска) Тогда
			ЭлементНоменклатурыКонтрагента.НоменклатураКонтрагента = Выборка.НоменклатураКонтрагента;
		КонецЕсли;
		Выборка.Сбросить();
		
		ТекстОшибки = "";
		СопоставлениеНоменклатурыКонтрагентов.СоздатьОбновитьНоменклатуруКонтрагента(
			ЭлементНоменклатурыКонтрагента, ЭлементНоменклатураИБ, Отказ, ТекстОшибки, ДополнительныеПараметры);
			
		Элемент.НоменклатураКонтрагента = ЭлементНоменклатурыКонтрагента.НоменклатураКонтрагента;
		
		Если Не ПустаяСтрока(ТекстОшибки) Тогда
			ТекстОшибкиПользователю = ?(ПустаяСтрока(ТекстОшибкиПользователю), "", ТекстОшибкиПользователю + Символы.ПС) + ТекстОшибки;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Функция ВыборкаСсылокНоменклатурыКонтрагентовПоТаблице(ТаблицаНоменклатурыКонтрагентовНаСохранение)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатурыКонтрагентовНаСохранение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Владелец КАК Владелец,
	|	ТаблицаНоменклатуры.Идентификатор КАК Идентификатор,
	|	ТаблицаНоменклатуры.НаименованиеНоменклатуры КАК НаименованиеНоменклатуры,
	|	ТаблицаНоменклатуры.НаименованиеХарактеристики КАК НаименованиеХарактеристики,
	|	ТаблицаНоменклатуры.НаименованиеУпаковки КАК НаименованиеУпаковки
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НоменклатураКонтрагентов.Ссылка КАК НоменклатураКонтрагента,
	|	ТаблицаНоменклатуры.Владелец КАК Владелец,
	|	ТаблицаНоменклатуры.Идентификатор КАК Идентификатор,
	|	1 КАК Приоритет
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|		ПО ТаблицаНоменклатуры.Владелец = НоменклатураКонтрагентов.ВладелецНоменклатуры
	|		И ТаблицаНоменклатуры.Идентификатор = НоменклатураКонтрагентов.Идентификатор
	|ГДЕ
	|	НЕ НоменклатураКонтрагентов.ПометкаУдаления
	|	И НЕ НоменклатураКонтрагентов.Недействителен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НоменклатураКонтрагентов.Ссылка КАК НоменклатураКонтрагента,
	|	ТаблицаНоменклатуры.Владелец КАК Владелец,
	|	ТаблицаНоменклатуры.Идентификатор КАК Идентификатор,
	|	2
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|		ПО ТаблицаНоменклатуры.НаименованиеНоменклатуры = НоменклатураКонтрагентов.НаименованиеНоменклатуры
	|		И ТаблицаНоменклатуры.НаименованиеХарактеристики = НоменклатураКонтрагентов.НаименованиеХарактеристики
	|		И ТаблицаНоменклатуры.НаименованиеУпаковки = НоменклатураКонтрагентов.НаименованиеУпаковки
	|		И ТаблицаНоменклатуры.Владелец = НоменклатураКонтрагентов.ВладелецНоменклатуры
	|ГДЕ
	|	НЕ НоменклатураКонтрагентов.ПометкаУдаления
	|	И НЕ НоменклатураКонтрагентов.Недействителен
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выбрать();
	
КонецФункции

#КонецОбласти

#Область СозданиеНоменклатуры

&НаСервереБезКонтекста
Функция ЕстьПравоДобавленияНоменклатуры()
	
	ЕстьПраво = Истина;
	
	Для каждого ТипНоменклатуры Из Метаданные.ОпределяемыеТипы.НоменклатураБЭД.Тип.Типы() Цикл
		
		МетаданныеТипа = Метаданные.НайтиПоТипу(ТипНоменклатуры);
		Если МетаданныеТипа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			ЕстьПравоДобавления = ПравоДоступа("Добавление", МетаданныеТипа);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ЕстьПраво = ЕстьПраво И ЕстьПравоДобавления;
		
	КонецЦикла;
	
	Возврат ЕстьПраво;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьВопросОСозданииНоменклатуры(Знач НаборЭлементовСопоставления)
	
	Если ТипЗнч(НаборЭлементовСопоставления) <> Тип("Массив") Тогда
		НаборЭлементовСопоставления = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НаборЭлементовСопоставления);
	КонецЕсли;
	
	КоличествоСтрок = НаборЭлементовСопоставления.Количество();
	
	Если КоличествоСтрок = 0 Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Для создания номенклатуры необходимо выбрать хотя бы одну строку.'"));
		
	ИначеЕсли КоличествоСтрок = 1 Тогда
		
		СоздатьНоменклатуруИнформационнойБазы(НаборЭлементовСопоставления);
		
	Иначе
		
		ТекстВопроса = НСтр("ru = 'Создать номенклатуру для выбранных строк?'");
		
		ДополнительныеПараметры = Новый Структура("НаборЭлементовСопоставления", НаборЭлементовСопоставления);
		
		ОбработкаОтвета = Новый ОписаниеОповещения("ОбработатьВопросОСозданииНоменклатуры", ЭтотОбъект,
			ДополнительныеПараметры);
		
		ПоказатьВопрос(ОбработкаОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВопросОСозданииНоменклатуры(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьНоменклатуруИнформационнойБазы(ДополнительныеПараметры.НаборЭлементовСопоставления);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНоменклатуруИнформационнойБазы(Знач НаборЭлементовСопоставления)
	
	// Пробуем создать номенклатуру автоматически.
	
	ПараметрыОбработки  = Новый Структура("НаборЭлементовСопоставления", НаборЭлементовСопоставления);
	ОбработкаЗавершения = Новый ОписаниеОповещения("ЗакончитьСозданиеНоменклатурыИнформационнойБазыАвтоматически", 
		ЭтотОбъект, ПараметрыОбработки);
	
	НачатьСозданиеНоменклатурыИнформационнойБазыАвтоматически(НаборЭлементовСопоставления, ОбработкаЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьСозданиеНоменклатурыИнформационнойБазыАвтоматически(Знач НаборЭлементовСопоставления, ОбработкаЗавершения)
	
	НаборНоменклатурыКонтрагентов = Новый Массив;
	Для Каждого ЭлементСопоставления Из НаборЭлементовСопоставления Цикл
		НаборНоменклатурыКонтрагентов.Добавить(ЭлементСопоставления.НоменклатураКонтрагента);
	КонецЦикла;
	
	Контекст = СопоставлениеНоменклатурыКонтрагентовСлужебныйКлиент.НовыйКонтекстСозданияНоменклатурыПоДаннымКонтрагента();
	Контекст.ОграничениеТипаНоменклатуры = Элементы.СопоставлениеНоменклатура.ОграничениеТипа;
	Контекст.ДополнительныеПараметрыПоиска = ДополнительныеПараметрыПоиска;
	
	СопоставлениеНоменклатурыКонтрагентовСлужебныйКлиент.НачатьСозданиеНоменклатурыПоДаннымКонтрагента(НаборНоменклатурыКонтрагентов, 
		ОбработкаЗавершения, Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьСозданиеНоменклатурыИнформационнойБазыАвтоматически(Знач СозданныеЭлементы, Знач ДополнительныеПараметры) Экспорт
	
	НаборЭлементовСопоставления = ДополнительныеПараметры.НаборЭлементовСопоставления;

	Если ЗначениеЗаполнено(СозданныеЭлементы) Тогда
		ОбработатьЭлементыСопоставленияНоменклатуры(СозданныеЭлементы);
		
		ОжидатьЗавершениеПоискаВариантов(Ложь);

	КонецЕсли;
	
	// Если создать номенклатуру автоматически не получилось
	// и выбрана только одна строка,
	// то предлагаем создать ее вручную.
	
	ВсегоЭлементов = НаборЭлементовСопоставления.Количество();
	СозданоЭлементов = СозданныеЭлементы.Количество();
	
	Если СозданоЭлементов = 0 И ВсегоЭлементов = 1 Тогда
		
		НачатьСозданиеНоменклатурыИнформационнойБазыВручную(НаборЭлементовСопоставления);
		
	ИначеЕсли ВсегоЭлементов <> СозданоЭлементов Тогда
		
		ТекстСообщения = НСтр("ru = 'Данных контрагента недостаточно для группового создания номенклатуры.'") + Символы.ПС
			+ НСтр("ru = 'Выберите одну строку и повторите попытку.'");
		ПоказатьПредупреждение(, ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьСозданиеНоменклатурыИнформационнойБазыВручную(Знач НаборЭлементовСопоставления)
	
	Для Каждого ЭлементСопоставления Из НаборЭлементовСопоставления Цикл
		
		ПараметрыФормы = Новый Структура;
				
		ОбщегоНазначенияБЭДКлиентСервер.УстановитьСвойствоСтруктуры(
			ПараметрыФормы, "ДополнительныеПараметры.СопоставлениеНоменклатурыКонтрагентов.НоменклатураКонтрагента", ЭлементСопоставления.НоменклатураКонтрагента);
			
		ПараметрыФормы.Вставить("ОграничениеТипаНоменклатуры", Элементы.СопоставлениеНоменклатура.ОграничениеТипа);
		
		// Передадим через ОписаниеОповещения элемент сопоставления для анализа в обработчике события СопоставлениеОбработкаЗаписиНового.
		ОбработкаЗакрытия = Новый ОписаниеОповещения("ПослеЗакрытияФормыСозданияНоменклатурыВручную", ЭтотОбъект, ЭлементСопоставления);
		СопоставлениеНоменклатурыКонтрагентовКлиентПереопределяемый.ОткрытьФормуНоменклатуры(
			ПараметрыФормы, Элементы.Сопоставление, ЭлементСопоставления, ОбработкаЗакрытия);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьСозданиеНоменклатурыИнформационнойБазыВручную(Знач ЭлементСопоставления, Знач НоваяНоменклатура)
	
	НоменклатураИБ = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы(НоваяНоменклатура);
	ЭлементСопоставления.НоменклатураИБ = НоменклатураИБ;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыСозданияНоменклатурыВручную(Знач Результат, Знач ЭлементСопоставления) Экспорт
	
	Если ЭлементСопоставления.НоменклатураИБ.Номенклатура = ПустаяНоменклатураБЭД Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьЭлементыСопоставленияНоменклатуры(ЭлементСопоставления);
	
	ОжидатьЗавершениеПоискаВариантов(Ложь);

КонецПроцедуры

#КонецОбласти

#Область РаботаССервисом

&НаКлиенте
Процедура ПоказатьПодборИзСервиса(Знач ЭлементСопоставления) 
	
	Если НЕ ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЭлектронноеВзаимодействие.РаботаСНоменклатурой") Тогда
		Возврат;
	КонецЕсли;
	
	МодульРаботаСНоменклатуройКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСНоменклатуройКлиент");
	
	ПараметрыФормыЗагрузки = МодульРаботаСНоменклатуройКлиент.ПараметрыФормыЗагрузкиНоменклатуры();
	ПараметрыФормыЗагрузки.СтрокаПоиска = ЭлементСопоставления.НоменклатураКонтрагента.Наименование;
	ПараметрыФормыЗагрузки.РежимВыбораНоменклатуры = Истина;
	ПараметрыФормыЗагрузки.СоздаватьНоменклатуруПриВыборе = Не ИспользоватьСервисИнтерактивно;
	ПараметрыФормыЗагрузки.ПодтверждатьСозданиеНоменклатуры = Истина;
	ПараметрыФормыЗагрузки.РежимПривязкиНоменклатуры = ИспользоватьСервисИнтерактивно;
	
	ПараметрыОбработки = Новый Структура("ЭлементСопоставления", ЭлементСопоставления);
	ОбработкаЗавершенияПодбора = Новый ОписаниеОповещения("ОбработатьПодборИзСервиса", ЭтотОбъект, ПараметрыОбработки);
	
	Если ИспользоватьСервисИнтерактивно Тогда
		ОбработкаЗавершенияПодбора = Новый ОписаниеОповещения("ОбработатьПодборИзСервисаИнтерактивно", 
			ЭтотОбъект, ПараметрыОбработки);
	КонецЕсли;
	
	МодульРаботаСНоменклатуройКлиент.ОткрытьФормуЗагрузкиНоменклатуры(
		ПараметрыФормыЗагрузки, ЭтотОбъект, ОбработкаЗавершенияПодбора, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПодборИзСервиса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Результат.ВыбранныеОбъекты) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСопоставлениеДаннымиИзСервиса(Результат.ВыбранныеОбъекты[0], ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПодборИзСервисаИнтерактивно(Результат, ДополнительныеПараметры) Экспорт
	
	ВыбранныеОбъекты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ВыбранныеОбъекты");
	Если ТипЗнч(ВыбранныеОбъекты) <> Тип("Массив") Или ВыбранныеОбъекты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЭлектронноеВзаимодействие.РаботаСНоменклатурой") Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаЗавершения = Новый ОписаниеОповещения("ОбработатьЗагрузкуНоменклатуруИзСервиса", ЭтотОбъект, 
		ДополнительныеПараметры);
	МодульРаботаСНоменклатурой = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСНоменклатуройКлиентПереопределяемый");
	МодульРаботаСНоменклатурой.СоздатьНоменклатуруИнтерактивно(ВыбранныеОбъекты[0], ОбработкаЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗагрузкуНоменклатуруИзСервиса(Знач НоменклатураСсылка, ДополнительныеПараметры) Экспорт
	
	Если НоменклатураСсылка = Неопределено Тогда
		ДополнительныеПараметры.Свойство("НоменклатураСсылка", НоменклатураСсылка);
	КонецЕсли;
	
	ЭлементСопоставления = ДополнительныеПараметры.ЭлементСопоставления;
	ЭлементСопоставления.НоменклатураИБ.Номенклатура = НоменклатураСсылка;
	ОбработатьЭлементыСопоставленияНоменклатуры(ЭлементСопоставления);
	ОжидатьЗавершениеПоискаВариантов(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗагрузкуНоменклатуруИзСервиса(Знач ЭлементСопоставления) 
	
	Если НЕ ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЭлектронноеВзаимодействие.РаботаСНоменклатурой") Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторНоменклатурыСервиса = ЭлементСопоставления.НоменклатураКонтрагента.ИдентификаторНоменклатурыСервиса;
	ИдентификаторХарактеристикиСервиса = ЭлементСопоставления.НоменклатураКонтрагента.ИдентификаторХарактеристикиСервиса;
	Если НЕ ЗначениеЗаполнено(ИдентификаторНоменклатурыСервиса) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСервиса = Новый Структура;
	ИдентификаторСервиса.Вставить("ИдентификаторНоменклатуры", ИдентификаторНоменклатурыСервиса);
	ИдентификаторСервиса.Вставить("ИдентификаторХарактеристики", ИдентификаторХарактеристикиСервиса);
	НаборИдентификаторовСервиса = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторСервиса);
	
	Если ИспользоватьСервисИнтерактивно Тогда
		ПараметрыОбработки = Новый Структура("ЭлементСопоставления", ЭлементСопоставления);
		ОбработкаЗавершенияПодбора = Новый ОписаниеОповещения("ОбработатьЗагрузкуНоменклатуруИзСервиса",
			ЭтотОбъект, ПараметрыОбработки);
		МодульРаботаСНоменклатурой = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСНоменклатуройКлиентПереопределяемый");
		МодульРаботаСНоменклатурой.СоздатьНоменклатуруИнтерактивно(ИдентификаторСервиса, ОбработкаЗавершенияПодбора);
		Возврат;
	КонецЕсли;
	
	Попытка
		
		МодульРаботаСНоменклатуройКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСНоменклатуройКлиент");
		
		ПараметрыОбработки = Новый Структура("ЭлементСопоставления", ЭлементСопоставления);
		ОбработкаЗавершенияЗагрузки = Новый ОписаниеОповещения("ЗакончитьЗагрузкуНоменклатурыИзСервиса", ЭтотОбъект, ПараметрыОбработки);
		
		МодульРаботаСНоменклатуройКлиент.ЗагрузитьНоменклатуруИХарактеристики(
			ОбработкаЗавершенияЗагрузки, НаборИдентификаторовСервиса, ЭтотОбъект);
		
	Исключение
		
		ТекстСообщения = НСтр("ru = 'Не удалось загрузить номенклатуру из сервиса 1С:Номенклатура по причине:'")
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьЗагрузкуНоменклатурыИзСервиса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Результат.НовыеЭлементы) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСопоставлениеДаннымиИзСервиса(Результат.НовыеЭлементы[0], ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСопоставлениеДаннымиИзСервиса(ВыбранныйЭлемент, ДополнительныеПараметры)
	
	Если ЗначениеЗаполнено(ВыбранныйЭлемент.Номенклатура) Тогда
		
		ЭлементСопоставления = ДополнительныеПараметры.ЭлементСопоставления;
		ЭлементСопоставления.НоменклатураИБ.Номенклатура = ВыбранныйЭлемент.Номенклатура;
		Если ЗначениеЗаполнено(ВыбранныйЭлемент.Характеристика) Тогда
			ЭлементСопоставления.НоменклатураИБ.Характеристика = ВыбранныйЭлемент.Характеристика;
		КонецЕсли;
		
		ОбработатьЭлементыСопоставленияНоменклатуры(ЭлементСопоставления);
		
		ОжидатьЗавершениеПоискаВариантов(Ложь);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботаСНоменклатуройПодсказкаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТребуетсяПересчетВариантов = Ложь;
	РаботаСНоменклатуройПодсказкаЗавершениеНаСервере(ТребуетсяПересчетВариантов);
		
	Если ТребуетсяПересчетВариантов Тогда
		
		ТекстВопроса = НСтр("ru = 'Подключен сервис 1С:Номенклатура. Выполнить поиск вариантов сопоставления?'");
		
		ВывестиВопросПовторногоПоискаВариантов(ТекстВопроса);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура РаботаСНоменклатуройПодсказкаЗавершениеНаСервере(ТребуетсяПересчетВариантов)
	
	ТекущееЗначениеИспользованияСервиса = ИспользоватьСервис;
	ОпределитьИспользованиеСервисаНоменклатуры();
	УстановитьСвойстваЭлементовНоменклатурыСервиса();
	
	ТребуетсяПересчетВариантов = ТекущееЗначениеИспользованияСервиса <> ИспользоватьСервис;
	
КонецПроцедуры

#КонецОбласти

#Область ПереопределениеФормы

&НаКлиентеНаСервереБезКонтекста
Функция КонтекстФормы(ЭтотОбъект, Элемент = Неопределено)
	
	Контекст = Новый Структура;
	Контекст.Вставить("Форма", ЭтотОбъект);
	Контекст.Вставить("Назначение", "СопоставлениеНоменклатуры");
	Контекст.Вставить("Префикс", "_");
	Контекст.Вставить("СтандартныйЭлемент", Неопределено);
	
	Если Элемент = ЭтотОбъект.Элементы.СопоставлениеХарактеристика Тогда
		Контекст.Вставить("СтандартныйЭлемент", "Характеристика");
	ИначеЕсли Элемент = ЭтотОбъект.Элементы.СопоставлениеУпаковка Тогда
		Контекст.Вставить("СтандартныйЭлемент", "Упаковка");
	КонецЕсли;
	
	Возврат Новый ФиксированнаяСтруктура(Контекст);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ЭлементПриИзменении(Элемент)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Элемент);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ЭлементФормыПодсистемыПриИзменении(Контекст, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Элемент);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ЭлементФормыПодсистемыНачалоВыбора(
		Контекст, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Элемент);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ЭлементФормыПодсистемыНачалоВыбораИзСписка(
		Контекст, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементОчистка(Элемент, СтандартнаяОбработка)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Элемент);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ЭлементФормыПодсистемыОчистка(
		Контекст, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементСоздание(Элемент, СтандартнаяОбработка)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Элемент);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ЭлементФормыПодсистемыСоздание(
		Контекст, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Элемент);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ЭлементФормыПодсистемыОбработкаВыбора(
		Контекст, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Элемент);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ЭлементФормыПодсистемыИзменениеТекстаРедактирования(
		Контекст, Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Элемент);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ЭлементФормыПодсистемыАвтоПодбор(
		Контекст, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Элемент);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ЭлементФормыПодсистемыОкончаниеВводаТекста(
		Контекст, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементНажатие(Элемент)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Элемент);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ЭлементФормыПодсистемыНажатие(
		Контекст, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Элемент);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ЭлементФормыПодсистемыОбработкаНавигационнойСсылки(
		Контекст, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаДействие(Команда)
	
	Контекст = КонтекстФормы(ЭтотОбъект, Команда);
	
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.КомандаФормыПодсистемыДействие(Контекст, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСоСловарем

&НаСервереБезКонтекста
Процедура ПроверитьВключитьФункциональностьСловаря()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИмяМетода = "СопоставлениеНоменклатурыКонтрагентовСлужебный.ОбновитьСловарьИСтатистикуПоИзменениям";
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне();
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Актуализация словаря сопоставления номенклатуры БЭД'");
	
	ИспользоватьСловарь = ПолучитьФункциональнуюОпцию("ИспользоватьСловарьСопоставленияНоменклатурыБЭД");
	Если Не ИспользоватьСловарь Тогда
		ИмяМетода = "СопоставлениеНоменклатурыКонтрагентовСлужебный.ПерезаполнениеСловаряСопоставленияНоменклатуры";
		СопоставлениеНоменклатурыКонтрагентовСлужебный.УстановитьИспользованиеСловаряСопоставления(Истина);
	КонецЕсли;
	
	ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяМетода);
	
КонецПроцедуры

#КонецОбласти

#Область ПравилаПоискаПоЧастиНаименования

&НаКлиенте
Функция ТекущееДействиеСПравиломПоиска()
	
	ТекущиеДанные = Элементы.Сопоставление.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат Неопределено;
	ИначеЕсли ПустаяСтрока(ТекущиеДанные.ХешПоисковойСтроки) Тогда
		Возврат "ДобавитьПравилоПоиска";
	Иначе
		Возврат "ИзменитьПравилоПоиска";
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьДействиеСПравиломПоиска(СписокДействий, ЭлементСопоставления)
	
	ТекущееДействие = ТекущееДействиеСПравиломПоиска();
	Если ТекущееДействие = Неопределено Тогда
		Возврат;
	ИначеЕсли ТекущееДействие = "ИзменитьПравилоПоиска" Тогда
		Представление = НСтр("ru = 'Изменить правило поиска по части наименования'");
		Картинка      = БиблиотекаКартинок["ПравилоПоискаИзменитьБЭД"];
		НовоеДействие = Новый Структура("Действие, ЭлементСопоставления", ТекущееДействие, ЭлементСопоставления);
		СписокДействий.Добавить(НовоеДействие, Представление,, Картинка);
	КонецЕсли;
	
	Представление = НСтр("ru = 'Добавить правило поиска по части наименования'");
	Картинка      = БиблиотекаКартинок["ПравилоПоискаДобавитьБЭД"];
	НовоеДействие = Новый Структура("Действие, ЭлементСопоставления", "ДобавитьПравилоПоиска", ЭлементСопоставления);
	СписокДействий.Добавить(НовоеДействие, Представление,, Картинка);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДействийСПравиломПоискаВКонтекстомМеню()
	
	ТекущееДействие = ТекущееДействиеСПравиломПоиска();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"СопоставлениеКонтекстноеМенюДобавитьПравилоПоискаПоЧастиНаименования",
		"Видимость",
		ТекущееДействие = "ДобавитьПравилоПоиска");
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"СопоставлениеКонтекстноеМенюИзменитьПравилоПоискаПоЧастиНаименования",
		"Видимость",
		ТекущееДействие = "ИзменитьПравилоПоиска");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПравилаПоиска(ДействиеСПравиломПоиска)
	
	ТекущиеДанные = Элементы.Сопоставление.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытияФормы = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(НовыеДанныеПравилаПоиска);
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытияФормы, ТекущиеДанные);
	ПараметрыОткрытияФормы.Вставить("ОбязательноеЗаполнениеХарактеристики", 
		ТекущиеДанные.ОбязательноеЗаполнениеХарактеристики);
	
	Если ДействиеСПравиломПоиска = "ДобавитьПравилоПоиска" Тогда
		ПараметрыОткрытияФормы.ПоисковаяСтрока = ТекущиеДанные.Наименование;
	Иначе
		ЗначениеКлюча = Новый Структура("Владелец, ХешПоисковойСтроки");
		ЗаполнитьЗначенияСвойств(ЗначениеКлюча, ТекущиеДанные);
		КлючЗаписи = Новый("РегистрСведенийКлючЗаписи.ПравилаПоискаПоЧастиНаименованияБЭД", 
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЗначениеКлюча));
		ПараметрыОткрытияФормы.Вставить("Ключ", КлючЗаписи);
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.ПравилаПоискаПоЧастиНаименованияБЭД.Форма.ПравилоПоискаПоЧастиНаименования", 
		ПараметрыОткрытияФормы, Элементы.Сопоставление, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоДействиеСПравиломПоиска(Знач ОписаниеДействия)
	
	Возврат ТипЗнч(ОписаниеДействия) = Тип("Структура") И ОписаниеДействия.Свойство("ХешПоисковойСтроки");
	
КонецФункции

&НаСервере
Процедура ОбработатьДействиеСПравиломПоиска(Знач Результат)
	
	ИсходныйКлюч = Результат.ИсходныйКлючЗаписи;
	
	Для каждого СтрокаСопоставления Из Объект.Сопоставление Цикл
		
		Если Не ПустаяСтрока(СтрокаСопоставления.ХешПоисковойСтроки)
			И СтрокаСопоставления.ХешПоисковойСтроки = ИсходныйКлюч.ХешПоисковойСтроки
			И СтрокаСопоставления.Владелец = ИсходныйКлюч.Владелец
			И (ИсходныйКлюч.ХешПоисковойСтроки <> Результат.ХешПоисковойСтроки
			Или ИсходныйКлюч.Владелец <> Результат.Владелец) Тогда
			СтрокаСопоставления.Номенклатура = Неопределено;
			СтрокаСопоставления.Характеристика = Неопределено;
			СтрокаСопоставления.Упаковка = Неопределено;
			СтрокаСопоставления.ХешПоисковойСтроки = "";
			СтрокаСопоставления.Сопоставлено = Ложь;
			СтрокаСопоставления.ПодобраноПоПравилуПоиска = Ложь;
		КонецЕсли;
		
		Если СтрокаСопоставления.Владелец = Результат.Владелец
			И СтрНайти(СтрокаСопоставления.Наименование, Результат.ПоисковаяСтрока) > 0 Тогда
			СтрокаСопоставления.Номенклатура = Результат.Номенклатура;
			СтрокаСопоставления.Характеристика = Результат.Характеристика;
			СтрокаСопоставления.Упаковка = Результат.Упаковка;
			СтрокаСопоставления.ХешПоисковойСтроки = Результат.ХешПоисковойСтроки;
			СтрокаСопоставления.Сопоставлено = ЗначениеЗаполнено(Результат.Номенклатура);
			СтрокаСопоставления.ПодобраноПоПравилуПоиска = Не ПустаяСтрока(Результат.ХешПоисковойСтроки);
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьСвойстваЭлементаОтбораПоСопоставлению();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти