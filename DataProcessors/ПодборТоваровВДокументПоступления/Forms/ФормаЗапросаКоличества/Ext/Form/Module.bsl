
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Номенклатура = Параметры.Номенклатура;
	Количество = Параметры.Количество;
	ЕдиницаИзмерения = Параметры.ЕдиницаИзмерения;
	Цена = Параметры.Цена;
	Валюта = Параметры.Валюта;
	
	Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		ЕдиницаИзмерения = НоменклатураСервер.ОсновнаяЕдиницаИзмерения(Номенклатура, ?(Параметры.ПодборВПоступление, НоменклатураКлиентСервер.ВидЕдиницы_ПотребительскаяУпаковка(), ""));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Количество) Тогда
		Количество = 1;
	КонецЕсли;
	
	Элементы.Цена.ТолькоПросмотр = Не Параметры.РедактироватьЦену;
	Элементы.Цена.ПропускатьПриВводе = Не Параметры.РедактироватьЦену;
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	КонецЕсли;
	
	Если Параметры.СкрытьЦену Тогда
		Элементы.Цена.Видимость = Ложь;
		Элементы.Валюта.Видимость = Ложь;
		Заголовок = НСтр("ru = 'Ввод количества'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Цена) Тогда
		ЦенаБазовойЕдиницы = Цена / НоменклатураСервер.КоэффициентЕдиницыИзмерения(Номенклатура, ЕдиницаИзмерения);
	КонецЕсли;
	
	Если Не Параметры.СкрытьСерию Тогда
		Действия = ОбработкаТабличнойЧастиКлиентСервер;
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить(Действия.Действие_ЗаполнитьПараметрыУчета(), Параметры.ПараметрыУчетаНоменклатуры);
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТабличнойЧасти(ЭтотОбъект, СтруктураДействий, Неопределено);
		
		СтатусыУказанияСерий = ЗапасыКлиентСерверПовтИсп.СтатусыУказанияСерий();
		Если СтатусУказанияСерий = СтатусыУказанияСерий.СтатусСерииНеУказываются Тогда
			Элементы.СерияНоменклатуры.Видимость = Ложь;
		КонецЕсли;
	Иначе
		Элементы.СерияНоменклатуры.Видимость = Ложь;
	КонецЕсли;
	
	УправляемаяФорма.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
	ТекущаяЕдиницаИзмерения = ЕдиницаИзмерения;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		ВыбранноеЗначение = Новый Структура;
		ВыбранноеЗначение.Вставить("Номенклатура", Номенклатура);
		ВыбранноеЗначение.Вставить("СерияНоменклатуры", СерияНоменклатуры);
		ВыбранноеЗначение.Вставить("Количество", Количество);
		ВыбранноеЗначение.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
		ВыбранноеЗначение.Вставить("Цена", Цена);
		
		ОповеститьОВыборе(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	Если ТекущаяЕдиницаИзмерения <> ЕдиницаИзмерения Тогда
		Если Цена <> 0 Тогда
			ПересчитатьЦенуПриИзмененииЕдиницы();
		КонецЕсли;
		ТекущаяЕдиницаИзмерения = ЕдиницаИзмерения;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	
	ОбработатьИзменениеЦены();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПересчитатьЦенуПриИзмененииЕдиницы()
	
	Цена = ЦенаБазовойЕдиницы * НоменклатураСервер.КоэффициентЕдиницыИзмерения(Номенклатура, ЕдиницаИзмерения);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеЦены()
	
	ЦенаБазовойЕдиницы = Цена / НоменклатураСервер.КоэффициентЕдиницыИзмерения(Номенклатура, ЕдиницаИзмерения);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
