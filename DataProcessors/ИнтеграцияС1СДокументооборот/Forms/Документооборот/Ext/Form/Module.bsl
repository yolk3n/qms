#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВнешнийОбъект = Параметры.ВнешнийОбъект;
	МетаданныеПотребителя = Параметры.ВнешнийОбъект.Метаданные();
	ИмяПотребителя = ?(МетаданныеПотребителя.Синоним <> "",
		МетаданныеПотребителя.Синоним,
		МетаданныеПотребителя.Имя);
	
	// Получим тип и представление создаваемого объекта, если они однозначны.
	Правила = ИнтеграцияС1СДокументооборотВызовСервера.ПодходящиеПравила(ВнешнийОбъект);
	Для Каждого Правило Из Правила Цикл
		Если Не ЗначениеЗаполнено(ТипСоздаваемогоОбъекта) Тогда
			ТипСоздаваемогоОбъекта = Правило.ТипОбъектаДО;
		ИначеЕсли ТипСоздаваемогоОбъекта <> Правило.ТипОбъектаДО Тогда
			ТипСоздаваемогоОбъекта = "";
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Правило Из Правила Цикл
		Если Не ЗначениеЗаполнено(ПредставлениеСоздаваемогоОбъекта) Тогда
			ПредставлениеСоздаваемогоОбъекта = Правило.ПредставлениеОбъектаДО;
		ИначеЕсли ПредставлениеСоздаваемогоОбъекта <> Правило.ПредставлениеОбъектаДО Тогда
			ПредставлениеСоздаваемогоОбъекта = "";
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПредставлениеСоздаваемогоОбъекта) Тогда
		
		Элементы.ДекорацияОбъектЗаголовок.Заголовок = ПредставлениеСоздаваемогоОбъекта + ":";
		
	ИначеЕсли ЗначениеЗаполнено(ТипСоздаваемогоОбъекта) Тогда
		
		Если ТипСоздаваемогоОбъекта = "DMIncomingDocument" Тогда
			Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Входящий документ:'");
			
		ИначеЕсли ТипСоздаваемогоОбъекта = "DMOutgoingDocument" Тогда
			Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Исходящий документ:'");
			
		ИначеЕсли ТипСоздаваемогоОбъекта = "DMInternalDocument" Тогда
			Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Внутренний документ:'");
			
		ИначеЕсли ТипСоздаваемогоОбъекта = "DMCorrespondent" Тогда
			Если ИнтеграцияС1СДокументооборотПовтИсп.ИспользоватьТерминКорреспонденты() Тогда
				Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Корреспондент:'");
			Иначе
				Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Контрагент:'");
			КонецЕсли;
		КонецЕсли;
		
	Иначе // нет подходящих правил
		
		Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Соответствие в 1С:Документообороте'");
		Если ПравоДоступа("Добавление", Метаданные.Справочники.ПравилаИнтеграцииС1СДокументооборотом) Тогда
			Элементы.ДекорацияИнфо.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
				СтрШаблон(
					НСтр("ru = 'Не настроено заполнение документа 1С:Документооборота из ""%1"". <a href = ""%2"">Настроить.</a>'"),
					ИмяПотребителя,
					МетаданныеПотребителя.ПолноеИмя()));
		Иначе
			Элементы.ДекорацияИнфо.Заголовок = СтрШаблон(
				НСтр("ru = 'Не настроено заполнение документа 1С:Документооборота из ""%1"".'"), ИмяПотребителя);
		КонецЕсли;
		
	КонецЕсли;
	
	МаксРазмерФайла =
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.МаксимальныйРазмерПередаваемогоФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПроверитьПодключение();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИнтеграцияС1СДокументооборотом_УспешноеПодключение" И Источник <> ЭтотОбъект Тогда
		ПриПодключении();
	КонецЕсли;
	
	Если Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница <>
			Элементы.СтраницаДокументооборотДоступен Тогда
		Возврат;
	КонецЕсли;
	
	Если (ИмяСобытия = "Запись_ДокументооборотДокумент"
				Или ИмяСобытия = "Запись_ДокументооборотОбъект")
			И Параметр.Свойство("ВнешнийОбъект")
			И Параметр.ВнешнийОбъект = ВнешнийОбъект Тогда
		
		ДокументID = Параметр.ID;
		ДокументТип = Параметр.type;
		Документ = Параметр.name;
		ДополнитьПредставлениеДокумента();
		
		ОбработатьЗаписьДокумента();
		
	ИначеЕсли ИмяСобытия = "Запись_ДокументооборотБизнесПроцесс" Тогда
		
		РаскрытыеЭлементы = Новый СписокЗначений;
		ПолучитьРаскрытыеЭлементы(
			Элементы.ДеревоБизнесПроцессовИЗадач,
			ДеревоБизнесПроцессовИЗадач.ПолучитьЭлементы(),
			РаскрытыеЭлементы);
		РаскрытыеЭлементы.Добавить(Параметр.ID);
		ЗагрузитьДеревоБизнесПроцессовИЗадач();
		УстановитьРазвернутостьЭлементовДерева(
			Элементы.ДеревоБизнесПроцессовИЗадач,
			ДеревоБизнесПроцессовИЗадач,
			РаскрытыеЭлементы);
		УстановитьТекущийЭлементВДереве(
			Элементы.ДеревоБизнесПроцессовИЗадач,
			ДеревоБизнесПроцессовИЗадач,
			Параметр.ID);
		
	ИначеЕсли ИмяСобытия = "Запись_ДокументооборотФайл"
			И ТипЗнч(Параметр) = Тип("Структура")
			И Параметр.Свойство("ВнешнийОбъект")
			И Параметр.ВнешнийОбъект = ВнешнийОбъект Тогда
		
		ОбновитьСписокФайловКлиент();
		
	ИначеЕсли ИмяСобытия = "Запись_ДокументооборотИсходящееПисьмо" Тогда
		
		Если Источник = ЭтотОбъект Тогда
			ПолучитьЗаполнитьДеревоПисем();
			РазвернутьДеревоПисем();
		ИначеЕсли ЗначениеЗаполнено(Параметр) Тогда
			Если Параметр.ПредметID = ДокументID Тогда
				ПолучитьЗаполнитьДеревоПисем();
				РазвернутьДеревоПисем();
			ИначеЕсли ПисьмоЕстьВСписке(Параметр.ПисьмоОснованиеID) Тогда
				ПолучитьЗаполнитьДеревоПисем();
				РазвернутьДеревоПисем();
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Документооборот_ВыбратьЗначениеИзСпискаЗавершение" И Источник = ЭтотОбъект Тогда
		
		Если Параметр = "Документ" Тогда
			ОбработатьВыборСвязанногоОбъекта();
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.Оповестить_ДобавлениеСвязи(
				ДокументID,
				ДокументТип,
				Параметры.ВнешнийОбъект);
			ДополнитьПредставлениеДокумента();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Документооборот_ДобавлениеСвязи" И Параметр.Объект = ВнешнийОбъект Тогда
		
		ПолучитьДанныеДокумента();
		ОбновитьДекорацииДокумента();
		ФайлыЗаполнены = Ложь;
		ДокументыЗаполнены = Ложь;
		ПисьмаЗаполнены = Ложь;
		ЗадачиЗаполнены = Ложь;
		ТрудозатратыЗаполнены = Ложь;
		ПриСменеСтраницыНаКлиенте(Элементы.ГруппаСтраницы.ТекущаяСтраница);
		
	ИначеЕсли ИмяСобытия = "Документооборот_УдалениеСвязи" И Параметр.Объект = ВнешнийОбъект Тогда
		
		Документ = "";
		ДокументID = "";
		ДокументТип = "";
		ФайлыЗаполнены = Ложь;
		ДокументыЗаполнены = Ложь;
		ПисьмаЗаполнены = Ложь;
		ЗадачиЗаполнены = Ложь;
		ТрудозатратыЗаполнены = Ложь;
		ОбновитьДекорацииДокумента();
		ПриСменеСтраницыНаКлиенте(Элементы.ГруппаСтраницы.ТекущаяСтраница);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъектаИС", НавигационнаяСсылкаФорматированнойСтроки);
	
	ОткрытьФорму("Справочник.ПравилаИнтеграцииС1СДокументооборотом.Форма.ФормаЭлемента",
		ПараметрыФормы,
		ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ПриСменеСтраницыНаКлиенте(ТекущаяСтраница);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатие(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("ДекорацияНастройкиАвторизацииНажатиеЗавершение", ЭтотОбъект);
	ИмяФормыПараметров = "Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.АвторизацияВ1СДокументооборот";
	
	ОткрытьФорму(ИмяФормыПараметров,, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатиеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ПриПодключении();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбъектСоздатьНажатие(Элемент)
	
	Если ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ДекорацияОбъектСоздатьНажатиеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ИнтеграцияС1СДокументооборотКлиент.НачатьСозданиеСвязанногоОбъектаДО(
		ВнешнийОбъект,
		ОписаниеОповещения,
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбъектВыбратьНажатие(Элемент)
	
	ДополнительныеПараметры = Новый Структура;
	
	Если ЗначениеЗаполнено(ТипСоздаваемогоОбъекта) Тогда
		
		ДекорацияОбъектВыбратьНажатиеПослеВыбораТипа(ТипСоздаваемогоОбъекта, ДополнительныеПараметры);
		
	Иначе // тип неизвестен, предложим выбор пользователю
		
		ДополнительныеПараметры = Новый Структура;
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ДекорацияОбъектВыбратьНажатиеПослеВыбораТипа", ЭтотОбъект, ДополнительныеПараметры);
			
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Выбрать в 1С:Документообороте'"));
		ПараметрыФормы.Вставить("ЗаголовокКоманды", НСтр("ru = 'Выбрать'"));
		ПараметрыФормы.Вставить("ОбъектИС", ВнешнийОбъект);
			
		ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.ВыборТипаОбъектаДокументооборота",
			ПараметрыФормы,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбъектНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВнешнийОбъект", Параметры.ВнешнийОбъект);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(ДокументТип, ДокументID, Элемент, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбъектОчиститьНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(Документ) Тогда 
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ДекорацияОбъектОчиститьНажатиеЗавершение", ЭтотОбъект);
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Очистить соответствие для
					|%1?'"),Строка(Параметры.ВнешнийОбъект));
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПоказатьВопросДаНет(Оповещение, ТекстВопроса, 
		НСтр("ru='Очистить'"), НСтр("ru='Не очищать'"), КодВозвратаДиалога.Нет);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоБизнесПроцессовИЗадач

&НаКлиенте
Процедура ДеревоБизнесПроцессовИЗадачВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьТекущуюЗадачуИлиПроцесс();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоБизнесПроцессовИЗадачПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьТекущуюЗадачуИлиПроцесс();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьВыполненныеПриИзменении(Элемент)
	
	РаскрытыеЭлементы = Новый СписокЗначений;
	ПолучитьРаскрытыеЭлементы(
		Элементы.ДеревоБизнесПроцессовИЗадач,
		ДеревоБизнесПроцессовИЗадач.ПолучитьЭлементы(),
		РаскрытыеЭлементы);
	ЗагрузитьДеревоБизнесПроцессовИЗадач();
	УстановитьРазвернутостьЭлементовДерева(
		Элементы.ДеревоБизнесПроцессовИЗадач,
		ДеревоБизнесПроцессовИЗадач,
		РаскрытыеЭлементы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокФайлов

&НаКлиенте
Процедура СписокФайловПриАктивизацииСтроки(Элемент)
	
	ОбновитьДоступностьКомандСпискаФайлов();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	Если Элементы.СписокФайлов.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("СписокФайловПередУдалениемЗавершение", ЭтотОбъект);
	
	Если Элементы.СписокФайлов.ВыделенныеСтроки.Количество() = 1 Тогда
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Пометить ""%1"" на удаление?'"),
			Элементы.СписокФайлов.ТекущиеДанные.Наименование);
	Иначе
		ТекстВопроса = НСтр("ru = 'Пометить выделенные элементы на удаление?'");
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПоказатьВопросДаНет(Оповещение, ТекстВопроса);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФайлВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	СоздатьФайлВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания,
	СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка,
	Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Файл") 
		И ПараметрыПеретаскивания.Значение.ЭтоФайл() = Истина Тогда
		
		СоздатьФайлСДискаПеретаскиванием(ПараметрыПеретаскивания.Значение);
		
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		
		Для Каждого ФайлПринятый Из ПараметрыПеретаскивания.Значение Цикл
			Если ТипЗнч(ФайлПринятый) = Тип("Файл") 
				И ФайлПринятый.ЭтоФайл() Тогда
				СоздатьФайлСДискаПеретаскиванием(ФайлПринятый);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьКарточкуФайлаВыполнить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоСвязей

&НаКлиенте
Процедура ДеревоСвязейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.ДеревоСвязей.ТекущиеДанные;
	ОткрытьСвязанныеДанныеСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПриАктивизацииСтроки(Элемент)
	
	Строка = Элемент.ТекущиеДанные;
	
	ДоступностьОткрытия = 
		(Строка <> Неопределено)
		И (Строка.Тип <> "DMRelationType");
	ДоступностьУдаления = ДоступностьОткрытия
		И (Строка.Тип <> "DMFile");
		
	Элементы.ДеревоСвязейОткрыть.Доступность = ДоступностьОткрытия;
	Элементы.ДеревоСвязейОткрытьКонтекст.Доступность = ДоступностьОткрытия;
	Элементы.ДеревоСвязейУдалить.Доступность = ДоступностьУдаления;
	Элементы.ДеревоСвязейУдалитьКонтекст.Доступность = ДоступностьОткрытия;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, 
	Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ДеревоСвязей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или ТекущиеДанные.Тип = "DMRelationType"
		Или ТекущиеДанные.Тип = "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"НачатьУдалениеСвязиЗавершение", ЭтотОбъект);
	
	СтрокаТипаСвязи = ТекущиеДанные.ПолучитьРодителя();
	
	ИсходныйДокумент = Новый Структура("ID, Тип, Представление",
		ДокументID, ДокументТип, Документ);
	СвязанныйДокумент = Новый Структура("ID, Тип, Представление",
		ТекущиеДанные.ID, ТекущиеДанные.Тип, ТекущиеДанные.Заголовок);
	ТипСвязи = Новый Структура("ID, Тип, Представление",
		СтрокаТипаСвязи.ID, СтрокаТипаСвязи.Тип, СтрокаТипаСвязи.Заголовок);
	
	ИнтеграцияС1СДокументооборотКлиент.НачатьУдалениеСвязи(
		ИсходныйДокумент,
		СвязанныйДокумент,
		ТипСвязи,
		ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсторияПереписки

&НаКлиенте
Процедура ДеревоПисемВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = ДеревоПисем.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(
		ТекущаяСтрока.ПисьмоТип,
		ТекущаяСтрока.ПисьмоID,
		ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВизыСогласованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияС1СДокументооборотКлиент.ОткрытьВизуСогласования(ЭтотОбъект,
		Элементы.ВизыСогласования.ТекущиеДанные);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Процессы и задачи

&НаКлиенте
Процедура ОткрытьКарточкуПроцессаЗадачи(Команда)
	
	ОткрытьТекущуюЗадачуИлиПроцесс();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБизнесПроцесс(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.СоздатьБизнесПроцессПоОбъектуДО(
		ДокументID,
		ДокументТип,
		Документ,
		ВнешнийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокБизнесПроцессовИЗадач(Команда)
	
	РаскрытыеЭлементы = Новый СписокЗначений;
	ПолучитьРаскрытыеЭлементы(
		Элементы.ДеревоБизнесПроцессовИЗадач,
		ДеревоБизнесПроцессовИЗадач.ПолучитьЭлементы(),
		РаскрытыеЭлементы);
	ЗагрузитьДеревоБизнесПроцессовИЗадач();
	УстановитьРазвернутостьЭлементовДерева(
		Элементы.ДеревоБизнесПроцессовИЗадач,
		ДеревоБизнесПроцессовИЗадач,
		РаскрытыеЭлементы);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Файлы

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	ОткрытьФайлВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьФайл(Команда)
	
	СоздатьФайлВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НачатьСохранениеВыделенныхФайлов(
		СписокФайлов,
		Элементы.СписокФайлов.ВыделенныеСтроки,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьСписокФайловКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзФайлаНаДиске(Команда)
	
	ТекущиеДанные = Элементы.СписокФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбновитьИзФайлаНаДискеЗавершение", ЭтотОбъект);
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОбновитьИзФайлаНаДиске(
		ТекущиеДанные.ID,
		ТекущиеДанные.Наименование,
		ТекущиеДанные.Расширение,
		МестноеВремя(ТекущиеДанные.ДатаМодификацииУниверсальная),
		УникальныйИдентификатор,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подписать(Команда)
	
	ТекущиеДанные = Элементы.СписокФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПодписейФайла = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ДанныеПодписей(
		ТекущиеДанные.ТаблицаПодписей);
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПодписатьФайл(
		ТекущиеДанные.ID,
		ТекущиеДанные.Наименование,
		ДокументID,
		ТекущиеДанные.Редактируется,
		ТекущиеДанные.Зашифрован,
		ТекущиеДанные.Описание,
		ДанныеПодписейФайла,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭПИзФайла(Команда)
	
	ТекущиеДанные = Элементы.СписокФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПодписейФайла = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ДанныеПодписей(
		ТекущиеДанные.ТаблицаПодписей);
	
	СвойстваФайла = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СвойстваФайла();
	СвойстваФайла.ИдентификаторФайла = ТекущиеДанные.ID;
	СвойстваФайла.ИмяФайла = ТекущиеДанные.Наименование;
	СвойстваФайла.ОписаниеФайла = ТекущиеДанные.Описание;
	СвойстваФайла.Редактируется = ТекущиеДанные.Редактируется;
	СвойстваФайла.Зашифрован = ТекущиеДанные.Зашифрован;
	СвойстваФайла.ДанныеПодписейФайла = ДанныеПодписейФайла;
	СвойстваФайла.УникальныйИдентификатор = УникальныйИдентификатор;
	СвойстваФайла.ВладелецФайла = ДокументID;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НачатьДобавлениеЭПИзФайла(СвойстваФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВместеСЭП(Команда)
	
	ТекущиеДанные = Элементы.СписокФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НачатьСохранениеВместеСЭП(
		ТекущиеДанные.ID,
		ТекущиеДанные.Расширение,
		ТекущиеДанные.Наименование,
		ТекущиеДанные.Размер * 1024,
		ТекущиеДанные.ДатаМодификацииУниверсальная,
		УникальныйИдентификатор);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Связи

// Начинает добавление связанного документа.
&НаКлиенте
Процедура ДобавитьСвязь(Команда)
	
	Отбор = Новый Структура;
	Если ЗначениеЗаполнено(КонтрагентID) Тогда
		Условие = Новый Структура;
		Условие.Вставить("Значение", Контрагент);
		Условие.Вставить("ЗначениеID", КонтрагентID);
		Отбор.Вставить("correspondent", Условие);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОрганизацияID) Тогда
		Условие = Новый Структура;
		Условие.Вставить("Значение", Организация);
		Условие.Вставить("ЗначениеID", ОрганизацияID);
		Отбор.Вставить("organization", Условие);
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"НачатьДобавлениеСвязиЗавершение", ЭтотОбъект);
	ИнтеграцияС1СДокументооборотКлиент.НачатьДобавлениеСвязи(
		ДокументID, ДокументТип, Документ,, ОписаниеОповещения, Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСвязанныеДанные(Команда)
	
	Строка = Элементы.ДеревоСвязей.ТекущиеДанные;
	Если Строка <> Неопределено Тогда
		ОткрытьСвязанныеДанныеСтроки(Строка);
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// История переписки

&НаКлиенте
Процедура ОбновитьПисьма(Команда)
	
	ПолучитьЗаполнитьДеревоПисем();
	РазвернутьДеревоПисем();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПисьмо(Команда)
	
	ТекущиеДанные = Элементы.ДеревоПисем.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(
			ТекущиеДанные.ПисьмоТип,
			ТекущиеДанные.ПисьмоID,
			ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПисьмо(Команда)
	
	ПараметрыФормы = Новый Структура("Предмет", Новый Структура);
	ПараметрыФормы.Предмет.Вставить("name", Документ);
	ПараметрыФормы.Предмет.Вставить("ID", ДокументID);
	ПараметрыФормы.Предмет.Вставить("type", ДокументТип);
	
	ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.ИсходящееПисьмо", ПараметрыФормы);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Трудозатраты

&НаКлиенте
Процедура ОбновитьТрудозатраты(Команда)
	
	ЗаполнитьТрудозатраты();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет подключение к ДО, выводя окно авторизации, если необходимо, и изменяя форму согласно результату.
//
&НаКлиенте
Процедура ПроверитьПодключение()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПодключениеЗавершение", ЭтотОбъект);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПроверитьПодключение(
		ОписаниеОповещения,
		ЭтотОбъект,,
		Ложь,
		Истина);
	
КонецПроцедуры

// Вызывается после проверки подключения к ДО и изменяет форму согласно результату.
//
&НаКлиенте
Процедура ПроверитьПодключениеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ПриПодключении();
	Иначе // не удалось подключиться к ДО
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПодключении()
	
	Если ОбработатьФормуСогласноВерсииСервиса() Тогда
		ПриСменеСтраницыНаКлиенте(Элементы.ГруппаСтраницы.ТекущаяСтраница);
		Если Не ДокументыЗаполнены Тогда
			ОбновитьДеревоСвязей();
			ДокументыЗаполнены = Истина;
			Если ЗначениеЗаполнено(АдресДляФайловСвязанныхДокументов) Тогда
				ПодключитьОбработчикОжидания("ОбновлениеФайловСвязанныхДокументов", 1, Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Изменяет форму согласно доступности сервиса ДО и номеру его версии.
//
&НаСервере
Функция ОбработатьФормуСогласноВерсииСервиса()
	
	ВерсияСервиса = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВерсияСервиса();
	
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СервисДоступен(ВерсияСервиса) Тогда
		Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаДокументооборотНедоступен;
		Возврат Ложь;
	КонецЕсли;
	
	ФормаОбработанаУспешно = Истина;
	
	Попытка
		
		Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаДокументооборотДоступен;
		
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("3.0.1.1") Тогда
			
			ФормаОбработанаУспешно = Ложь;
			Обработки.ИнтеграцияС1СДокументооборот.ОбработатьФормуПриНедоступностиФункционалаВерсииСервиса(ЭтотОбъект);
			Элементы.ГруппаДокумент.Видимость = Ложь;
			Элементы.ГруппаОтступ.Видимость = Ложь;
			Элементы.ГруппаИнфо.Видимость = Ложь;
			
		Иначе
			
			Элементы.ГруппаФункционалНеПоддерживается.Видимость = Ложь;
			Элементы.ГруппаДокумент.Видимость = Истина;
			Элементы.ГруппаОтступ.Видимость = Истина;
			Элементы.ГруппаИнфо.Видимость = Не ЗначениеЗаполнено(ПредставлениеСоздаваемогоОбъекта)
				И Не ЗначениеЗаполнено(ТипСоздаваемогоОбъекта);
			
			ДоступенФункционалПочта = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.2.8.1.CORP");
			ДоступенФункционалКонтрагенты = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3");
			ДоступенФункционалВложенныеПапки = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.2.7.3");
			ДоступенФункционалЗадачи = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.2.6.2");
			ДоступенФункционалВизы = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.0.5.4");
			ДоступенФункционалРезолюции = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.0.5.4");
			ДоступенФункционалХронометраж = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP");
			
			// Результаты выполнения задач.
			Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3") Тогда
				Элементы.ДеревоБизнесПроцессовИЗадачВыполнено.Видимость = Истина;
				Элементы.ДеревоБизнесПроцессовИЗадачКартинка.Видимость = Ложь;
			КонецЕсли;
			Элементы.ГруппаТрудозатраты.Видимость = ДоступенФункционалХронометраж;
			Элементы.ГруппаИсторияПереписки.Видимость = ДоступенФункционалПочта;
			Элементы.ПоказыватьВыполненные.Видимость = ДоступенФункционалЗадачи;
			
			ИспользоватьФайловоеХранилище1СДокументооборота =
				Константы.ИспользоватьФайловоеХранилище1СДокументооборота.Получить();
			ИспользоватьПроцессыИЗадачи1СДокументооборота =
				Константы.ИспользоватьПроцессыИЗадачи1СДокументооборота.Получить();
			ИспользоватьСвязанныеДокументы1СДокументооборота =
				Константы.ИспользоватьСвязанныеДокументы1СДокументооборота.Получить();
			ИспользоватьЭлектроннуюПочту1СДокументооборота =
				Константы.ИспользоватьЭлектроннуюПочту1СДокументооборота.Получить();
			ИспользоватьЕжедневныеОтчеты1СДокументооборота =
				Константы.ИспользоватьЕжедневныеОтчеты1СДокументооборота.Получить();
			
			ПолучитьДанныеДокумента();
			
			// Сохраненная закладка может быть неактуальной.
			Если ЗначениеЗаполнено(ТекущаяЗакладка) Тогда
				Если Элементы.Найти(ТекущаяЗакладка) = Неопределено Тогда
					ТекущаяЗакладка = "";
					
				ИначеЕсли ТекущаяЗакладка = "ГруппаФайлы" И Не ИспользоватьФайловоеХранилище1СДокументооборота Тогда
					ТекущаяЗакладка = "";
					
				ИначеЕсли ТекущаяЗакладка = "ГруппаЗадачи" И Не ИспользоватьПроцессыИЗадачи1СДокументооборота Тогда
					ТекущаяЗакладка = "";
					
				ИначеЕсли ТекущаяЗакладка = "ГруппаСвязанныеДокументы"
						И Не ИспользоватьСвязанныеДокументы1СДокументооборота Тогда
					ТекущаяЗакладка = "";
					
				ИначеЕсли ТекущаяЗакладка = "ГруппаИсторияПереписки"
						И (Не ИспользоватьЭлектроннуюПочту1СДокументооборота
							Или Не ДоступенФункционалПочта) Тогда
					ТекущаяЗакладка = "";
					
				ИначеЕсли ТекущаяЗакладка = "ГруппаТрудозатраты"
						И (Не ИспользоватьЕжедневныеОтчеты1СДокументооборота
							Или Не ДоступенФункционалХронометраж) Тогда
					ТекущаяЗакладка = "";
					
				ИначеЕсли Не Элементы[ТекущаяЗакладка].Видимость Тогда
					ТекущаяЗакладка = "";
				КонецЕсли;
			КонецЕсли;
			
			Если ТекущаяЗакладка = "" Тогда
				ТекущаяЗакладка = Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя;
			Иначе
				Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы[ТекущаяЗакладка];
			КонецЕсли;
			
			ОбновитьДекорацииДокумента();
			
		КонецЕсли;
		
	Исключение
		
		ОбработатьИсключение(ИнформацияОбОшибке());
		
	КонецПопытки;
	
	Возврат ФормаОбработанаУспешно;
	
КонецФункции

&НаКлиенте
Процедура ПриСменеСтраницыНаКлиенте(ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.ГруппаФайлы И Элементы.ГруппаФайлы.Видимость Тогда
		
		Если Не ФайлыЗаполнены Тогда
			ЗаполнитьДанныеФайлов();
			ФайлыЗаполнены = Истина;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ГруппаЗадачи И Элементы.ГруппаЗадачи.Видимость Тогда
		
		Если Не ЗадачиЗаполнены Тогда
			ЗагрузитьДеревоБизнесПроцессовИЗадач();
			ЗадачиЗаполнены = Истина;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ГруппаСвязанныеДокументы И Элементы.ГруппаСвязанныеДокументы.Видимость Тогда
		
		Если Не ДокументыЗаполнены Тогда
			ОбновитьДеревоСвязей();
			ДокументыЗаполнены = Истина;
			Если ЗначениеЗаполнено(АдресДляФайловСвязанныхДокументов) Тогда
				ПодключитьОбработчикОжидания("ОбновлениеФайловСвязанныхДокументов", 1, Истина);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ГруппаИсторияПереписки И Элементы.ГруппаИсторияПереписки.Видимость Тогда
		
		Если Не ПисьмаЗаполнены Тогда
			ПолучитьЗаполнитьДеревоПисем();
			РазвернутьДеревоПисем();
			ПисьмаЗаполнены = Истина;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ГруппаТрудозатраты И Элементы.ГруппаТрудозатраты.Видимость Тогда
		
		Если Не ТрудозатратыЗаполнены Тогда
			ЗаполнитьТрудозатраты();
			ТрудозатратыЗаполнены = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ТекущаяЗакладка = ТекущаяСтраница.Имя;
	
	ДополнитьПредставлениеДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеТекущейСтраницыНаСервере(ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.ГруппаФайлы И Элементы.ГруппаФайлы.Видимость Тогда
		
		Если Не ФайлыЗаполнены Тогда
			ЗаполнитьДанныеФайлов();
			ФайлыЗаполнены = Истина;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ГруппаЗадачи И Элементы.ГруппаЗадачи.Видимость Тогда
		
		Если Не ЗадачиЗаполнены Тогда
			ЗагрузитьДеревоБизнесПроцессовИЗадач();
			ЗадачиЗаполнены = Истина;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ГруппаСвязанныеДокументы И Элементы.ГруппаСвязанныеДокументы.Видимость Тогда
		
		Если Не ДокументыЗаполнены Тогда
			ОбновитьДеревоСвязей();
			ДокументыЗаполнены = Истина;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ГруппаИсторияПереписки И Элементы.ГруппаИсторияПереписки.Видимость Тогда
		
		Если Не ПисьмаЗаполнены Тогда
			ПолучитьЗаполнитьДеревоПисем();
			ПисьмаЗаполнены = Истина;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ГруппаТрудозатраты И Элементы.ГруппаТрудозатраты.Видимость Тогда
		
		Если Не ТрудозатратыЗаполнены Тогда
			ЗаполнитьТрудозатраты();
			ТрудозатратыЗаполнены = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбъектСоздатьНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбъектОчиститьНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	СохраненныйID = ДокументID;
	СохраненныйТип = ДокументТип;
	УдалитьСвязьНаСервере();
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.Оповестить_УдалениеСвязи(
		СохраненныйID,
		СохраненныйТип,
		ВнешнийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбъектВыбратьНажатиеПослеВыбораТипа(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Строка")
		Или Результат = "" Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОтборПриВыбореСвязанногоОбъекта(
		ВнешнийОбъект,
		Результат);
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка(
		Результат,
		"Документ",
		ЭтотОбъект,
		Отбор);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборСвязанногоОбъекта()
	
	Если ЗначениеЗаполнено(ДокументID) И ЗначениеЗаполнено(ДокументТип) Тогда // добавление новой связи
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ДобавитьСвязь(
			ДокументID,
			ДокументТип,
			Параметры.ВнешнийОбъект);
	КонецЕсли;
	
	ПолучитьДанныеДокумента();
	
	ТекущаяСтраница = Элементы.ГруппаСтраницы.ТекущаяСтраница;
	
	ФайлыЗаполнены = Ложь;
	ЗадачиЗаполнены = Ложь;
	ДокументыЗаполнены = Ложь;
	ПисьмаЗаполнены = Ложь;
	ТрудозатратыЗаполнены = Ложь;
	
	ЗаполнитьДанныеТекущейСтраницыНаСервере(ТекущаяСтраница);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьЗаписьДокумента()
	
	ОбновитьДекорацииДокумента();
	Если ИспользоватьПроцессыИЗадачи1СДокументооборота Тогда
		ЗагрузитьДеревоБизнесПроцессовИЗадач();
	КонецЕсли;
	Если ИспользоватьСвязанныеДокументы1СДокументооборота Тогда
		ОбновитьДеревоСвязей();
	КонецЕсли;
	Если ЗначениеЗаполнено(ДокументID) Тогда
		РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.ДобавитьСвязь(
			ДокументID,
			ДокументТип,
			ВнешнийОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСвязьНаСервере()
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.УдалитьСвязь(
		ДокументID,
		ДокументТип,
		Параметры.ВнешнийОбъект);
	
	ТекущаяСтраница = Элементы.ГруппаСтраницы.ТекущаяСтраница;
	
	ДокументID = "";
	ДокументТип = "";
	Документ = "";
	
	Элементы.ГруппаВизы.Видимость = Ложь;
	Элементы.ГруппаРезолюции.Видимость = Ложь;
	
	ОбновитьДекорацииДокумента();
	
	ФайлыЗаполнены = Ложь;
	ЗадачиЗаполнены = Ложь;
	ДокументыЗаполнены = Ложь;
	ПисьмаЗаполнены = Ложь;
	ТрудозатратыЗаполнены = Ложь;
	
	ЗаполнитьДанныеТекущейСтраницыНаСервере(ТекущаяСтраница);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДекорацииДокумента()
	
	ДокументЗаполнен = ЗначениеЗаполнено(ДокументID);
	
	Элементы.ДекорацияОбъектСоздать.Видимость = Не ДокументЗаполнен;
	Элементы.ДекорацияОбъектИли.Видимость = Не ДокументЗаполнен;
	Элементы.ДекорацияОбъектВыбрать.Видимость = Не ДокументЗаполнен;
	
	Элементы.ДекорацияОбъект.Видимость = ДокументЗаполнен;
	Элементы.ДекорацияОбъектОчистить.Видимость = ДокументЗаполнен;
	Элементы.ДеревоБизнесПроцессовИЗадач.Доступность = ДокументЗаполнен;
	
	Элементы.ДеревоСвязейДобавить.Доступность = ДокументЗаполнен;
	Элементы.ДеревоСвязейОткрыть.Доступность =  ДокументЗаполнен;
	Элементы.ДеревоСвязейУдалить.Доступность =  ДокументЗаполнен;
	
	Элементы.ДеревоСвязейДобавитьКонтекст.Доступность = ДокументЗаполнен;
	Элементы.ДеревоСвязейОткрытьКонтекст.Доступность =  ДокументЗаполнен;
	Элементы.ДеревоСвязейУдалитьКонтекст.Доступность =  ДокументЗаполнен;
	
	Элементы.ДеревоПисемСоздатьПисьмо.Доступность = ДокументЗаполнен;
	Элементы.ДеревоПисемОткрытьПисьмо.Доступность = ДокументЗаполнен;
	Элементы.ДеревоПисемОбновитьПисьма.Доступность = ДокументЗаполнен;
	
	Элементы.РаботыОбновить.Доступность = ДокументЗаполнен;
	
	Если ДокументТип = "DMCorrespondent" Тогда
		Элементы.ГруппаСвязанныеДокументы.Видимость = Ложь;
		Элементы.ГруппаТрудозатраты.Видимость = Ложь;
		Элементы.ДеревоПисемНомер.Видимость = Истина;
		Элементы.ДеревоПисемАдресаты.Видимость = Ложь;
		Элементы.ДеревоПисемПисьмо.Заголовок = НСтр("ru='Документ'");
	Иначе
		Элементы.ГруппаСвязанныеДокументы.Видимость = Истина;
		// хронометраж
		Элементы.ГруппаТрудозатраты.Видимость =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.КОРП");
		Элементы.ДеревоПисемНомер.Видимость = Ложь;
		Элементы.ДеревоПисемАдресаты.Видимость = Истина;
		Элементы.ДеревоПисемПисьмо.Заголовок = НСтр("ru='Письмо'");
	КонецЕсли;
	
	Модифицированность = Ложь;

КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеДокумента()
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetDocumentListRequest");
	
	СписокВнешнихОбъектов = Запрос.externalObjects; // СписокXDTO
	СписокВнешнихОбъектов.Добавить(
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьExternalObject(Прокси, ВнешнийОбъект));
	
	ПолучаемыеПоля = Запрос.columnSet; // СписокXDTO
	ПолучаемыеПоля.Добавить("name");
	Если ДоступенФункционалВизы Тогда
		ПолучаемыеПоля.Добавить("visas");
	КонецЕсли;
	Если ДоступенФункционалРезолюции Тогда
		ПолучаемыеПоля.Добавить("resolutions");
	КонецЕсли;
	
	Результат = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, Результат, "DMError") Тогда 
		ВызватьИсключение Результат.description;
	КонецЕсли;
		
	ОбъектXDTO = Неопределено;
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, Результат, "DMGetObjectListResponse") Тогда
		Если Результат.items.Количество() > 0 Тогда
			ОбъектXDTO = Результат.items[0].object;
		КонецЕсли;
	Иначе // совместимость со старыми версиями сервиса
		Если Результат.documents.Количество() > 0 Тогда
			ОбъектXDTO = Результат.documents[0];
		КонецЕсли;
	КонецЕсли;
		
	Если ОбъектXDTO = Неопределено Тогда
		Элементы.ГруппаРезолюции.Видимость = Ложь;
		Элементы.ГруппаВизы.Видимость = Ложь;
	Иначе
		// Заполним основные реквизиты.
		ДокументID = ОбъектXDTO.objectID.ID;
		ДокументТип = ОбъектXDTO.objectID.type;
		Документ = ОбъектXDTO.name;
		// Обработаем резолюции.
		Если ОбъектXDTO.Свойства().Получить("resolutions") <> Неопределено
			И ОбъектXDTO.resolutions.Количество() <> 0 Тогда
			ИнтеграцияС1СДокументооборотВызовСервера.ОбновитьПредставлениеРезолюций(
				ОбъектXDTO.resolutions, РезолюцииHTMLПредставление, Элементы.ГруппаРезолюции);
			Элементы.ГруппаРезолюции.Видимость = Истина;
		Иначе
			Элементы.ГруппаРезолюции.Видимость = Ложь;
		КонецЕсли;
		// Обработаем визы.
		Если ОбъектXDTO.Свойства().Получить("visas") <> Неопределено
			И ОбъектXDTO.visas.Количество() <> 0 Тогда
			ИнтеграцияС1СДокументооборотВызовСервера.ОбновитьВизыСогласования(
				ОбъектXDTO.visas, ВизыСогласования, Элементы.ГруппаВизы);
			Элементы.ГруппаВизы.Видимость = Истина;
		Иначе
			Элементы.ГруппаВизы.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнитьПредставлениеДокумента()
	
	Если ЗначениеЗаполнено(Документ) Тогда 
		
		Если ДокументТип = "DMIncomingDocument" Тогда 
			Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Входящий документ:'");
		ИначеЕсли ДокументТип = "DMOutgoingDocument" Тогда 
			Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Исходящий документ:'");
		ИначеЕсли ДокументТип = "DMInternalDocument" Тогда
			Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Внутренний документ:'");
		ИначеЕсли ДокументТип = "DMCorrespondent" Тогда
			Если ИнтеграцияС1СДокументооборотКлиентПовтИсп.ИспользоватьТерминКорреспонденты() Тогда
				Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Корреспондент:'");
			Иначе
				Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Контрагент:'");
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ГруппаСтраницыСвязанныйОбъект.ТекущаяСтраница =
			Элементы.ГруппаСтраницаЕстьСвязанныйОбъект;
		
	Иначе // нет связанного объекта
		
		Если ЗначениеЗаполнено(ПредставлениеСоздаваемогоОбъекта) Тогда
			Элементы.ДекорацияОбъектЗаголовок.Заголовок = ПредставлениеСоздаваемогоОбъекта + ":";
		Иначе
			Если ТипСоздаваемогоОбъекта = "DMIncomingDocument" Тогда 
				Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Входящий документ:'");
			ИначеЕсли ТипСоздаваемогоОбъекта = "DMOutgoingDocument" Тогда 
				Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Исходящий документ:'");
			ИначеЕсли ТипСоздаваемогоОбъекта = "DMInternalDocument" Тогда
				Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Внутренний документ:'");
			ИначеЕсли ТипСоздаваемогоОбъекта = "DMCorrespondent" Тогда 
				Если ИнтеграцияС1СДокументооборотКлиентПовтИсп.ИспользоватьТерминКорреспонденты() Тогда
					Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Корреспондент:'");
				Иначе
					Элементы.ДекорацияОбъектЗаголовок.Заголовок = НСтр("ru = 'Контрагент:'");
				КонецЕсли;
			Иначе // нет подходящих правил
				Элементы.ДекорацияОбъектЗаголовок.Заголовок = 
					НСтр("ru = 'Соответствие в 1С:Документообороте:'");
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ГруппаСтраницыСвязанныйОбъект.ТекущаяСтраница =
			Элементы.ГруппаСтраницаНетСвязанногоОбъекта;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИсключение(ИнформацияОбОшибке)
	
	ВерсияСервиса = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВерсияСервиса();
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СервисДоступен(ВерсияСервиса) Тогда
		ОбработатьФормуСогласноВерсииСервиса();
	Иначе
		Если ТипЗнч(ИнформацияОбОшибке) = Тип("Строка") Тогда
			ПредставлениеОшибки = ИнформацияОбОшибке;
		Иначе
			ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		КонецЕсли;
		ВызватьИсключение ПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Процессы и задачи

&НаСервере
Процедура УстановитьУсловноеОформлениеПроцессовИЗадач()
	
	Если УсловноеОформление.Элементы.Количество() = 3 Тогда
		ЭлементДляУдаления = УсловноеОформление.Элементы[2];
		УсловноеОформление.Элементы.Удалить(ЭлементДляУдаления);
	КонецЕсли;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	Отборы = ЭлементУсловногоОформления.Отбор.Элементы;
	
	ЭлементОтбораДанных = Отборы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = 
		Новый ПолеКомпоновкиДанных("ДеревоБизнесПроцессовИЗадач.СрокИсполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = Отборы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = 
		Новый ПолеКомпоновкиДанных("ДеревоБизнесПроцессовИЗадач.Выполнено");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Ложь;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = Отборы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = 
		Новый ПолеКомпоновкиДанных("ДеревоБизнесПроцессовИЗадач.СрокИсполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбораДанных.ПравоеЗначение = ТекущаяДатаСеанса();
	ЭлементОтбораДанных.Использование = Истина;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ДеревоБизнесПроцессовИЗадачНаименование");
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ДеревоБизнесПроцессовИЗадачСрокИсполнения");
		
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.ПросроченныеДанныеЦвет.Значение; 
	ЭлементЦветаОформления.Использование = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПостроитьДеревоЗадачИзОтветаВебСервиса(СтрокиДереваЗадач, СтрокиОтвета)
	
	ДоступенФункционалРезультатыВыполненияЗадач = 
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3");
		
	Для Каждого ОднаСтрокаОтвета Из СтрокиОтвета Цикл
		
		Важность = 1;
		Если ОднаСтрокаОтвета.importance.objectID.ID = "Низкая" Тогда //@NON-NLS-1
			Важность = 0;
		ИначеЕсли ОднаСтрокаОтвета.importance.objectID.ID = "Обычная" Тогда //@NON-NLS-1
			Важность = 1;
		ИначеЕсли ОднаСтрокаОтвета.importance.objectID.ID = "Высокая" Тогда //@NON-NLS-1
			Важность = 2;
		КонецЕсли;
		
		Если Найти(ОднаСтрокаОтвета.objectID.type, "BusinessProcess") > 0
			И Найти(ОднаСтрокаОтвета.objectID.type, "Task") = 0 Тогда
			
			НоваяСтрока = СтрокиДереваЗадач.Добавить();
			НоваяСтрока.Важность = Важность;
			НоваяСтрока.Выполнено = ЗначениеЗаполнено(ОднаСтрокаОтвета.endDate);
			НоваяСтрока.Наименование = ОднаСтрокаОтвета.name;
			НоваяСтрока.ДатаНачала = ОднаСтрокаОтвета.beginDate;
			НоваяСтрока.Автор = ОднаСтрокаОтвета.author.name;
			НоваяСтрока.Тип = 0;
			НоваяСтрока.DMТип = ОднаСтрокаОтвета.objectID.type;
			НоваяСтрока.DMИд = ОднаСтрокаОтвета.objectID.ID;
			
			Если ДоступенФункционалРезультатыВыполненияЗадач Тогда
				НоваяСтрока.Картинка = ИнтеграцияС1СДокументооборот.ИндексКартинкиПометкиЗавершения(
					ОднаСтрокаОтвета.completionMark);
			КонецЕсли;
				
			ПостроитьДеревоЗадачИзОтветаВебСервиса(НоваяСтрока.Строки, ОднаСтрокаОтвета.tasks);
			
		ИначеЕсли Найти(ОднаСтрокаОтвета.objectID.type, "Task") > 0 Тогда
			
			Если ЭтоСлужебнаяЗадача(ОднаСтрокаОтвета) Тогда
				
				ПостроитьДеревоЗадачИзОтветаВебСервиса(СтрокиДереваЗадач, ОднаСтрокаОтвета.businessProcesses);
				
			Иначе
				
				НоваяСтрока = СтрокиДереваЗадач.Добавить();
				НоваяСтрока.Важность = Важность;
				НоваяСтрока.Выполнено = ОднаСтрокаОтвета.executed;
				НоваяСтрока.Наименование = ОднаСтрокаОтвета.name;
				НоваяСтрока.СрокИсполнения = ОднаСтрокаОтвета.dueDate;
				НоваяСтрока.ДатаНачала = ОднаСтрокаОтвета.beginDate;
				
				Если ОднаСтрокаОтвета.performer.Установлено("user") Тогда
					НоваяСтрока.Исполнитель = ОднаСтрокаОтвета.performer.user.name;
				ИначеЕсли ОднаСтрокаОтвета.performer.Установлено("role") Тогда
					НоваяСтрока.Исполнитель = ОднаСтрокаОтвета.performer.role.name;
					Если ОднаСтрокаОтвета.performer.Установлено("mainAddressingObject") Тогда
						НоваяСтрока.Исполнитель = НоваяСтрока.Исполнитель + ", " 
							+ ОднаСтрокаОтвета.performer.mainAddressingObject.name;
					КонецЕсли;
					Если ОднаСтрокаОтвета.Performer.Установлено("secondaryAddressingObject") Тогда
						НоваяСтрока.Исполнитель = НоваяСтрока.Исполнитель + ", " 
							+ ОднаСтрокаОтвета.performer.secondaryAddressingObject.name;
					КонецЕсли;
				КонецЕсли;
				
				НоваяСтрока.Тип = 1;
				НоваяСтрока.DMТип = ОднаСтрокаОтвета.objectID.type;
				НоваяСтрока.DMИд = ОднаСтрокаОтвета.objectID.ID;
				НоваяСтрока.ТочкаМаршрута = ОднаСтрокаОтвета.businessProcessStep;
				
				Если ДоступенФункционалРезультатыВыполненияЗадач Тогда
					НоваяСтрока.Картинка = ИнтеграцияС1СДокументооборот.ИндексКартинкиПометкиЗавершения(
						ОднаСтрокаОтвета.executionMark);
					КонецЕсли;
					
				ПостроитьДеревоЗадачИзОтветаВебСервиса(НоваяСтрока.Строки, ОднаСтрокаОтвета.businessProcesses);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекущуюЗадачуИлиПроцесс()
	
	ТекущиеДанные = Элементы.ДеревоБизнесПроцессовИЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектID = ТекущиеДанные.DMИд;
	ОбъектТип = ТекущиеДанные.DMТип;
	
	Если Найти(ТекущиеДанные.DMТип, "Task") > 0 Тогда
		
		ПараметрыФормы = Новый Структура("ID, type", ОбъектID, ОбъектТип);
		Если ДоступенФункционалЗадачи Тогда
			ФормаИмя = "Обработка.ИнтеграцияС1СДокументооборот.Форма.Задача";
		Иначе
			ФормаИмя = "Обработка.ИнтеграцияС1СДокументооборот.Форма.КарточкаЗадачи";
		КонецЕсли;
		ОткрытьФорму(ФормаИмя, ПараметрыФормы, Элементы.ДеревоБизнесПроцессовИЗадач, ОбъектID);
		
	Иначе
		
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(
			ОбъектТип,
			ОбъектID,
			Элементы.ДеревоБизнесПроцессовИЗадач);
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДеревоБизнесПроцессовИЗадач()
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetTasksTreeRequest");
	
	Запрос.query = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetTasksTreeQuery");
	
	СписокВнешнихОбъектов = Запрос.query.externalTarget; // СписокXDTO
	СписокВнешнихОбъектов.Добавить(
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьExternalObject(Прокси, ВнешнийОбъект));
	
	Если Запрос.query.Свойства().Получить("withExecuted") <> Неопределено Тогда
		Запрос.query.withExecuted = ПоказыватьВыполненные;
	КонецЕсли;
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	ДеревоЗадач = РеквизитФормыВЗначение("ДеревоБизнесПроцессовИЗадач", Тип("ДеревоЗначений"));
	ДеревоЗадач.Строки.Очистить();
	ПостроитьДеревоЗадачИзОтветаВебСервиса(ДеревоЗадач.Строки, Ответ.businessProcesses);
	ЗначениеВРеквизитФормы(ДеревоЗадач, "ДеревоБизнесПроцессовИЗадач");
	УстановитьУсловноеОформлениеПроцессовИЗадач();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьРаскрытыеЭлементы(ДеревоЭлемент, МассивСтрокОдногоУровня, СписокРаскрытыхЭлементов)
	
	Для Каждого СтрокаОдногоУровня Из МассивСтрокОдногоУровня Цикл
		ИдЭлемента = СтрокаОдногоУровня.ПолучитьИдентификатор();
		Если ДеревоЭлемент.Развернут(ИдЭлемента) <> Неопределено И ДеревоЭлемент.Развернут(ИдЭлемента) Тогда
			СписокРаскрытыхЭлементов.Добавить(СтрокаОдногоУровня.DMИд);
		КонецЕсли;
		ПолучитьРаскрытыеЭлементы(
			ДеревоЭлемент,
			СтрокаОдногоУровня.ПолучитьЭлементы(),
			СписокРаскрытыхЭлементов);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРазвернутостьЭлементовДерева(ДеревоЭлемент, ДеревоРеквизит,
		СписокЭлементовДляРазвертывания)
	
	Если СписокЭлементовДляРазвертывания <> Неопределено Тогда
		Для Каждого ЭлементСписка Из СписокЭлементовДляРазвертывания Цикл
			Индекс = -1;
			НайтиЭлементВДереве(
				ДеревоБизнесПроцессовИЗадач.ПолучитьЭлементы(),
				ЭлементСписка.Значение,
				Индекс);
			Если Индекс > -1 Тогда
				Если ДеревоРеквизит.НайтиПоИдентификатору(Индекс).ПолучитьЭлементы().Количество() > 0 Тогда
					ДеревоЭлемент.Развернуть(
						ДеревоБизнесПроцессовИЗадач.НайтиПоИдентификатору(Индекс).ПолучитьИдентификатор(),
						Ложь);
				Иначе
					Если ДеревоРеквизит.НайтиПоИдентификатору(Индекс).ПолучитьРодителя() <> Неопределено Тогда
						Родитель = ДеревоБизнесПроцессовИЗадач.НайтиПоИдентификатору(Индекс).ПолучитьРодителя();
						ДеревоЭлемент.Развернуть(Родитель.ПолучитьИдентификатор(), Ложь);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиЭлементВДереве(КоллекцияЭлементовОдногоУровня, ИскомыйЭлемент, Индекс) 
	
	Если КоллекцияЭлементовОдногоУровня.Количество() > 0 Тогда
		Для Каждого ЭлементДерева Из КоллекцияЭлементовОдногоУровня Цикл
			Если ЭлементДерева.DMИд = ИскомыйЭлемент Тогда
				Индекс = ЭлементДерева.ПолучитьИдентификатор();
			Иначе
				НайтиЭлементВДереве(ЭлементДерева.ПолучитьЭлементы(), ИскомыйЭлемент, Индекс);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущийЭлементВДереве(ДеревоЭлемент, ДеревоРеквизит, ТекущийЭлемент) 
	
	Если ТекущийЭлемент <> Неопределено Тогда
		Индекс = -1;
		НайтиЭлементВДереве(ДеревоРеквизит.ПолучитьЭлементы(), ТекущийЭлемент, Индекс);
		Если Индекс > -1 Тогда
			ДеревоЭлемент.ТекущаяСтрока = Индекс;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭтоСлужебнаяЗадача(Задача)
	
	Служебная = Ложь;
	
	Если Задача.parentBusinessProcess.objectID.type = "DMComplexBusinessProcess" Тогда
		Если Задача.businessProcessStep = "Выполнить все действия процесса" Тогда //@NON-NLS-1
			Служебная = Истина;
		КонецЕсли;
	ИначеЕсли Задача.parentBusinessProcess.objectID.type = "DMBusinessProcessInternalDocumentProcessing"
			Или Задача.parentBusinessProcess.objectID.type = "DMBusinessProcessIncomingDocumentProcessing"
			Или Задача.parentBusinessProcess.objectID.type = "DMBusinessProcessOutgoingDocumentProcessing" Тогда
		Служебная = Истина;
	КонецЕсли;
	
	Возврат Служебная;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////////
// Файлы

&НаКлиенте
Процедура СписокФайловПередУдалениемЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПометитьНаУдаление(Элементы.СписокФайлов.ВыделенныеСтроки);
		ОбновитьСписокФайловКлиент();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокФайлов()
	
	СписокФайлов.Очистить();
	
	Файлы = ИнтеграцияС1СДокументооборотВызовСервера.ФайлыПоОбъектуПотребителю(
		ВнешнийОбъект.УникальныйИдентификатор(),
		Строка(ВнешнийОбъект),
		ВнешнийОбъект.Метаданные().ПолноеИмя());
	
	Для Каждого СведенияОФайле Из Файлы.files Цикл
		НоваяСтрока = СписокФайлов.Добавить();
		
		НоваяСтрока.Наименование = СведенияОФайле.name;
		НоваяСтрока.Расширение = СведенияОФайле.extension;
		НоваяСтрока.Описание = СведенияОФайле.description;
		НоваяСтрока.Размер = Окр(СведенияОФайле.size/1024, 0);
		НоваяСтрока.ПодписанЭП = СведенияОФайле.signed;
		НоваяСтрока.Автор = СведенияОФайле.author.name;
		НоваяСтрока.ID = СведенияОФайле.objectID.ID;
		НоваяСтрока.ДатаСоздания = СведенияОФайле.creationDate;
		НоваяСтрока.ДатаМодификацииУниверсальная = СведенияОФайле.modificationDateUniversal;
		
		ПометкаУдаления = Ложь;
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(СведенияОФайле, "deletionMark") Тогда
			ПометкаУдаления = СведенияОФайле.deletionMark;
		КонецЕсли;
		НоваяСтрока.ИндексКартинки =
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИндексПиктограммыФайла(
				НоваяСтрока.Расширение,
				ПометкаУдаления);
		
		Для Каждого ПодписьXDTO Из СведенияОФайле.signatures Цикл
			Подпись = НоваяСтрока.ТаблицаПодписей.Добавить();
			
			Подпись.КомуВыданСертификат = ПодписьXDTO.author;
			Подпись.ДатаПодписи = ПодписьXDTO.date;
			Подпись.Комментарий = ПодписьXDTO.comment;
			Подпись.Отпечаток = ПодписьXDTO.thumbprint;
			
			ДвоичныеДанные = ПодписьXDTO.signature;
			Подпись.АдресПодписи = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
			
			ДвоичныеДанныеСертификата = ПодписьXDTO.certificate;
			Если ДвоичныеДанныеСертификата <> Неопределено Тогда 
				Подпись.АдресСертификата = ПоместитьВоВременноеХранилище(
					ДвоичныеДанныеСертификата, УникальныйИдентификатор);
			КонецЕсли;
			
			Подпись.ИмяФайлаПодписи = ПодписьXDTO.signatureFileName;
			Подпись.УстановившийПодпись = ПодписьXDTO.signer.name;
			Подпись.УстановившийПодписьИд = ПодписьXDTO.signer.objectID.ID;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокФайловКлиент(ИдентификаторФайла = Неопределено)
	
	Если ИдентификаторФайла <> Неопределено Тогда
		ТекущийИдентификаторФайла = ИдентификаторФайла;
	ИначеЕсли Элементы.СписокФайлов.ТекущиеДанные <> Неопределено Тогда
		ТекущийИдентификаторФайла = Элементы.СписокФайлов.ТекущиеДанные.ID;
	КонецЕсли;
	
	ОбновитьСписокФайлов();
	
	// восстановим положение в списке
	Для Каждого Строка Из СписокФайлов Цикл
		Если Строка.ID = ТекущийИдентификаторФайла Тогда
			Элементы.СписокФайлов.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуФайлаВыполнить()
	
	ТекущиеДанные = Элементы.СписокФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект("DMFile", ТекущиеДанные.ID);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлВыполнить()
	
	ТекущиеДанные = Элементы.СписокФайлов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьФайл(
			ТекущиеДанные.ID,
			ТекущиеДанные.Наименование,
			ТекущиеДанные.Расширение,
			УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьФайлВыполнить()
	
	ПараметрыОткрытия = Новый Структура("РежимСоздания", 1);
	
	Оповещение = Новый ОписаниеОповещения("СоздатьФайлВыполнитьЗавершение", ЭтотОбъект);
	ОткрытьФорму(
		"Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.СозданиеНовогоФайла",
		ПараметрыОткрытия,,,,,
		Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьФайлВыполнитьЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если (ТипЗнч(Результат) = Тип("КодВозвратаДиалога")) Или (Результат = Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = 1 Тогда
		СоздатьФайлСДиска();
		
	ИначеЕсли Результат = 2 Или Результат = 3 Тогда
		ВыбратьСуществующийФайлВ1СДокументооборот(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзФайлаНаДискеЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	ОбновитьСписокФайловКлиент();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьИдентификаторКорневойПапкиФайлов1СДокументооборот()
	
	УстановитьПривилегированныйРежим(Истина);
	ИдентификаторКорневойПапкиФайлов1СДокументооборот =
		Константы.ИдентификаторКорневойПапкиФайлов1СДокументооборот.Получить();
	
	Возврат ИдентификаторКорневойПапкиФайлов1СДокументооборот;
	
КонецФункции

&НаКлиенте
Процедура ВыбратьСуществующийФайлВ1СДокументооборот(Результат)
	
	Оповещение = Новый ОписаниеОповещения(
		"ВыбратьСуществующийФайлВ1СДокументооборотЗавершение",
		ЭтотОбъект,
		Результат);
	ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.ВыборФайла",,,,,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСуществующийФайлВ1СДокументооборотЗавершение(Результат, ПараметрыОповещения) Экспорт

	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		Если ПараметрыОповещения = 2 Тогда
			ДобавитьСсылкуНаСуществующийФайл(Результат);
		Иначе
			СкопироватьСуществующийФайлВПапку(Результат);
		КонецЕсли;
		ОбновитьСписокФайловКлиент(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСсылкуНаСуществующийФайл(ИдентификаторФайла)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMAddObjectLinkRequest");
	
	Запрос.ownerObject = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьExternalObject(
		Прокси,
		ВнешнийОбъект);
	
	Запрос.linkedObject = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ИдентификаторФайла,
		"DMFile");
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

&НаСервере
Процедура СкопироватьСуществующийФайлВПапку(ИдентификаторФайла)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	СписокОбъектов = Запрос.objectIDs; // СписокXDTO
	ПолучаемыеПоля = Запрос.columnSet; // СписокXDTO
	
	ОбъектID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ИдентификаторФайла,
		"DMFile");
	СписокОбъектов.Добавить(ОбъектID);
	
	ПолучаемыеПоля.Добавить("objectID");
	ПолучаемыеПоля.Добавить("name");
	ПолучаемыеПоля.Добавить("binaryData");
	ПолучаемыеПоля.Добавить("extension");
	ПолучаемыеПоля.Добавить("modificationDate");
	ПолучаемыеПоля.Добавить("modificationDateUniversal");
	ПолучаемыеПоля.Добавить("size");
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	ДанныеФайла = Ответ.objects[0];
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.РазмерФайлаПревышаетМаксимальноДопустимый(
		МаксРазмерФайла,
		ДанныеФайла.size,
		ДанныеФайла.name);
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMAddLinkedFileRequest");
	
	Запрос.rootFolderID = ИдентификаторКорневойПапки1СДокументооборот;
	
	Запрос.file = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
		Прокси,
		"DMFile",
		ВнешнийОбъект);
	
	Запрос.file.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(Прокси, "", "DMFile");
	Запрос.file.binaryData = ДанныеФайла.binaryData;
	Запрос.file.extension = ДанныеФайла.extension;
	Запрос.file.modificationDate = ДанныеФайла.modificationDate;
	Запрос.file.modificationDateUniversal = ДанныеФайла.modificationDateUniversal;
	Запрос.file.name = ДанныеФайла.name;
	Запрос.file.size = ДанныеФайла.size;
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

&НаСервере
Процедура ПометитьНаУдаление(Знач ВыделенныеСтроки)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	СписокОбъектов = Новый Массив;
	
	Для Каждого НомерСтроки Из ВыделенныеСтроки Цикл
		Данные = СписокФайлов.НайтиПоИдентификатору(НомерСтроки);
		СписокОбъектов.Добавить(Новый Структура("ID, Тип", Данные.ID, "DMFile"));
	КонецЦикла;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПометитьНаУдалениеСнятьПометкуОбъектов(
		Прокси,
		СписокОбъектов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьКомандСпискаФайлов()
	
	УстановитьДоступностьКоманд(Элементы.СписокФайлов.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКоманд(ТекущиеДанные)
	
	Если ТекущиеДанные = Неопределено Тогда 
		
		Элементы.СписокФайловКонтекстноеМенюОткрытьФайл.Доступность = Ложь;
		Элементы.СписокФайловКонтекстноеМенюСохранитьКак.Доступность = Ложь;
		Элементы.СписокФайловКонтекстноеМенюОбновитьИзФайлаНаДиске.Доступность = Ложь;
		Элементы.СписокФайловКонтекстноеМенюПодписать.Доступность = Ложь;
		Элементы.СписокФайловКонтекстноеМенюДобавитьЭПИзФайла.Доступность = Ложь;
		Элементы.СписокФайловКонтекстноеМенюСохранитьВместеСЭП.Доступность = Ложь;
		
		Элементы.СписокФайловОткрытьФайл.Доступность = Ложь;
		Элементы.СписокФайловСохранитьКак.Доступность = Ложь;
		Элементы.СписокФайловОбновитьИзФайлаНаДиске.Доступность = Ложь;
		Элементы.СписокФайловПодписать.Доступность = Ложь;
		Элементы.СписокФайловДобавитьЭПИзФайла.Доступность = Ложь;
		Элементы.СписокФайловСохранитьВместеСЭП.Доступность = Ложь;
		
	Иначе	
		
		Редактируется = ТекущиеДанные.Редактируется;
		ПодписанЭП = ТекущиеДанные.ПодписанЭП;
		Зашифрован = ТекущиеДанные.Зашифрован;
		
		Элементы.СписокФайловКонтекстноеМенюОткрытьФайл.Доступность = Истина;
		Элементы.СписокФайловКонтекстноеМенюСохранитьКак.Доступность = Истина;
		Элементы.СписокФайловКонтекстноеМенюОбновитьИзФайлаНаДиске.Доступность = Истина;
		Элементы.СписокФайловКонтекстноеМенюПодписать.Доступность =
			Не Редактируется И Не Зашифрован;
		Элементы.СписокФайловКонтекстноеМенюДобавитьЭПИзФайла.Доступность =
			Не Редактируется И Не Зашифрован;
		Элементы.СписокФайловКонтекстноеМенюСохранитьВместеСЭП.Доступность = ПодписанЭП;
		
		Элементы.СписокФайловОткрытьФайл.Доступность = Истина;
		Элементы.СписокФайловСохранитьКак.Доступность = Истина;
		Элементы.СписокФайловОбновитьИзФайлаНаДиске.Доступность = Истина;
		Элементы.СписокФайловПодписать.Доступность =
			Не Редактируется И Не Зашифрован;
		Элементы.СписокФайловДобавитьЭПИзФайла.Доступность =
			Не Редактируется И Не Зашифрован;
		Элементы.СписокФайловСохранитьВместеСЭП.Доступность = ПодписанЭП;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФайлов()
	
	ОбновитьСписокФайлов();
	
	НастройкиДО = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьНастройки();
	ИспользоватьЭлектронныеЦифровыеПодписиВБСП =
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ИспользоватьЭлектронныеЦифровыеПодписи();
	
	Если НастройкиДО.ИспользоватьЭлектронныеЦифровыеПодписи = Ложь Тогда
		Элементы.СписокФайловПодписанЭП.Видимость = Ложь;
		Элементы.СписокФайловСохранитьВместеСЭП.Видимость = Ложь;
		Элементы.СписокФайловКонтекстноеМенюСохранитьВместеСЭП.Видимость = Ложь;
	КонецЕсли;
	
	Если НастройкиДО.ИспользоватьЭлектронныеЦифровыеПодписи = Ложь
			Или ИспользоватьЭлектронныеЦифровыеПодписиВБСП = Ложь Тогда
		Элементы.СписокФайловПодписать.Видимость = Ложь;
		Элементы.СписокФайловКонтекстноеМенюПодписать.Видимость = Ложь;
		Элементы.СписокФайловДобавитьЭПИзФайла.Видимость = Ложь;
		Элементы.СписокФайловКонтекстноеМенюДобавитьЭПИзФайла.Видимость = Ложь;
	КонецЕсли;
	
	ИдентификаторКорневойПапки1СДокументооборот = ПолучитьИдентификаторКорневойПапкиФайлов1СДокументооборот();
	
КонецПроцедуры

// Начало создания файла из файла на диске.
//
&НаКлиенте
Процедура СоздатьФайлСДиска()
	
	Если Не ЗначениеЗаполнено(ИдентификаторКорневойПапки1СДокументооборот) Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Не указана корневая папка для хранения файлов 1С:Документооборота.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения(
		"СоздатьФайлСДискаПослеПодключенияРасширения", ЭтотОбъект, ПараметрыОповещения));
	
КонецПроцедуры

// Начало создания файла из файла на диске.
//
// Параметры:
//   Файл - Файл
//
&НаКлиенте
Процедура СоздатьФайлСДискаПеретаскиванием(Файл)
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ПолноеИмяФайла", Файл.ПолноеИмя);
	ПараметрыОповещения.Вставить("Файл", Файл);
	
	Файл.НачатьПолучениеРазмера(Новый ОписаниеОповещения(
		"СоздатьФайлСДискаПослеПолученияРазмера", ЭтотОбъект, ПараметрыОповещения));
	
КонецПроцедуры

// Выполняется в ходе создания файла с диска после подключения расширения.
//
// Параметры:
//   Подключено - Булево
//   ПараметрыОповещения - Структура
//
&НаКлиенте
Процедура СоздатьФайлСДискаПослеПодключенияРасширения(Подключено, ПараметрыОповещения) Экспорт
	
	Если Подключено Тогда
		
		ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ВыборФайла.МножественныйВыбор = Ложь;
		ВыборФайла.Заголовок = НСтр("ru = 'Выбор файла'");
		ВыборФайла.Фильтр = НСтр("ru = 'Все файлы (*.*)|*.*'");
		ВыборФайла.Показать(Новый ОписаниеОповещения(
			"СоздатьФайлСДискаПослеДиалогаВыбораФайла", ЭтотОбъект, ПараметрыОповещения));
		
	Иначе // веб-клиент без расширения
		
		НачатьПомещениеФайла(
			Новый ОписаниеОповещения(
				"СоздатьФайлСДискаПослеПомещенияФайлаБезРасширения",
				ЭтотОбъект,
				ПараметрыОповещения),,,
			Истина,
			УникальныйИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняется в ходе создания файла с диска после диалога выбора файла.
//
// Параметры:
//   ВыбранныеФайлы - Массив из Строка
//   ПараметрыОповещения - Структура
//
&НаКлиенте
Процедура СоздатьФайлСДискаПослеДиалогаВыбораФайла(ВыбранныеФайлы, ПараметрыОповещения) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайла = ВыбранныеФайлы[0];
	Файл = Новый Файл(ПолноеИмяФайла);
	
	ПараметрыОповещения.Вставить("ПолноеИмяФайла", ПолноеИмяФайла);
	ПараметрыОповещения.Вставить("Файл", Файл);
	Файл.НачатьПолучениеРазмера(Новый ОписаниеОповещения(
		"СоздатьФайлСДискаПослеПолученияРазмера", ЭтотОбъект, ПараметрыОповещения));
	
КонецПроцедуры

// Выполняется в ходе создания файла с диска после получения размера.
//
// Параметры:
//   Размер - Число
//   ПараметрыОповещения - Структура:
//     * ПолноеИмяФайла - Строка
//     * Файл - Файл
//
&НаКлиенте
Процедура СоздатьФайлСДискаПослеПолученияРазмера(Размер, ПараметрыОповещения) Экспорт
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.РазмерФайлаПревышаетМаксимальноДопустимый(
		МаксРазмерФайла,
		Размер,
		ПараметрыОповещения.ПолноеИмяФайла);
	
	ПараметрыОповещения.Вставить("Размер", Размер);
	ПараметрыОповещения.Файл.НачатьПолучениеВремениИзменения(Новый ОписаниеОповещения(
		"СоздатьФайлСДискаПослеПолученияВремениИзменения", ЭтотОбъект, ПараметрыОповещения));
	
КонецПроцедуры

// Выполняется в ходе создания файла с диска после получения времени изменения.
//
// Параметры:
//   ВремяИзменения - Дата
//   ПараметрыОповещения - Структура:
//     * ПолноеИмяФайла - Строка
//     * Файл - Файл
//     * Размер - Число
//
&НаКлиенте
Процедура СоздатьФайлСДискаПослеПолученияВремениИзменения(ВремяИзменения, ПараметрыОповещения) Экспорт
	
	ПараметрыОповещения.Вставить("ВремяИзменения", ВремяИзменения);
	ПараметрыОповещения.Файл.НачатьПолучениеУниверсальногоВремениИзменения(Новый ОписаниеОповещения(
		"СоздатьФайлСДискаПослеПолученияУниверсальногоВремениИзменения", ЭтотОбъект, ПараметрыОповещения));
	
КонецПроцедуры

// Выполняется в ходе создания файла с диска после получения универсального времени изменения.
//
// Параметры:
//   ВремяИзмененияУниверсальное - Дата
//   ПараметрыОповещения - Структура:
//     * ПолноеИмяФайла - Строка
//     * Файл - Файл
//     * Размер - Число
//     * ВремяИзменения - Дата
//
&НаКлиенте
Процедура СоздатьФайлСДискаПослеПолученияУниверсальногоВремениИзменения(ВремяИзмененияУниверсальное,
		ПараметрыОповещения) Экспорт
	
	ПараметрыОповещения.Вставить("ВремяИзмененияУниверсальное", ВремяИзмененияУниверсальное);
	ПомещаемыеФайлы = Новый Массив;
	ПомещаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(ПараметрыОповещения.ПолноеИмяФайла, ""));
	
	РазмерВМб = ПараметрыОповещения.Размер / (1024 * 1024);
	ТекстПояснения = СтрШаблон(НСтр("ru = 'Идет сохранение файла ""%1"" (%2 Мб).
			|Пожалуйста, подождите...'"),
		ПараметрыОповещения.ПолноеИмяФайла,
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ПолучитьСтрокуСРазмеромФайла(РазмерВМб));
	Состояние(ТекстПояснения);
	
	НачатьПомещениеФайлов(Новый ОписаниеОповещения(
		"СоздатьФайлСДискаПослеПомещенияФайла", ЭтотОбъект, ПараметрыОповещения),
		ПомещаемыеФайлы, , Ложь, УникальныйИдентификатор);
	
КонецПроцедуры

// Выполняется в ходе создания файла с диска после помещения файла (тонкий и веб с расширением).
//
// Параметры:
//   ПомещенныеФайлы - Массив из ОписаниеПереданногоФайла
//   ПараметрыОповещения - Структура:
//     * ПолноеИмяФайла - Строка
//     * Файл - Файл
//     * Размер - Число
//     * ВремяИзменения - Дата
//     * ВремяИзмененияУниверсальное - Дата
//
&НаКлиенте
Процедура СоздатьФайлСДискаПослеПомещенияФайла(ПомещенныеФайлы, ПараметрыОповещения) Экспорт
	
	Состояние();
	Если ПомещенныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения.Вставить("АдресВременногоХранилищаФайла", ПомещенныеФайлы[0].Хранение);
	
	НастройкиДокументооборот = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентПовтИсп.ПолучитьНастройки();
	Если НастройкиДокументооборот.НужноИзвлечьТекст Тогда
		ИзвлеченныйТекст = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ИзвлечьТекст(
			ПараметрыОповещения.ПолноеИмяФайла);
		ПараметрыОповещения.Вставить("Текст", ИзвлеченныйТекст);
	Иначе
		ПараметрыОповещения.Вставить("Текст", "");
	КонецЕсли;
	
	СоздатьФайлСДискаЗавершение(ПараметрыОповещения);
	
КонецПроцедуры

// Выполняется в ходе создания файла с диска после помещения файла (веб без расширения).
//
// Параметры:
//   Помещен - Булево
//   АдресВременногоХранилищаФайла - Строка
//   ВыбранноеИмяФайла - Строка
//   ПараметрыОповещения - Структура:
//     * ПолноеИмяФайла - Строка
//     * Файл - Файл
//     * Размер - Число
//     * ВремяИзменения - Дата
//     * ВремяИзмененияУниверсальное - Дата
//     * АдресВременногоХранилищаФайла - Строка
//     * Текст - Строка
//
&НаКлиенте
Процедура СоздатьФайлСДискаПослеПомещенияФайлаБезРасширения(Помещен, АдресВременногоХранилищаФайла,
		ВыбранноеИмяФайла, ПараметрыОповещения) Экспорт
	
	Если Не Помещен Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения.Вставить("АдресВременногоХранилищаФайла", АдресВременногоХранилищаФайла);
	ПараметрыОповещения.Вставить("ПолноеИмяФайла", ВыбранноеИмяФайла);
	ПараметрыОповещения.Вставить("Размер", 0);
	ПараметрыОповещения.Вставить("ВремяИзменения", Дата('00010101'));
	ПараметрыОповещения.Вставить("ВремяИзмененияУниверсальное", Дата('00010101'));
	ПараметрыОповещения.Вставить("Текст", "");
	
	СоздатьФайлСДискаЗавершение(ПараметрыОповещения);
	
КонецПроцедуры

// Выполняется в ходе создания файла с диска после помещения файла (универсальное завершение).
//
// Параметры:
//   ПараметрыОповещения - Структура:
//     * ПолноеИмяФайла - Строка
//     * Файл - Файл
//     * Размер - Число
//     * ВремяИзменения - Дата
//     * ВремяИзмененияУниверсальное - Дата
//     * АдресВременногоХранилищаФайла - Строка
//     * Текст - Строка
//
&НаКлиенте
Процедура СоздатьФайлСДискаЗавершение(ПараметрыОповещения)
	
	// Разберем полный путь на имя и расширение.
	СтруктураИмени = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.РазложитьПолноеИмяФайла(
		ПараметрыОповещения.ПолноеИмяФайла);
	ПараметрыОповещения.Вставить("Расширение", СтруктураИмени.Расширение);
	ПараметрыОповещения.Вставить("Имя", СтруктураИмени.ИмяБезРасширения);
	
	ПараметрыОповещения.Удалить("Файл");
	ИдентификаторСозданногоФайла = СоздатьФайлВПапкеИзФайлаНаДиске(ПараметрыОповещения);
	Состояние();
	
	ОбновитьСписокФайловКлиент(ИдентификаторСозданногоФайла);
	
	Оповестить("Запись_ДокументооборотФайл", Параметры, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция СоздатьФайлВПапкеИзФайлаНаДиске(ПараметрыСоздания)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMAddLinkedFileRequest");
	
	Запрос.rootFolderID = ИдентификаторКорневойПапки1СДокументооборот;
	
	Запрос.file = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
		Прокси,
		"DMFile",
		ВнешнийОбъект);
	
	Запрос.file.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(Прокси, "", "DMFile");
	Запрос.file.binaryData = ПолучитьИзВременногоХранилища(ПараметрыСоздания.АдресВременногоХранилищаФайла);
	Запрос.file.extension = ПараметрыСоздания.Расширение;
	Запрос.file.modificationDate = ПараметрыСоздания.ВремяИзменения;
	Запрос.file.modificationDateUniversal = ПараметрыСоздания.ВремяИзмененияУниверсальное;
	Запрос.file.name = ПараметрыСоздания.Имя;
	Запрос.file.size = ПараметрыСоздания.Размер;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.РазмерФайлаПревышаетМаксимальноДопустимый(
		МаксРазмерФайла,
		ПараметрыСоздания.Размер,
		ПараметрыСоздания.Имя);
	
	Если Не ПустаяСтрока(ПараметрыСоздания.Текст) Тогда
		Запрос.file.text = ПараметрыСоздания.Текст;
	КонецЕсли;
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	Возврат Ответ.file.objectID.ID;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////////
// Связи

// Открывает текущий связанный документ или его файл.
//
&НаКлиенте
Процедура ОткрытьСвязанныеДанныеСтроки(Строка)
	
	Если Строка.Тип = "DMRelationType" Тогда // объект ИС
		Возврат;
		
	ИначеЕсли Строка.Тип = "DMFile" Тогда // файл
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьФайл(
			Строка.ID,
			Строка.Заголовок,
			Строка.Расширение,
			УникальныйИдентификатор);
		
	Иначе // объект ДО
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(Строка.Тип, Строка.ID, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается после добавления связи и обновляет дерево связей.
//
&НаКлиенте
Процедура НачатьДобавлениеСвязиЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДеревоСвязей(, Результат.СвязанныйДокумент.ID);
	
	Если ЗначениеЗаполнено(АдресДляФайловСвязанныхДокументов) Тогда
		ПодключитьОбработчикОжидания("ОбновлениеФайловСвязанныхДокументов", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается после удаления связи и обновляет дерево связей.
//
&НаКлиенте
Процедура НачатьУдалениеСвязиЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДеревоСвязей();
	
	Если ЗначениеЗаполнено(АдресДляФайловСвязанныхДокументов) Тогда
		ПодключитьОбработчикОжидания("ОбновлениеФайловСвязанныхДокументов", 1, Истина);
	КонецЕсли;
		
КонецПроцедуры

// Обновляет данные связей документа, получая их заново, если необходимо.
//
// Параметры:
//   ОбъектXDTO - ОбъектXDTO - полученный из ДО документ, или
//              - Неопределено - признак необходимости получить его.
//   ДобавленныйДокумент - Строка - идентификатор документа, чьи файлы нужно обновить, или
//                       - Неопределено - признак необходимости обновить все файлы.
//
&НаСервере
Процедура ОбновитьДеревоСвязей(Знач ОбъектXDTO = Неопределено, ДобавленныйДокумент = Неопределено)
	
	Контрагент = "";
	КонтрагентID = "";
	Организация = "";
	ОрганизацияID = "";
	
	Если Не ЗначениеЗаполнено(ДокументID) Тогда
		ДеревоСвязей.ПолучитьЭлементы().Очистить();
		Элементы.ГруппаСвязанныеДокументы.Заголовок = НСтр("ru = 'Связи'");
		Возврат;
	КонецЕсли;
	
	Если ОбъектXDTO = Неопределено Тогда
		
		Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
		Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMRetrieveRequest");
		СписокОбъектов = Запрос.objectIDs; // СписокXDTO
		ПолучаемыеПоля = Запрос.columnSet; // СписокXDTO
		
		ОбъектИд = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(Прокси, ДокументID, ДокументТип);
		СписокОбъектов.Добавить(ОбъектИд);
		
		ПолучаемыеПоля.Добавить("relations");
		Если ИнтеграцияС1СДокументооборотКлиентСервер.ЭтоДокумент(ДокументТип) Тогда
			ПолучаемыеПоля.Добавить("correspondent");
			ПолучаемыеПоля.Добавить("organization");
		КонецЕсли;
		
		Результат = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Результат);
		ОбъектXDTO = Результат.objects[0];
		
		// Заполним организацию и контрагента для дальнейшего использования в отборах.
		Если ОбъектXDTO.Свойства().Получить("correspondent") <> Неопределено
			И ОбъектXDTO.Установлено("correspondent") Тогда
			Контрагент = ОбъектXDTO.correspondent.name;
			КонтрагентID = ОбъектXDTO.correspondent.objectID.ID;
		КонецЕсли;
		Если ОбъектXDTO.Свойства().Получить("organization") <> Неопределено
			И ОбъектXDTO.Установлено("organization") Тогда
			Организация = ОбъектXDTO.organization.name;
			ОрганизацияID = ОбъектXDTO.organization.objectID.ID;
		КонецЕсли;
		
	КонецЕсли;
	
	АдресДляФайловСвязанныхДокументов = Обработки.ИнтеграцияС1СДокументооборот.
		ЗаполнитьДеревоСвязейИНачатьПолучениеФайлов(
			ЭтотОбъект,
			ОбъектXDTO,
			Элементы.ГруппаСвязанныеДокументы,
			ДобавленныйДокумент);
	
КонецПроцедуры

// Обновляет файлы связанных документов данными временного хранилища, заполняемыми
// в асинхронном задании, откладывая обновление, если данные еще не готовы.
//
&НаКлиенте
Процедура ОбновлениеФайловСвязанныхДокументов()
	
	Если Не ЗначениеЗаполнено(АдресДляФайловСвязанныхДокументов) Тогда
		Возврат;
	КонецЕсли;
	
	// Сохраним текущее положение по ID объекта с учетом типа связи.
	ТекущиеДанные = Элементы.ДеревоСвязей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ТекущийID = Неопределено;
		ТекущийТип = Неопределено;
		ТекущийТипСвязи = Неопределено;
	Иначе
		ТекущийID = ТекущиеДанные.ID;
		ТекущийТип = ТекущиеДанные.Тип;
		Если ТекущийТип = "DMRelationType" Тогда
			СтрокаТипаСвязи = ТекущиеДанные;
		ИначеЕсли ТекущийТип = "DMFile" Тогда
			СтрокаТипаСвязи = ТекущиеДанные.ПолучитьРодителя().ПолучитьРодителя();
		Иначе
			СтрокаТипаСвязи = ТекущиеДанные.ПолучитьРодителя();
		КонецЕсли;
		ТекущийТипСвязи = СтрокаТипаСвязи.ID;
	КонецЕсли;
	
	Если ОбновлениеФайловСвязанныхДокументовСервер() Тогда // данные еще не готовы
		
		ПодключитьОбработчикОжидания("ОбновлениеФайловСвязанныхДокументов", 1, Истина);
		
	Иначе // данные получены, развернем дерево и восстановим позицию в нем
		
		СтрокиТипСвязи = ДеревоСвязей.ПолучитьЭлементы();
		Для Каждого СтрокаТипСвязи Из СтрокиТипСвязи Цикл
			
			Идентификатор = СтрокаТипСвязи.ПолучитьИдентификатор();
			Элементы.ДеревоСвязей.Развернуть(Идентификатор, Истина);
			
			Если ТекущийТип = Неопределено Тогда
				Продолжить;
				
			ИначеЕсли ТекущийТип = "DMRelationType" Тогда
				Если СтрокаТипСвязи.ID = ТекущийID Тогда
					Элементы.ДеревоСвязей.ТекущаяСтрока = Идентификатор;
				КонецЕсли;
				
			ИначеЕсли СтрокаТипСвязи.ID = ТекущийТипСвязи Тогда
				
				СтрокиДокумент = СтрокаТипСвязи.ПолучитьЭлементы();
				Для Каждого СтрокаДокумент Из СтрокиДокумент Цикл
					
					Если ТекущийТип = "DMFile" Тогда
						СтрокиФайл = СтрокиДокумент.ПолучитьЭлементы();
						Для Каждого СтрокаФайл Из СтрокиФайл Цикл
							Если СтрокаФайл.ID = ТекущийID Тогда
								Элементы.ДеревоСвязей.ТекущаяСтрока = СтрокаФайл.ПолучитьИдентификатор();
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
					ИначеЕсли СтрокаДокумент.ID = ТекущийID Тогда
						Элементы.ДеревоСвязей.ТекущаяСтрока = СтрокаДокумент.ПолучитьИдентификатор();
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Обновляет файлы связанных документов данными временного хранилища, если они готовы.
//
// Возвращаемое значение:
//   Булево - Истина, если требуется повторное обновление, и Ложь, если не требуется.
//
&НаСервере
Функция ОбновлениеФайловСвязанныхДокументовСервер()
	
	Результат = ПолучитьИзВременногоХранилища(АдресДляФайловСвязанныхДокументов);
	Если Результат = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Дерево = РеквизитФормыВЗначение("ДеревоСвязей");
	
	Для Каждого СвязанныйДокумент Из Результат Цикл
		
		ФайлыСвязанногоДокумента = СвязанныйДокумент.Файлы; // Массив из Структура
		
		СтруктураПоиска = Новый Структура("ID, Тип",
			СвязанныйДокумент.ID,
			СвязанныйДокумент.Тип);
		СтрокиДокументов = Дерево.Строки.НайтиСтроки(СтруктураПоиска, Истина);
		Для Каждого СтрокаДокумента Из СтрокиДокументов Цикл
			СтрокаДокумента.Строки.Очистить();
			Для Каждого Файл Из ФайлыСвязанногоДокумента Цикл
				СтрокаФайла = СтрокаДокумента.Строки.Добавить();
				СтрокаФайла.ID = Файл.ID;
				СтрокаФайла.Тип = Файл.Тип;
				СтрокаФайла.Заголовок = Файл.Наименование;
				СтрокаФайла.Расширение = Файл.Расширение;
				СтрокаФайла.Картинка =
					ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИндексПиктограммыФайла(СтрокаФайла.Расширение);
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоСвязей");
	
	Возврат Ложь;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////////
// История переписки

&НаСервере
Процедура ПолучитьЗаполнитьДеревоПисем()
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	СписокУсловий = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetCorrespondenceTreeQuery");
	ОбъектыПереписки = СписокУсловий.target; // СписокXDTO
	
	ПредметXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObject");
	ПредметXDTO.name = "";
	ПредметXDTO.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(Прокси, ДокументID, ДокументТип);
	ОбъектыПереписки.Добавить(ПредметXDTO);
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetCorrespondenceTreeRequest");
	Запрос.query = СписокУсловий;
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	Дерево = РеквизитФормыВЗначение("ДеревоПисем");
	Дерево.Строки.Очистить();
	Для Каждого Элемент Из Ответ.followers Цикл
		ЗаполнитьСтрокуДерева(Дерево.Строки, Элемент);
	КонецЦикла;
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПисем");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокуДерева(СтрокиДерева, СтрокаXDTO)
	
	СтрокаДерева = СтрокиДерева.Добавить();
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(
		СтрокаДерева,
		СтрокаXDTO.object,
		"Письмо");
	СтрокаДерева.Адресаты = СтрокаXDTO.recipients;
	СтрокаДерева.Дата = СтрокаXDTO.date;
	Если ДокументТип = "DMCorrespondent" Или ДокументТип = "DMOutgoingEMail" Тогда
		Если СтрокаXDTO.Свойства().Получить("regNumber") <> Неопределено Тогда
			СтрокаДерева.Номер = СтрокаXDTO.regNumber;
		КонецЕсли;
	КонецЕсли;
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(СтрокаXDTO, "sent") Тогда
		СтрокаДерева.Отправлен = СтрокаXDTO.sent;
	Иначе
		СтрокаДерева.Отправлен = Ложь;
	КонецЕсли;
	
	Если СтрокаДерева.ПисьмоТип = "DMIncomingEMail" Тогда
		СтрокаДерева.Картинка = 0;
	ИначеЕсли СтрокаДерева.ПисьмоТип = "DMOutgoingEMail" Тогда
		// заполнение свойства sent
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP") Тогда
			Если СтрокаДерева.Отправлен = Истина Тогда
				СтрокаДерева.Картинка = 1;
			Иначе
				СтрокаДерева.Картинка = 3;
			КонецЕсли;
		Иначе
			СтрокаДерева.Картинка = 1;
		КонецЕсли;
	ИначеЕсли ДокументТип = "DMCorrespondent" Тогда
		Если СтрокаДерева.ПисьмоТип = "DMInternalDocument" Тогда
			СтрокаДерева.Картинка = 2;
		ИначеЕсли СтрокаДерева.ПисьмоТип = "DMIncomingDocument" Тогда
			СтрокаДерева.Картинка = 0;
		Иначе
			Если СтрокаДерева.Отправлен Тогда
				СтрокаДерева.Картинка = 1;
			Иначе
				СтрокаДерева.Картинка = 3;
			КонецЕсли;
		КонецЕсли;
	Иначе
		СтрокаДерева.Картинка = 2;
	КонецЕсли;
	
	Для Каждого Элемент Из СтрокаXDTO.followers Цикл
		ЗаполнитьСтрокуДерева(СтрокаДерева.Строки, Элемент);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоПисем()
	
	ЭлементыДерева = ДеревоПисем.ПолучитьЭлементы();
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		Элементы.ДеревоПисем.Развернуть(ЭлементДерева.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПисьмоЕстьВСписке(ПисьмоID)
	
	ЭлементыДерева = ДеревоПисем.ПолучитьЭлементы();
	Возврат ПроверитьНаличиеЭлементаДерева(ЭлементыДерева,ПисьмоID);
	
КонецФункции

&НаКлиенте
Функция ПроверитьНаличиеЭлементаДерева(ЭлементыДерева, ПисьмоID)
	
	ПисьмоСуществует = Ложь;
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		Если ЭлементДерева.ПисьмоID = ПисьмоID Тогда 
			ПисьмоСуществует = Истина;
			Возврат ПисьмоСуществует;
		Иначе
			Подчиненные = ЭлементДерева.ПолучитьЭлементы();
		    ПисьмоСуществует = ПроверитьНаличиеЭлементаДерева(Подчиненные, ПисьмоID)
		КонецЕсли;
	КонецЦикла;

	Возврат ПисьмоСуществует;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////////
// Трудозатраты

&НаСервере
Процедура ЗаполнитьТрудозатраты()
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	СписокУсловий = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListQuery");
	УсловияОтбора = СписокУсловий.conditions; // СписокXDTO
	
	Условие = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "source";
	
	Условие.value = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(Прокси, ДокументID, ДокументТип);
	
	УсловияОтбора.Добавить(Условие);
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.НайтиСписокОбъектов(
		Прокси,
		"DMActualWork",
		СписокУсловий);
	
	Работы.Очистить();
	
	Для Каждого СтрокаОтвета Из Ответ.items Цикл
		
		Строка = СтрокаОтвета.object;
		
		НоваяСтрока = Работы.Добавить();
		НоваяСтрока.ДатаДобавления = Строка.addDate;
		НоваяСтрока.Начало = Строка.begin;
		НоваяСтрока.Окончание = Строка.end;
		НоваяСтрока.Работа = Строка.description;
		НоваяСтрока.Длительность = Строка.duration / 3600;
		НоваяСтрока.ДлительностьСтр = ИнтеграцияС1СДокументооборотКлиентСервер.ЧислоВСтроку(
			НоваяСтрока.Длительность);
		
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(
			НоваяСтрока, Строка.workType, "ВидРабот", Ложь);
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(
			НоваяСтрока, Строка.project,"Проект", Ложь);
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(
			НоваяСтрока, Строка.projectTask,"ПроектнаяЗадача", Ложь);
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(
			НоваяСтрока, Строка.source, "Источник", Ложь);
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(
			НоваяСтрока, Строка.user, "Пользователь", Ложь);
		
		НоваяСтрока.ПроектЗадача = ИнтеграцияС1СДокументооборотКлиентСервер.ПредставлениеПроектаЗадачи(
			НоваяСтрока.Проект, НоваяСтрока.ПроектнаяЗадача);
		
	КонецЦикла;
		
	Элементы.РаботыДлительность.ТекстПодвала = Формат(Работы.Итог("Длительность"),"ЧЦ=15; ЧДЦ=2; ЧН=0,00");
	
	Работы.Сортировать("ДатаДобавления");
	
КонецПроцедуры

#КонецОбласти