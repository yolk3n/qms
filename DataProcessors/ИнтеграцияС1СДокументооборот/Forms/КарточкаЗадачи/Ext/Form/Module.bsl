#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	HTMLПредставление = ПолучитьHTMLПредставлениеФормыЗадачи(Параметры.Type, Параметры.ID)
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура HTMLПредставлениеПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ДанныеСобытия.Anchor <> Неопределено И ЗначениеЗаполнено(ДанныеСобытия.Anchor.href) Тогда
		
		МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ДанныеСобытия.Anchor.href, "/");
		
		СтрокаСсылка = МассивПодстрок[МассивПодстрок.Количество() - 1];
		
		МассивПараметров = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСсылка, "$");
		
		ОбъектТип = МассивПараметров[0];
		ОбъектID = МассивПараметров[1];
		
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(ОбъектТип, ОбъектID, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьБизнесПроцесс(Команда)
	
	ПараметрыБП = ПолучитьПараметрыБизнесПроцесса();
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(ПараметрыБП.type, ПараметрыБП.ID, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьHTMLПредставлениеФормыЗадачи(Тип, Ид)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	ДанныеЗадачи = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
		Прокси,
		Тип,
		Ид,
		"HTMLView");
	ID = ДанныеЗадачи.objectID.ID;
	Тип = ДанныеЗадачи.objectID.type;
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Задача ""%1""'"), ДанныеЗадачи.name);
	Возврат ДанныеЗадачи.htmlView;
	
КонецФункции

&НаСервере
Функция ПолучитьПараметрыБизнесПроцесса()
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	ДанныеЗадачи = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
		Прокси,
		"DMBusinessProcessTask",
		ID);
	
	ПараметрыБизнесПроцесса = Новый Структура;
	ПараметрыБизнесПроцесса.Вставить("ID", ДанныеЗадачи.parentBusinessProcess.objectID.ID);
	ПараметрыБизнесПроцесса.Вставить("type", ДанныеЗадачи.parentBusinessProcess.objectID.type);
	
	Возврат ПараметрыБизнесПроцесса;
	
КонецФункции

#КонецОбласти
