#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Файлы. Доступность отдельных команд настраивается при активизации файла в дереве предметов.
	ДоступенЗахватФайлов = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.4.9.1");
	// Мультипредметность.
	ДоступнаМультипредметность = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.0.3.1");
	Элементы.ДобавитьПредмет.Видимость = ДоступнаМультипредметность;
	Элементы.ФормаДобавитьПредмет.Видимость = ДоступнаМультипредметность;
	Элементы.ДобавитьПредметИзДекорации.Видимость = ДоступнаМультипредметность;
	Элементы.УдалитьПредмет.Видимость = ДоступнаМультипредметность;
	Элементы.ФормаУдалитьПредмет.Видимость = ДоступнаМультипредметность;
	// Почта.
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.2.8.1.CORP") Тогда
		Элементы.ФормаСоздатьПисьмо.Видимость = Ложь;
	КонецЕсли;
	// Решение вопросов выполнения задач.
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.2.7.3") Тогда
		Элементы.ЗадатьВопросАвтору.Видимость = Ложь;
	КонецЕсли;
	// Хронометраж.
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP") Тогда
		Элементы.ФормаУказатьТрудозатраты.Видимость = Ложь;
		Элементы.ФормаПереключитьХронометраж.Видимость = Ложь;
	КонецЕсли;
	// Принятие задач к исполнению.
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.2.7.3.CORP") Тогда
		Элементы.ФормаПринятьКИсполнению.Видимость = Ложь;
	КонецЕсли;
	// Отмена принятия задач к исполнению.
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP") Тогда
		Элементы.ФормаОтменитьПринятиеКИсполнению.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.РезультатВыполнения.ПодсказкаВвода = НСтр("ru = 'Комментарий'");
	
	ИмяПользователя = ПараметрыСеанса.ИнтеграцияС1СДокументооборотИмяПользователя;
	
	НастройкиДокументооборота = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьНастройки();
	ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи =
		НастройкиДокументооборота.ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи;
	ФактическийИсполнительЗадач = НастройкиДокументооборота.ФактическийИсполнительЗадач;
	Если ФактическийИсполнительЗадач <> "taskPerformer" Тогда
		ТекущийПользовательИСотрудники =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ТекущийПользовательДокументооборота();
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(
			ЭтотОбъект, ТекущийПользовательИСотрудники[0], "ТекущийПользователь");
	КонецЕсли;
	
	Элементы.ФормаИзменитьДатуВыполнения.Видимость = НастройкиДокументооборота.ИзменятьЗаданияЗаднимЧислом;
	
	ЗаполнитьФормуЗадачи(Параметры);
	УстановитьПризнакЗадачаПросрочена();
	УстановитьОформлениеЗадач(УсловноеОформление);
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ДополнительнаяОбработкаФормыЗадачи(
		ЭтотОбъект,
		Отказ,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбработатьФормуПоНаличиюПредметов();
	УстановитьДоступностьКомандДереваПриложений();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстПредупреждения = "";
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы,,ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметры, Источник)
	
	Если ИмяСобытия = "Запись_ДокументооборотДокумент"
			И ТипЗнч(Параметры) = Тип("Структура")
			И Параметры.Свойство("ID")
			И Параметры.Свойство("type") Тогда
		ОбработкаОповещенияЗаписьПредмета(Параметры.ID, Параметры.type);
		
	ИначеЕсли ИмяСобытия = "Запись_ДокументооборотФайл"
			И ТипЗнч(Параметры) = Тип("Структура")
			И Параметры.Свойство("ИдентификаторФайла") Тогда
		ОбработкаОповещенияЗаписьПредмета(Параметры.ИдентификаторФайла, "DMFile");
		
	ИначеЕсли ИмяСобытия = "Запись_ДокументооборотТрудозатраты" И Источник = ID Тогда
		ВключенХронометраж = Ложь;
		ДатаНачалаХронометража = '00010101';
		УстановитьСвойстваЭлементовХронометражаСервер();
		
	ИначеЕсли ИмяСобытия = "Документооборот_ДобавлениеСвязи" Тогда
		ЭлементыДерева = ДеревоПриложений.ПолучитьЭлементы();
		Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
			Если ЭлементДерева.ПредметID = Параметры.ID Тогда
				ЭлементДерева.Ссылка = Параметры.Объект;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ИмяСобытия = "Документооборот_УдалениеСвязи" Тогда
		ЭлементыДерева = ДеревоПриложений.ПолучитьЭлементы();
		Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
			Если ЭлементДерева.ПредметID = Параметры.ID Тогда
				ЭлементыДерева.Удалить(ЭлементДерева);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ИмяСобытия = "Документооборот_ДействиеНадЗадачей" Тогда
		Если ТипЗнч(Параметры) = Тип("Структура")
				И Параметры.Свойство("ИмяОперации")
				И Параметры.Свойство("ID")
				И Параметры.Свойство("Тип")
				И Параметры.ID = ID
				И Параметры.Тип = Тип
				И Параметры.ИмяОперации = "Перенаправить" Тогда
			Закрыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаИсполненияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИсполненияВремяПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатВыполненияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура БизнесПроцессПредметаРассмотренияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(
		БизнесПроцессПредметаРассмотренияТип, БизнесПроцессПредметаРассмотренияID, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметРассмотренияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ID", ПредметРассмотренияID);
	ПараметрыФормы.Вставить("type", ПредметРассмотренияТип);
	
	ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.Задача", ПараметрыФормы, ЭтотОбъект, ПредметРассмотренияID);
	
КонецПроцедуры

&НаКлиенте
Процедура HTMLПредставлениеПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Найти(ДанныеСобытия.Href, "ИзменитьСрокВыполнения") Тогда //@NON-NLS-1
		СогласоватьПереносСрока(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПредметыИФайлыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДобавитьПредмет(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйСрокДатаПриИзменении(Элемент)
	
	ДлительностьПереноса = ИнтеграцияС1СДокументооборотВызовСервера.ПодписьДлительностиПереносаСрока(
		Автор,
		АвторID,
		АвторТип,
		СтарыйСрок,
		НовыйСрок);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйСрокВремяПриИзменении(Элемент)
	
	ДлительностьПереноса = ИнтеграцияС1СДокументооборотВызовСервера.ПодписьДлительностиПереносаСрока(
		Автор,
		АвторID,
		АвторТип,
		СтарыйСрок,
		НовыйСрок);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсполнители

&НаКлиенте
Процедура ИсполнителиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ИсполнителиКомментарийПроверяющего" 
		Или Поле.Имя = "ИсполнителиОтправленоНаДоработку" Тогда
		Возврат;
	Иначе
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(Исполнители[ВыбраннаяСтрока].ЗадачаID) Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ID", Исполнители[ВыбраннаяСтрока].ЗадачаID);
			ПараметрыФормы.Вставить("type", Исполнители[ВыбраннаяСтрока].ЗадачаТип);
			ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.Задача", ПараметрыФормы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиОтправленоНаДоработкуПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиКомментарийПроверяющегоПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоПриложений

&НаКлиенте
Процедура ДеревоПриложенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Строка = Элемент.ТекущиеДанные;
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Строка.Ссылка) Тогда // объект ИС
		ПоказатьЗначение(, Строка.Ссылка);
		
	ИначеЕсли Строка.ПредметТип = "DMFile" Тогда // файл
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьФайл(
			Строка.ПредметID,
			Строка.Предмет,
			Строка.Расширение,
			УникальныйИдентификатор);
		
	Иначе // объект ДО
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(Строка.ПредметТип, Строка.ПредметID, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийПриАктивизацииСтроки(Элемент)
	
	УстановитьДоступностьКомандДереваПриложений();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСвойства

&НаКлиенте
Процедура СвойстваЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыбратьЗначениеДополнительногоРеквизита(
		ЭтотОбъект,
		Элемент,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Свойства.ТекущиеДанные;
	Если ТекущиеДанные.СписокДоступныхТипов.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
		
	Если ТипЗнч(ТекущиеДанные.Значение) = Тип("Строка") Тогда
		ТипXDTO =ТекущиеДанные.СписокДоступныхТипов[0].Значение.xdtoClassName;
		Если ТипXDTO = "integer" Тогда
			ТекущиеДанные.Значение = 0;
		ИначеЕсли ТипXDTO = "boolean" Тогда
			ТекущиеДанные.Значение = Ложь;
		ИначеЕсли ТипXDTO = "date" Тогда
			ТекущиеДанные.Значение = Дата(1, 1, 1);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СвойстваЗначениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Свойства.ТекущиеДанные;
	Если ТекущиеДанные.СписокДоступныхТипов.Количество() <> 1 Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
		
	ТипXDTO = ТекущиеДанные.СписокДоступныхТипов[0].Значение.xdtoClassName;
	
	Если ТипXDTO = "integer"
		Или ТипXDTO = "boolean"
		Или ТипXDTO = "date"
		Или ТипXDTO = "string" Тогда
		Возврат;
	КонецЕсли;
		
	Если ТипXDTO = "DMObjectPropertyValue" Тогда
		ДополнительноеСвойство = Новый Структура;
		ДополнительноеСвойство.Вставить("ID", ТекущиеДанные.СвойствоID);
		ДополнительноеСвойство.Вставить("type", ТекущиеДанные.СвойствоТип);
		Отбор = Новый Структура("additionalProperty", ДополнительноеСвойство);
	Иначе
		Отбор = Неопределено;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ДанныеДляАвтоПодбора(
		ТипXDTO, ДанныеВыбора, Текст, СтандартнаяОбработка, Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваЗначениеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Свойства.ТекущиеДанные;
	Если ТекущиеДанные.СписокДоступныхТипов.Количество() <> 1 Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ТипXDTO = ТекущиеДанные.СписокДоступныхТипов[0].Значение.xdtoClassName;
	
	Если ТипXDTO = "integer"
			Или ТипXDTO = "boolean"
			Или ТипXDTO = "string"
			Или ТипXDTO = "date" Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипXDTO = "DMObjectPropertyValue" Тогда
		ДополнительноеСвойство = Новый Структура;
		ДополнительноеСвойство.Вставить("ID", ТекущиеДанные.СвойствоID);
		ДополнительноеСвойство.Вставить("type", ТекущиеДанные.СвойствоТип);
		Отбор = Новый Структура("additionalProperty", ДополнительноеСвойство);
	Иначе
		Отбор = Неопределено;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ДанныеДляАвтоПодбора(
		ТипXDTO, ДанныеВыбора, Текст, СтандартнаяОбработка, Отбор);
	
	Если ДанныеВыбора.Количество() = 1 Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
			"Значение", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтотОбъект, Истина, Элемент);
		СтандартнаяОбработка = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Значение", ВыбранноеЗначение, СтандартнаяОбработка, ЭтотОбъект, Истина, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваЗначениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элементы.Свойства.ТекущиеДанные.Значение = "";
	Элементы.Свойства.ТекущиеДанные.ЗначениеID = "";
	Элементы.Свойства.ТекущиеДанные.ЗначениеТип = "";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЭтапы

&НаКлиенте
Процедура ЭтапыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.Этапы.ТекущиеДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(Элементы.Этапы.ТекущиеДанные.ЗапущенныйБизнесПроцессID) Тогда
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(
				Элементы.Этапы.ТекущиеДанные.ЗапущенныйБизнесПроцессТип,
				Элементы.Этапы.ТекущиеДанные.ЗапущенныйБизнесПроцессID, ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьЗадачуПервая(Команда)
	
	Если ПроцессТип = "DMBusinessProcessRegistration" Тогда
		Если ЭтоТочкаМаршрута("Зарегистрировать") Тогда //@NON-NLS-1
			Для Каждого СтрокаПредмета Из ДеревоПриложений.ПолучитьЭлементы() Цикл
				Если (Не ДоступнаМультипредметность Или СтрокаПредмета.РольПредмета = "Основной") //@NON-NLS-1
					И ИнтеграцияС1СДокументооборотКлиентСервер.ЭтоДокумент(СтрокаПредмета.ПредметТип) Тогда
					ИнтеграцияС1СДокументооборотВызовСервера.
						ЗарегистрироватьДокументПриНеобходимости(СтрокаПредмета.ПредметТип, СтрокаПредмета.ПредметID);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Тип = "DMBusinessProcessIssuesSolutionTaskQuestion" Тогда
		Если ВидВопросаID = "Иное" И Не ЗначениеЗаполнено(РезультатВыполнения) Тогда //@NON-NLS-1
			ОчиститьСообщения();
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Комментарий'")),,
				"РезультатВыполнения");
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ПроцессТип = "DMBusinessProcessConsideration"
			И ТочкаМаршрута = "Рассмотреть" И Не ЗначениеЗаполнено(РезультатВыполнения) Тогда //@NON-NLS-2
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле ""Резолюция"" не заполнено'"),,
			"РезультатВыполнения");
		Возврат;
	КонецЕсли;
	
	Если ПроцессТип = "DMBusinessProcessInvitation" И РезультатID = "ПринятоОбязательнымиУчастниками" Тогда //@NON-NLS-2
		Оповещение = Новый ОписаниеОповещения("ВыполнитьЗадачуПерваяЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Есть не принявшие приглашения участники.
			|При подтверждении они будут исключены из списка участников. Продолжить?'");
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПоказатьВопросДаНет(Оповещение, ТекстВопроса,,, КодВозвратаДиалога.Да);
	Иначе
		ВыполнитьЗадачуОповеститьЗакрытьФорму(1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗадачуВторая(Команда)
	
	Если (ПроцессТип = "DMBusinessProcessApproval" И ЭтоТочкаМаршрута("Согласовать")) //@NON-NLS-2
			Или (ПроцессТип = "DMBusinessProcessInvitation" И ЭтоТочкаМаршрута("Пригласить")) Тогда //@NON-NLS-2
		Если Не ЗначениеЗаполнено(РезультатВыполнения) Тогда
			ОчиститьСообщения();
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Комментарий'")),,
				"РезультатВыполнения");
			Возврат;
		КонецЕсли;
		
	ИначеЕсли Тип = "DMBusinessProcessIssuesSolutionTaskAnswer" Тогда
		Если Не ЗначениеЗаполнено(РезультатВыполнения) Тогда
			ОчиститьСообщения();
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'При отправке на уточнение требуется указать комментарий.'"),,
				"РезультатВыполнения");
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ПроцессТип = "DMBusinessProcessConfirmation" И ЭтоТочкаМаршрута("Утвердить") Тогда //@NON-NLS-2
		Если Не ЗначениеЗаполнено(РезультатВыполнения) Тогда
			ОчиститьСообщения();
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Укажите причину отклонения документа'"),,
				"РезультатВыполнения");
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ПроцессТип = "DMBusinessProcessRegistration" И ЭтоТочкаМаршрута("Зарегистрировать") Тогда //@NON-NLS-2
		Если Не ЗначениеЗаполнено(РезультатВыполнения) Тогда
			ОчиститьСообщения();
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Укажите причину отказа в регистрации'"),,
				"РезультатВыполнения");
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ПроцессТип = "DMBusinessProcessIssuesSolution" И ЭтоТочкаМаршрута("РассмотрениеИнициатором") Тогда //@NON-NLS-2
		Если ВидВопросаID = "ПереносСрока" И Не ЗначениеЗаполнено(РезультатВыполнения) Тогда //@NON-NLS-1
			ОчиститьСообщения();
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Комментарий'")),,
				"РезультатВыполнения");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ID, type, taskID", ПроцессID, ПроцессТип, ID);
	Оповещение = Новый ОписаниеОповещения("ВыполнитьЗадачуВтораяЗавершение", ЭтотОбъект);
	
	Если ПроцессТип = "DMBusinessProcessApproval" И ЭтоТочкаМаршрута("Ознакомиться") Тогда //@NON-NLS-2
		ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.БизнесПроцессСогласование",
			ПараметрыФормы,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ПроцессТип = "DMBusinessProcessConfirmation" И ЭтоТочкаМаршрута("Ознакомиться") Тогда //@NON-NLS-2
		ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.БизнесПроцессУтверждение",
			ПараметрыФормы,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ПроцессТип = "DMBusinessProcessRegistration" И ЭтоТочкаМаршрута("Ознакомиться") Тогда //@NON-NLS-2
		ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.БизнесПроцессРегистрация",
			ПараметрыФормы,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ПроцессТип = "DMBusinessProcessInvitation" И ЭтоТочкаМаршрута("Ознакомиться") Тогда //@NON-NLS-2
		ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.БизнесПроцессПриглашение",
			ПараметрыФормы,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		ВыполнитьЗадачуОповеститьЗакрытьФорму(2);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗадачуТретья(Команда)
	
	Если ПроцессТип = "DMBusinessProcessApproval" Тогда
		Если Не ЗначениеЗаполнено(РезультатВыполнения) Тогда
			ОчиститьСообщения();
			ОбщегоНазначенияКлиент.СообщитьПользователю(
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Комментарий'")),,
			"РезультатВыполнения");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьЗадачуОповеститьЗакрытьФорму(3);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если Модифицированность Тогда
		Если ЗаписатьОбъект() Тогда
			Модифицированность = Ложь;
			ПараметрыОповещения = Новый Структура("name, ID, type", Наименование, ID, Тип);
			Оповестить("Запись_ДокументооборотЗадача", ПараметрыОповещения, ВладелецФормы);
			Закрыть();
		КонецЕсли;
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВыполнить(Команда)
	
	Если Модифицированность Тогда
		Если ЗаписатьОбъект() Тогда
			Модифицированность = Ложь;
			ПараметрыОповещения = Новый Структура("name, ID, type", Наименование, ID, Тип);
			Оповестить("Запись_ДокументооборотЗадача", ПараметрыОповещения, ВладелецФормы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточку(Команда)
	
	Строка = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Строка.Ссылка) Тогда // объект ИС
		ПоказатьЗначение(, Строка.Ссылка);
		
	Иначе // объект ДО
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(Строка.ПредметТип, Строка.ПредметID,
			ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлДляПросмотра(Команда)
	
	Строка = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если Строка <> Неопределено И Строка.ПредметТип = "DMFile" Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьФайл(
			Строка.ПредметID,
			Строка.Предмет,
			Строка.Расширение,
			УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗадачу(Команда)
	
	ВыбраннаяСтрока = Элементы.Исполнители.ТекущаяСтрока;
	
	Если ЗначениеЗаполнено(Исполнители[ВыбраннаяСтрока].ЗадачаID) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ID", Исполнители[ВыбраннаяСтрока].ЗадачаID);
		ПараметрыФормы.Вставить("type", Исполнители[ВыбраннаяСтрока].ЗадачаТип);
		ПараметрыФормы.Вставить("ТочкаМаршрута",Исполнители[ВыбраннаяСтрока].ТочкаМаршрута);
		ПараметрыФормы.Вставить("ТипПроцесса",ПроцессТип);
			
		ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.Задача", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьБизнесПроцесс(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(ПроцессТип, ПроцессID, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОсновнойПредмет(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(
		ОсновнойПредметТип,
		ОсновнойПредметID,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Перенаправить(Команда)
	
	Если Модифицированность Тогда
		ЗаписатьОбъект();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Задача", Наименование);
	ПараметрыФормы.Вставить("ЗадачаID", ID);
	ПараметрыФормы.Вставить("ЗадачаТип", Тип);
	ОткрытьФорму(
		"Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.ПеренаправлениеЗадачи",
		ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьКИсполнению(Команда)
	
	ПринятаКИсполнению = ПринятьЗадачуКИсполнениюНаСервере();
	
	Если ПринятаКИсполнению Тогда
		Состояние(НСтр("ru = 'Задача принята к исполнению'"));
		Элементы.ФормаПринятьКИсполнению.Доступность = Ложь;
		Элементы.ФормаОтменитьПринятиеКИсполнению.Доступность = Истина;
		ПараметрыОповещения = Новый Структура("name, ID, type", Наименование, ID, Тип);
		Оповестить("Запись_ДокументооборотЗадача", ПараметрыОповещения, ВладелецФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьПринятиеКИсполнению(Команда)
	
	Если ОтменитьПринятиеКИсполнениюНаСервере() Тогда
		ПринятаКИсполнению = Ложь;
		Состояние(НСтр("ru = 'Задача помечена как не принятая к исполнению'"));
		Элементы.ФормаПринятьКИсполнению.Доступность = Истина;
		Элементы.ФормаОтменитьПринятиеКИсполнению.Доступность = Ложь;
		ПараметрыОповещения = Новый Структура("name, ID, type", Наименование, ID, Тип);
		Оповестить("Запись_ДокументооборотЗадача", ПараметрыОповещения, ВладелецФормы);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик привязывается к программно создаваемым командам.
//
// Параметры:
//   Команда - КомандаФормы
//
&НаКлиенте
Процедура Подключаемый_СоздатьСвязанныйОбъект(Команда)
	
	// Создать_Справочник_Контрагенты__1 -> Справочник.Контрагенты_1
	ИмяКоманды = Команда.Имя;
	ИмяТипа = Сред(ИмяКоманды, 9);
	ИмяТипа = СтрЗаменить(ИмяТипа, "___", "~");
	ИмяТипа = СтрЗаменить(ИмяТипа, "__", "&");
	ИмяТипа = СтрЗаменить(ИмяТипа, "_", ".");
	ИмяТипа = СтрЗаменить(ИмяТипа, "&", "_");
	ИмяТипа = СтрЗаменить(ИмяТипа, "~", "._");
	
	Если ДоступнаМультипредметность Тогда
		Предметы = ДеревоПриложений.ПолучитьЭлементы();
		Для Каждого СтрокаПредмета Из Предметы Цикл
			Если СтрокаПредмета.ПредметТип <> "DMFile" Тогда
				ИнтеграцияС1СДокументооборотКлиент.СоздатьИнтегрированныйОбъектПоТипу(
					ЭтотОбъект, ИмяТипа, СтрокаПредмета.ПредметТип, СтрокаПредмета.ПредметID);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		ИнтеграцияС1СДокументооборотКлиент.СоздатьИнтегрированныйОбъектПоТипу(
			ЭтотОбъект, ИмяТипа, ПредметТип, ПредметID);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПисьмо(Команда)
	
	ПараметрыФормы = Новый Структура("Предмет", Новый Структура);
	
	ПараметрыФормы.Предмет.Вставить("name", Наименование);
	ПараметрыФормы.Предмет.Вставить("ID", ID);
	ПараметрыФормы.Предмет.Вставить("type", Тип);
	
	ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.ИсходящееПисьмо", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьВопросАвтору(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ID", ID);
	ПараметрыФормы.Вставить("type", Тип);
	ПараметрыФормы.Вставить("ВидВопроса", НСтр("ru = 'Иное'"));
	ПараметрыФормы.Вставить("ВидВопросаID", "Иное"); //@NON-NLS-2
	
	ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.БизнесПроцессРешениеВопросовНовыйВопрос",
		ПараметрыФормы, ЭтотОбъект, ID);
	
КонецПроцедуры

&НаКлиенте
Процедура СогласоватьПереносСрока(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ID", ID);
	ПараметрыФормы.Вставить("type", Тип);
	ПараметрыФормы.Вставить("ВидВопроса", НСтр("ru = 'Перенос срока'"));
	ПараметрыФормы.Вставить("ВидВопросаID", "ПереносСрока"); //@NON-NLS-2
	
	ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.БизнесПроцессРешениеВопросовНовыйВопрос",
		ПараметрыФормы, ЭтотОбъект, ID);
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьТрудозатраты(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Источник", Представление);
	ПараметрыФормы.Вставить("ИсточникID", ID);
	ПараметрыФормы.Вставить("ИсточникТип", Тип);
	
	ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.ДобавлениеРаботы", ПараметрыФормы, ID);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометраж(Команда)
	
	НуженДиалог = Истина;
	ДлительностьРаботы = ИнтеграцияС1СДокументооборотКлиент.ПолучитьДлительностьРаботы(ДатаНачалаХронометража);
	Если ДлительностьРаботы < 60 Тогда // меньше 1 минуты
		НуженДиалог = Ложь;
	КонецЕсли;
	
	Если НуженДиалог Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Источник", Представление);
		ПараметрыФормы.Вставить("ИсточникID", ID);
		ПараметрыФормы.Вставить("ИсточникТип", Тип);
		
		ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.ДобавлениеРаботы", ПараметрыФормы, ID);
	Иначе
		Если ВключенХронометраж Тогда
			ПереключитьХронометражСервер();
			Если ВключенХронометраж Тогда
				Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Включен хронометраж по ""%1""'"), Строка(Представление));
				Состояние(Текст);
			КонецЕсли;
		Иначе 
			АктивныеЗаписи = ИнтеграцияС1СДокументооборотВызовСервера.АктивныеЗаписиХронометража();
			Если АктивныеЗаписи.Количество() = 0 Тогда
				ПереключитьХронометражСервер();
				Если ВключенХронометраж Тогда
					Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Включен хронометраж по ""%1""'"), Строка(Представление));
					Состояние(Текст);
				КонецЕсли;
			Иначе
				ПараметрыОповещения = Новый Структура;
				ПараметрыОповещения.Вставить("АктивныеЗаписи", АктивныеЗаписи);
				
				Оповещение = Новый ОписаниеОповещения("ПереключитьХронометражЗавершение", ЭтотОбъект, ПараметрыОповещения);
				СтрокаОбъектовХронометража = "";
				Для Каждого Запись Из АктивныеЗаписи Цикл 
					СтрокаОбъектовХронометража = СтрокаОбъектовХронометража + Символы.Таб + Запись.Источник + Символы.ПС;
				КонецЦикла;
				ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Включен хронометраж по другим объектам:
						|%1
						|Отключить хронометраж и зафиксировать трудозатраты перед включением нового хронометража?'"),
						СтрокаОбъектовХронометража);
				Кнопки = Новый СписокЗначений;
				Кнопки.Добавить(КодВозвратаДиалога.Да,НСтр("ru='Да'"));
				Кнопки.Добавить(КодВозвратаДиалога.Отмена,НСтр("ru='Отмена'"));
				
				ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, 60, КодВозвратаДиалога.Отмена);
				
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДатуВыполнения(Команда)
	
	ПоказатьВводДаты(
		Новый ОписаниеОповещения("ПослеВводаДатаИсполнения", ЭтотОбъект),
		ДатаИсполнения,
		НСтр("ru = 'Укажите дату фактического выполнения задачи'"),
		ЧастиДаты.ДатаВремя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаДатаИсполнения(Дата, Параметры) Экспорт
	
	Если Дата <> Неопределено Тогда
		ДатаИсполнения = Дата;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
	Если ЗаписатьОбъект() Тогда
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ID", ПроцессID);
		ПараметрыОповещения.Вставить("Стартован", Истина);
		
		// Соберем предметы.
		Предметы = Новый Массив;
		Если ДоступнаМультипредметность Тогда
			ЭлементыДерева = ДеревоПриложений.ПолучитьЭлементы();
			Для Каждого СтрокаПредмета Из ЭлементыДерева Цикл
				Если СтрокаПредмета.РольПредмета = "Основной" Тогда //@NON-NLS-1
					ОписаниеПредмета = Новый Структура;
					ОписаниеПредмета.Вставить("ID", СтрокаПредмета.ПредметID);
					ОписаниеПредмета.Вставить("Тип", СтрокаПредмета.ПредметТип);
					Предметы.Добавить(ОписаниеПредмета);
				КонецЕсли;
			КонецЦикла;
		Иначе // один предмет
			ОписаниеПредмета = Новый Структура;
			ОписаниеПредмета.Вставить("ID", ПредметID);
			ОписаниеПредмета.Вставить("Тип", ПредметТип);
			Предметы.Добавить(ОписаниеПредмета);
		КонецЕсли;
		ПараметрыОповещения.Вставить("Предметы", Предметы);
		
		Оповестить("Запись_ДокументооборотБизнесПроцесс", ПараметрыОповещения, ВладелецФормы);
		
		ПараметрыОповещения = Новый Структура("name, ID, type", Представление, ID, Тип);
		Оповестить("Запись_ДокументооборотЗадача", ПараметрыОповещения, ВладелецФормы);
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометражЗавершение(РезультатВыбора, ПараметрыОповещения) Экспорт
	
	Если РезультатВыбора = КодВозвратаДиалога.Да Тогда
		
		Записи = ИнтеграцияС1СДокументооборотВызовСервера.ПереключитьХронометражПоОбъектамДокументооборота(ПараметрыОповещения.АктивныеЗаписи);
		
		Для Каждого Запись Из Записи Цикл
			ПараметрыОповещения = Новый Структура;
			ПараметрыОповещения.Вставить("name", Запись.Источник);
			ПараметрыОповещения.Вставить("ID", Запись.ИсточникID);
			ПараметрыОповещения.Вставить("type", Запись.ИсточникТип);
			Оповестить("Запись_ДокументооборотТрудозатраты", ПараметрыОповещения, Запись.ИсточникID);
		КонецЦикла;
	
		ПереключитьХронометражСервер();
		Если ВключенХронометраж Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Включен хронометраж по ""%1""'"), Строка(Представление));
			Состояние(Текст);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗадачуПерваяЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыполнитьЗадачуОповеститьЗакрытьФорму(1);
	ИначеЕсли ТипЗнч(Ответ) = Тип("Структура") И Ответ.Успешно = Истина Тогда
		РезультатВыполнения = Ответ.Комментарий;
		ВыполнитьЗадачуОповеститьЗакрытьФорму(1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗадачуВтораяЗавершение(Параметр, ПараметрыОповещения) Экспорт
	
	Если ТипЗнч(Параметр) = Тип("Булево") И Параметр = Истина Тогда
		ВыполнитьЗадачуОповеститьЗакрытьФорму(2);
	ИначеЕсли ТипЗнч(Параметр) = Тип("Структура") И Параметр.Успешно = Истина Тогда
		РезультатВыполнения = Параметр.Комментарий;
		ВыполнитьЗадачуОповеститьЗакрытьФорму(2);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовХронометражаСервер()
	
	Обработки.ИнтеграцияС1СДокументооборот.УстановитьСвойстваЭлементовХронометража(
		ВключенХронометраж, Команды.ПереключитьХронометраж, Элементы.ФормаПереключитьХронометраж);
		
КонецПроцедуры

&НаСервере
Процедура ПереключитьХронометражСервер()
	
	Обработки.ИнтеграцияС1СДокументооборот.ПереключитьХронометраж(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФормуЗадачи(ПараметрыЗадачи)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	ОбъектXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
		Прокси,
		ПараметрыЗадачи.type,
		ПараметрыЗадачи.ID,
		"withDependentObjects");
	
	Обработки.ИнтеграцияС1СДокументооборотБазоваяФункциональность.УстановитьНавигационнуюСсылку(ЭтотОбъект, ОбъектXDTO);
	Обработки.ИнтеграцияС1СДокументооборот.ЗаполнитьПараметрыХронометража(ЭтотОбъект, ОбъектXDTO);
	
	// Заполним общие реквизиты задач.
	ID = ОбъектXDTO.objectID.ID;
	Тип = ОбъектXDTO.objectID.type;
	Представление = ОбъектXDTO.objectID.presentation;
	Наименование = ОбъектXDTO.Name;
	Заголовок = ОбъектXDTO.objectID.presentation;
	Выполнена = ОбъектXDTO.executed;
	ПринятаКИсполнению = ОбъектXDTO.accepted;
	Дата = ОбъектXDTO.beginDate;
	Номер = ОбъектXDTO.number;
	СрокИсполнения = ОбъектXDTO.dueDate;
	РезультатВыполнения = ОбъектXDTO.executionComment;
	ДатаИсполнения = ОбъектXDTO.endDate;
	Описание = ОбъектXDTO.description;
	
	Если ОбъектXDTO.Свойства().Получить("project") <> Неопределено И ОбъектXDTO.Установлено("project") Тогда
		Проект = ОбъектXDTO.project.name;
	КонецЕсли;
	Если ОбъектXDTO.Свойства().Получить("projectTask") <> Неопределено И ОбъектXDTO.Установлено("projectTask") Тогда
		ПроектнаяЗадача = ОбъектXDTO.projectTask.name;
	КонецЕсли;
	
	Элементы.ГруппаПроект.Видимость = (ЗначениеЗаполнено(Проект) Или ЗначениеЗаполнено(ПроектнаяЗадача));
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект, ОбъектXDTO.importance,"Важность");
	Если ОбъектXDTO.Свойства().Получить("state") <> Неопределено Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект, ОбъектXDTO.state, "Состояние");
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
		ОбъектXDTO.parentBusinessProcess, "Процесс", Истина);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект, ОбъектXDTO.author, "Автор");
	ЗаполнитьДанныеАдресации(ЭтотОбъект, ОбъектXDTO.performer);
	ТочкаМаршрута = ИнтеграцияС1СДокументооборот.КраткоеИмяТочкиМаршрута(ПроцессТип, ОбъектXDTO.businessProcessStep);
	
	ПроцессОбъект = ОбъектXDTO.parentBusinessProcess;
	
	// Заполним дерево приложений.
	Если ПроцессТип = "DMBusinessProcessIssuesSolution" Тогда
		ЗаполнитьДеревоПриложенийПоФайлам(ОбъектXDTO.files);
	ИначеЕсли ДоступнаМультипредметность Тогда
		ЗаполнитьДеревоПриложенийПоПредметам(ОбъектXDTO.targets.items);
	ИначеЕсли ОбъектXDTO.Установлено("target") Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(
			ЭтотОбъект, ОбъектXDTO.target, "Предмет");
		ПоддержкаСовместимости_ЗаполнитьПроцессПредметЗадачи(
			Прокси, ОбъектXDTO, ОбъектXDTO.target, ПроцессОбъект);
		ЗаполнитьДеревоПриложенийПоПредмету(ОбъектXDTO.target);
		ЗаполнитьКомандыСозданияПоПредмету(ОбъектXDTO.target);
	КонецЕсли;
	
	ОсновнойПредметТип = "";
	ОсновнойПредметID = "";
	
	Для Каждого Элемент Из ДеревоПриложений.ПолучитьЭлементы() Цикл
		Если Элемент.РольПредмета = "Основной" Тогда //@NON-NLS-1
			ОсновнойПредметТип = Элемент.ПредметТип;
			ОсновнойПредметID = Элемент.ПредметID;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ФормаОткрытьОсновнойПредмет.Доступность = ЗначениеЗаполнено(ОсновнойПредметТип)
		И ЗначениеЗаполнено(ОсновнойПредметID);
	
	Обработки.ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПоместитьДополнительныеРеквизитыНаФорму(
		ЭтотОбъект,
		ОбъектXDTO);
	
	// Определим возможность выполнения и остановки.
	Если Не (Выполнена
			Или ОбъектXDTO.executionMark = "ReadyToExecute"
			Или СостояниеID = "Остановлен" //@NON-NLS-2
			Или СостояниеID = "Прерван") Тогда //@NON-NLS-1
		ДатаИсполнения = ТекущаяДатаСеанса();
		ДоступноВыполнение = Истина;
	Иначе
		Элементы.ТекстРезультатаВыполнения.Видимость = Истина;
		ДоступноВыполнение = Ложь;
	КонецЕсли;
	Если СостояниеID = "Остановлен" Тогда //@NON-NLS-1
		Элементы.ГруппаСведенияОЗадаче.Видимость = Истина;
		Элементы.ТекстСостояние.Заголовок = НСтр("ru = 'Задача остановлена'");
		КартинкаСостояние = 1;
	ИначеЕсли СостояниеID = "Прерван" Тогда //@NON-NLS-1
		Элементы.ГруппаСведенияОЗадаче.Видимость = Истина;
		Элементы.ТекстСостояние.Заголовок = НСтр("ru = 'Задача прервана'");
		КартинкаСостояние = 2;
	ИначеЕсли ОбъектXDTO.executionMark = "ReadyToExecute" Тогда
		Элементы.ГруппаСведенияОЗадаче.Видимость = Истина;
		Элементы.ТекстСостояние.Заголовок = НСтр(
			"ru = 'Задача находится в очереди для выполнения. Выполнение задачи произойдет автоматически в ближайшее время.'");
		КартинкаСостояние = 9;
	ИначеЕсли ОбъектXDTO.executionMark = "ExecutionCanceled" Тогда
		Элементы.ГруппаСведенияОЗадаче.Видимость = Истина;
		Элементы.ТекстСостояние.Заголовок = НСтр(
			"ru = 'Ошибка при выполнении задачи'");
		КартинкаСостояние = 10;
	Иначе
		Элементы.ГруппаСведенияОЗадаче.Видимость = Ложь;
	КонецЕсли;
	
	// Заполним описание.
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ОбъектXDTO, "htmlView") Тогда
		Элементы.ГруппаОписание.ТекущаяСтраница = Элементы.ГруппаHTMLПредставление;
		HTMLПредставление = ОбъектXDTO.htmlView;
	Иначе
		Элементы.ГруппаОписание.ТекущаяСтраница = Элементы.ГруппаТекстовоеОписание;
		ЗаполнитьПолноеОписаниеЗадачи(ПроцессОбъект, ОбъектXDTO);
	КонецЕсли;
	
	// Настроим видимость и заполним реквизиты, специфичные для точки маршрута.
	Элементы.РезультатВыполнения.ТолькоПросмотр = Не ДоступноВыполнение;
	Элементы.ДатаИсполнения.Видимость = Не ДоступноВыполнение;
	Элементы.ДатаИсполненияВремя.Видимость = Не ДоступноВыполнение;
	Элементы.ДатаИсполнения.ТолькоПросмотр = Не ДоступноВыполнение;
	Элементы.ДатаИсполненияВремя.ТолькоПросмотр = Не ДоступноВыполнение;
	Элементы.ГруппаНовыйСрок.ТолькоПросмотр = Не ДоступноВыполнение;
	
	// Заполним реквизиты, специфичные для типа процесса.
	Если ПроцессТип = "DMBusinessProcessApproval" Тогда
		ЗаполнитьДанныеБПСогласование(ОбъектXDTO);
	ИначеЕсли ПроцессТип = "DMBusinessProcessConfirmation" Тогда
		ЗаполнитьДанныеБПУтверждение(ОбъектXDTO);
	ИначеЕсли ПроцессТип = "DMBusinessProcessRegistration" Тогда
		ЗаполнитьДанныеБПРегистрация(ОбъектXDTO);
	ИначеЕсли ПроцессТип = "DMBusinessProcessConsideration" Тогда
		ЗаполнитьДанныеБПРассмотрение(ОбъектXDTO);
	ИначеЕсли ПроцессТип = "DMBusinessProcessOrder" Тогда
		ЗаполнитьДанныеБППоручение(Прокси, ОбъектXDTO);
	ИначеЕсли ПроцессТип = "DMBusinessProcessPerformance" Тогда
		ЗаполнитьДанныеБПИсполнение(Прокси, ОбъектXDTO);
	ИначеЕсли ПроцессТип = "DMComplexBusinessProcess" Тогда
		ЗаполнитьДанныеБПКомплексныйПроцесс(Прокси, ПроцессОбъект);
	ИначеЕсли ПроцессТип = "DMBusinessProcessAcquaintance" Тогда
		ЗаполнитьДанныеБПОзнакомление();
	ИначеЕсли ПроцессТип = "DMBusinessProcessIssuesSolution" Тогда
		ЗаполнитьДанныеБПРешениеВопросов(ОбъектXDTO);
	ИначеЕсли ПроцессТип = "DMBusinessProcessInvitation" Тогда
		ЗаполнитьДанныеБППриглашение(ОбъектXDTO);
	Иначе
		ВывестиИнформациюОНедоступностиВыполненияЗадачи();
	КонецЕсли;
	
	// Обработаем доступность задачи для изменения.
	РазрешеноИзменение =
		?(ОбъектXDTO.Установлено("changeRight"), ОбъектXDTO.changeRight, Ложь)
			И Не (ОбъектXDTO.executionMark = "ReadyToExecute")
			И Не (СостояниеID = "Остановлен") //@NON-NLS-1
			И Не (СостояниеID = "Прерван") //@NON-NLS-1
			И Не Выполнена;
	ТолькоПросмотр = Не РазрешеноИзменение;
	Если Не РазрешеноИзменение Или Не ДоступнаМультипредметность Тогда // уберем гиперссылку "Добавить..."
		Элементы.ДекорацияПредметыИФайлы.Заголовок = НСтр("ru = 'У задачи нет предметов.'");
	КонецЕсли;
	Элементы.ФормаПринятьКИсполнению.Доступность = Не ПринятаКИсполнению И РазрешеноИзменение;
	Элементы.ФормаОтменитьПринятиеКИсполнению.Доступность = ПринятаКИсполнению И РазрешеноИзменение;
	Элементы.ФормаПеренаправить.Доступность = РазрешеноИзменение;
	Элементы.ФормаДобавитьПредмет.Доступность = РазрешеноИзменение;
	Элементы.ФормаУдалитьПредмет.Доступность = РазрешеноИзменение;
	Элементы.ДобавитьПредмет.Доступность = РазрешеноИзменение;
	Элементы.УдалитьПредмет.Доступность = РазрешеноИзменение;
	Элементы.ДобавитьПредметИзДекорации.Доступность = РазрешеноИзменение;
	Если ПроцессТип = "DMBusinessProcessIssuesSolution" Тогда
		Элементы.ЗадатьВопросАвтору.Видимость = Ложь;
		Элементы.ФормаСогласоватьПереносСрока.Видимость = Ложь;
	Иначе
		Элементы.ЗадатьВопросАвтору.Видимость = Истина;
		Элементы.ФормаСогласоватьПереносСрока.Видимость = Не Выполнена
			И ЗначениеЗаполнено(ОбъектXDTO.dueDate)
			И ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоддержкаСовместимости_ЗаполнитьПроцессПредметЗадачи(Прокси, ОбъектXDTO, ПредметОбъект, ПроцессОбъект)
	
	// заполняются ли сразу предмет и процесс задачи?
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.2.7.3") Тогда
		
		МассивОбъектов = Новый Массив;
		МассивКолонок = Новый Массив;
		
		МассивОбъектов.Добавить(
			Новый Структура("ID, Тип",
				ОбъектXDTO.parentBusinessProcess.ObjectID.ID,
				ОбъектXDTO.parentBusinessProcess.ObjectID.type));
		МассивКолонок.Добавить("name");
		МассивКолонок.Добавить("objectID");
		МассивКолонок.Добавить("checkResults");
		МассивКолонок.Добавить("executionComment");
		
		Если ОбъектXDTO.Установлено("target") Тогда
			Если Найти(ОбъектXDTO.target.objectID.type,"Document") <> 0 Тогда
				МассивОбъектов.Добавить(
					Новый Структура("ID, Тип", ОбъектXDTO.target.ObjectID.ID, ОбъектXDTO.target.ObjectID.type));
				МассивКолонок.Добавить("files");
				МассивКолонок.Добавить("externalObject");
			ИначеЕсли ОбъектXDTO.target.objectID.type = "DMFile" Тогда
				МассивОбъектов.Добавить(
					Новый Структура("ID, Тип", ОбъектXDTO.target.ObjectID.ID, ОбъектXDTO.target.ObjectID.type));
				МассивКолонок.Добавить("extension");
				МассивКолонок.Добавить("modificationDateUniversal");
				МассивКолонок.Добавить("size");
			КонецЕсли;
		КонецЕсли;
		
		СписокОбъектов = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъекты(
			Прокси,
			МассивОбъектов,
			МассивКолонок);
		
		ПроцессОбъект = СписокОбъектов[0];
		
		Если СписокОбъектов.Количество() > 1 Тогда
			ПредметОбъект = СписокОбъектов[1];
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Для БП "Решение вопросов выполнения задач" дерево приложений заполняется по файлам, а не по предметам.
//
&НаСервере
Процедура ЗаполнитьДеревоПриложенийПоФайлам(ФайлыXDTO)
	
	Дерево = РеквизитФормыВЗначение("ДеревоПриложений");
	
	ДополнитьДеревоПриложенийФайлами(Дерево.Строки, ФайлыXDTO, Истина);
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПриложений");
	
КонецПроцедуры

// Для поддерживающих мультипредметность версий веб-сервиса.
//
&НаСервере
Процедура ЗаполнитьДеревоПриложенийПоПредметам(СтрокиПредметовXDTO)
	
	Дерево = РеквизитФормыВЗначение("ДеревоПриложений");
	Дерево.Строки.Очистить();
	
	Для Каждого СтрокаПредметаXDTO Из СтрокиПредметовXDTO Цикл
		
		СтрокаПредмета = Дерево.Строки.Добавить();
		
		СтрокаПредмета.ИмяПредмета = СтрокаПредметаXDTO.name;
		СтрокаПредмета.РольПредмета = СтрокаПредметаXDTO.role.objectID.ID;
		СтрокаПредмета.ДоступноУдаление = СтрокаПредметаXDTO.allowDeletion;
		
		ЗаполнитьСтрокуПредметаЕгоСвойствами(СтрокаПредмета, СтрокаПредметаXDTO.target);
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПриложений");
	
КонецПроцедуры

// Для версий веб-сервиса, не поддерживающих мультипредметность.
//
&НаСервере
Процедура ЗаполнитьДеревоПриложенийПоПредмету(ПредметXDTO)
	
	Дерево = РеквизитФормыВЗначение("ДеревоПриложений");
	Дерево.Строки.Очистить();
	
	СтрокаПредмета = Дерево.Строки.Добавить();
	ЗаполнитьСтрокуПредметаЕгоСвойствами(СтрокаПредмета, ПредметXDTO);
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПриложений");
	
КонецПроцедуры

// Заполняет команды создания по предмету.
//
&НаСервере
Процедура ЗаполнитьКомандыСозданияПоПредмету(ПредметXDTO)
	
	Если ПредметXDTO.objectID.type = "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураКоманд = ИнтеграцияС1СДокументооборотВызовСервера.СтруктураКомандСозданияИОткрытия(
		ПредметXDTO.objectID.type, ПредметXDTO.objectID.ID);
	
	ОбработанныеТипы = Новый Массив;
	
	Для Каждого КомандаСоздания Из СтруктураКоманд.КомандыСоздания Цикл
		
		Если ОбработанныеТипы.Найти(КомандаСоздания.Тип) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Представление = КомандаСоздания.Представление;
		
		ИмяКоманды = СтрЗаменить(КомандаСоздания.Тип, "_", "__");
		ИмяКоманды = СтрЗаменить(ИмяКоманды, ".", "_");
		ИмяКоманды = "Создать_" + ИмяКоманды;
		
		Если Команды.Найти(ИмяКоманды) = Неопределено Тогда
			КомандаСоздатьСвязанныйОбъект = Команды.Добавить(ИмяКоманды);
			КомандаСоздатьСвязанныйОбъект.Действие = "Подключаемый_СоздатьСвязанныйОбъект";
		Иначе
			Продолжить;
		КонецЕсли;
		
		Кнопка = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Элементы.СоздатьНаОсновании);
		Кнопка.Заголовок = Представление;
		Кнопка.ИмяКоманды = ИмяКоманды;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет строку дерева приложений по предмету XDTO.
//
&НаСервере
Процедура ЗаполнитьСтрокуПредметаЕгоСвойствами(СтрокаПредмета, ПредметXDTO)
	
	СтрокаПредмета.Предмет = ПредметXDTO.name;
	СтрокаПредмета.ПредметID = ПредметXDTO.objectID.ID;
	СтрокаПредмета.ПредметТип = ПредметXDTO.objectID.type;
	
	ЗаполнитьКомандыСозданияПоПредмету(ПредметXDTO);
	
	Если СтрокаПредмета.ПредметТип = "DMFile" Тогда
		СтрокаПредмета.Расширение = ПредметXDTO.extension;
		СтрокаПредмета.Редактируется = ПредметXDTO.editing;
		Если ПредметXDTO.Установлено("editingUser") Тогда
			ТекущийПользовательИСотрудники =
				ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ТекущийПользовательДокументооборота();
			СтрокаПредмета.РедактируетсяТекущимПользователем =
				ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ФайлРедактируетсяТекущимПользователем(
					ПредметXDTO.editingUser.objectID.ID,
					ПредметXDTO.editingUser.objectID.type,
					ТекущийПользовательИСотрудники);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьКартинкуПредмета(СтрокаПредмета);
	ДополнитьСтрокуПредметаСвязаннымОбъектомИС(СтрокаПредмета, ПредметXDTO);
	ДополнитьДеревоПриложенийФайламиПредмета(СтрокаПредмета);
	
КонецПроцедуры

// Устанавливает картинку предмета по его роли или по расширению файла.
//
&НаСервере
Процедура УстановитьКартинкуПредмета(СтрокаПредмета)
	
	Если СтрокаПредмета.ПредметТип = "DMFile" Тогда
		// Для файла - по расширению.
		СтрокаПредмета.Картинка =
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИндексПиктограммыФайла(СтрокаПредмета.Расширение);
	Иначе
		// Для прочих типов - по роли.
		СтрокаПредмета.Картинка = ИнтеграцияС1СДокументооборотКлиентСервер.КартинкаПоРолиПредмета(
			СтрокаПредмета.РольПредмета);
	КонецЕсли;
	
КонецПроцедуры

// Дополняет строку сведениями об объекте ИС, меняя представление.
&НаСервере
Процедура ДополнитьСтрокуПредметаСвязаннымОбъектомИС(СтрокаПредмета, ПредметXDTO)
	
	// Найдем связанный объект ИС.
	Если ПредметXDTO.Установлено("externalObject") Тогда
		СсылкиПредметов = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СсылкиПоВнешнимОбъектам(ПредметXDTO);
		Если СсылкиПредметов.Количество() <> 0 Тогда
			СтрокаПредмета.Ссылка = СсылкиПредметов[0];
		КонецЕсли;
	КонецЕсли;
	// Представление, как в ДО.
	Представление = СтрокаПредмета.Предмет;
	
	Если СтрокаПредмета.ПредметТип <> "DMFile" Тогда
		Если ЗначениеЗаполнено(СтрокаПредмета.ИмяПредмета) Тогда // мультипредметность, имя известно
			Представление = Представление + " (" + СтрокаПредмета.ИмяПредмета + ")";
		Иначе // без мультипредметности, дадим пояснение согласно типу
			ДополнитьИмяПредметаПоТипуОбъекта(Представление, СтрокаПредмета.ПредметТип);
		КонецЕсли;
		Представление = Представление + НСтр("ru = ', документ ДО'");
	КонецЕсли;
	
	// Заменим наименование предмета в ДО его представлением в ИС. NB: возможна ситуация, когда права в ДО
	// есть, а в ИС нет. В этом случае форма задачи должна открываться нормально, а пользователь увидит
	// ошибку "Нарушение прав доступа" лишь при попытке открыть предмет в ИС.
	Попытка
		Если ЗначениеЗаполнено(СтрокаПредмета.Ссылка) Тогда // представление как в ИС
			СокращенноеНаименованиеКонфигурации = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СокращенноеНаименованиеКонфигурации();
			Если ЗначениеЗаполнено(СокращенноеНаименованиеКонфигурации) Тогда
				ПредставлениеОбъекта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = ', объект %1'"), СокращенноеНаименованиеКонфигурации);
			Иначе
				ПредставлениеОбъекта = НСтр("ru = ', объект ИС'");
			КонецЕсли;
			СтрокаПредмета.Представление = Строка(СтрокаПредмета.Ссылка) + ПредставлениеОбъекта;
		Иначе // представление как в ДО
			СтрокаПредмета.Представление = Представление;
		КонецЕсли;
	Исключение
		СтрокаПредмета.Представление = Представление;
	КонецПопытки;
	
КонецПроцедуры

// Получает вызовом сервиса файлы предмета и дополняет ими дерево приложений.
//
&НаСервере
Процедура ДополнитьДеревоПриложенийФайламиПредмета(СтрокаПредмета)
	
	Если СтрокаПредмета.ПредметТип = "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	СписокФайлов = ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ФайлыПоВладельцу(
		СтрокаПредмета.ПредметID,
		СтрокаПредмета.Предмет,
		СтрокаПредмета.ПредметТип);
	ДополнитьДеревоПриложенийФайлами(СтрокаПредмета.Строки, СписокФайлов.files);
	
КонецПроцедуры

// Дополняет дерево приложений переданной коллекцией файлов.
//
&НаСервере
Процедура ДополнитьДеревоПриложенийФайлами(СтрокиДерева, ФайлыXDTO, ДоступноУдаление = Ложь)
	
	СтрокиДерева.Очистить();
	
	Для Каждого ФайлXDTO Из ФайлыXDTO Цикл
		СтрокаФайла = СтрокиДерева.Добавить();
		СтрокаФайла.Предмет = ФайлXDTO.name;
		СтрокаФайла.ПредметТип = ФайлXDTO.objectID.type;
		СтрокаФайла.ПредметID = ФайлXDTO.objectID.ID;
		СтрокаФайла.Расширение = ФайлXDTO.extension;
		
		ПометкаУдаления = Ложь;
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ФайлXDTO, "deletionMark") Тогда
			ПометкаУдаления = ФайлXDTO.deletionMark;
		КонецЕсли;
		СтрокаФайла.Картинка =
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИндексПиктограммыФайла(
				СтрокаФайла.Расширение,
				ПометкаУдаления);
		
		Если ДоступенЗахватФайлов Тогда
			СтрокаФайла.Редактируется = ФайлXDTO.editing;
			Если ФайлXDTO.Установлено("editingUser") Тогда
				СтрокаФайла.РедактируетсяТекущимПользователем =
					(ВРег(ФайлXDTO.editingUser.name) = ВРег(ИмяПользователя));
			КонецЕсли;
		КонецЕсли;
		СтрокаФайла.Представление = СтрокаФайла.Предмет;
		СтрокаФайла.ДоступноУдаление = ДоступноУдаление;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЭтоТочкаМаршрута(ИмяТочкиМаршрута)
	
	Если ТочкаМаршрута = ИмяТочкиМаршрута Тогда
		Возврат Истина
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеБПСогласование(ЗадачаОбъект)
	
	// Заполнение номера итерации
	НомерИтерации = ЗадачаОбъект.iterationNumber;
	Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;

	Если ЭтоТочкаМаршрута("Согласовать") Тогда //@NON-NLS-1
		
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Согласовано'");
		Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Согласовано
															|с замечаниями'");
		Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		Элементы.ВыполнитьЗадачуТретья.Заголовок = НСтр("ru='Не согласовано'");
		
		Элементы.ГруппаЦикл.Видимость = Истина;
		
		// Заполнение результата согласования задачи
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
			ЗадачаОбъект.approvalResult, "Результат");
		
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = Строка(Результат) + ".";
			Если РезультатID = "Согласовано" Или РезультатID = "СогласованоСЗамечаниями" Тогда //@NON-NLS-1 @NON-NLS-2
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			ИначеЕсли РезультатID = "НеСогласовано" Тогда //@NON-NLS-1
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ЭтоТочкаМаршрута("Ознакомиться") Тогда //@NON-NLS-1
		
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Ознакомился'");
		Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Повторить согласование...'");
		Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		
		// Заполнение результата согласования
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
			ЗадачаОбъект.approvalResult, "Результат");
		ОтправленоНаПовторноеСогласование = ЗадачаОбъект.returned;
		
		// Заполнение листа согласования
		Для Каждого СтрокаРезультата Из ЗадачаОбъект.approvalResults Цикл
			СтрИсполнения = Исполнители.Добавить();
			СтрИсполнения.РезультатВыполнения = СтрокаРезультата.approvalComment;
			СтрИсполнения.ДатаИсполнения = СтрокаРезультата.approvalDate;
			СтрИсполнения.КартинкаСостояния = 1;
			ЗаполнитьДанныеАдресации(СтрИсполнения, СтрокаРезультата.approvalPerformer);
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(
				СтрИсполнения,
				СтрокаРезультата.approvalResult,
				"Результат");
		КонецЦикла;
		
		Элементы.ГруппаСогласование.Видимость = Истина;
		Элементы.Исполнители.Видимость = Истина;
		Элементы.ИсполнителиИсполнитель.Заголовок = НСтр("ru = 'Согласующее лицо'");
		Элементы.ИсполнителиДатаИсполнения.Видимость = Ложь;
		Элементы.ИсполнителиДатаСогласования.Видимость = Истина;
		Элементы.ИсполнителиРезультат.Видимость = Истина;
		Элементы.РезультатВыполнения.Видимость = Ложь;
		Элементы.РезультатВыполненияСкрытый.Видимость = Истина;
		Элементы.Исполнители.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;

		Если (РезультатID = "Согласовано") Или (РезультатID = "СогласованоСЗамечаниями") Тогда //@NON-NLS-1 @NON-NLS-2
			Элементы.РезультатСогласования.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		ИначеЕсли РезультатID = "НеСогласовано" Тогда //@NON-NLS-1
			Элементы.РезультатСогласования.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		КонецЕсли;
		
		// Заголовки кнопок
		Если РезультатID = "НеСогласовано" Тогда //@NON-NLS-1
			Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Завершить согласование'");
		Иначе	
			Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		КонецЕсли;
		
		// Заполнение текста результата выполнения для выполненной задачи
		Если ОтправленоНаПовторноеСогласование <> Неопределено Тогда
			Если ОтправленоНаПовторноеСогласование Тогда
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Отправлено на повторное согласование.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			Иначе	
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Ознакомился.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НомерИтерации <= 1 Тогда 
		Элементы.НомерИтерацииСогласования.Видимость = Ложь;
		Элементы.НомерИтерацииСогласование.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеБПУтверждение(ЗадачаОбъект)
	
	Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
	
	// Заполнение номера итерации
	НомерИтерации = ЗадачаОбъект.iterationNumber;
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоСуществует(ЗадачаОбъект.parentBusinessProcess, "processType") Тогда
		ЭтоПодписание = (ЗадачаОбъект.parentBusinessProcess.processType.objectID.ID = "Подписание"); //@NON-NLS-1
	Иначе
		ЭтоПодписание = Ложь;
	КонецЕсли;
	
	Если ЭтоТочкаМаршрута("Утвердить") Тогда //@NON-NLS-1
		
		Если ЭтоПодписание Тогда
			Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Подписать'");
			Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Отклонить'");
		Иначе
			Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Утверждено'");
			Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Не утверждено'");
		КонецЕсли;
		Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		
		Элементы.ГруппаЦикл.Видимость = Истина;
		
		// Заполнение результата утверждения
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
			ЗадачаОбъект.confirmationResult, "Результат");
		
		Если Выполнена Тогда
			Если ЭтоПодписание Тогда
				Если РезультатID = "Утверждено" Тогда //@NON-NLS-1
					Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru='Подписано.'");
				Иначе
					Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru='Не подписано.'");
				КонецЕсли;
			Иначе
				Элементы.ТекстРезультатаВыполнения.Заголовок = Строка(Результат) + ".";
			КонецЕсли;
			Если РезультатID = "Утверждено" Тогда //@NON-NLS-1
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			ИначеЕсли РезультатID = "НеУтверждено" Тогда //@NON-NLS-1
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ЭтоТочкаМаршрута("Ознакомиться") Тогда //@NON-NLS-1
		
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Ознакомился'");
		Если ЭтоПодписание Тогда
			Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Повторить подписание...'");
			Элементы.РезультатУтверждения.Заголовок = НСтр("ru='Результат подписания'");
			Элементы.Утверждающий.Заголовок = НСтр("ru='Подписант'");
			Элементы.ДатаУтверждения.Заголовок = НСтр("ru='Дата подписания'");
		Иначе
			Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Повторить утверждение...'");
		КонецЕсли;
		Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		
		ОтправленоНаПовторноеУтверждение = ЗадачаОбъект.returned;
		
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
			ЗадачаОбъект.confirmationResult, "Результат");
		Если ЭтоПодписание Тогда
			Если РезультатID = "Утверждено" Тогда //@NON-NLS-1
				Результат = НСтр("ru='Подписано'");
			Иначе
				Результат = НСтр("ru='Не подписано'");
			КонецЕсли;
		КонецЕсли;
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
			ЗадачаОбъект.confirmationPerformer.user, "Утверждающий");
		ДатаУтверждения = ЗадачаОбъект.confirmationDate;
		РезультатВыполненияУтверждения = ЗадачаОбъект.confirmationComment;
		
		Элементы.ГруппаУтверждение.Видимость = Истина;
		Элементы.РезультатВыполнения.Видимость = Ложь;
		Элементы.РезультатВыполненияСкрытый.Видимость = Истина;
		
		Если (РезультатID = "Утверждено") Тогда //@NON-NLS-1
			Элементы.РезультатУтверждения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		ИначеЕсли РезультатID = "НеУтверждено" Тогда //@NON-NLS-1
			Элементы.РезультатУтверждения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		КонецЕсли;
		
		// Заголовки кнопок
		Если РезультатID = "НеУтверждено" Тогда //@NON-NLS-1
			Если ЭтоПодписание Тогда
				Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Завершить подписание'");
			Иначе
				Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Завершить утверждение'");
			КонецЕсли;
		Иначе
			Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		КонецЕсли;
		
		// Заполнение текста результата выполнения для выполненной задачи
		Если ОтправленоНаПовторноеУтверждение <> Неопределено Тогда
			Если ОтправленоНаПовторноеУтверждение Тогда
				Если ЭтоПодписание Тогда
					Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Отправлено на повторную подпись.'");
				Иначе
					Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Отправлено на повторное утверждение.'");
				КонецЕсли;
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			Иначе
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Ознакомился.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаНейтральногоВыполненияЗадачи;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НомерИтерации <= 1 Тогда
		Элементы.НомерИтерацииУтверждения.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеБППоручение(Прокси, ЗадачаОбъект)
	
	Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
	
	Если ЭтоТочкаМаршрута("Выполнить") Тогда //@NON-NLS-1
		
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Выполнено'");
		Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Выполнено.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		КонецЕсли;
		
	ИначеЕсли ЭтоТочкаМаршрута("Проверить") Тогда //@NON-NLS-1
		
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Завершить поручение'");
		Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Вернуть на доработку'");
		Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		
		// Заполнение результата выполнения 
		Если ЗадачаОбъект.Установлено("returned") Тогда
			ВозвращеноНаДоработку = ЗадачаОбъект.returned;
		Иначе
			ВозвращеноНаДоработку = Ложь;
		КонецЕсли;
		
		// Заполнение текста результата выполнения для выполненной задачи
		Если Выполнена Тогда
			Если ВозвращеноНаДоработку Тогда
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Возвращено на доработку.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			Иначе
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Ознакомился.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ЭтоТочкаМаршрута("Контролировать") Тогда //@NON-NLS-1
		
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Снять с контроля'");
		Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Снято с контроля.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		КонецЕсли;
		
		Элементы.Исполнители.Видимость = Истина;
		Элементы.Исполнители.ПодчиненныеЭлементы.ИсполнителиСрокИсполнения.Видимость = Истина;
		Элементы.Исполнители.ПодчиненныеЭлементы.ИсполнителиВыполнена.Видимость = Истина;
		
		СписокЗадач = ПолучитьЗадачиБизнесПроцесса(Прокси);
		Для Каждого Задача Из СписокЗадач Цикл
			Если Найти(Задача.businessProcessStep, "Контролировать") <> 0 Тогда  //@NON-NLS-1
				СтрИсполнения = Исполнители.Добавить();
				ЗаполнитьСтрокуЗадачи(СтрИсполнения,Задача);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеБПИсполнение(Прокси, ЗадачаОбъект)
	
	Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
	
	Если ЭтоТочкаМаршрута("Исполнить") Тогда //@NON-NLS-1
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Исполнено'");
		Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Исполнено.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		КонецЕсли;
		
	ИначеЕсли ЭтоТочкаМаршрута("ОтветственноеИсполнение") Тогда //@NON-NLS-1
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Исполнено'");
		Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Исполнено и проверено.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		КонецЕсли;
		
		Элементы.Исполнители.Видимость = Истина;
		Элементы.Исполнители.Заголовок = "Соисполнители";
		Элементы.Исполнители.ПодчиненныеЭлементы.ИсполнителиСрокИсполнения.Видимость = Истина;
		Элементы.Исполнители.ПодчиненныеЭлементы.ИсполнителиВыполнена.Видимость = Истина;
		Элементы.ИсполнителиВыполнена.Видимость = Ложь;
		
		СписокЗадач = ПолучитьЗадачиБизнесПроцесса(Прокси);
		
		Для Каждого Задача Из СписокЗадач Цикл
			Если Задача.businessProcessStep = "Исполнить" Тогда //@NON-NLS-1
				СтрИсполнения = Исполнители.Добавить();
				ЗаполнитьСтрокуЗадачи(СтрИсполнения,Задача);
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ЭтоТочкаМаршрута("Контролировать") Тогда //@NON-NLS-1
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Снять с контроля'");
		Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Проконтролировано.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		КонецЕсли;
		
		Элементы.Исполнители.Видимость = Истина;
		Элементы.Исполнители.Заголовок = НСтр("ru = 'Ход исполнения'");
		Элементы.ИсполнителиСрокИсполнения.Видимость = Истина;
		Элементы.ИсполнителиВыполнена.Видимость = Истина;
		Элементы.ИсполнителиКартинкаСостояния.Видимость = Ложь;
		Элементы.ИсполнителиКартинкаОтветственный.Видимость = Истина;
		
		СписокЗадач = ПолучитьЗадачиБизнесПроцесса(Прокси);
		Для Каждого Задача Из СписокЗадач Цикл
			Если Найти(Задача.businessProcessStep, "Контролировать") = 0 Тогда  //@NON-NLS-1
				СтрИсполнения = Исполнители.Добавить();
				ЗаполнитьСтрокуЗадачи(СтрИсполнения,Задача);
				Если Найти(Задача.businessProcessStep, "Ответственное") <> 0 Тогда //@NON-NLS-1
					СтрИсполнения.КартинкаОтветственный = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ЭтоТочкаМаршрута("Проверить") Тогда //@NON-NLS-1
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Проверено'");
		Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Завершено.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			Элементы.ИсполнителиОтправленоНаДоработку.ТолькоПросмотр = Истина;
			Элементы.ИсполнителиКомментарийПроверяющего.ТолькоПросмотр = Истина;
		КонецЕсли;
		Элементы.ГруппаЦиклИсполнение.Видимость = Истина;
		Элементы.РезультатВыполнения.Видимость = Ложь;
		Элементы.Исполнители.Видимость = Истина;
		Элементы.Исполнители.ТолькоПросмотр = Ложь;
		Элементы.Исполнители.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ИсполнителиКомментарий.Заголовок = НСтр("ru = 'Результат выполнения'");
		Элементы.ИсполнителиОтправленоНаДоработку.Видимость = Истина;
		Элементы.ИсполнителиОтправленоНаДоработку.Заголовок = НСтр("ru = 'Вернуть на доработку'");
		Элементы.ИсполнителиКомментарийПроверяющего.Видимость = Истина;
		Элементы.ИсполнителиКартинкаСостояния.Видимость = Ложь;
		Элементы.ИсполнителиКартинкаОтветственный.Видимость = Истина;
		
		НомерИтерации = ЗадачаОбъект.iterationNumber;
		
		Для Каждого СтрокаXDTO Из ЗадачаОбъект.checkResults Цикл
			СтрИсполнения = Исполнители.Добавить();
			ЗаполнитьСтрокуЗадачи(СтрИсполнения, СтрокаXDTO.executorTask);
			СтрИсполнения.ЗадачаID = СтрокаXDTO.executorTask.objectID.ID;
			СтрИсполнения.ОтправленоНаДоработку = СтрокаXDTO.returned;
			СтрИсполнения.КомментарийПроверяющего = СтрокаXDTO.checkComment;
			СтрИсполнения.НомерИтерации = ЗадачаОбъект.iterationNumber;
			Если Найти(СтрокаXDTO.executorTask.businessProcessStep, "Ответственное") <> 0 Тогда //@NON-NLS-1
				СтрИсполнения.КартинкаОтветственный = Истина;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если НомерИтерации <= 1 Тогда
		Элементы.НомерИтерацииИсполнения.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеБПОзнакомление()
	
	Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
	Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Ознакомился'");
	Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
	Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
	
	Если Выполнена Тогда
		Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Снято с контроля.'");
		Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
	КонецЕсли;
		
	Если ЭтоТочкаМаршрута("Ознакомиться") Тогда //@NON-NLS-1
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Ознакомился.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеБПРегистрация(ЗадачаОбъект)
	
	Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
	
	// Итерации
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.3.1.3") Тогда
		
		Если ЭтоТочкаМаршрута("Зарегистрировать") Тогда //@NON-NLS-1
			Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Зарегистрировано'");
			Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
			Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
			Если Выполнена Тогда
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСТР("ru = 'Зарегистрировано и отправлено.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			КонецЕсли;
		ИначеЕсли ЭтоТочкаМаршрута("Ознакомиться") Тогда //@NON-NLS-1
			Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Ознакомился'");
			Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
			Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
			Если Выполнена Тогда
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСТР("ru = 'Ознакомился.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		// Заполнение номера итерации
		НомерИтерации = ЗадачаОбъект.iterationNumber;
		
		Если ЭтоТочкаМаршрута("Зарегистрировать") Тогда //@NON-NLS-1
			
			Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Зарегистрировать'");
			Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Не регистрировать'");
			Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
			
			Если НомерИтерации > 1 Тогда
				Элементы.ГруппаЦикл.Видимость = Истина;
			КонецЕсли;
			
			// Заполнение результата утверждения
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(
				ЭтотОбъект,
				ЗадачаОбъект.registrationResult,
				"Результат");
			
			Если Выполнена Тогда
				Элементы.ТекстРезультатаВыполнения.Заголовок = Строка(Результат) + ".";
				Если РезультатID = "Зарегистрировано" Тогда //@NON-NLS-1
					Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
				ИначеЕсли РезультатID = "НеЗарегистрировано" Тогда //@NON-NLS-1
					Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ЭтоТочкаМаршрута("Ознакомиться") Тогда //@NON-NLS-1
			
			Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Ознакомился'");
			Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Повторить регистрацию...'");
			Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
			
			ОтправленоНаПовторнуюРегистрацию = ЗадачаОбъект.returned;
			
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(
				ЭтотОбъект,
				ЗадачаОбъект.registrationResult,
				"Результат");
			
			Элементы.ГруппаРегистрация.Видимость = Истина;
			Элементы.РезультатВыполнения.Видимость = Ложь;
			Элементы.РезультатВыполненияСкрытый.Видимость = Истина;
			
			Если (РезультатID = "Зарегистрировано") Тогда //@NON-NLS-1
				Элементы.РезультатРегистрации.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			ИначеЕсли РезультатID = "НеЗарегистрировано" Тогда //@NON-NLS-1
				Элементы.РезультатРегистрации.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			КонецЕсли;
			
			// Заголовки кнопок
			Если РезультатID = "НеЗарегистрировано" Тогда //@NON-NLS-1
				Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Завершить регистрацию'");
			Иначе
				Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
			КонецЕсли;
			
			// Заполнение текста результата выполнения для выполненной задачи
			Если ОтправленоНаПовторнуюРегистрацию <> Неопределено Тогда
				Если ОтправленоНаПовторнуюРегистрацию Тогда
					Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Отправлено на повторную регистрацию.'");
					Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
				Иначе	
					Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Ознакомился.'");
					Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НомерИтерации <= 1 Тогда 
			Элементы.НомерИтерацииРегистрации.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеБПРассмотрение(ЗадачаОбъект)
	
	Если ЭтоТочкаМаршрута("Рассмотреть") Тогда //@NON-NLS-1
		Элементы.РезультатВыполнения.ПодсказкаВвода = НСтр("ru = 'Ваша резолюция'");
		
		Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Рассмотрено'");
		Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Рассмотрено.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаНейтральногоВыполненияЗадачи;
		КонецЕсли;
		Элементы.РезультатВыполнения.АвтоОтметкаНезаполненного = Истина;
		
	ИначеЕсли ЭтоТочкаМаршрута("Ознакомиться") Тогда //@NON-NLS-1
		Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Обработано'");
		Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Обработано.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		КонецЕсли;
		
		Резолюция = ЗадачаОбъект.resolution;
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
			ЗадачаОбъект.resolutionPerformer.user, "РезолюцияРассмотрел");
		РезолюцияДатаРассмотрения = ЗадачаОбъект.resolutionDate;
		
		Элементы.Резолюция.Видимость = Истина;
		Элементы.Резолюция.Заголовок = СтрШаблон(НСтр("ru = 'Резолюция (%1, %2)'"),
			РезолюцияРассмотрел, РезолюцияДатаРассмотрения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеБПКомплексныйПроцесс(Прокси, ПроцессОбъект)
	
	Элементы.РезультатВыполнения.Видимость = Ложь;
	Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
	
	Если ЭтоТочкаМаршрута("Контролировать") Тогда //@NON-NLS-1
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Проконтролировано'");
		Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Снято с контроля.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		КонецЕсли;
		
		Элементы.Этапы.Видимость = Истина;
		НомерСтроки = 1;
		Для Каждого Этап Из ПроцессОбъект.stages Цикл
			НоваяСтрока = Этапы.Добавить();
			НоваяСтрока.НомерСтроки = НомерСтроки;
			
			НоваяСтрока.Важность = 1;
			Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(Этап, "importance") Тогда
				Если Этап.importance.objectID.ID = "Низкая" Тогда //@NON-NLS-1
					НоваяСтрока.Важность = 0;
				ИначеЕсли Этап.importance.objectID.ID = "Обычная" Тогда //@NON-NLS-1
					НоваяСтрока.Важность = 1;
				ИначеЕсли Этап.importance.objectID.ID = "Высокая" Тогда //@NON-NLS-1
					НоваяСтрока.Важность = 2;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока.ИдентификаторЭтапа = Этап.stageID;
			НоваяСтрока.ИсполнителиЭтапаСтрокой = Этап.participants;
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(НоваяСтрока, Этап.template, "ШаблонБизнесПроцесса");
			ДополнитьИмяШаблонаПоТипуШаблона(НоваяСтрока);
			НоваяСтрока.ПредшественникиЭтапаСтрокой = Этап.stagePredecessors;
			НоваяСтрока.ПредшественникиВариантИспользования = Этап.predecessorsUseOption;
			НоваяСтрока.БезусловныйПереходКСледующемуБылВыполнен = Этап.unconditionalPassageExecuted;
			НоваяСтрока.ЗадачаВыполнена = Этап.executed;
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(НоваяСтрока, Этап.businessProcess, "ЗапущенныйБизнесПроцесс");
			НоваяСтрока.Срок = Этап.duration;
			НомерСтроки = НомерСтроки + 1;
			Если Этап.Установлено("businessProcess") Тогда
				НоваяСтрока.СрокВыполнения = Этап.businessProcess.dueDate;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеБПРешениеВопросов(ЗадачаОбъект)
	
	// решение вопросов выполнения задач
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.2.7.3") Тогда
		ВывестиИнформациюОНедоступностиВыполненияЗадачи();
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
		ЗадачаОбъект.issueTask.parentBusinessProcess,
		"БизнесПроцессПредметаРассмотрения",
		Истина);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
		ЗадачаОбъект.issueTask,
		"ПредметРассмотрения",
		Истина);
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ЗадачаОбъект, "issueType") Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
			ЗадачаОбъект.issueType,
			"ВидВопроса");
	ИначеЕсли ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1") Тогда
		ВидВопроса = НСтр("ru = 'Иное'");
		ВидВопросаID = "Иное"; //@NON-NLS-1
		ВидВопросаТип = "DMIssueType";
	КонецЕсли;
	
	Если ЭтоТочкаМаршрута("РассмотрениеИнициатором") И ВидВопросаID = "ПереносСрока" //@NON-NLS-1 @NON-NLS-2
			И Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP") Тогда
		ВывестиИнформациюОНедоступностиВыполненияЗадачи();
		Возврат;
	КонецЕсли;
	
	Элементы.ЗадатьВопросАвтору.Видимость = Ложь;
	Элементы.ФормаПеренаправить.Видимость = Ложь;
	Элементы.ГруппаПредметРассмотрения.Видимость = Истина;
	
	НомерИтерации = ЗадачаОбъект.iterationNumber;
	Элементы.Наименование.Заголовок = НСтр("ru='Наименование'");
	Элементы.ПолноеОписаниеЗадачиЗаголовок.Заголовок =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='История вопроса (цикл: %1)'"),НомерИтерации);
	
	Если ЭтоТочкаМаршрута("РассмотрениеИнициатором") И ВидВопросаID = "Иное" Тогда //@NON-NLS-1 @NON-NLS-2
		Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Рассмотрено'");
		Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Рассмотрено.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		КонецЕсли;
		Элементы.РезультатВыполнения.АвтоОтметкаНезаполненного = Истина;
		Элементы.РезультатВыполнения.АвтоВыборНезаполненного = Истина;
		
	ИначеЕсли ЭтоТочкаМаршрута("РассмотрениеИнициатором") И ВидВопросаID = "ПереносСрока" Тогда //@NON-NLS-1 @NON-NLS-2
		Если ЗадачаОбъект.parentBusinessProcess.Свойства().Получить("newDueDate") <> Неопределено
				И ЗадачаОбъект.parentBusinessProcess.Свойства().Получить("issueTask") <> Неопределено Тогда
			СтарыйСрок = ЗадачаОбъект.parentBusinessProcess.issueTask.dueDate;
			НовыйСрок = ЗадачаОбъект.parentBusinessProcess.newDueDate;
			Элементы.ГруппаСрок.Видимость = Истина;
			Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP") Тогда
				Элементы.ДлительностьПереноса.Видимость = Истина;
				ДлительностьПереноса = ИнтеграцияС1СДокументооборотВызовСервера.ПодписьДлительностиПереносаСрока(
					Автор,
					АвторID,
					АвторТип,
					СтарыйСрок,
					НовыйСрок);
			Иначе
				Элементы.ДлительностьПереноса.Видимость = Ложь;
			КонецЕсли;
		Иначе
			Элементы.ГруппаСрок.Видимость = Ложь;
		КонецЕсли;
		Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Перенести срок'");
		Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Отказать в переносе'");
		Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		Если Выполнена Тогда
			Если ЗадачаОбъект.executionMark = "ExecutedPositive" Тогда
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Срок исполнения перенесен.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			ИначеЕсли ЗадачаОбъект.executionMark = "ExecutedNegative" Тогда
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Отказано в переносе срока.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			Иначе
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Рассмотрено.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ЭтоТочкаМаршрута("ОзнакомлениеСРезультатомРассмотрения") Тогда //@NON-NLS-1
		Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Закрыть вопрос'");
		Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Уточнить'");
		Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		Если ЗадачаОбъект.Установлено("returned") Тогда
			ВозвращеноНаДоработку = ЗадачаОбъект.returned;
		Иначе
			ВозвращеноНаДоработку = Ложь;
		КонецЕсли;
		Если ВидВопросаID = "ПереносСрока" Тогда //@NON-NLS-1
			Элементы.РезультатРассмотрения.Видимость = Истина;
			Если ЗадачаОбъект.postponingAmount = 0 Тогда
				РезультатРассмотрения = НСтр("ru = 'Перенос срока не согласован.'");
				Элементы.РезультатРассмотрения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			Иначе
				РезультатРассмотрения = НСтр("ru = 'Автор задачи перенес срок.'");
			КонецЕсли;
		КонецЕсли;
		Если Выполнена Тогда
			Если ВозвращеноНаДоработку Тогда
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСТР("ru = 'Отправлено на уточнение.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаНейтральногоВыполненияЗадачи;
			Иначе
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСТР("ru = 'Выполнено.'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ВывестиИнформациюОНедоступностиВыполненияЗадачи();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеБППриглашение(ЗадачаОбъект)
	
	// БП Приглашение
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.2.7.3.CORP") Тогда
		ВывестиИнформациюОНедоступностиВыполненияЗадачи();
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
	НомерИтерации = ЗадачаОбъект.iterationNumber;
	
	Если ЭтоТочкаМаршрута("Пригласить") Тогда //@NON-NLS-1
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Принять приглашение'");
		Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Отклонить приглашение'");
		Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		
		Элементы.ГруппаМестоВремя.Видимость = Истина;
		ДатаНачала = ЗадачаОбъект.activityBegin;
		ДатаОкончания = ЗадачаОбъект.activityEnd;
		МестоПроведения = ЗадачаОбъект.activityVenue;
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
			ЗадачаОбъект.invitationResult, "Результат");
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = Строка(Результат) + ".";
			Если РезультатID = "Принято" Тогда //@NON-NLS-1
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			ИначеЕсли РезультатID = "НеПринято" Тогда //@NON-NLS-1
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ЭтоТочкаМаршрута("Ознакомиться") Тогда //@NON-NLS-1
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Подтвердить приглашения'");
		Элементы.ВыполнитьЗадачуВторая.Заголовок = НСтр("ru='Повторить приглашение...'");
		Элементы.ВыполнитьЗадачуТретья.Заголовок = НСтр("ru='Отменить приглашение'");
		Если Выполнена Тогда
			Если ЗадачаОбъект.invitationReturned Тогда
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Отправлено на повторное приглашение.'");
			Иначе
				Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Приглашение завершено.'");
			КонецЕсли;
		КонецЕсли;
		
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
			ЗадачаОбъект.invitationResult, "Результат");
		
		// Цвет результата
		Если РезультатID = "ПринятоВсемиУчастниками" Или РезультатID = "ПринятоОбязательнымиУчастниками" Тогда //@NON-NLS-1 @NON-NLS-2
			Элементы.РезультатПриглашения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		Иначе
			Элементы.РезультатПриглашения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		КонецЕсли;
		
		// Заголовки кнопок
		Если РезультатID = "ПринятоВсемиУчастниками" Тогда //@NON-NLS-1
			Элементы.ВыполнитьЗадачуПервая.Видимость = Истина;
			Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
			Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
			
		ИначеЕсли РезультатID = "ПринятоОбязательнымиУчастниками" Тогда //@NON-NLS-1
			Элементы.ВыполнитьЗадачуПервая.Видимость = Истина;
			Элементы.ВыполнитьЗадачуВторая.Видимость = Истина;
			Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
			Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
			
		ИначеЕсли РезультатID = "НеПринятоОбязательнымиУчастниками" //@NON-NLS-1
				Или РезультатID = "НеПринятоВсемиУчастниками" Тогда //@NON-NLS-1
			Элементы.ВыполнитьЗадачуПервая.Видимость = Ложь;
			Элементы.ВыполнитьЗадачуВторая.Видимость = Истина;
			Элементы.ВыполнитьЗадачуВторая.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
			Элементы.ВыполнитьЗадачуТретья.Видимость = Истина;
			
		КонецЕсли;
		
		Элементы.ГруппаПриглашение.Видимость = Истина;
		Элементы.Исполнители.Видимость = Истина;
		Элементы.Исполнители.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ИсполнителиИсполнитель.Заголовок = НСтр("ru='Участник'");
		Элементы.ИсполнителиРезультат.Видимость = Истина;
		Элементы.ИсполнителиЯвкаОбязательна.Видимость = Истина;
		Элементы.ИсполнителиДатаИсполнения.Видимость = Ложь;
		Элементы.РезультатВыполнения.Видимость = Ложь;
		Элементы.РезультатВыполненияСкрытый.Видимость = Истина;
		
		// Заполнение листа приглашения
		Для Каждого СтрокаРезультата Из ЗадачаОбъект.invitationResults Цикл
			СтрИсполнения = Исполнители.Добавить();
			СтрИсполнения.РезультатВыполнения = СтрокаРезультата.invitationComment;
			СтрИсполнения.ДатаОтвета = СтрокаРезультата.invitationDate;
			СтрИсполнения.КартинкаСостояния = 1;
			ЗаполнитьДанныеАдресации(СтрИсполнения, СтрокаРезультата.invitationPerformer);
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(СтрИсполнения,
				СтрокаРезультата.invitationResult, "Результат");
			СтрИсполнения.ЯвкаОбязательна =  СтрокаРезультата.invitationPerformer.attendanceCompulsory;
		КонецЦикла;
		
		Если НомерИтерации <= 1 Тогда
			Элементы.НомерИтерацииПриглашения.Видимость = Ложь;
		КонецЕсли;
		
	ИначеЕсли ЭтоТочкаМаршрута("Оповестить") Тогда //@NON-NLS-1
		
		Элементы.ВыполнитьЗадачуПервая.Заголовок = НСтр("ru='Ознакомился'");
		Элементы.ВыполнитьЗадачуВторая.Видимость = Ложь;
		Элементы.ВыполнитьЗадачуТретья.Видимость = Ложь;
		
		Элементы.ГруппаМестоВремя.Видимость = Истина;
		Элементы.ГруппаПриглашение.Видимость = Истина;
		Элементы.ГруппаКомандыВыполнения.Видимость = ДоступноВыполнение;
		
		ДатаНачала = ЗадачаОбъект.activityBegin;
		ДатаОкончания = ЗадачаОбъект.activityEnd;
		МестоПроведения = ЗадачаОбъект.activityVenue;
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект,
			ЗадачаОбъект.invitationResult, "Результат");
		
		Если Выполнена Тогда
			Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru = 'Ознакомился.'");
			Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		КонецЕсли;
		
		// Результат приглашения
		Если РезультатID = "ПринятоВсемиУчастниками" //@NON-NLS-1
				Или РезультатID = "ПринятоОбязательнымиУчастниками" Тогда //@NON-NLS-1
			Результат =  НСтр("ru = 'Приглашение подтверждено с указанным местом и временем'");
			Элементы.РезультатПриглашения.ЦветТекста = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
		Иначе
			Результат =  НСтр("ru = 'Приглашение отменено с указанным местом и временем'");
			Элементы.РезультатПриглашения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		КонецЕсли;
		
		Если НомерИтерации <= 1 Тогда
			Элементы.НомерИтерацииПриглашения.Видимость = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиИнформациюОНедоступностиВыполненияЗадачи()
	
	Элементы.ГруппаКомандыВыполнения.Видимость = Ложь;
	Элементы.ТекстРезультатаВыполнения.Видимость = Истина;
	Элементы.ДекорацияВнимание.Видимость = Истина;
	
	Элементы.ТекстРезультатаВыполнения.Заголовок = НСтр("ru='Выполнение задач такого типа не поддерживается в данной версии 1С:Документооборота.'");
	Элементы.ТекстРезультатаВыполнения.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьИмяШаблонаПоТипуШаблона(СтрокаЭтапа)
	
	Дополнение = "";
	Если СтрокаЭтапа.ШаблонБизнесПроцессаТип = "DMBusinessProcessOrderTemplate"	Тогда Дополнение = НСтр("ru = 'Поручение'");
	ИначеЕсли СтрокаЭтапа.ШаблонБизнесПроцессаТип = "DMBusinessProcessConsiderationTemplate" Тогда Дополнение = НСтр("ru = 'Рассмотрение'");
	ИначеЕсли СтрокаЭтапа.ШаблонБизнесПроцессаТип = "DMBusinessProcessRegistrationTemplate" Тогда Дополнение = НСтр("ru = 'Регистрация'");
	ИначеЕсли СтрокаЭтапа.ШаблонБизнесПроцессаТип = "DMBusinessProcessApprovalTemplate" 	Тогда Дополнение = НСтр("ru = 'Согласование'");
	ИначеЕсли СтрокаЭтапа.ШаблонБизнесПроцессаТип = "DMBusinessProcessConfirmationTemplate" Тогда Дополнение = НСтр("ru = 'Утверждение'");
	ИначеЕсли СтрокаЭтапа.ШаблонБизнесПроцессаТип = "DMBusinessProcessPerformanceTemplate" 	Тогда Дополнение = НСтр("ru = 'Исполнение'");
	ИначеЕсли СтрокаЭтапа.ШаблонБизнесПроцессаТип = "DMBusinessProcessAcquaintanceTemplate" Тогда Дополнение = НСтр("ru = 'Ознакомление'");
	ИначеЕсли СтрокаЭтапа.ШаблонБизнесПроцессаТип = "DMCompoundBusinessProcessTemplate" 	Тогда Дополнение = НСтр("ru = 'Составной процесс'");
	ИначеЕсли СтрокаЭтапа.ШаблонБизнесПроцессаТип = "DMComplexBusinessProcessTemplate" 		Тогда Дополнение = НСтр("ru = 'Комплексный процесс'");
	КонецЕсли;
	Если Не ПустаяСтрока(Дополнение) Тогда
		СтрокаЭтапа.ШаблонБизнесПроцесса = Дополнение + ": " + СтрокаЭтапа.ШаблонБизнесПроцесса;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокуЗадачи(СтрокаЗадачи,Задача)
	
	ЗаполнитьДанныеАдресации(СтрокаЗадачи, Задача.performer);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(СтрокаЗадачи,Задача,"Задача");
	СтрокаЗадачи.СрокИсполнения = Задача.dueDate;
	СтрокаЗадачи.РезультатВыполнения = Задача.executionComment;
	СтрокаЗадачи.ДатаИсполнения = Задача.endDate;
	СтрокаЗадачи.Выполнена = Задача.executed;
	СтрокаЗадачи.ТочкаМаршрута = Задача.businessProcessStep;
	СтрокаЗадачи.КартинкаСостояния = ?(СтрокаЗадачи.Выполнена, 1, 0);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЗадачиБизнесПроцесса(Прокси)
	
	Запрос = СоздатьОбъект(Прокси, "DMGetTasksTreeRequest");
	Отбор = СоздатьОбъект(Прокси, "DMGetTasksTreeQuery");
	ОтборБизнесПроцессов = Отбор.businessProcess; // СписокXDTO
	
	ОбъектXDTO = СоздатьОбъект(Прокси,"DMObject");
	ОбъектXDTO.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ПроцессID,
		ПроцессТип);
	ОбъектXDTO.name = Процесс;
	
	ОтборБизнесПроцессов.Добавить(ОбъектXDTO);
	
	Запрос.query = Отбор;
	
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	Возврат Ответ.businessProcesses[0].tasks;
	
КонецФункции

&НаСервере
Процедура ДополнитьИмяПредметаПоТипуОбъекта(ИмяПредмета, ТипОбъекта)
	
	Если ТипОбъекта = "DMInternalDocument" Тогда
		ИмяТипаПредмета = НСтр("ru = 'Внутренний документ'");
	ИначеЕсли ТипОбъекта = "DMIncomingDocument" Тогда
		ИмяТипаПредмета = НСтр("ru = 'Входящий документ'");
	ИначеЕсли ТипОбъекта = "DMOutgoingDocument" Тогда
		ИмяТипаПредмета = НСтр("ru = 'Исходящий документ'");
	ИначеЕсли ТипОбъекта = "DMFile" Тогда
		ИмяТипаПредмета = НСтр("ru = 'Файл'");
	ИначеЕсли ТипОбъекта = "DMActivity" Тогда
		ИмяТипаПредмета = НСтр("ru = 'Мероприятие'");
	ИначеЕсли ТипОбъекта = "DMIncomingEMail" Тогда
		ИмяТипаПредмета = НСтр("ru = 'Входящее письмо'");
	ИначеЕсли ТипОбъекта = "DMOutgoingEMail" Тогда
		ИмяТипаПредмета = НСтр("ru = 'Исходящее письмо'");
	ИначеЕсли ТипОбъекта = "DMProject" Тогда
		ИмяТипаПредмета = НСтр("ru = 'Проект'");
	ИначеЕсли ТипОбъекта = "DMProjectTask" Тогда
		ИмяТипаПредмета = НСтр("ru = 'Проектная задача'");
	ИначеЕсли ТипОбъекта = "DMDiscussionMessage" Тогда
		ИмяТипаПредмета = НСтр("ru = 'Сообщение'");
	ИначеЕсли Лев(ТипОбъекта, 2) <> "DM" Тогда
		КлассТип = СтрРазделить(ТипОбъекта,".");
		Если Найти(КлассТип[0],"Документ") <> 0 Тогда
			КлассТип[0] = "Документы";
		ИначеЕсли Найти(КлассТип[0],"Справочник") <> 0 Тогда
			КлассТип[0] = "Справочники";
		ИначеЕсли Найти(КлассТип[0],"БизнесПроцесс") <> 0 Тогда
			КлассТип[0] = "БизнесПроцессы";
		ИначеЕсли Найти(КлассТип[0],"Задача") <> 0 Тогда
			КлассТип[0] = "Задачи";
		КонецЕсли;
		ИмяТипаПредмета = " (" + Метаданные[КлассТип[0]][КлассТип[1]].Синоним + ")";
	КонецЕсли;
	
	ИмяПредмета = СтрШаблон("%1 (%2)", ИмяПредмета, ИмяТипаПредмета);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПризнакЗадачаПросрочена()
	Если СрокИсполнения < ТекущаяДатаСеанса() И СрокИсполнения <> '00010101000000' Тогда 
		ЗадачаПросрочена = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЗадач(Знач УсловноеОформление)

	// установка оформления для просроченных задач
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Исполнители.СрокИсполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Исполнители.СрокИсполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбораДанных.ПравоеЗначение = КонецДня(ТекущаяДатаСеанса());
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Исполнители.Выполнена");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Ложь;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение =  Метаданные.ЭлементыСтиля.ПросроченныеДанныеЦвет.Значение; 
	ЭлементЦветаОформления.Использование = Истина;
	
	ЭлементОбластиОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ЭлементОбластиОформления.Поле = Новый ПолеКомпоновкиДанных("ИсполнителиСрокИсполнения");
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Этапы.СрокВыполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Этапы.СрокВыполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбораДанных.ПравоеЗначение = ТекущаяДатаСеанса();
	ЭлементОтбораДанных.Использование = Истина;
		
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Этапы.ЗапущенныйБизнесПроцессID");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОбластиОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ЭлементОбластиОформления.Поле = Новый ПолеКомпоновкиДанных("ЭтапыШаблонБизнесПроцесса");
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение =  Метаданные.ЭлементыСтиля.ПросроченныеДанныеЦвет.Значение; 
	ЭлементЦветаОформления.Использование = Истина;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКомандДереваПриложений()
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Элементы.ОткрытьФайлДляПросмотра.Доступность = Ложь;
		Элементы.Редактировать.Доступность = Ложь;
		Элементы.ЗакончитьРедактирование.Доступность = Ложь;
		Элементы.ОтменитьРедактирование.Доступность = Ложь;
		Элементы.УдалитьПредмет.Доступность = Ложь;
		Элементы.ФормаУдалитьПредмет.Доступность = Ложь;
	Иначе
		Если ТекущиеДанные.ПредметТип = "DMFile" Тогда
			Элементы.ОткрытьФайлДляПросмотра.Доступность = Истина;
			Элементы.Редактировать.Доступность = ДоступенЗахватФайлов
				И (Не ТекущиеДанные.Редактируется Или ТекущиеДанные.РедактируетсяТекущимПользователем);
			Элементы.ЗакончитьРедактирование.Доступность = ДоступенЗахватФайлов
				И ТекущиеДанные.РедактируетсяТекущимПользователем;
			Элементы.ОтменитьРедактирование.Доступность = ДоступенЗахватФайлов
				И ТекущиеДанные.РедактируетсяТекущимПользователем;
		Иначе
			Элементы.ОткрытьФайлДляПросмотра.Доступность = Ложь;
			Элементы.Редактировать.Доступность = Ложь;
			Элементы.ЗакончитьРедактирование.Доступность = Ложь;
			Элементы.ОтменитьРедактирование.Доступность = Ложь;
		КонецЕсли;
		Элементы.УдалитьПредмет.Доступность = ТекущиеДанные.ДоступноУдаление И Не ТекущиеДанные.Редактируется;
		Элементы.ФормаУдалитьПредмет.Доступность = ТекущиеДанные.ДоступноУдаление И Не ТекущиеДанные.Редактируется;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеАдресации(Приемник, ИсполнительXDTO)
	
	Если ИсполнительXDTO.Установлено("user") Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(Приемник, ИсполнительXDTO.user,"Исполнитель")
	ИначеЕсли ИсполнительXDTO.Установлено("role") Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(Приемник, ИсполнительXDTO.role,"Исполнитель")
	КонецЕсли;
	
	Если ИсполнительXDTO.Установлено("mainAddressingObject") Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(Приемник, ИсполнительXDTO.mainAddressingObject, "ОсновнойОбъектАдресации");
	КонецЕсли;
	
	Если ИсполнительXDTO.Установлено("secondaryAddressingObject") Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(Приемник, ИсполнительXDTO.secondaryAddressingObject, "ДополнительныйОбъектАдресации");
	КонецЕсли;
	
	Исполнитель = Исполнитель 
		+ ?(ПустаяСтрока(ОсновнойОбъектАдресации), "", ", " + ОсновнойОбъектАдресации) 
		+ ?(ПустаяСтрока(ДополнительныйОбъектАдресации), "", ", " + ДополнительныйОбъектАдресации); 
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьXDTOПоОбъектномуРеквизиту(Прокси, ИмяРеквизита, ИмяСвойстваОбъекта, ОбъектXDTO)
	
	СвойствоОбъекта = СоздатьОбъект(Прокси, ЭтотОбъект[ИмяРеквизита+"Тип"]);
	СвойствоОбъекта.name = ЭтотОбъект[ИмяРеквизита];
	СвойствоОбъекта.ObjectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ЭтотОбъект[ИмяРеквизита+"ID"],
		ЭтотОбъект[ИмяРеквизита+"Тип"]);
	
	ОбъектXDTO[ИмяСвойстваОбъекта] = СвойствоОбъекта;
	
КонецПроцедуры

&НаСервере
Функция СоздатьОбъект(Прокси, Тип)
	
	Возврат ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, Тип);
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьЗадачуОповеститьЗакрытьФорму(НомерКнопки)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьЗадачуОповеститьЗакрытьФормуЗавершение",
		ЭтотОбъект,
		НомерКнопки);
	
	Если ФактическийИсполнительЗадач = "userChoice"
		И ИсполнительТип = "DMUser"
		И ИсполнительID <> ТекущийПользовательID Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Исполнитель", Исполнитель);
		ПараметрыФормы.Вставить("ТекущийПользователь", ТекущийПользователь);
		ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.ВыборИсполнителяЗадачи",
			ПараметрыФормы,
			ЭтотОбъект,,,,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	Иначе // без вопроса
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, ФактическийИсполнительЗадач);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗадачуОповеститьЗакрытьФормуЗавершение(ВыбранныйИсполнитель, НомерКнопки) Экспорт
	
	Если ВыбранныйИсполнитель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыполненияЗадачи = Новый Структура;
	РезультатВыполненияЗадачи.Вставить("НомерКнопки", НомерКнопки);
	РезультатВыполненияЗадачи.Вставить("ВыбранныйИсполнитель", ВыбранныйИсполнитель);
	РезультатВыполненияЗадачи.Вставить("ПредметСсылка", Неопределено);
	
	Если ЗаписатьОбъект(РезультатВыполненияЗадачи) Тогда
		
		Если ЗначениеЗаполнено(РезультатВыполненияЗадачи.ПредметСсылка) Тогда
			ОповеститьОбИзменении(РезультатВыполненияЗадачи.ПредметСсылка);
		КонецЕсли;
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ID", ПроцессID);
		ПараметрыОповещения.Вставить("Стартован", Истина);
		
		// Соберем предметы.
		Предметы = Новый Массив;
		Если ДоступнаМультипредметность Тогда
			ЭлементыДерева = ДеревоПриложений.ПолучитьЭлементы();
			Для Каждого СтрокаПредмета Из ЭлементыДерева Цикл
				Если СтрокаПредмета.РольПредмета = "Основной" Тогда //@NON-NLS-1
					ОписаниеПредмета = Новый Структура;
					ОписаниеПредмета.Вставить("ID", СтрокаПредмета.ПредметID);
					ОписаниеПредмета.Вставить("Тип", СтрокаПредмета.ПредметТип);
					Предметы.Добавить(ОписаниеПредмета);
				КонецЕсли;
			КонецЦикла;
		Иначе // один предмет
			ОписаниеПредмета = Новый Структура;
			ОписаниеПредмета.Вставить("ID", ПредметID);
			ОписаниеПредмета.Вставить("Тип", ПредметТип);
			Предметы.Добавить(ОписаниеПредмета);
		КонецЕсли;
		ПараметрыОповещения.Вставить("Предметы", Предметы);
		
		Оповестить("Запись_ДокументооборотБизнесПроцесс", ПараметрыОповещения, ВладелецФормы);
		
		ПараметрыОповещения = Новый Структура("name, ID, type", Наименование, ID, Тип);
		Оповестить("Запись_ДокументооборотЗадача", ПараметрыОповещения, ВладелецФормы);
		
		Модифицированность = Ложь;
		Закрыть();
		Если ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Источник", Представление);
			ПараметрыФормы.Вставить("ИсточникID", ID);
			ПараметрыФормы.Вставить("ИсточникТип", Тип);
			ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.ДобавлениеРаботы", ПараметрыФормы, ID);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Записывает задачу, возможно, выполняя ее.
//
// Параметры:
//   РезультатВыполненияЗадачи - Неопределено
//                             - Структура:
//     * НомерКнопки - Число - номер кнопки, выполняющей задачу.
//     * ВыбранныйИсполнитель - Строка - currentUser, если это текущий пользователь, или
//         taskPerformer, если это фактический исполнитель задачи.
//     * ПредметСсылка - ЛюбаяСсылка - неявно возвращаемый параметр, предмет задачи,
//         требующий оповещения об изменении.
//
&НаСервере
Функция ЗаписатьОбъект(РезультатВыполненияЗадачи = Неопределено)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	ПроцессXDTO = СоздатьОбъект(Прокси, ПроцессТип);
	ПроцессXDTO.name = Процесс;
	ПроцессXDTO.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ПроцессID,
		ПроцессТип);
	
	Задача = СоздатьОбъект(Прокси, Тип);
	
	Если ДоступнаМультипредметность Тогда
		ЗаполнитьЗадачуПредметамиИзДереваПриложений(Прокси, ДеревоПриложений.ПолучитьЭлементы(), Задача);
	КонецЕсли;
	
	Задача.executed = ?(РезультатВыполненияЗадачи <> Неопределено, Истина, Выполнена);
	Задача.parentBusinessProcess = ПроцессXDTO;
	
	ЗаполнитьСвойстваОбъектаПоЗадаче(Задача, Прокси);
	ЗаполнитьXDTOПоОбъектномуРеквизиту(Прокси, "Важность", "importance", Задача);
	
	// Заполним результат и исполнителя задачи при выполнении.
	ДанныеИсполнитель = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		ИсполнительID,
		ИсполнительТип,
		Исполнитель);
	ДанныеОсновнойОбъект = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		ОсновнойОбъектАдресацииID,
		ОсновнойОбъектАдресацииТип,
		ОсновнойОбъектАдресации);
	ДанныеДополнительныйОбъект = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		ДополнительныйОбъектАдресацииID,
		ДополнительныйОбъектАдресацииТип,
		ДополнительныйОбъектАдресации);
	
	Если РезультатВыполненияЗадачи <> Неопределено Тогда
		ЗаполнитьСвойстваОбъектаПоТипуЗадачиПриВыполнении(Прокси, Задача, РезультатВыполненияЗадачи.НомерКнопки);
		Если РезультатВыполненияЗадачи.ВыбранныйИсполнитель = "currentUser" Тогда
			ДанныеИсполнитель = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
				ТекущийПользовательID,
				ТекущийПользовательТип,
				ТекущийПользователь);
		КонецЕсли;
	КонецЕсли;
	
	Задача.performer = ИнтеграцияС1СДокументооборот.УчастникЗадач(
		Прокси,
		ДанныеИсполнитель,
		ДанныеОсновнойОбъект,
		ДанныеДополнительныйОбъект);
	
	Обработки.ИнтеграцияС1СДокументооборотБазоваяФункциональность.СформироватьДополнительныеСвойства(
		Прокси,
		Задача,
		ЭтотОбъект);
	
	РезультатЗаписи = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьОбъект(Прокси, Задача);
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, РезультатЗаписи, "DMError") Тогда
		
		Возврат Ложь;
		
	Иначе // Изменение состояния согласования требует особой обработки с оповещением предмета.
		
		Если Тип = "DMBusinessProcessApprovalTaskApproval"
				И РезультатВыполненияЗадачи <> Неопределено Тогда
			
			НовоеСостояние = Неопределено;
			Если РезультатВыполненияЗадачи.НомерКнопки = 3 Тогда
				НовоеСостояние = Перечисления.СостоянияСогласованияВДокументообороте.НеСогласован;
			Иначе
				ДанныеПроцесса = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
					Прокси,
					ПроцессТип,
					ПроцессID,
					"completed");
				Если ДанныеПроцесса.completed = Истина Тогда
					НовоеСостояние = Перечисления.СостоянияСогласованияВДокументообороте.Согласован;
				КонецЕсли;
			КонецЕсли;
			Если НовоеСостояние <> Неопределено Тогда
				Для Каждого СтрокаПредмета Из ДеревоПриложений.ПолучитьЭлементы() Цикл
					Если (Не ДоступнаМультипредметность Или СтрокаПредмета.РольПредмета = "Основной") //@NON-NLS-1
						И ИнтеграцияС1СДокументооборотКлиентСервер.ЭтоДокумент(СтрокаПредмета.ПредметТип) Тогда
						ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ПриИзмененииСостоянияСогласования(
							СтрокаПредмета.ПредметID,
							СтрокаПредмета.ПредметТип,
							НовоеСостояние,
							Ложь,
							РезультатВыполненияЗадачи.ПредметСсылка);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		Модифицированность = Ложь;
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции

// Заполняет объект XDTO задачи бизнес-процесса предметами из дерева приложений формы.
//
&НаСервере
Процедура ЗаполнитьЗадачуПредметамиИзДереваПриложений(Прокси, СтрокиДерева, Задача)
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, Задача, "DMBusinessProcessIssuesSolutionTaskQuestion")
			Или ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, Задача, "DMBusinessProcessIssuesSolutionTaskAnswer")Тогда
		
		Задача.files.Очистить();
		
		Для Каждого СтрокаПредмета Из СтрокиДерева Цикл
			
			file = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, СтрокаПредмета.ПредметТип);
			file.name = СтрокаПредмета.Предмет;
			file.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				СтрокаПредмета.ПредметID,
				СтрокаПредмета.ПредметТип);
			
			Задача.files.Добавить(file);
			
		КонецЦикла;
		
	Иначе
		
		targetCollection = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
			Прокси, "DMBusinessProcessTaskTargetCollection");
		ОписаниеПредмета = targetCollection.items; // СписокXDTO
		
		Для Каждого СтрокаПредмета Из СтрокиДерева Цикл
			
			target = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMBusinessProcessTaskTarget");
			
			target.name = СтрокаПредмета.ИмяПредмета;
			
			target.role = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMBusinessProcessTargetRole");
			target.role.name = СтрокаПредмета.РольПредмета;
			target.role.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				СтрокаПредмета.РольПредмета,
				"DMBusinessProcessTargetRole");
			
			target.target = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, СтрокаПредмета.ПредметТип);
			target.target.name = СтрокаПредмета.Предмет;
			target.target.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				СтрокаПредмета.ПредметID,
				СтрокаПредмета.ПредметТип);
			
			target.allowDeletion = СтрокаПредмета.ДоступноУдаление;
			
			ОписаниеПредмета.Добавить(target);
			
		КонецЦикла;
		
		Задача.targets = targetCollection;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвойстваОбъектаПоЗадаче(Задача, Прокси)
	
	Задача.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(Прокси, ID, Тип);
	Задача.name = Наименование;
	Задача.number = Номер;
	Задача.description = Описание;
	Задача.beginDate = Дата;
	Задача.dueDate = СрокИсполнения;
	Задача.endDate = ДатаИсполнения;
	Задача.executionComment = РезультатВыполнения;
	Задача.businessProcessStep = ТочкаМаршрута;
	
	ИмяТипа = Задача.Тип().Имя;
	
	Если ИмяТипа = "DMBusinessProcessPerfomanceTaskCheckup" Тогда
		РезультатыПроверки = Задача.checkResults; // СписокXDTO
		
		Задача.iterationNumber = НомерИтерации;
		Для Каждого Строка Из Исполнители Цикл
			СтрокаXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
				Прокси,
				"DMBusinessProcessPerfomanceTaskCheckupResult");
			СтрокаXDTO.returned = Строка.ОтправленоНаДоработку;
			СтрокаXDTO.checkComment = Строка.КомментарийПроверяющего;
			ЗадачаИсточник = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
				Прокси,
				Строка.ЗадачаТип,
				Строка.ЗадачаID);
			ЗадачаПриемник = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
				Прокси,
				"DMBusinessProcessTask");
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьЗначенияСвойствXDTO(
				Прокси,
				ЗадачаПриемник,
				ЗадачаИсточник);
			СтрокаXDTO.executorTask = ЗадачаПриемник;
			РезультатыПроверки.Добавить(СтрокаXDTO);
		КонецЦикла
		
	ИначеЕсли ИмяТипа = "DMBusinessProcessConsiderationTaskAcquaint" Тогда
		Задача.resolution = Резолюция;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвойстваОбъектаПоТипуЗадачиПриВыполнении(Прокси, Задача, НомерКнопки)
	
	ИмяТипа = Задача.Тип().Имя;
	
	Если ИмяТипа = "DMBusinessProcessOrderTaskCheckup" Тогда
		Задача.returned = ?(НомерКнопки = 1, Ложь, Истина);
		
	ИначеЕсли ИмяТипа = "DMBusinessProcessApprovalTaskApproval" Тогда
		Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси,"DMApprovalResult");
		Если НомерКнопки = 1 Тогда
			Ответ.name = НСтр("ru = 'Согласовано'");
			Ответ.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				"Согласовано", //@NON-NLS-1
				"DMApprovalResult");
		ИначеЕсли НомерКнопки = 2 Тогда
			Ответ.name = НСтр("ru = 'Согласовано с замечаниями'");
			Ответ.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				"СогласованоСЗамечаниями", //@NON-NLS-1
				"DMApprovalResult");
		ИначеЕсли НомерКнопки = 3 Тогда
			Ответ.name = НСтр("ru = 'Не согласовано'");
			Ответ.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				"НеСогласовано", //@NON-NLS-1
				"DMApprovalResult");
		КонецЕсли;
		Задача.approvalResult = Ответ;
		
	ИначеЕсли ИмяТипа = "DMBusinessProcessApprovalTaskCheckup" Тогда
		Задача.returned = ?(НомерКнопки = 1, Ложь, Истина);
		
	ИначеЕсли ИмяТипа = "DMBusinessProcessConfirmationTaskConfirmation" Тогда
		Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси,"DMConfirmationResult");
		Если НомерКнопки = 1 Тогда
			Ответ.name = НСтр("ru = 'Утверждено'");
			Ответ.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				"Утверждено", //@NON-NLS-1
				"DMConfirmationResult");
		ИначеЕсли НомерКнопки = 2 Тогда
			Ответ.name = НСтр("ru = 'Не утверждено'");
			Ответ.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				"НеУтверждено", //@NON-NLS-1
				"DMConfirmationResult");
		КонецЕсли;
		Задача.confirmationResult = Ответ;
		
	ИначеЕсли ИмяТипа = "DMBusinessProcessConfirmationTaskCheckup" Тогда
		Задача.returned = ?(НомерКнопки = 1, Ложь, Истина);
		
	ИначеЕсли ИмяТипа = "DMBusinessProcessRegistrationTaskRegistration" Тогда
		Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси,"DMRegistrationResult");
		Если НомерКнопки = 1 Тогда
			Ответ.name = НСтр("ru = 'Зарегистрировано'");
			Ответ.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				"Зарегистрировано", //@NON-NLS-1
				"DMRegistrationResult");
		ИначеЕсли НомерКнопки = 2 Тогда
			Ответ.name = НСтр("ru = 'Не зарегистрировано'");
			Ответ.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				"НеЗарегистрировано", //@NON-NLS-1
				"DMRegistrationResult");
		КонецЕсли;
		Задача.registrationResult = Ответ;
		
	ИначеЕсли ИмяТипа = "DMBusinessProcessRegistrationTaskCheckup" Тогда
		Задача.returned = ?(НомерКнопки = 1, Ложь, Истина);
		
	ИначеЕсли ИмяТипа = "DMBusinessProcessIssuesSolutionTaskAnswer" Тогда
		Задача.returned = ?(НомерКнопки = 1, Ложь, Истина);
		
	ИначеЕсли ИмяТипа = "DMBusinessProcessInvitationTaskInvitation" Тогда
		Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси,"DMInvitationResult");
		Если НомерКнопки = 1 Тогда
			Ответ.name = НСтр("ru = 'Принято'");
			Ответ.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				"Принято", //@NON-NLS-1
				"DMInvitationResult");
		ИначеЕсли НомерКнопки = 2 Тогда
			Ответ.name = НСтр("ru = 'Не принято'");
			Ответ.objectID = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
				Прокси,
				"НеПринято", //@NON-NLS-1
				"DMInvitationResult");
		КонецЕсли;
		Задача.invitationResult = Ответ;
		
	ИначеЕсли ИмяТипа = "DMBusinessProcessInvitationTaskAcquaint" Тогда
		Если НомерКнопки = 1 Тогда
			Задача.invitationAccepted = Истина;
		ИначеЕсли НомерКнопки = 2 Тогда
			Задача.invitationReturned = Истина;
		КонецЕсли;
		
	ИначеЕсли ИмяТипа = "DMBusinessProcessIssuesSolutionTaskQuestion" Тогда
		Если НомерКнопки = 1 Тогда
			Задача.executionMark = "ExecutedPositive";
			Если Задача.parentBusinessProcess.Свойства().Получить("newDueDate") <> Неопределено Тогда
				Задача.parentBusinessProcess.newDueDate = НовыйСрок;
			КонецЕсли;
		ИначеЕсли НомерКнопки = 2 Тогда
			Задача.executionMark = "ExecutedNegative";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПолноеОписаниеЗадачи(ПроцессОбъект, ОбъектXDTO)
	
	Если Элементы.Найти("ПолноеОписаниеЗадачи") <> Неопределено Тогда
		
		// Состояния процесса "Прерван", "Остановлен".
		Если ПроцессОбъект.Свойства().Получить("state") <> Неопределено
			И ПроцессОбъект.Установлено("state") Тогда
			
			Если ПроцессОбъект.state.name = "Остановлен" Тогда //@NON-NLS-1
				Если Не ПустаяСтрока(ПолноеОписаниеЗадачи) Тогда
					ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + Символы.ПС;
				КонецЕсли;
				ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + НСтр("ru = 'Задача остановлена'") + Символы.ПС;
			ИначеЕсли ПроцессОбъект.state.name = "Прерван" Тогда //@NON-NLS-1
				Если Не ПустаяСтрока(ПолноеОписаниеЗадачи) Тогда
					ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + Символы.ПС;
				КонецЕсли;
				ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + НСтр("ru = 'Задача прервана'") + Символы.ПС;
			КонецЕсли;
			
		КонецЕсли;
		
		// Описание задачи.
		Если Не ПустаяСтрока(ОбъектXDTO.description) Тогда
			Если Не ПустаяСтрока(ПолноеОписаниеЗадачи) Тогда
				ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + Символы.ПС;
			КонецЕсли;
			ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + ОбъектXDTO.description + Символы.ПС;
		КонецЕсли;
		
		// Важность.
		Если Не ПустаяСтрока(ПолноеОписаниеЗадачи) Тогда
			ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + Символы.ПС;
		КонецЕсли;
		ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи
			+ СтрШаблон(НСтр("ru = 'Важность: %1'"), ОбъектXDTO.importance.objectID.ID)
			+ Символы.ПС;
		
		// Кому.
		Если Не ПустаяСтрока(ПолноеОписаниеЗадачи) Тогда
			ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + Символы.ПС;
		КонецЕсли;
		
		ПредставлениеИсполнителя = "";
		
		Если ОбъектXDTO.performer.Установлено("user") Тогда // адресована пользователю
			
			ПредставлениеИсполнителя = ОбъектXDTO.performer.user.name;
			
		ИначеЕсли ОбъектXDTO.performer.Установлено("role") Тогда // адресована роли
			
			ПредставлениеИсполнителя = ОбъектXDTO.performer.role.name;
			
			Если ОбъектXDTO.performer.Установлено("mainAddressingObject") Тогда
				ПредставлениеИсполнителя = ПредставлениеИсполнителя + ", "
					+ ОбъектXDTO.performer.mainAddressingObject.name;
			КонецЕсли;
			Если ОбъектXDTO.performer.Установлено("secondaryAddressingObject") Тогда
				ПредставлениеИсполнителя = ПредставлениеИсполнителя + ", "
					+ ОбъектXDTO.performer.secondaryAddressingObject.name;
			КонецЕсли;
				
		КонецЕсли;
		
		ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + СтрШаблон(
			НСтр("ru = 'Кому: %1'"),
			ПредставлениеИсполнителя);
		
		// Срок.
		Если ЗначениеЗаполнено(ОбъектXDTO.dueDate) Тогда
			Если Не ПустаяСтрока(ПолноеОписаниеЗадачи) Тогда
				ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + Символы.ПС;
			КонецЕсли;
			ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи
				+ СтрШаблон(НСтр("ru = 'Срок: %1'"), Формат(ОбъектXDTO.dueDate, "dd.MM.yyyy HH:mm"));
		КонецЕсли;
		
		// Автор.
		Если ОбъектXDTO.Установлено("author") Тогда
			Если Не ПустаяСтрока(ПолноеОписаниеЗадачи) Тогда
				ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + Символы.ПС;
			КонецЕсли;
			ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи
				+ СтрШаблон(НСтр("ru = 'Автор: %1'"), ОбъектXDTO.author.name);
		КонецЕсли;
		
		// История выполнения.
		Если ПроцессОбъект.Свойства().Получить("executionComment") <> Неопределено
				И Не ПустаяСтрока(ПроцессОбъект.executionComment) Тогда
			Если Не ПустаяСтрока(ПолноеОписаниеЗадачи) Тогда
				ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи + Символы.ПС;
			КонецЕсли;
			ПолноеОписаниеЗадачи = ПолноеОписаниеЗадачи
				+ Символы.ПС + НСтр("ru = 'История выполнения:'")
				+ Символы.ПС + ПроцессОбъект.executionComment;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещенияЗаписьПредмета(ПредметID, ПредметТип)
	
	ИдентификаторТекущейСтроки = Неопределено;
	ТекущийПредметID = Неопределено;
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущийПредметID = Элементы.ДеревоПриложений.ТекущиеДанные.ПредметID
	КонецЕсли;
	
	ОбработкаОповещенияЗаписьПредметаСервер(ПредметID, ПредметТип);
	
	// Развернем дерево и установим фокус на текущую строку.
	ЭлементыДерева = ДеревоПриложений.ПолучитьЭлементы();
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		ИдентификаторСтроки = ЭлементДерева.ПолучитьИдентификатор();
		Элементы.ДеревоПриложений.Развернуть(ИдентификаторСтроки);
		Если ЭлементДерева.ПредметID = ТекущийПредметID Тогда
			ИдентификаторТекущейСтроки = ИдентификаторСтроки;
		КонецЕсли;
		Если ИдентификаторТекущейСтроки = Неопределено Тогда
			Для Каждого ПодчиненныйЭлементДерева Из ЭлементДерева.ПолучитьЭлементы() Цикл
				ИдентификаторСтроки = ПодчиненныйЭлементДерева.ПолучитьИдентификатор();
				Если ПодчиненныйЭлементДерева.ПредметID = ТекущийПредметID Тогда
					ИдентификаторТекущейСтроки = ИдентификаторСтроки;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если ИдентификаторТекущейСтроки <> Неопределено Тогда
		Элементы.ДеревоПриложений.ТекущаяСтрока = ИдентификаторТекущейСтроки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НайтиСтрокуПредмета(СтрокиДерева, ПредметID, ПредметТип)
	
	НайденнаяСтрока = Неопределено;
	
	Для Каждого СтрокаПредмета Из СтрокиДерева Цикл
		
		Если ПредметID = СтрокаПредмета.ПредметID И ПредметТип = СтрокаПредмета.ПредметТип Тогда
			НайденнаяСтрока = СтрокаПредмета;
		Иначе
			Если ТипЗнч(СтрокаПредмета) = Тип("ДанныеФормыЭлементДерева") Тогда
				СтрокиФайлов = СтрокаПредмета.ПолучитьЭлементы();
			Иначе
				СтрокиФайлов = СтрокаПредмета.Строки;
			КонецЕсли;
			Для Каждого СтрокаФайла Из СтрокиФайлов Цикл
				Если ПредметID = СтрокаФайла.ПредметID И ПредметТип = СтрокаФайла.ПредметТип Тогда
					НайденнаяСтрока = СтрокаФайла;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НайденнаяСтрока <> Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НайденнаяСтрока;
	
КонецФункции

// Обновляет дерево приложений при изменении предмета в других формах.
//
&НаСервере
Процедура ОбработкаОповещенияЗаписьПредметаСервер(ПредметID, ПредметТип)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	МассивКолонок = Новый Массив;
	Если ИнтеграцияС1СДокументооборотКлиентСервер.ЭтоДокумент(ПредметТип) Тогда
		МассивКолонок.Добавить("files");
		МассивКолонок.Добавить("externalObject");
	ИначеЕсли ПредметТип = "DMFile" Тогда
		МассивКолонок.Добавить("extension");
		МассивКолонок.Добавить("modificationDateUniversal");
		МассивКолонок.Добавить("size");
	КонецЕсли;
	
	Если ДоступнаМультипредметность Тогда // найдем нужную строку и обновим ее
		
		Дерево = РеквизитФормыВЗначение("ДеревоПриложений");
		
		НайденнаяСтрока = НайтиСтрокуПредмета(Дерево.Строки, ПредметID, ПредметТип);
		Если НайденнаяСтрока <> Неопределено Тогда
			ПредметXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
				Прокси,
				ПредметТип,
				ПредметID,
				МассивКолонок);
			ЗаполнитьСтрокуПредметаЕгоСвойствами(НайденнаяСтрока, ПредметXDTO);
		КонецЕсли;
		
		ЗначениеВРеквизитФормы(Дерево, "ДеревоПриложений");
		
	Иначе // мультипредметности нет, перезаполним дерево целиком
		
		Если ПредметID <> ID Тогда
			Возврат;
		КонецЕсли;
		
		ПредметXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(
			Прокси,
			ПредметТип,
			ПредметID,
			МассивКолонок);
		
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьОбъектныйРеквизит(ЭтотОбъект, ПредметXDTO, "Предмет");
		ЗаполнитьДеревоПриложенийПоПредмету(ПредметXDTO);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПринятьЗадачуКИсполнениюНаСервере()
	
	ЗадачиКИсполнению = Новый Массив;
	ЗадачиКИсполнению.Добавить(ID);
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Возврат ИнтеграцияС1СДокументооборот.ПринятьЗадачуКИсполнению(Прокси, ЗадачиКИсполнению);
	
КонецФункции

&НаСервере
Функция ОтменитьПринятиеКИсполнениюНаСервере()
	
	ЗадачиКИсполнению = Новый Массив;
	ЗадачиКИсполнению.Добавить(ID);
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Возврат ИнтеграцияС1СДокументооборот.ОтменитьПринятиеЗадачКИсполнению(Прокси, ЗадачиКИсполнению);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ВыполнитьПрограммноДобавленнуюКоманду(Команда)
	
	// Вызовем обработчик команды, которая добавлена программно при создании формы на сервере.
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентПереопределяемый.ВыполнитьПрограммноДобавленнуюКоманду(Команда, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПредмет(Команда)
	
	Если Не РазрешеноИзменение Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьПредметЗавершение", ЭтотОбъект);
	ИнтеграцияС1СДокументооборотКлиент.ДобавитьПредмет(ЭтотОбъект, "Вспомогательный", ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПредметЗавершение(ДобавленныеПредметы, ПараметрыОбработчика) Экспорт
	
	Если ДобавленныеПредметы = Неопределено Или ДобавленныеПредметы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьПредметЗавершениеНаСервере(ДобавленныеПредметы);
	Модифицированность = Истина;
	ОбработатьФормуПоНаличиюПредметов();
	
	// Развернем дерево и установим фокус на последнюю строку с добавленным предметом.
	ЭлементыДерева = ДеревоПриложений.ПолучитьЭлементы();
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		ПоследнийИдентификатор = ЭлементДерева.ПолучитьИдентификатор();
		Элементы.ДеревоПриложений.Развернуть(ПоследнийИдентификатор);
	КонецЦикла;
	Элементы.ДеревоПриложений.ТекущаяСтрока = ПоследнийИдентификатор;
	
	Если ДобавленныеПредметы[0].ПредметТип = "DMFile" Тогда
		ЗаписатьВыполнить(Неопределено);
		ЗаполнитьФормуЗадачи(Новый Структура("ID, type", ID, Тип));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПредметЗавершениеНаСервере(ДобавленныеПредметы)
	
	Дерево = РеквизитФормыВЗначение("ДеревоПриложений");
	
	Для Каждого ОписаниеПредмета Из ДобавленныеПредметы Цикл
		СтрокаПредмета = Дерево.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПредмета, ОписаниеПредмета);
		СтрокаПредмета.ДоступноУдаление = Истина;
		Если ЗначениеЗаполнено(СтрокаПредмета.Ссылка) Тогда
			СтрокаПредмета.Представление = Строка(СтрокаПредмета.Ссылка);
		КонецЕсли;
		УстановитьКартинкуПредмета(СтрокаПредмета);
		ДополнитьДеревоПриложенийФайламиПредмета(СтрокаПредмета);
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПриложений");
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПредмет(Команда)
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ДеревоПриложений.ПолучитьЭлементы().Удалить(ТекущиеДанные);
	КонецЕсли;
	
	ОбработатьФормуПоНаличиюПредметов();
	
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.ПредметТип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	Если ТекущиеДанные.Редактируется И Не ТекущиеДанные.РедактируетсяТекущимПользователем Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Этот файл сейчас редактируется другим пользователем.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Команда", "Редактировать");
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"КомандыРедактированияЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьФайл(
		ТекущиеДанные.ПредметID,
		ТекущиеДанные.Предмет,
		ТекущиеДанные.Расширение,
		УникальныйИдентификатор,
		Ложь,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
			Или ТекущиеДанные.ПредметТип <> "DMFile"
			Или Не ТекущиеДанные.РедактируетсяТекущимПользователем Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Команда", "ЗакончитьРедактирование");
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"КомандыРедактированияЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ЗакончитьРедактированиеФайла(
		ТекущиеДанные.ПредметID,
		ТекущиеДанные.Предмет,
		ТекущиеДанные.Расширение,
		УникальныйИдентификатор,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРедактирование(Команда)
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
			Или ТекущиеДанные.ПредметТип <> "DMFile"
			Или Не ТекущиеДанные.РедактируетсяТекущимПользователем Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Команда", "ОтменитьРедактирование");
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"КомандыРедактированияЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОтменитьРедактированиеФайла(
		ТекущиеДанные.ПредметID,
		ОписаниеОповещения,,
		УникальныйИдентификатор);
	
КонецПроцедуры

// Общее завершение команд редактирования.
&НаКлиенте
Процедура КомандыРедактированияЗавершение(Результат, Параметры) Экспорт
	
	СтрокаПредмета = НайтиСтрокуПредмета(ДеревоПриложений.ПолучитьЭлементы(), Результат.ID, Результат.Тип);
	
	Если СтрокаПредмета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Команда = "Редактировать" Тогда
		СтрокаПредмета.Редактируется = Истина;
		СтрокаПредмета.РедактируетсяТекущимПользователем = Истина;
	Иначе // "ЗакончитьРедактирование" Или "ОтменитьРедактирование"
		СтрокаПредмета.Редактируется = Ложь;
		СтрокаПредмета.РедактируетсяТекущимПользователем = Ложь;
	КонецЕсли;
	УстановитьДоступностьКомандДереваПриложений();
	
КонецПроцедуры

// Управляет видимостью элементов формы в зависимости от наличия предметов.
&НаКлиенте
Процедура ОбработатьФормуПоНаличиюПредметов()
	
	ЕстьПредметы = (ДеревоПриложений.ПолучитьЭлементы().Количество() <> 0);
	Если ЕстьПредметы Тогда
		Если Элементы.ГруппаПриложения.ТекущаяСтраница = Элементы.СтраницаНетПредметов Тогда
			Элементы.ГруппаПриложения.ТекущаяСтраница = Элементы.СтраницаЕстьПредметы;
		КонецЕсли;
	Иначе
		Если Элементы.ГруппаПриложения.ТекущаяСтраница = Элементы.СтраницаЕстьПредметы Тогда
			Элементы.ГруппаПриложения.ТекущаяСтраница = Элементы.СтраницаНетПредметов;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПодзадачу(Команда)
	
	ПараметрыФормы = Новый Структура("ГлавнаяЗадача", Новый Структура);
	
	ПараметрыФормы.ГлавнаяЗадача.Вставить("name", Наименование);
	ПараметрыФормы.ГлавнаяЗадача.Вставить("id", ID);
	ПараметрыФормы.ГлавнаяЗадача.Вставить("type", Тип);
	
	ПредметПодзадачи = Неопределено;
	Приложения = ДеревоПриложений.ПолучитьЭлементы();

	Для Каждого Элемент Из Приложения Цикл
		Если Элемент.РольПредмета = "Основной" Тогда //@NON-NLS-1
			ПредметПодзадачи = Новый Структура;
			ПредметПодзадачи.Вставить("name", Элемент.Предмет);
			ПредметПодзадачи.Вставить("id", Элемент.ПредметID);
			ПредметПодзадачи.Вставить("type", Элемент.ПредметТип);
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если ПредметПодзадачи = Неопределено И Приложения.Количество() > 0 Тогда
		ПредметПодзадачи = Новый Структура;
		ПредметПодзадачи.Вставить("name", Приложения[0].Предмет);
		ПредметПодзадачи.Вставить("id", Приложения[0].ПредметID);
		ПредметПодзадачи.Вставить("type", Приложения[0].ПредметТип);
	КонецЕсли;

	ПараметрыФормы.Вставить("Предмет", ПредметПодзадачи);
	
	ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.СозданиеБизнесПроцесса", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти