
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Номенклатура = Параметры.Номенклатура;
	ТорговоеНаименование = Параметры.ТорговоеНаименование;
	ДействующиеВеществаМНН = Параметры.ДействующиеВеществаМНН;
	ФормаВыпуска = Параметры.ФормаВыпуска;
	Количество = Параметры.Количество;
	ЕдиницаИзмерения = Параметры.ЕдиницаИзмерения;
	Цена = Параметры.Цена;
	Валюта = Параметры.Валюта;
	
	Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		ЕдиницаИзмерения = ОтборНоменклатуры.ЕдиницаИзмеренияПоОтбору(ЭтотОбъект);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Количество) Тогда
		Количество = 1;
	КонецЕсли;
	
	Элементы.Цена.ТолькоПросмотр = Не Параметры.РедактироватьЦену;
	Элементы.Цена.ПропускатьПриВводе = Не Параметры.РедактироватьЦену;
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ЗначениеНастроекБольничнаяАптекаПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	КонецЕсли;
	
	Если Параметры.СкрытьЦену Тогда
		Элементы.Цена.Видимость = Ложь;
		Элементы.Валюта.Видимость = Ложь;
		Заголовок = НСтр("ru = 'Ввод количества'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		Наименование = СокрЛП(Номенклатура);
	Иначе
		Если ЗначениеЗаполнено(ТорговоеНаименование) Тогда
			Наименование = СокрЛП(ТорговоеНаименование);
		Иначе
			Наименование = СокрЛП(ДействующиеВеществаМНН);
		КонецЕсли;
		Наименование = Наименование + ", " + СокрЛП(ФормаВыпуска);
	КонецЕсли;
	
	УправляемаяФорма.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		ВыбранноеЗначение = Новый Структура;
		ВыбранноеЗначение.Вставить("Номенклатура", Номенклатура);
		ВыбранноеЗначение.Вставить("ТорговоеНаименование", ТорговоеНаименование);
		ВыбранноеЗначение.Вставить("ДействующиеВеществаМНН", ДействующиеВеществаМНН);
		ВыбранноеЗначение.Вставить("ФормаВыпуска", ФормаВыпуска);
		ВыбранноеЗначение.Вставить("Количество", Количество);
		ВыбранноеЗначение.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
		ВыбранноеЗначение.Вставить("Цена", Цена);
		
		ОповеститьОВыборе(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы
