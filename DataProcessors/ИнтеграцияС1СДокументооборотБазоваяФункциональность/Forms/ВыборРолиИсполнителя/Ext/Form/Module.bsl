#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Роль = Параметры.Роль;
	РольТип = Параметры.РольТип;
	РольID = Параметры.РольID;
	
	ОбновитьДанныеРоли(Параметры.РольТип, Параметры.РольID);
	
	Если ДанныеРоли.ИспользуетсяБезОбъектовАдресации И Не ДанныеРоли.ИспользуетсяСОбъектамиАдресации Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОсновнойОбъектАдресацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьОбъектАдресации("ОсновнойОбъектАдресации", "ТипОсновногоОбъектаАдресации");
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныйОбъектАдресацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьОбъектАдресации("ДополнительныйОбъектАдресации", "ТипДополнительногоОбъектаАдресации");
	
КонецПроцедуры

&НаКлиенте
Процедура РольОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнойОбъектАдресацииОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныйОбъектАдресацииОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РольОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнойОбъектАдресацииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОчиститьОбъектАдресации("ОсновнойОбъектАдресации");
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныйОбъектАдресацииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОчиститьОбъектАдресации("ДополнительныйОбъектАдресации");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Отказ = Ложь;
	ОчиститьСообщения();
	Если ДанныеРоли.ИспользуетсяСОбъектамиАдресации И Не ДанныеРоли.ИспользуетсяБезОбъектовАдресации Тогда
		
		Если Элементы.ОсновнойОбъектАдресации.Видимость И Не ЗначениеЗаполнено(ОсновнойОбъектАдресации) Тогда
			Текст = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, Элементы.ОсновнойОбъектАдресации.Заголовок);
			ОбщегоНазначенияКлиент.СообщитьПользователю(Текст,,, "ОсновнойОбъектАдресации");
			Отказ = Истина;
		КонецЕсли;
		
		Если Элементы.ДополнительныйОбъектАдресации.Видимость И Не ЗначениеЗаполнено(ДополнительныйОбъектАдресации) Тогда
			Текст = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, Элементы.ДополнительныйОбъектАдресации.Заголовок);
			ОбщегоНазначенияКлиент.СообщитьПользователю(Текст,,, "ДополнительныйОбъектАдресации");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВозврата = Новый Структура;
	
	ДанныеВозврата.Вставить("Результат", "ОК");
	
	ДанныеВозврата.Вставить("Исполнитель", Роль);
	ДанныеВозврата.Вставить("ИсполнительID", РольID);
	ДанныеВозврата.Вставить("ИсполнительТип", РольТип);
	
	ДанныеВозврата.Вставить("ОсновнойОбъектАдресации", ОсновнойОбъектАдресации);
	ДанныеВозврата.Вставить("ОсновнойОбъектАдресацииID", ОсновнойОбъектАдресацииID);
	ДанныеВозврата.Вставить("ОсновнойОбъектАдресацииТип", ОсновнойОбъектАдресацииТип);
	
	ДанныеВозврата.Вставить("ДополнительныйОбъектАдресации", ДополнительныйОбъектАдресации);
	ДанныеВозврата.Вставить("ДополнительныйОбъектАдресацииID", ДополнительныйОбъектАдресацииID);
	ДанныеВозврата.Вставить("ДополнительныйОбъектАдресацииТип", ДополнительныйОбъектАдресацииТип);
	
	Закрыть(ДанныеВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ДанныеВозврата = Новый Структура;
	ДанныеВозврата.Вставить("Результат", "Отмена");
	
	Закрыть(ДанныеВозврата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанныеРоли(Тип, ID)
	
	ДанныеРоли = Новый Структура;
	
	Элементы.ОсновнойОбъектАдресации.Видимость = Ложь;
	Элементы.ДополнительныйОбъектАдресации.Видимость = Ложь;
	Элементы.ОсновнойОбъектАдресации.Заголовок = "";
	Элементы.ДополнительныйОбъектАдресации.Заголовок = "";
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	ПолученнаяРоль = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъект(Прокси, Тип, ID);
	
	ДанныеРоли.Вставить("ИспользуетсяСОбъектамиАдресации", ПолученнаяРоль.withAddressingObjects);
	ДанныеРоли.Вставить("ИспользуетсяБезОбъектовАдресации", ПолученнаяРоль.withoutAddressingObjects);
	
	Если ПолученнаяРоль.withAddressingObjects Тогда
		
		Элементы.ОсновнойОбъектАдресации.Заголовок = ПолученнаяРоль.mainAddressingObjectName;
		Элементы.ДополнительныйОбъектАдресации.Заголовок = ПолученнаяРоль.secondaryAddressingObjectName;
		
		ДанныеРоли.Вставить("ИмяОсновногоОбъектаАдресации", ПолученнаяРоль.mainAddressingObjectName);
		ДанныеРоли.Вставить("ИмяДополнительногоОбъектаАдресации", ПолученнаяРоль.secondaryAddressingObjectName);
		
		ТипОсновногоОбъектаАдресации = Новый СписокЗначений;
		Для Каждого ТипЭлемента Из ПолученнаяРоль.mainAddressingObjectType Цикл
			ДанныеТипа = Новый Структура;
			ДанныеТипа.Вставить("XDTOClassName", ТипЭлемента.xdtoClassName);
			ДанныеТипа.Вставить("Presentation", ТипЭлемента.presentation);
			ТипОсновногоОбъектаАдресации.Добавить(ДанныеТипа);
		КонецЦикла;
		
		ТипДополнительногоОбъектаАдресации = Новый СписокЗначений;
		Для Каждого ТипЭлемента Из ПолученнаяРоль.secondaryAddressingObjectType Цикл
			ДанныеТипа = Новый Структура;
			ДанныеТипа.Вставить("XDTOClassName", ТипЭлемента.xdtoClassName);
			ДанныеТипа.Вставить("Presentation", ТипЭлемента.presentation);
			ТипДополнительногоОбъектаАдресации.Добавить(ДанныеТипа);
		КонецЦикла;
		
		ДанныеРоли.Вставить("ТипОсновногоОбъектаАдресации", ТипОсновногоОбъектаАдресации);
		ДанныеРоли.Вставить("ТипДополнительногоОбъектаАдресации", ТипДополнительногоОбъектаАдресации);
		
		Элементы.ОсновнойОбъектАдресации.Видимость = ЗначениеЗаполнено(Элементы.ОсновнойОбъектАдресации.Заголовок);
		Элементы.ДополнительныйОбъектАдресации.Видимость =
			ЗначениеЗаполнено(Элементы.ДополнительныйОбъектАдресации.Заголовок);
		
		Элементы.ОсновнойОбъектАдресации.ОтметкаНезаполненного = Ложь;
		Элементы.ДополнительныйОбъектАдресации.ОтметкаНезаполненного = Ложь;
		
		Элементы.ОсновнойОбъектАдресации.АвтоОтметкаНезаполненного = Ложь;
		Элементы.ДополнительныйОбъектАдресации.АвтоОтметкаНезаполненного = Ложь;
		
		Если ДанныеРоли.ИспользуетсяСОбъектамиАдресации И Не ДанныеРоли.ИспользуетсяБезОбъектовАдресации Тогда
			Элементы.ОсновнойОбъектАдресации.АвтоОтметкаНезаполненного = Элементы.ОсновнойОбъектАдресации.Видимость;
			Элементы.ДополнительныйОбъектАдресации.АвтоОтметкаНезаполненного = Элементы.ДополнительныйОбъектАдресации.Видимость;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОбъектАдресации(ИмяПеременной, ИмяПоля)
	
	ИмяТипа = "";
	
	Если ДанныеРоли[ИмяПоля].Количество() > 1 Тогда
		
		ЗаголовокФормы = НСтр("ru = 'Тип объекта адресации'");
		ПараметрыФормы = Новый Структура("СписокДоступныхТипов, ЗаголовокФормы", ДанныеРоли[ИмяПоля], ЗаголовокФормы);
		Оповещение = Новый ОписаниеОповещения("ВыбратьОбъектАдресацииВыборТипаЗавершение", ЭтотОбъект, ИмяПеременной);
		
		ОткрытьФорму(
			"Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.ВыборОдногоТипаИзСоставногоТипа",
			ПараметрыФормы,
			ЭтотОбъект,,,,
			Оповещение);
		
	ИначеЕсли ДанныеРоли[ИмяПоля].Количество() = 1 Тогда
		
		ИмяТипа = ДанныеРоли[ИмяПоля][0].Значение.XDTOClassName;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТипОбъектаВыбора", ИмяТипа);
		
		Оповещение = Новый ОписаниеОповещения("ВыбратьОбъектАдресацииЗавершение", ЭтотОбъект, ИмяПеременной);
		
		ОткрытьФорму(
			"Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.ВыборИзСписка",
			ПараметрыФормы,
			ЭтотОбъект,,,,
			Оповещение);
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОбъектАдресацииВыборТипаЗавершение(РезультатВыбораТипа, ИмяПеременной) Экспорт
	
	Если РезультатВыбораТипа = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТипа = РезультатВыбораТипа;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъектаВыбора", ИмяТипа);
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьОбъектАдресацииЗавершение", ЭтотОбъект, ИмяПеременной);
	
	ОткрытьФорму(
		"Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.ВыборИзСписка",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОбъектАдресацииЗавершение(РезультатВыбора, ИмяПеременной) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		ЭтотОбъект[ИмяПеременной] = РезультатВыбора.РеквизитПредставление;
		ЭтотОбъект[ИмяПеременной + "ID"] = РезультатВыбора.РеквизитID;
		ЭтотОбъект[ИмяПеременной + "Тип"] = РезультатВыбора.РеквизитТип;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОбъектАдресации(ИмяПеременной)
	
	ЭтотОбъект[ИмяПеременной] = "";
	ЭтотОбъект[ИмяПеременной + "ID"] = "";
	ЭтотОбъект[ИмяПеременной + "Тип"] = "";
	
КонецПроцедуры

#КонецОбласти