#Если Сервер Или ТонкийКлиент Или ТолстыйКлиентУправляемоеПриложение Тогда

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(
		ЭтотОбъект,
		Параметры,
		"ПолныйПуть,
		|ДатаВремяНаДиске,
		|ДатаВремяВДокументообороте,
		|РазмерНаДиске,
		|РазмерВДокументообороте,
		|Сообщение,
		|Заголовок");
	
	Если ДатаВремяНаДиске > ДатаВремяВДокументообороте Тогда
		Элементы.ИспользоватьСуществующий.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ВзятьИзДокументооборота.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Если ДатаВремяНаДиске = ДатаВремяВДокументообороте Тогда
		Элементы.ДатаВремяНаДиске.Видимость = Ложь;
		Элементы.ДатаВремяВДокументообороте.Видимость = Ложь;
	Иначе
		ЧасовойПояс = ЧасовойПоясСеанса();
		ДатаВремяНаДиске = МестноеВремя(ДатаВремяНаДиске, ЧасовойПояс);
		ДатаВремяВДокументообороте = МестноеВремя(ДатаВремяВДокументообороте, ЧасовойПояс);
	КонецЕсли;
	
	Если РазмерНаДиске = РазмерВДокументообороте Тогда
		Элементы.РазмерНаДиске.Видимость = Ложь;
		Элементы.РазмерВДокументообороте.Видимость = Ложь;
	Иначе
		ПредставлениеРазмераНаДиске =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ПодробноеПредставлениеРазмера(
				РазмерНаДиске);
		ПредставлениеРазмераВДокументообороте =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ПодробноеПредставлениеРазмера(
				РазмерВДокументообороте);
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Сообщение;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВзятьИзДокументооборота(Команда)
	
	Закрыть("ВзятьИзДокументооборота");
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСуществующий(Команда)
	
	Закрыть("ИспользоватьСуществующий");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКаталог(Команда)
	
	ФайловаяСистемаКлиент.ОткрытьПроводник(ПолныйПуть);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли