#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектИС", ОбъектИС);
	Параметры.Свойство("ИдентификаторОбъектаДО", ИдентификаторОбъектаДО);
	Параметры.Свойство("ТипОбъектаДО", ТипОбъектаДО);
	Параметры.Свойство("РольФайлаID", РольФайлаID);
	
	ТипОбъектаИС = ОбъектИС.Метаданные().ПолноеИмя();
	ТипПечатнойФормы = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
		"ИнтеграцияС1СДокументооборот",
		"ТипПечатнойФормы" + ТипОбъектаИС,
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ТипФайлаСохраняемойПечатнойФормыПоУмолчанию());
	
	// Захват и редактирование файлов.
	Элементы.ДобавитьИРедактировать.Видимость =
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ДоступенФункционалВерсииСервиса("1.4.9.1");
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьТаблицуМенеджеровПечатиОбъекта(
		ТипОбъектаИС,
		ПечатныеФормы);
	
	Если ПечатныеФормы.Количество() = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПечатныеФормыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДобавитьПечатнуюФорму(Элементы.ПечатныеФормы.ТекущиеДанные.ИмяКоманды, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	ТекущиеДанные = Элементы.ПечатныеФормы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ДобавитьПечатнуюФорму(ТекущиеДанные.ИмяКоманды, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИРедактировать(Команда)
	
	ТекущиеДанные = Элементы.ПечатныеФормы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ДобавитьПечатнуюФорму(ТекущиеДанные.ИмяКоманды, Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьПечатнуюФорму(ИмяКоманды, НаЧтение)
	
	СозданныеФайлы =
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ПрисоединитьПечатнуюФормуОбъектаКДокументу(
			ОбъектИС,
			ИдентификаторОбъектаДО,
			ТипОбъектаДО,
			"",
			ИмяКоманды,
			ТипПечатнойФормы,
			РольФайлаID);
	Для Каждого СозданныйФайл Из СозданныеФайлы Цикл
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Создан файл ""%1""'"), СозданныйФайл.ТекущийФайл.Наименование);
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ИнформироватьОбИзмененииФайла(
			"СозданиеФайла",
			СозданныйФайл.ТекущийФайл.Наименование,
			СозданныйФайл.ТекущийФайл.ID,
			ИдентификаторОбъектаДО,
			ТекстСообщения,,
			УникальныйИдентификатор);
	КонецЦикла;
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(
		"ИнтеграцияС1СДокументооборот",
		"ТипПечатнойФормы" + ТипОбъектаИС,
		ТипПечатнойФормы);
	
	Если Не НаЧтение Тогда
		Для Каждого СозданныйФайл Из СозданныеФайлы Цикл
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьФайл(
				СозданныйФайл.ТекущийФайл.ID,
				СозданныйФайл.ТекущийФайл.Наименование,
				СозданныйФайл.ТекущийФайл.Расширение,
				УникальныйИдентификатор,
				НаЧтение,,
				ИдентификаторОбъектаДО);
		КонецЦикла;
	КонецЕсли;
	
	Закрыть(СозданныеФайлы);
	
КонецПроцедуры

#КонецОбласти