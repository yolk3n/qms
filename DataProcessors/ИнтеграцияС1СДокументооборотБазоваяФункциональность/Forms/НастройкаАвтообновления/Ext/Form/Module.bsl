#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Автообновление") Тогда
		Автообновление = Параметры.Автообновление;
		Если Параметры.Свойство("ПериодАвтообновления") Тогда
			ПериодАвтообновления = Параметры.ПериодАвтообновления;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("ВМинутах") Тогда
		ВМинутах = Параметры.ВМинутах;
	Иначе
		ВМинутах = Ложь;
	КонецЕсли;
	
	Если ВМинутах Тогда
		Элементы.НадписьСек.Заголовок = НСтр("ru = 'мин.'");
	КонецЕсли;
	
	Элементы.ПериодАвтообновления.Доступность = Автообновление;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АвтообновлениеПриИзменении(Элемент)
	
	Элементы.ПериодАвтообновления.Доступность = Автообновление;
	
	// Значение по умолчанию если период еще не задан.
	Если ВМинутах Тогда
		Если ПериодАвтообновления < 1 Тогда
			ПериодАвтообновления = 15; // Минут.
		КонецЕсли;
	Иначе
		Если ПериодАвтообновления < 10 Тогда
			ПериодАвтообновления = 900; // Секунд.
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодАвтообновленияПриИзменении(Элемент)
	
	Если ВМинутах Тогда
		Если ПериодАвтообновления < 1 Тогда
			ПериодАвтообновления = 1; // Не меньше 1 минуты.
		КонецЕсли;
	Иначе
		Если ПериодАвтообновления < 10 Тогда
			ПериодАвтообновления = 10; // Не меньше 10 секунд.
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Установить(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("Автообновление", Автообновление);
	Результат.Вставить("ПериодАвтообновления", ПериодАвтообновления);
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти
