#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЭтотОбъект.КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
	ЗаполнитьКонтекстОтправкиПриСоздании();
	
	Если Не ЗначениеЗаполнено(КонтекстОтправки.ДокументОснование) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДокументОснование = КонтекстОтправки.ДокументОснование;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Документы, "ДокументОснование", ДокументОснование, ВидСравненияКомпоновкиДанных.Равно, , Истина);
	
	НастроитьЭлементыФормы();
	
	ИмяОбъектаМетаданныхОснования = ДокументОснование.Метаданные().ПолноеИмя();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", ДокументОснование);
	
	ФормаОснования = ПолучитьФорму(ИмяОбъектаМетаданныхОснования + ".ФормаОбъекта", ПараметрыФормы);
	
	Если ФормаОснования = Неопределено Тогда
		ИмяФормыОснования = "";
	Иначе
		ИмяФормыОснования = ФормаОснования.ИмяФормы;
	КонецЕсли;
	
	ЗаполнитьКомандыОтправитьКак(ИмяФормыОснования);
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО() Тогда
		Элементы.Документы.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Посмотреть(Команда)
	ТекущиеДанные = Элементы.Документы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьКак(Команда)
	
	ОписаниеКоманды = КоллекцияКомандПечатиОснования[Команда.Имя];
	
	Если ОписаниеКоманды <> Неопределено Тогда
		ИнтерфейсДокументовЭДОКлиент.СформироватьНеформализованныйЭДОПоКомандеПечати(
			ОписаниеКоманды, ДокументОснование);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоУмолчанию(Команда)
	
	ИнтерфейсДокументовЭДОКлиент.СформироватьНеформализованныеЭДОПоКонтекстуОтправки(КонтекстОтправки);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьКонтекстОтправкиПриСоздании()
	
	Контекст = ИнтерфейсДокументовЭДОКлиентСервер.НовыйКонтекстОтправкиПечатныхФормПоЭДО();
	
	Если Параметры.Свойство("ДокументОснование") Тогда
		Контекст.ДокументОснование = Параметры.ДокументОснование;
	КонецЕсли;
	
	Если Параметры.Свойство("ДанныеПечатныхФорм") И ТипЗнч(Параметры.ДанныеПечатныхФорм) = Тип("Массив") Тогда
		
		Для Каждого Данные Из Параметры.ДанныеПечатныхФорм Цикл
			
			ДанныеКонтекста = ИнтерфейсДокументовЭДОКлиентСервер.НовыеДанныеПечатнойФормыДляНеформализованногоЭДО();
			ЗаполнитьЗначенияСвойств(ДанныеКонтекста, Данные);
			
			Контекст.ДанныеПечатныхФорм.Добавить(ДанныеКонтекста);
			
		КонецЦикла;
		
	КонецЕсли;
	
	КонтекстОтправки = Новый ФиксированнаяСтруктура(Контекст);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	Элементы.ПояснениеНадпись.Заголовок = СтрШаблон(
		НСтр("ru = 'Для документа %1 уже имеются сформированные документы ЭДО. Перед отправкой нового документа убедитесь, что не произойдет дублирования документов.'"),
		ДокументОснование);
	
	Если КонтекстОтправки.ДанныеПечатныхФорм.Количество() = 0 Тогда
		
		Элементы.ОтправитьПоУмолчанию.Видимость = Ложь;
		
	ИначеЕсли КонтекстОтправки.ДанныеПечатныхФорм.Количество() = 1 Тогда
		
		ДанныеПечатнойФормы = КонтекстОтправки.ДанныеПечатныхФорм[0];
		
		Элементы.ОтправитьПоУмолчанию.Заголовок = СтрШаблон(
			НСтр("ru = 'Отправить как ""%1""'"),
			ДанныеПечатнойФормы.НаименованиеФайла);
		
	Иначе
		
		Элементы.ОтправитьПоУмолчанию.Заголовок = СтрокаСЧислом(
			НСтр("ru = ';Отправить %1 документ; ;Отправить %1 документа;Отправть %1 документов;Отправить %1 документов'"),
			КонтекстОтправки.ДанныеПечатныхФорм.Количество(), ВидЧисловогоЗначения.Количественное);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКомандыОтправитьКак(ИмяФормыОснования)
	
	ИнициализироватьКоллекциюКомандПечати(ИмяФормыОснования);
	
	Для Каждого Элемент Из КоллекцияКомандПечатиОснования Цикл
		
		ИмяКоманды = Элемент.Ключ;
		ДанныеКомандыПечати = Элемент.Значение;
		
		НоваяКоманда = Команды.Добавить(ИмяКоманды);
		НоваяКоманда.Заголовок = ДанныеКомандыПечати.Представление;
		НоваяКоманда.Действие = "ОтправитьКак";
		
		ЭлементКоманды = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Элементы.ОтправитьКакПодменю);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКоллекциюКомандПечати(ИмяФормыОснования)
	
	КоллекцияКоманд = ИнтеграцияЭДО.КомандыПечатиДляОтправкиНеформализованногоЭДО(
		ДокументОснование.Метаданные(), ИмяФормыОснования);
	
	МассивКоманд = ОбщегоНазначения.ТаблицаЗначенийВМассив(КоллекцияКоманд);
	
	СоответствиеКоманд = Новый Соответствие();
	Для Каждого Команда Из МассивКоманд Цикл
		ИмяКоманды = СтрШаблон("ОтправитьКак_%1", Команда.Идентификатор);
		СоответствиеКоманд[ИмяКоманды] = Команда;
	КонецЦикла;
	
	КоллекцияКомандПечатиОснования = Новый ФиксированноеСоответствие(СоответствиеКоманд);
	
КонецПроцедуры

#КонецОбласти