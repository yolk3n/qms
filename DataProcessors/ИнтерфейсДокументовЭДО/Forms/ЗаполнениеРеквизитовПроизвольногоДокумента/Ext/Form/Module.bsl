#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("ОписаниеФайла") Тогда
		
		ЗаполнитьДанныеПоОписаниюФайла(Параметры.ОписаниеФайла);
		
	ИначеЕсли Параметры.Свойство("ОбъектУчета") Тогда
		
		ЗаполнитьДанныеПоОбъектуУчета(Параметры.ОбъектУчета);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Номер", Номер);
	Результат.Вставить("Дата", Дата);
	Результат.Вставить("Сумма", Сумма);
	Результат.Вставить("ВидДокумента", ВидДокумента);
	
	Закрыть(Результат);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДанныеПоОписаниюФайла(ОписаниеФайла)
	
	СодержаниеСообщения = ЭлектронныеДокументыЭДО.СодержаниеСообщения(ОписаниеФайла);
	Если ЗначениеЗаполнено(СодержаниеСообщения) Тогда
		Номер = СодержаниеСообщения.НомерДокумента;
		Дата = СодержаниеСообщения.ДатаДокумента;
		Сумма = СодержаниеСообщения.СуммаДокумента;
		Если ЗначениеЗаполнено(СодержаниеСообщения.ПрикладнойТипДокумента) Тогда
			ВидДокумента = ЭлектронныеДокументыЭДО.ВидДокументаПоПрикладномуТипу(
				СодержаниеСообщения.ПрикладнойТипДокумента);
		Иначе
			ВидДокумента = ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(СодержаниеСообщения.ТипДокумента);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПоОбъектуУчета(ОбъектУчета)
	
	Если Не ЗначениеЗаполнено(ОбъектУчета) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыОбъектаУчета = ИнтеграцияЭДО.ОписаниеОснованияЭлектронногоДокумента(ОбъектУчета);
	
	Номер = РеквизитыОбъектаУчета.Номер;
	Дата = РеквизитыОбъектаУчета.Дата;
	Сумма = РеквизитыОбъектаУчета.СуммаДокумента;
	ВидДокумента = ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(Перечисления.ТипыДокументовЭДО.Прочее);
	
КонецПроцедуры

#КонецОбласти
