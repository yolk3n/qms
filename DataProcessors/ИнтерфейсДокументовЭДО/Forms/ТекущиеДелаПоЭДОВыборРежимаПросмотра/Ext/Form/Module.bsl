
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("РежимОтображения", РежимОтображения);
	Параметры.Свойство("ПакетноеОтображение", ПакетноеОтображение);
	Параметры.Свойство("ОбластьЛегенды", ОбластьЛегенды);
	Параметры.Свойство("ОбластьПросмотра", ОбластьПросмотра);
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
	КонецЕсли;
	
	ВсегдаИспользоватьНовыйИнтерфейс = 
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ИнтерфейсЭДО", 
		"ВсегдаИспользоватьНовыйИнтерфейс", Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимОтображенияКИсполнениюПриИзменении(Элемент)

	ПриИзмененииРежимаПросмотра()
	
КонецПроцедуры

&НаКлиенте
Процедура РежимОтображенияМоиДокументыПриИзменении(Элемент)
	
	ПриИзмененииРежимаПросмотра()
	
КонецПроцедуры

&НаКлиенте
Процедура РежимОтображенияВсеДокументыПриИзменении(Элемент)
	
	ПриИзмененииРежимаПросмотра()
	
КонецПроцедуры

&НаКлиенте
Процедура ВсегдаИспользоватьНовыйИнтерфейсПриИзменении(Элемент)
	
	Если Не ВсегдаИспользоватьНовыйИнтерфейс Тогда
		ТекстВопроса = НСтр("ru='Будет использоваться традиционный интерфейс ЭДО по умолчанию. Продолжить?'");	
	Иначе
		ТекстВопроса = НСтр("ru='Будет использоваться новый интерфейс ЭДО по умолчанию. Продолжить?'");
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОтветаНаВопросПереходНаНовыйИнтерфейс", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, 
		ТекстВопроса, 
		РежимДиалогаВопрос.ОКОтмена, 
		60, 
		КодВозвратаДиалога.ОК, , 
		КодВозвратаДиалога.Отмена);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("РежимОтображения", РежимОтображения);
	Результат.Вставить("ПакетноеОтображение", ПакетноеОтображение);
	Результат.Вставить("ОбластьЛегенды", ОбластьЛегенды);
	Результат.Вставить("ОбластьПросмотра", ОбластьПросмотра);
			
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриИзмененииРежимаПросмотра()
	
	#Если МобильныйКлиент Тогда
		Закрыть(РежимОтображения);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросПереходНаНовыйИнтерфейс(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВсегдаИспользоватьНовыйИнтерфейс = Не ВсегдаИспользоватьНовыйИнтерфейс;
		Возврат;
	КонецЕсли;
	
    Если Результат = КодВозвратаДиалога.ОК Тогда
    	СохранитьНастройку();
		Если ВсегдаИспользоватьНовыйИнтерфейс Тогда 
			ОткрытьФорму("Обработка.ИнтерфейсДокументовЭДО.Форма.НовыйИнтерфейсЭДО");
			Оповестить("ВсегдаИспользоватьНовыйИнтерфейс");
		КонецЕсли;
	Иначе
		ВсегдаИспользоватьНовыйИнтерфейс = Не ВсегдаИспользоватьНовыйИнтерфейс;	
    КонецЕсли;	
 
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройку()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ИнтерфейсЭДО", "ВсегдаИспользоватьНовыйИнтерфейс", ВсегдаИспользоватьНовыйИнтерфейс);
	
КонецПроцедуры

#КонецОбласти