
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("ЭлектронныеДокументы") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ТекущийКомментарий", Комментарий);
	
	ОбъектыДляОбработки = Новый Массив;
	Если ТипЗнч(Параметры.ЭлектронныеДокументы) <> Тип("Массив") Тогда
		ОбъектыДляОбработки.Добавить(Параметры.ЭлектронныеДокументы);
	Иначе
		ОбъектыДляОбработки = Параметры.ЭлектронныеДокументы;
	КонецЕсли;
	АдресХранилища = ПоместитьВоВременноеХранилище(ОбъектыДляОбработки, УникальныйИдентификатор);
	
	Если ОбъектыДляОбработки.Количество() > 1 Тогда
		Элементы.ОбъектыДляОбработки.Заголовок = НСтр("ru = 'Список'");
	КонецЕсли;
	
	Если Параметры.Свойство("Ответственный") Тогда
		Пользователь = Параметры.Ответственный;
	КонецЕсли;
	Если ОбъектыДляОбработки.Количество() = 1 Тогда
		
		Документооборот = ОбъектыДляОбработки[0];
		
		ПараметрыПредставления = ЭлектронныеДокументыЭДО.НовыеСвойстваПредставленияДокумента();
		ПараметрыПредставления.ВидДокумента = Документооборот.ВидДокумента;
		ПараметрыПредставления.НомерДокумента = Документооборот.НомерДокумента;
		ПараметрыПредставления.ДатаДокумента = Документооборот.ДатаДокумента;
		
		ТекстГиперссылки = ЭлектронныеДокументыЭДО.ПредставлениеДокументаПоСвойствам(ПараметрыПредставления);
		
	Иначе
		ТекстГиперссылки = НСтр("ru = 'Электронные документы (%1)'");
		ТекстГиперссылки = СтрЗаменить(ТекстГиперссылки, "%1", ОбъектыДляОбработки.Количество());
	КонецЕсли;
	
	ТекстГиперссылки = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ТекстГиперссылки);
	НадписьОбъектыДляОбработки = ТекстГиперссылки;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Элементы.ГруппаМК.Видимость = Истина;
		ИнтерфейсДокументовЭДО.ПереместитьЭлемент(Элементы, "ОК", "ГруппаМК");
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОК", "Шрифт", ШрифтыСтиля.КрупныйШрифтТекста);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОК", "ГоризонтальноеПоложениеВГруппе", ГоризонтальноеПоложениеЭлемента.Центр);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОК", "РастягиватьПоГоризонтали", Истина);
		ИнтерфейсДокументовЭДО.ПереместитьЭлемент(Элементы, "Отмена", "ГруппаМК");
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отмена", "ГоризонтальноеПоложениеВГруппе", ГоризонтальноеПоложениеЭлемента.Центр);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Справка", "Видимость", Ложь);
		Элементы.Комментарий.ЦветФона = ЦветаСтиля.ЦветФонаПоляВводаМКБЭД;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбъектыДляОбработкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Документообороты = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если Документообороты.Количество() > 1 Тогда
		ОткрытьФорму("Обработка.ИнтерфейсДокументовЭДО.Форма.СписокВыгружаемыхЭлектронныхДокументов",
			Новый Структура("Документообороты", Документообороты), ЭтотОбъект);
	Иначе
		ИнтерфейсДокументовЭДОКлиент.ОткрытьЭлектронныйДокумент(Документообороты[0]);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указан ответственный.'"),, "Пользователь");
		Возврат;
	КонецЕсли;
	
	Результат = ПеренаправитьЭД();
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	
	ИнтерфейсДокументовЭДОКлиент.ОповеститьПользователяОСменеОтветственного(Пользователь,
		Результат.КоличествоДокументовВсего, Результат.КоличествоДокументов);
	Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО());
	
	РезультатОбработки = Новый Структура;
	РезультатОбработки.Вставить("Успех", Результат.КоличествоДокументов > 0);
	РезультатОбработки.Вставить("КоличествоОбработанных", Результат);
	
	Закрыть(РезультатОбработки);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПеренаправитьЭД()
	
	Результат = Новый Структура;
	Результат.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики());
	Результат.Вставить("КоличествоДокументов", 0);
	Результат.Вставить("КоличествоДокументовВсего", 0);
	Результат.Вставить("Ответственный", Пользователь);
	
	ЭлектронныеДокументы = ПолучитьИзВременногоХранилища(АдресХранилища);
	Результат.КоличествоДокументовВсего = ЭлектронныеДокументы.Количество();
	Для Каждого Документооборот Из ЭлектронныеДокументы Цикл
		Если ЭлектронныеДокументыЭДО.УстановитьОтветственногоПоДокументу(Документооборот, Пользователь,
			Результат.КонтекстДиагностики, Комментарий) Тогда
			Результат.КоличествоДокументов = Результат.КоличествоДокументов + 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
