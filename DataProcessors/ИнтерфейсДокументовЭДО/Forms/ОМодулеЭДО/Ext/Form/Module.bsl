
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НавигационнаяСсылка = "e1cib/app/" + ИмяФормы;
	
	Шаблон = НСтр("ru = 'Версия модуля ЭДО: %1%2%2'");
	ВерсияМодуляЭДО = СтрШаблон(Шаблон, ОбновлениеИнформационнойБазыБЭД.ВерсияБиблиотеки(), Символы.ВК);
		
	ШаблонКонфигурации = НСтр("ru = 'Конфигурация: %2%1 (%3)%2%2'");
	Конфигурация = СтрШаблон(ШаблонКонфигурации, 
		Метаданные.ПодробнаяИнформация, Символы.ВК, Метаданные.Версия);
	
	СистемнаяИнформация = Новый СистемнаяИнформация;	
	ШаблонСистемнаяИнформация = НСтр("ru = 'Версия платформы: %1%2%2'");
	ВерсияПлатформы = СтрШаблон(ШаблонСистемнаяИнформация, СистемнаяИнформация.ВерсияПриложения, 
		Символы.ВК);
			
	Попытка
	НомерТекущегоСоединения = НомерСоединенияИнформационнойБазы();
	ШаблонИмяПриложения = НСтр("ru = 'Приложение: %1%2%2'");
	ИмяПриложения = СтрШаблон(ШаблонИмяПриложения, ИмяПриложенияТекущегоСоединения(НомерТекущегоСоединения), 
		Символы.ВК);
	
	УстановленныеРасширенияКонфигурации = РасширенияКонфигурации.Получить();
	ПеречислениеУстановленныхРасширений = "";
	
	Для каждого Расширение Из УстановленныеРасширенияКонфигурации Цикл

		ШаблонРасширения = НСтр("ru = '%1- %2;%3'");
		ПеречислениеУстановленныхРасширений = СтрШаблон(ШаблонРасширения, 
			ПеречислениеУстановленныхРасширений, Расширение.Имя, Символы.ВК);  		
		
	КонецЦикла;
	Исключение
		КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
		ТекстОшибки = НСтр("ru = 'Не удалось получить список расширений конфигурации'", 
			КодЯзыка);
		ЗаписьЖурналаРегистрации(ТекстОшибки, УровеньЖурналаРегистрации.Предупреждение, , , ТекстОшибки);
	КонецПопытки;
	
	Шаблон = НСтр("ru = 'Установленные расширения: %2%1%2'");
	Расширения = СтрШаблон(Шаблон, ПеречислениеУстановленныхРасширений, Символы.ВК);
	
	Информация = СтрШаблон("%1%2%3%4%5", ВерсияМодуляЭДО, Конфигурация, ВерсияПлатформы, ИмяПриложения, Расширения); 
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИмяПриложенияТекущегоСоединения(НомерТекущегоСоединения)
	
	ИмяПриложения = "";  
	СеансыИнформационнойБазы = ПолучитьСеансыИнформационнойБазы(); 
	
	Для Каждого Сеанс Из СеансыИнформационнойБазы Цикл         
		
		Если Сеанс.НомерСоединения = НомерТекущегоСоединения Тогда			
			ИмяПриложения = ПредставлениеПриложения(Сеанс.ИмяПриложения); 			
		КонецЕсли;   
		
	КонецЦикла;
	
	Возврат ИмяПриложения;
	
КонецФункции

#КонецОбласти
